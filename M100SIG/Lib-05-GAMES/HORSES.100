10 REM saved as horse
20 REM a horse race for up to 4 people
30 CLEAR 200
40 DEFINT C-R
80 FOR N=0 TO 4:READ HN$(N):NEXT N
90 DATA No Choice this race,Citation,Man of War,Seabiscuit,Feedlebaum
100 H$=CHR$(224)+CHR$(243)+CHR$(243)+CHR$(225)
110 BT$=" SsPpWw":YE$=" YyNn":F$="$$###,###.##":F1$="#.#/1"
120 ST(1)=85:ST(2)=125:ST(3)=165:ST(4)=205:SP=200
130 FOR N=3 TO 0 STEP -1:READ WN$(N),WM$(N):NEXT N
140 DATA WINNER,2.0-1,PLACE,1.5-1,SHOW,1.2-1,Also ran,0
199 REM driver program
200 GOSUB 1000:REM load player info
202 RA=RA+1
203 FOR N=1 TO 4:D(N)=0:W(N)=0:NEXT N
205 GOSUB 1100: REM takes bets on current race
210 GOSUB 1300: REM draws race track
220 GOSUB 1400:REM STARTS RACE AND DETERMINES WINNER
230 GOSUB 1500:REM DETERMINES PLACE
240 GOSUB 1600: REM DETERMINES SHOW
250 GOSUB 1700:REM finishes race
260 GOSUB 730: REM delay
270 GOSUB 1800:REM displays tote board
275 GOSUB 2600:GOSUB 2600:GOSUB 2620
280 GOSUB 2000: REM Calculates winnings
290 CLS:INPUT"Do you want to race again (Y/N)";YE$
300 IF YE$="y" OR YE$="Y" THEN 202 
310 CLS:PRINT"Hope you had a good time":END
690 END
700 INPUT"Press <ENTER> to continue";E:RETURN
720 FOR S=1 TO SP:NEXT S:RETURN
730 FOR S=1 TO 2000:NEXT S:RETURN
998 REM
999 REM information for players
1000 CLS
1010 PRINT"Welcome to Hollywood Park"
1030 INPUT"Do you want instructions Y/N";Y$
1040 Y=INT(INSTR(YE$,Y$)/2):IF Y=0 THEN 1040 ELSE IF Y=1 THEN GOSUB 10000
1050 INPUT"    How many players (1-4)";PL:IF PL<1 OR PL>4 THEN 1050
1060 FOR N=1 TO PL:BR(N)=100:NEXT N:REM each player gets a $100 bankroll
1070 FOR N=1 TO PL
1080 PRINT"Name of player # ";N;:INPUT NM$(N)
1090 NEXT N
1095 RETURN
1098 REM
1099 REM
1100 FOR N=1 TO PL
1110 CLS
1120 PRINT TAB(5)"Race #";RA;"  Horses are:"
1130 FOR K=0 TO 4:PRINT K;" = ";HN$(K):NEXT K
1180 PRINT TAB(5)NM$(N);"-Please select one"
1190 INPUT HN(N):IF HN(N)=0 THEN 1230 ELSE IF HN(N)>5 GOTO 1190
1200 INPUT" Type bet <W>in <P>lace <S>how";BT$(N)
1210 BT(N)=INT(INSTR(BT$,BT$(N))/2):IF BT(N)=0 THEN 1200
1220 INPUT"Amount bet in $ ";BA(N)
1230 NEXT N
1240 RETURN
1297 REM
1298 REM
1299 REM  **** DRAWS THE RACE TRACK
1300 CLS
1310 PRINT@8,"HOLLYWOOD PARK RACE TRACK";
1320 LINE(34,10)-(220,10):LINE(34,53)-(220,53)
1325 LINE(34,11)-(220,11):LINE(34,54)-(220,54)
1330 LINE(34,7)-(34,58):LINE(220,7)-(220,58)
1340 LINE(34,20)-(220,20):LINE(34,28)-(220,28)
1350 LINE(34,36)-(220,36):LINE(34,44)-(220,44)
1360 PRINT@81,"Cit";:PRINT@121,"MOW";:PRINT@161,"Sea";:PRINT@201,"Fed";
1380 RETURN
1399 REM  **** STARTS RACE AND DETERMINES WINNER
1400 FOR N=1 TO 4:PRINT@ST(N),H$;:NEXT N
1405 S=VAL(RIGHT$(TIME$,2)):FOR N=1 TO S:D=RND(1):NEXT N
1406 GOSUB 2500:GOSUB 720
1407 BEEP
1410 R=INT(RND(1)*4+1)
1420 IF D(R)>=30 AND W(R)=0 THEN PRINT@ST(R)+15,WN$(3);:W(R)=3:RETURN
1430 D(R)=D(R)+1:PRINT@ST(R)+D(R),H$;
1440 GOTO 1410
1497 REM
1498 REM
1499 REM  **** DETERMINES PLACE HORSE
1500 R=INT(RND(1)*4+1)
1510 IF W(R)=3 THEN 1500
1520 IF D(R)>=30 AND W(R)=0 THEN PRINT@ST(R)+15,WN$(2);:W(R)=2:RETURN
1530 D(R)=D(R)+1:PRINT@ST(R)+D(R),H$;
1540 GOTO 1500
1597 REM
1598 REM
1599 REM **** DETERMINES SHOW HORSE
1600 R=INT(RND(1)*4+1)
1610 IF W(R)=3 OR W(R)=2 THEN 1600
1620 IF D(R)>=30 AND W(R)=0 THEN PRINT@ST(R)+15,WN$(1);:W(R)=1:RETURN
1630 D(R)=D(R)+1:PRINT@ST(R)+D(R),H$;
1640 GOTO 1600
1699 REM finishes race
1700 R=INT(RND(1)*4+1)
1710 IF W(R)=3 OR W(R)=2 OR W(R)=1 THEN 1700
1720 IF D(R)>=30 AND W(R)=0 THEN PRINT@ST(R)+15,WN$(0);:RETURN
1730 D(R)=D(R)+1:PRINT@ST(R)+D(R),H$;
1740 GOTO 1700
1799 REM print the tote board
1800 CLS
1820 PRINT@48,"Results of race #";RA
1830 PRINT TAB(5)"Horse"TAB(18)"Position"TAB(27)"Pays"
1840 FOR N=1 TO 4
1850 PRINTTAB(5) HN$(N);TAB(18)WN$(W(N));TAB(27)WM$(W(N))
1860 NEXT N
1865 LINE(22,5)-(220,57),1,B
1880 RETURN
1999 REM Accumulates bets
2000 FOR N=1 TO PL
2010 IF HN(N)=0 THEN 2060
2020 IF BT(N)>W(HN(N)) THEN BR(N)=BR(N)-BA(N): GOTO 2060
2030 IF BT(N)=3 THEN BR(N)=BR(N)+BA(N)*2
2040 IF BT(N)=2 THEN BR(N)=BR(N)+BA(N)*1.5
2050 IF BT(N)=1 THEN BR(N)=BR(N)+BA(N)*1.2
2060 NEXT N
2099 REM Displays earnings
2100 CLS
2110 PRINT@48,"Accumulated earnings"
2120 PRINT TAB(5)"Player";TAB(20)"Bankroll"
2130 FOR N=1 TO PL
2140 PRINT TAB(5)NM$(N);TAB(20)BR(N)
2150 NEXT N
2160 LINE(22,5)-(220,57),1,B
2170 GOSUB 730
2180 RETURN
2500 REM bugle call
2510 SOUND6269,7:SOUND4697,7:SOUND3728,7:SOUND3134,25:SOUND3134,7:SOUND3134,7:SOUND3134,7:SOUND3728,25:SOUND3728,7:SOUND3728,7
2520 SOUND3728,7:SOUND4697,25:SOUND3728,12:SOUND4697,12:SOUND6269,50:SOUND6269,7:SOUND4697,7:SOUND3728,7:SOUND3134,25:SOUND3134,7
2530 SOUND3134,7:SOUND3134,7:SOUND3728,25:SOUND3728,7:SOUND3728,7:SOUND3728,7:SOUND4697,25:SOUND6269,18:SOUND6269,18:SOUND4697,75
2540 RETURN
2599 REM you're in the money
2600 SOUND7456,25:SOUND6269,25:SOUND7456,6:SOUND7032,12:SOUND6269,50
2610 RETURN
2620 SOUND3728,25:SOUND3728,25:SOUND4697,8:SOUND4184,12:SOUND4697,12:SOUND4184,12:SOUND4697,12:SOUND3728,12:SOUND4697,25:SOUND4184,12
2630 SOUND4697,37
2640 RETURN
10000 CLS:PRINT"Instructions for Horse Race."
10010 PRINT"Select number of players (1-4)"
10020 PRINT"Enter each name when requested."
10030 PRINT"The betting will then start."
10040 PRINT"Select a horse.  If you decide to pass"
10050 PRINT"Select 0- no bet."
10060 PRINT"Then select <W>in, <P>lace, or <S>how."
10070 GOSUB 700
10080 CLS:PRINT"You start with a bankroll of $100.00"
10090 PRINT"Bet what you want. Up to your limit."
10100 PRINT"Win  bet pays 2-1, Place 1.5 to 1"
10110 PRINT"and Show 1.2 to 1."
10120 PRINT"The race will then automatically start."
10130 PRINT"At the end winners will be shown"
10135 GOSUB 700
10140 CLS:PRINT"and bets calculated.  The tote board"
10150 PRINT"will show the placing of the horses"
10160 PRINT"and the subsequent listing will show"
10170 PRINT"how you are doing on your betting."
10180 PRINT"Decide if you want to race again."
10190 GOSUB 700:CLS:RETURN
