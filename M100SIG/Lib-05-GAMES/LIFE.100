0 ' Written by John Mifsud [70067,501]
10 '12/16/83.  Modified version of Life from Creative Computing.
20 'Instructions are in LIFE.DOC
30 'CONTACT ME VIA EMAIL FOR ANY INFO OR BUGS.
100 CLEAR
110 CLS
120 PRINT@(18),"LIFE.100
130 PRINT
140 I9=2
150 INPUT"Want a printout";J$
160 IFLEFT$(J$,1)="y"ORLEFT$(J$,1)="Y"THENX9=1ELSEX9=0
170 X1=1:Y1=1:X2=24:Y2=70
180 DIMA(24,70),B$(24)
190 C=1
191 PRINT:PRINTTAB(10);"Enter d or D when finished."
195 PRINT:PRINT"Enter your Configuration
200 LINEINPUT"?";B$(C)
210 IFLEFT$(B$(C),1)="D"ORLEFT$(B$(C),1)="d"THENB$(C)="":GOTO240
220 C=C+1
230 GOTO200
240 C=C-1:L=0
250 FORX=1TOC-1
260 IFLEN(B$(X))>LTHENL=LEN(B$(X))
270 NEXTX
280 X1=11-C/2
290 Y1=33-L/2
300 FORX=1TOC
310 FORY=1TOLEN(B$(X))
320 IFMID$(B$(X),Y,1)<>" "THENA(X1+X,Y1+Y)=1:P=P+1
330 NEXTY
340 NEXTX
350 GOSUB780
360 GOSUB820:IFP=0THENGOSUB900
370 X3=24:Y3=70:X4=1:Y4=1:P=0
380 G=G+1
390 FORX=1TOX1-1:GOSUB780:NEXTX
400 FORX=X1TOX2
410 GOSUB780
420 FORY=Y1TOY2
430 IFA(X,Y)=2THENA(X,Y)=0:GOTO510
440 IFA(X,Y)=3THENA(X,Y)=1:GOTO460
450 IFA(X,Y)<>1THEN510
460 GOSUB860
470 IFX<X3THENX3=X
480 IFX>X4THENX4=X
490 IFY<Y3THENY3=Y
500 IFY>Y4THENY4=Y
510 NEXTY
520 NEXTX
530 FORX=X2+1TO24:GOSUB780:NEXTX
540 X1=X3:X2=X4:Y1=Y3:Y2=Y4
550 IFX1<3THENX1=3:I9=-1
560 IFX2>22THENX2=22:I9=-1
570 IFY1<3THENY1=3:I9=-1
580 IFY2>68THENY2=68:I9=-1
590 P=0
600 FORX=X1-1TOX2+1
610 FORY=Y1-1TOY2+1
620 C=0
630 FORI=X-1TOX+1
640 FORJ=Y-1TOY+1
650 IFA(I,J)=1ORA(I,J)=2THENC=C+1
660 NEXTJ
670 NEXTI
680 IFA(X,Y)=0THEN720
690 IFC<3ORC>4THENA(X,Y)=2:GOTO710
700 P=P+1
710 GOTO730
720 IFC=3THENA(X,Y)=3:P=P+1
730 NEXTY
740 NEXTX
750 X1=X1-1:Y1=Y1-1:X2=X2+1:Y2=Y2+1
760 GOTO360
770 END
780 'print
790 PRINT
800 IFX9=1THENLPRINT
810 RETURN
820 'update report
830 PRINT"Generation:";G,"Population:";P;
840 IFX9=1THENLPRINT"Generation:";G,"Population:";P;
850 RETURN
860 'print *
870 PRINTTAB(Y-16);"*";
880 IFX9=1THENLPRINTTAB(Y);"*";
890 RETURN
900 'dead population
910 PRINT:PRINT"Colony Termination in";G;"Generations.":SOUND1000,20:SOUND16000,25
920 IFX9=0THENEND
930 LPRINT:LPRINTTAB(20),"Colony Termination in";G;"Generations."
940 END

