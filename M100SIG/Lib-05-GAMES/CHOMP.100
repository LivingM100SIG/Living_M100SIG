
1 REM COOKIE MONSTER 100 PROGRAM
2 REM ADAPTED FROM CHOMP FOR THE
3 REM R/S 100 BY CHRIS MORGAN 70076,150
5 CLS:PRINT:PRINT:PRINT:PRINT"DO YOU WANT INSTRUCTIONS? "
6 A$=INKEY$:IFA$=""THENGOTO6
7 IFA$="Y"THENGOSUB5500
8 CLS:PRINT:PRINT:PRINT
10 DIMA(13),B(13)
20 INPUT"WHO IS PLAYING";P$
25 CLS:PRINT:PRINT:PRINT
30 PRINT"HOW MANY ROWS (FROM 3 TO 6)";
32 R1$=INKEY$:IFR1$=""THEN32
34 R1=VAL(R1$):PRINTR1
40 IF R1<3 OR R1>6 THEN 30
50 PRINT"HOW MANY COLUMNS (FROM 3 TO 9)";
52 C1$=INKEY$:IFC1$=""THEN52
54 C1=VAL(C1$):PRINTC1
60 IF C1<3ORC1>9 THEN50
70 IF R1<>C1THEN110
80 IFC1=13THENC1=12ELSEC1=C1+1
85 CLS
90 PRINT:PRINT"SQUARES ARE TRIVIAL;":PRINT"LETS MAKE IT ";C1;" COLUMNS,"
100 PRINT"JUST TO BE INTERESTING. ":FORI=0TO800:NEXTI:PRINT
110 CLS
115 FORI=1TOR1:A(I)=C1:NEXTI:FORI=1TOC1:B(I)=R1
120 NEXTI:GOTO150
130 FORI=R2TOR1:IFA(I)>=C2THENA(I)=C2-1:NEXTI
140 FORI=C2TOC1:IFB(I)>=R2THENB(I)=R2-1:NEXTI
145 CLS
150 PRINT"  ";
160 FORI=1TOA(1)
170 PRINTI;
190 NEXT
200 PRINT
210 PRINT" 1 P";
220 IFA(1)<2THEN240
230 FORI=2TOA(1):PRINT"  *";:NEXT
240 PRINT
250 FORI=2TOR1:IFA(I)=0THEN290
270 PRINTI;"*";
275 IFA(I)=1THENPRINT:GOTO290
280 FORJ=2TOA(I):PRINT"  *";:NEXTJ:PRINT
290 NEXTI:IFF1=1THEN440
300 PRINT"COORDINATES OF CHOMP (ROW, COLUMN)?";
320 R2$=INKEY$:IFR2$=""THEN320ELSER2=VAL(R2$)
325 SOUND1000,1
330 PRINTR2;
340 C2$=INKEY$:IFC2$=""THEN340ELSEC2=VAL(C2$)
345 SOUND900,1
350 PRINTC2
360 IFP$="C"THEN380
380 IFR2<1ORC2<1ORR2>R1ORC2>C1THEN390ELSE400
390 PRINT"THERE NEVER WAS ANY COOKIE THERE!":GOSUB5000:GOTO300
400 IFA(R2)<C2ORB(C2)<R2THEN410ELSE420
410 PRINT"NO FAIR. THAT'S ALREADY BEEN CHOMPED!":GOSUB5000:GOTO300
420 IFR2=1ANDC2=1THEN790
430 F1=1:GOTO130
440 FORI=1TO500:NEXTI
450 IFA(2)>1THEN530
460 IFA(1)<>B(1)THEN500
470 K=K+1:IFK>2THEN900:R0=RND(1):IFR0<.5THEN490
480 R2=1:C2=B(1):GOTO760
490 C2=1:R2=A(1):GOTO760
500 IFA(1)>B(1)THEN520
510 C2=1:R2=A(1)+1:GOTO760
520 R2=1:C2=B(1)+1:GOTO760
530 IFA(1)<>B(1)THEN550
540 C2=2:R2=2:GOTO760
550 IFA(3)>0THEN570
560 X1=0:IFA(1)-1<>A(2)THEN640
570 IFB(3)>0THEN700
580 X1=1:IFB(1)-1<>B(2)THEN670
590 R0=RND(0):IFR0<.5THEN620
600 IFX1=1THENC2=2ELSER2=2
610 IFX1=1THENR2=B(2)ELSEC2=A(2):GOTO760
620 IFX1=1THENC2=1ELSER2=1
630 IFX1=1THENR2=B(1)ELSEC2=A(1):GOTO760
640 IFA(1)-1>A(2)THEN680
650 IFX1=1THENC2=2ELSER2=2
660 IFX1=1THENR2=B(1)ELSEC2=A(1):GOTO760
670 IFB(1)-1<B(2)THEN750
680 IFX1=1THENC2=1ELSER2=1
690 IFX1=1THENR2=B(2)+2 ELSEC2=A(2)+2:GOTO760
700 IFB(1)>3THENJ1=2ELSEJ1=0
710 R2=INT((B(1)-J1)*RND(1))+J1+1
720 IFA(R2)>3THENI1=2ELSEI1=0
730 C2=INT((A(R2)-I1)*(RND(0)))+I1+1
740 IFR2=1ANDC2=B(1)+1THEN710
750 IFC2=1ANDR2=A(1)+1THEN710
760 PRINT:PRINT"THE COOKIE MONSTER 100 CHOMPS ";R2;",";:SOUND900,1:PRINTC2:SOUND1000,1
770 F1=0:IFR2<>1ORC2<>1THEN130
775 FORX=1TO750:NEXT
780 CLS:PRINT:PRINT"YOU WIN!!!":SOUND4697,10:SOUND3516,10:SOUND2793,10:SOUND2348,15:SOUND2793,5:SOUND2348,10:F1=1:W=W+1:GOTO800
790 CLS:PRINT:PRINT"YOU CHOMPED THE POISON COOKIE!!"
795 PRINT"           YOU LOSE!!":L=L+1:GOSUB5000
800 CLS:PRINT:PRINT
802 IFW=LTHENS$="EVEN"
803 IFW>LTHENS$="AHEAD"
804 IFW<LTHENS$="BEHIND"
806 PRINT"YOU ARE ";S$;" ";W;" TO ";L:PRINT
808 PRINT:PRINT"CARE FOR ANOTHER GAME?":PRINT:IFW>LTHENS$="AHEAD"
810 A1$=INKEY$:IFA1$=""THEN810
820 IFA1$="Y"THEN25ELSE830
830 CLS:PRINT:PRINT:PRINT"  YOU PLAYED ";W+L;" GAMES ";P$
840 PRINT:PRINT"             WON ";W
850 PRINT  "            LOST ";L
855 FORX=1TO1000:NEXT:CLS
860 IFL>WTHEN870ELSE910
870 FORX=1TO3:PRINT:NEXTX
880 PRINT"  YOU ATE ";L;" POISON COOKIES!!":PRINT:GOSUB5000: PRINT"     HOW IS YOUR STOMACH?":GOSUB7000
890 FORX=1TO3:PRINT:END
900 PRINT"THE COOKIE MONSTER 100 RESIGNS.":FORX=1TO750:NEXT:GOTO780
910 FORX=1TO4:PRINT:NEXTX
920 IFL=0THENPRINT"      YOU MUST BE A GENIUS!!!!":PRINT:PRINT:PRINT:GOSUB6000:GOTO890
930 L=L+W
940 IFW/L>.9THENPRINT"      YOU MUST BE PRACTICING!!":GOSUB8000:GOTO890
950 IFW/L>.8THENPRINT"      NOT BAD!!":GOSUB8000:GOTO890
960 IFW/L>.7THENPRINT"      GOOD, BUT YOUR SYSTEM NEEDS WORK!":GOSUB8000:GOSUB7000:GOTO890
970 IFW/L>.6THENPRINT"      BETTER THAN AVERAGE!":SOUND4697,50:GOTO890
980 PRINT"      JUST ABOUT BROKE EVEN":SOUND16380,50:GOTO890
5000 FORZ=1TO20:SOUND15000,2:SOUND14500,2:NEXTZ:RETURN
5500 PRINT:PRINT"IN ORDER TO WIN THIS GAME, YOU"
5510 PRINT"MUST MAKE THE COMPUTER EAT THE"
5520 PRINT"POISON COOKIE (P).  WHEN YOU"
5530 PRINT"SELECT YOUR COORDINATES (ROW,"
5540 PRINT"COLUMN), YOU HAVE CONSUMED ALL"
5545 GOSUB5600
5547 PRINT
5550 PRINT"THE COOKIES TO THE RIGHT"
5560 PRINT"AND DOWN FROM THAT POSITION."
5570 PRINT"IF YOU WIN THE GAME, THE COMPUTER"
5580 PRINT"MOVES FIRST ON THE SUBSEQUENT"
5590 PRINT"GAME."
5595 GOSUB5600:PRINT:PRINT"WANT TO SEE THE INSTRUCTIONS AGAIN?"
5596 A$=INKEY$:IFA$=""THEN5596
5597 IFA$="Y"THENGOTO5500ELSEPRINT:PRINT:PRINT"   GOOD LUCK !!!!":PRINT:PRINT:RETURN
5600 FORX=1TO3000:NEXT:RETURN
6000 FORZ=1TO2
6010 FOR X=2000TO1000STEP-15
6020 SOUNDX,1
6030 NEXT
6040 FOR X=1000TO2000STEP15
6050 SOUNDX,1
6060 NEXT
6070 NEXT Z
6080 RETURN
7000 SOUND9394,30:SOUND9394,10:SOUND7032,40:SOUND0,10:SOUND9394,30:SOUND7032,5:SOUND5586,40:RETURN
8000 FORZ=1TO20: SOUND9394,1:SOUND4697,1:NEXTZ:RETURN
