1 REM WATPLT DUAL CURVE PLOTTING V1.1 FOR WATOR DATA 6/3/85(CIS)
2 REM JAY HOLOVACS 74756,413
3 CLS:PRINT:PRINT " * * * WATOR CENSUS PLOT * * *"
4 PRINT:PRINT"After viewing, hit any key to EXIT":PRINT
5 INPUT "NO OF CHRONONS COVERED BY THIS CENSUS   FILE";CH%
6 DEFSNG X,Y:DIM X%(CH%),Y%(1,CH%)
20 CLS:XN=1E6:XX=-1E6:YN=1E6:YX=-1E6
30 GOSUB 700 'READ FILE
58 YI=64/(YX-YN)*.98:XI=240/(XX-XN)*.99
59 CLS
60 GOSUB 800
61 GOSUB 850
62  FOR Q=1 TO N:X%(Q)=INT((X%(Q)-XN)*XI):NEXT Q ' X-PLOT COORDINATES
63 FOR Z=0 TO 1:FOR Q=1 TO N:Y%(Z,Q)=INT((YX-Y%(Z,Q))*YI):GOSUB 300:NEXT Q,Z 'PLOT BOTH SETS OF Y COORDINATES
110 FOR Z=0 TO 1:FOR Q=2 TO N:LINE(X%(Q),Y%(Z,Q))-(X%(Q-1),Y%(Z,Q-1)):NEXT Q,Z
120 R$=INKEY$:IF R$="" THEN 120 ELSE MENU
300 REM PLOT INDIVIDUAL POINTS
315 PSET(X%(Q),Y%(Z,Q)):RETURN 
500 REM CHECK FOR MAX &MIN X,Y
503 IF X%(Q)<XN THEN XN=X%(Q)
504 IF X%(Q)>XX THEN XX=X%(Q)
505 IF Y%(Z,Q)<YN THEN YN=Y%(Z,Q)
506 IF Y%(Z,Q)>YX THEN YX=Y%(Z,Q)
510 RETURN
700 REM FILE ENTRY
705 CLS:PRINT "FOR WATOR FILES IN THE FORMAT X,Y1,Y2..":FILES
710 INPUT "NAME OF CENSUS FILE";F$:OPEN F$ FOR INPUT AS 1:Q=0:PRINT "PLEASE WAIT..."
715 Q=Q+1:INPUT #1,X%(Q):FOR Z=0 TO 1:INPUT #1,Y%(Z,Q):GOSUB 500
716 NEXT Z:IF EOF(1) OR Q=CH% THEN 720
718 GOTO 715 
720 N=Q:CLOSE:RETURN 
800 REM MAX/MIN Y VALUES
810 PRINT @18,YX;CHR$(152);:PRINT @298,YN;CHR$(153);
820 RETURN
850 REM MAX/MIN X VALUES
860 PRINT @120,CHR$(155);XN;
870 XX$=STR$(XX):HX=159-LEN(XX$):PRINT @HX,XX$;CHR$(154);
880 RETURN

