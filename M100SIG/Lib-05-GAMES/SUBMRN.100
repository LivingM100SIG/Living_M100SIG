10 CLEAR300
20 ONERRORGOTO510
30 CLS:PRINT@84,CHR$(235):PRINT@123,STRING$(2,239):PRINT@161,CHR$(252)+STRING$(6,239)+CHR$(254)
40 PRINT@201,CHR$(253)+STRING$(6,239)+CHR$(251)
50 FORZ=1TO300:NEXT
60 T$="     SINK THE SUBMARINE"
70 FORX=1TO2
80 FORZ=209TO238
90 PRINT@Z," "+CHR$(232)
100 PRINT@Z,MID$(T$,Z-208,1)
110 NEXTZ:PRINT@238,"  ":FORY=1TO300:NEXTY
120 T$="  by Joe Wasserman 73117,3600"
130 NEXTX:CLS
140 PRINT"There is an enemy sub within 50 to 200  miles of your ship.":PRINT
150 PRINT"You have 12 missiles to destroy it but  your sonar is being jammed. It can tell by how much you missed the sub ";
160 PRINT"but not  the direction.":PRINT@290,"Any Key to continue";
170 IFINKEY$=""THEN170
180 CLS:PRINT"You must detonate within 3 miles of the sub to destroy it.";
190 PRINT" Enter RANGE and ANGLEand your computer will keep you posted  of the co-ordinates of your closest hit."
200 PRINT@290,"Any Key to Continue";
210 IFINKEY$=""THEN210
220 CLS
230 LINE(77,20)-(120,63):LINE(120,3)-(120,63):LINE(162,21)-(120,63)
240 PRINT@289,"0";:PRINT@91,"45":PRINT@108,"135":PRINT@311,"180";
250 PRINT@0,"RANGE":PRINT@25,"ANGLE":PRINT@120,"Miss>":PRINT@200,"closest":PRINT@281,"r   a";
260 RESTORE:FORZ=1TO37:READX1,Y1:PSET(X1,Y1):NEXT
270 IFMAXRAM=61104THENPOKE63277,PEEK(61983)ELSEPOKE64634,PEEK(63791)
280 FORZ=1TOVAL(RIGHT$(TIME$,2)):X=RND(1):NEXT
290 R1=INT(RND(1)*151+50):A1=INT(RND(1)*180+1)
300 CL=400:FORU=1TO12:PRINT@191,USING"READY ##";U
310 PRINT@5,"";:INPUTR:IFR<50ORR>200THENPRINT@6,"     ":GOTO310
320 PRINT@30,"";:INPUTA:IFA<0ORA>180THENPRINT@31,"     ":GOTO320
330 X=R*.3*COS(A*.0174533):Y=R*.3*SIN(A*.0174533)
340 LINE(119-X,63-Y)-(121-X,63-Y):LINE(120-X,63-Y)-(120-X,62-Y)
350 PRINT@191,CHR$(27)"p"" FIRE "CHR$(27)"q":FORZ=4000TO9000STEP100:SOUNDZ,1:NEXTZ
360 D=SQR(R*R+R1*R1-(2*R*R1*COS((A-A1)*.0174533))):IFD<CLTHENCL=D:R2=R:A2=A
370 PRINT@125,USING"###.##";D:PRINT@240,USING"### ###";R2;A2:IFD<=3THEN420
380 PRINT@5,"     ":PRINT@30,"      "
390 NEXTU
400 PRINT@0,CHR$(27)"p"" MISSILES ":PRINT@25," EXHAUSTED ":GOSUB530:PRINT@0,"SUB AT"R1"MILES":PRINT@25,A1"DEGREES"CHR$(27)"q"
410 FORZ=1TO2E3:NEXT:GOTO220
420 IFY<2THENY=2
430 FORZ=1TO15
440 FORW=1TO2
450 LINE(119-X,62-Y)-(121-X,64-Y),W,B:LINE(118-X,61-Y)-(122-X,65-Y),W,B
460 NEXTW:SOUND4184,3
470 NEXTZ
480 PRINT@0,CHR$(27)"p"" SUB DESTROYED AT"R1"MILES"A1"DEGREES"CHR$(27)"q":FORZ=1TO1500:NEXT:GOTO220
490 DATA60,63,60,57,60,52,62,47,63,42,65,37,68,32,70,28,74,24,77,20,81,17,85,13,90,11,94,8,99,6,104,5,109,3,114,3,120,3,125,3
500 DATA130,3,135,5,140,6,145,8,150,11,154,13,158,17,162,20,165,24,169,28,171,33,174,37,176,42,177,47,180,52,180,57,180,63
510 IFERL=350THENRESUMENEXT
520 PRINTERL;ERR:STOP
530 X=R1*.3*COS(A1*.0174533):Y=R1*.3*SIN(A1*.0174533):Y=Y-1*(Y<=0)
540 FORZ=1TO10
550 FORV=1TO2
560 LINE(119-X,62-Y)-(121-X,64-Y),V,B
570 FORW=1TO35:NEXTW
580 NEXTV:SOUND4184,2
590 NEXTZ:PSET(120-X,63-Y)
600 RETURN

