5 GOTO200
10 CLS:LINE(41,15)-(96,50),1,B
11 LINE(28,2)-(41,15):LINE(96,15)-(109,2):LINE(28,63)-(41,50):LINE(96,50)-(109,63)
12 LINE(23,2)-(23,63):LINE(114,2)-(114,63):LINE(22,2)-(22,63):LINE(115,2)-(115,63):RETURN
15 X1=61:Y1=7:X2=75
16 FORK=0TO5:LINE(X1,Y1-K)-(X2,Y1-K):X1=X1-1:X2=X2+1:NEXT
17 LINE(62,6)-(74,3),2,BF:RETURN
20 X1=61:Y1=58:X2=75
21 FORK=0TO5:LINE(X1,Y1+K)-(X2,Y1+K):X1=X1-1:X2=X2+1:NEXT
22 LINE(62,59)-(74,62),2,BF:RETURN
25 LINE(109,15)-(109,63):LINE(108,16)-(104,20)
26 LINE(104,21)-(108,58),1,BF:LINE(105,59)-(108,62),2:RETURN
30 LINE(28,15)-(28,63):LINE(29,16)-(33,20)
31 LINE(29,21)-(33,58),1,BF:LINE(29,62)-(32,59),2:RETURN
35 LINE(60,50)-(76,24),1,BF
36 X1=64:X2=72:FORY1=46TO50:LINE(X1,Y1)-(X2,Y1),2:X1=X1-1:X2=X2+1:NEXT
37 LINE(61,25)-(63,27),2:LINE(75,25)-(73,27),2:LINE(64,46)-(72,28),2,BF:RETURN
40 RETURN
45 W=6*E+30*NS+150*U+I:RETURN
50 ?@222,"Sorry, you can't":?@262,"go that way!    "
51 FORN=1TO1000:NEXT
52 ?@222,K$:?@262,K$:RETURN
60 ONFGOSUB62,63,64,65
61 ?@62,"Facing: "P$D$Q$:RETURN
62 D$=" North ":RETURN
63 D$=" South ":RETURN
64 D$=" East ":RETURN
65 D$=" West ":RETURN
70 ?@142,"N S E W U D ? * F":RETURN
80 ?@222,"Position: ";STR$(E+1);STR$(NS+1);STR$(U+1):RETURN
85 ?@262,"Exit:     ";STR$(EE+1);STR$(EN+1);STR$(EU+1):RETURN
90 ?@62,"Face which way?":GOSUB70
91 A$=INKEY$:IFA$=""THEN91
92 IFA$="N"ORA$="n"THENF=1:RETURN
93 IFA$="S"ORA$="s"THENF=2:RETURN
94 IFA$="E"ORA$="e"THENF=3:RETURN
95 IFA$="W"ORA$="w"THENF=4:RETURN
96 IFA$=CHR$(13)THENRETURN
97 BEEP:GOTO91
100 GOSUB10
104 FORN=1TO6:I=N:GOSUB45
108 IFMZ(W)<>1THEN112
110 J=FC(F,N):ONJGOSUB15,20,25,30,35,40
112 NEXT
114 GOSUB60:GOSUB70:D=0
115 IFPTHENGOSUB80
116 IFEXTHENGOSUB85
117 A$=INKEY$:IFA$=""THEN117
118 IFA$="U"ORA$="u"THEND=1:GOTO142
120 IFA$="D"ORA$="d"THEND=2:GOTO142
122 IFA$="E"ORA$="e"THEND=3:GOTO142
124 IFA$="W"ORA$="w"THEND=4:GOTO142
126 IFA$="N"ORA$="n"THEND=5:GOTO142
128 IFA$="S"ORA$="s"THEND=6:GOTO142
130 IFA$="*"ANDP=1THENP=0:?@222,K$:GOTO117
132 IFA$="?"ANDEX=1THENEX=0:?@262,K$:GOTO117
134 IFA$="*"THENP=1:GOSUB80:D=0:A$="":GOTO117
136 IFA$="?"THENEX=1:GOSUB85:D=0:A$="":GOTO117
138 IFA$="F"ORA$="f"THENGOSUB90:GOTO100
140 BEEP:GOTO117
142 I=D:GOSUB45
144 IFMZ(W)=0THENBEEP:GOSUB50:GOSUB70:GOTO115
146 ONDGOTO148,150,152,154,156,158
148 U=U+1:GOTO160
150 U=U-1:GOTO160
152 E=E+1:GOTO160
154 E=E-1:GOTO160
156 NS=NS+1:GOTO160
158 NS=NS-1
160 IFU<0ORU>4ORE<0ORE>4ORNS<0ORNS>4THEN300
162 GOTO100
200 DEFINTA-Z:DIMMZ(750),FC(4,6):C$=CHR$(27):P$=C$+"p":Q$=C$+"q":K$=C$+"K":P=0:EX=0:KEYOFF
202 GOSUB10:GOSUB15:GOSUB20:GOSUB25:GOSUB30:GOSUB35
203 KEY(4)ON:KEY(8)ON:ONKEYGOSUB,,,316,,,,315
204 ?@65,P$" MAZE 3D "Q$
206 ?@143,"Initializing...":?@223,"Timing starts":?@263,"at the BEEP!";
208 FORN=1TO4:FORI=1TO6:READFC(N,I):NEXT:NEXT
210 FORN=1TOVAL(RIGHT$(TIME$,2)):R=RND(1):NEXT
216 E=0
218 NS=0
220 U=0
222 IFRND(1)<0.7ANDU<4THENI=1:GOSUB45:MZ(W)=1
224 IFRND(1)<0.7ANDE<4THENI=3:GOSUB45:MZ(W)=1
226 IFRND(1)<0.7ANDNS<4THENI=5:GOSUB45:MZ(W)=1
228 IFU>0THENU=U-1:I=1:GOSUB45:U=U+1:IFMZ(W)=1THENI=2:GOSUB45:MZ(W)=1
230 IFE>0THENE=E-1:I=3:GOSUB45:E=E+1:IFMZ(W)=1THENI=4:GOSUB45:MZ(W)=1
232 IFNS>0THENNS=NS-1:I=5:GOSUB45:NS=NS+1:IFMZ(W)=1THENI=6:GOSUB45:MZ(W)=1
234 U=U+1:IFU<5THEN222
236 NS=NS+1:IFNS<5THEN220
238 E=E+1:IFE<5THEN218
240 E=RND(1)*5:NS=RND(1)*5:U=RND(1)*5
246 I=RND(1)*6+1:ONIGOTO248,250,252,254,256,258
248 U=4:GOTO260
250 U=0:GOTO260
252 E=4:GOTO260
254 E=0:GOTO260
256 NS=4:GOTO260
258 NS=0
260 GOSUB45:MZ(W)=1:EE=E:EN=NS:EU=U
262 E=RND(1)*5:NS=RND(1)*5:U=RND(1)*5
268 T1$=TIME$:BEEP:F=1:GOTO100
300 CLS:T2$=TIME$
302 S1=VAL(RIGHT$(T1$,2)):M1=VAL(MID$(T1$,4,2)):S2=VAL(RIGHT$(T2$,2)):M2=VAL(MID$(T2$,4,2))
303 IFS2<S1THENS2=S2+60
304 IFM2<M1THENM2=M2+60
305 M$=STR$(M2-M1):S$=STR$(S2-S1):IF(S2-S1)<10THENS$="0"+RIGHT$(S$,1)ELSES$=RIGHT$(S$,2)
306 ?:?:?" YOU WIN!! "
307 ?:?" Time = ";M$;":";S$
312 ?:?" Play again? (Y/N)"
313 A$=INKEY$:IFA$=""THEN313
314 IFA$="Y"ORA$="y"THENRUN
315 MENU
316 RUN
400 DATA1,2,3,4,5,6,1,2,4,3,6,5,1,2,5,6,4,3,1,2,6,5,3,4
