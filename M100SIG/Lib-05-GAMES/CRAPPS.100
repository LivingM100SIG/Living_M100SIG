0 GOTO263:REM CRAPPS.100 by Don Zeikel [CIS 75775,1430] v. 1.2 copyright 1986, all rights reserved
1 A$=INKEY$:IFA$=""THENFORI=1TO11:E=RND(1):NEXT:GOTO1ELSEIFA$=CHR$(27)THENGOSUB2:EN=1:GOTO43ELSERETURN
2 FORI=27TO277STEP40:PRINT@I,SPACE$(13);:NEXT:PRINT@307,SPACE$(12);:RETURN
3 XX%=R%:YY%=0:R1%=XX%/2
4 YY%=YY%+1:R1%=R1%-YY%:IFR1%<0THENXX%=XX%-1:R1%=R1%+XX%
5 PSET(XX%+XC%,YY%+YC%):PSET(XC%+YY%,YC%+XX%):PSET(XC%-YY%,YC%+XX%):PSET(XC%-XX%,YC%+YY%):PSET(XC%-XX%,YC%-YY%):PSET(XC%-YY%,YC%-XX%):PSET(XC%+YY%,YC%-XX%):PSET(XC%+XX%,YC%-YY%):IFXX%>YY%THEN4
6 RETURN
7 IFERR=52THENCLS:BEEP:PRINT@42,"INSTALL "CHR$(34)"CRAPS!.CO"CHR$(34)" AND TRY AGAIN!":ENDELSECLS:PRINT@42,"ERROR"ERR"IN LINE"ERL;:END
8 ONERRORGOTO7:XX%=R%:YY%=0:R1%=XX%/2
9 YY%=YY%+1:R1%=R1%-YY%:IFR1%<0THENXX%=XX%-1:R1%=R1%+XX%
10 PRESET(XX%+XC%,YY%+YC%):PRESET(XC%+YY%,YC%+XX%):PRESET(XC%-YY%,YC%+XX%):PRESET(XC%-XX%,YC%+YY%):PRESET(XC%-XX%,YC%-YY%):PRESET(XC%-YY%,YC%-XX%):PRESET(XC%+YY%,YC%-XX%):PRESET(XC%+XX%,YC%-YY%):IFXX%>YY%THEN9
11 RETURN
12 GOSUB2:PRINT@27,"YOU BET":PRINT@67,"$";V:RETURN
13 FORR%=1TO3:GOSUB3:NEXT:FORR%=1TO3:GOSUB8:NEXT
14 IFBTHENFORR%=1TO3:GOSUB3:NEXTELSEFORR%=1TO3:GOSUB8:NEXT
15 IFBTHEN16ELSEFORI=XC%-3TOXC%+3:PRESET(I,YC%):NEXT:FORI=YC%-3TOYC%+3:PRESET(XC%,I):NEXT:RETURN
16 FORI=XC%-3TOXC%+3:PSET(I,YC%):NEXT:FORI=YC%-3TOYC%+3:PSET(XC%,I):NEXT:RETURN
17 BEEP:PRINT@L,SPACE$(5);:PRINT@L,"";
18 V=0:NB=0:X$="":LINEINPUTX$:IFX$="0"THENNB=1:RETURNELSEV=VAL(X$):IFV<5ORV<>INT(V)THEN17
19 IFMTHEN20ELSE21
20 IFVMODM<>0THEN17ELSE21
21 C(WA)=C(WA)+V:MB=MB-V:GOSUB12:RETURN
22 PRINT@307,P$"ENTER"Q$;:RETURN
23 IFNN=0THENA$="":FORI=1TO650:NEXT:RETURNELSEPRINT@307,P$"ENTER"Q$;
24 GOSUB1:IFA$=CHR$(13)THENRETURNELSE24
25 GOSUB2:GOSUB39:RETURN
26 GOSUB13:GOSUB23:RETURN
27 GOSUB74:GOSUB18:GOSUB32:RETURN
28 GOSUB85:GOSUB84:RETURN
29 GOSUB85:GOSUB90:RETURN
30 GOSUB105:GOSUB26:RETURN
31 GOSUB114:GOSUB26:RETURN
32 IFNB=1THENNB=0:GOTO64ELSERETURN
33 PRINT@27,"NUMBER":PRINT@67,"OF CHIPS":PRINT@107,"TO BET ON":RETURN
34 PRINT@27,"CRAPS!":RETURN
35 PRINT@27,"YOU MADE":PRINT@67,"YOUR POINT":RETURN
36 PRINT@27,C$" BET":RETURN
37 PRINT@27,D$;C$:RETURN
38 PRINT@67,W$:RETURN:IFMTHENMI=MELSEMI=5
39 IFMTHENMI=MELSEMI=5
40 PRINT@307,"";:PRINTUSING"$$#";MI;:PRINT" MINIMUM";:RETURN
41 FORI=1TOINT(((VAL(RIGHT$(TIME$,2))))*((VAL(MID$(TIME$,4,2)))/2)*(VAL(MID$(DATE$,4,2)))/100):I3=RND(1):NEXT:CALLCA:GOTO46
42 GOSUB2:IFRO=1THENU$=""ELSEU$="S"
43 IFMB=0THENTV$="EVEN"ELSEIFMB>0THENTV$="AHEAD"ELSEIFMB<0THENTV$="BEHIND
44 PRINT@27,"AFTER"RO:PRINT@67,"ROLL"U$:PRINT@107,"YOU ARE":PRINT@147,TV$:IFMB=0THEN45ELSEPRINT@187,"$";ABS(MB);
45 IFEN=1THENNN=0:GOSUB23:CLEAR0,MAXRAM:MENUELSEGOSUB23
46 IFP>0THEN52ELSEIFC(0)>0ORC(1)>0THEN49
47 GOSUB2:PRINT@27,P$"P"Q$"ASS":PRINT@67,P$"D"Q$"ON'T "S$:PRINT@107,P$"S"Q$"IDE BETS";:PRINT@187,S$" OR":PRINT@227,D$;S$:PRINT@267,"REQUIRED":PRINT@307,"BEFORE ROLL";
48 GOSUB1:ONINSTR(1,"PpDdSs",A$)GOTO54,54,56,56,58,58:BEEP:GOTO48
49 GOSUB2:PRINT@27,P$"S"Q$"IDE BETS":PRINT@107,P$"R"Q$"OLL";:PRINT@147,"";
50 GOSUB1:ONINSTR(1,"SsRr",A$)GOTO58,58,116,116ELSEBEEP:GOTO51
51 BEEP:GOTO50
52 SP=0:GOSUB2:PRINT@27,P$"C"Q$"OME":PRINT@67,P$"D"Q$"ON'T "C$:PRINT@107,P$"S"Q$"IDE BETS":PRINT@187,P$"R"Q$"OLL":PRINT@227,"";
53 GOSUB1:ONINSTR(1,"CcDdSsRr",A$)GOTO97,97,106,106,58,58,116,116:BEEP:GOTO53
54 M=0:GOSUB25:GOSUB33:PRINT@147,S$"?":PRINT@187,"";:WA=0:L=187:GOSUB18:IFNB=1THENNB=0:GOTO46ELSEXC%=88:YC%=59:B=1:SP=1:GOSUB26
55 GOTO46
56 M=0:GOSUB25:GOSUB33:PRINT@147,D$:PRINT@187,S$"?":PRINT@227,"";:WA=1:L=227:GOSUB18:IFNB=1THENNB=0:GOTO46ELSEXC%=71:YC%=51:B=1:SP=1:GOSUB26
57 GOTO46
58 M=0:GOSUB2:PRINT@27,P$"P"Q$"LACE BETS":PRINT@67,"P"P$"R"Q$"ROPO-":PRINT@107,"SITION BETS":GOSUB22
59 GOSUB1:ONINSTR(1,"PpRr",A$)GOTO64,64,61,61:IFA$=CHR$(13)THEN46
60 BEEP:GOTO59
61 M=0:GOSUB2:PRINT@27,P$"H"Q$"ARD WAYS":PRINT@67,P$"T"Q$"HREE 14/1":PRINT@107,P$"S"Q$"EVEN 4/1":PRINT@147,P$"E"Q$"LEVEN 14/1":PRINT@187,P$"C"Q$"RAPS 7/1":PRINT@227,"C"P$"R"Q$"APS/11":PRINT@267,P$"F"Q$"IELD 2/1";:GOSUB22
62 GOSUB1:ONINSTR(1,"HhTtSsEeCcRrFf",A$)GOTO75,75,91,91,92,92,93,93,94,94,95,95,96,96:IFA$=CHR$(13)THEN58
63 BEEP:GOTO62
64 M=0:GOSUB2:PRINT@27,"PLACE BETS":PRINT@67,P$"F"Q$"OUR  9-5":PRINT@107,"F"P$"I"Q$"VE  7-5":PRINT@147,P$"S"Q$"IX   7-6":PRINT@187,P$"E"Q$"IGHT 7-6":PRINT@227,P$"N"Q$"INE  7-5":PRINT@267,P$"T"Q$"EN   9-5":GOSUB22
65 GOSUB1:ONINSTR(1,"FfIiSsEeNnTt",A$)GOTO67,67,68,68,69,69,70,70,71,71,72,72:IFA$=CHR$(13)THEN58
66 BEEP:GOTO65
67 HA$="4?":WA=15:M$="5":GOSUB27:XC%=44:GOTO73
68 HA$="5?":WA=16:M$="5":GOSUB27:XC%=54:GOTO73
69 HA$="6?":WA=17:M$="6":GOSUB27:XC%=66:GOTO73
70 HA$="8?":WA=18:M$="6":GOSUB27:XC%=79:GOTO73
71 HA$="9?":WA=19:M$="5":GOSUB27:XC%=91:GOTO73
72 HA$="10?":WA=20:M$="5":GOSUB27:XC%=106:GOTO73
73 YC%=13:B=1:GOSUB26:GOTO64
74 GOSUB2:GOSUB33:PRINT@147,HA$:PRINT@267,"MULTIPLE OF "M$;:M=VAL(M$):L=187:GOSUB39:PRINT@187,"";:RETURN
75 GOSUB2:PRINT@27,"HARD WAYS":PRINT@67,P$"T"Q$"WO 29-1":PRINT@107,P$"F"Q$"OUR 7-1":PRINT@147,P$"S"Q$"IX 9-1":PRINT@187,P$"E"Q$"IGHT 9-1":PRINT@227,"TE"P$"N"Q$" 7-1":PRINT@267,"T"P$"W"Q$"ELVE 29-1";:GOSUB22
76 GOSUB1:ONINSTR(1,"TtFfSsEeNnWw",A$)GOTO78,78,79,79,80,80,81,81,82,82,83,83:IFA$=CHR$(13)THEN61
77 BEEP:GOTO76
78 HA$=" 2?":WA=6:GOSUB28:XC%=10:YC%=27:GOTO88
79 HA$=" 4?":WA=5:GOSUB28:XC%=30:YC%=16:GOTO88
80 HA$=" 6?":WA=2:GOSUB28:XC%=10:YC%=5:GOTO88
81 HA$=" 8?":WA=4:GOSUB28:XC%=10:YC%=16:GOTO88
82 HA$=" 10?":WA=3:GOSUB28:XC%=30:YC%=5:GOTO88
83 HA$=" 12?":WA=7:GOSUB28:XC%=30:YC%=27:GOTO88
84 IFNB=1THENNB=0:GOTO75ELSERETURN
85 IFWA>1ANDWA<8THENH$="HARD"ELSEH$=""
86 GOSUB25
87 GOSUB33:PRINT@147,H$;HA$:PRINT@187,"";:L=187:GOSUB18:RETURN
88 B=0:GOSUB26:GOTO75
89 GOSUB26:GOTO61
90 IFNB=1THENNB=0:GOTO61ELSERETURN
91 HA$="3?":WA=8:GOSUB29:XC%=10:YC%=38:B=0:GOTO89
92 HA$="7?":WA=33:GOSUB29:XC%=46:YC%=29:B=1:GOTO89
93 HA$="11?":WA=9:GOSUB29:XC%=10:YC%=49:B=0:GOTO89
94 HA$="CRAPS?":WA=11:GOSUB29:XC%=14:YC%=59:B=1:GOTO89
95 HA$="CRAPS/11?":H$="":WA=10:M=2:MI=6:GOSUB2:PRINT@267,"MULTIPLE OF 2";:GOSUB40:GOSUB87:GOSUB90:XC%=24:YC%=41:B=1:GOTO89
96 HA$="THE FIELD?":WA=14:GOSUB29:XC%=64:YC%=43:B=1:GOTO89
97 GOSUB25:GOSUB33:PRINT@147,C$"?":PRINT@187,"";:WA=12:L=187:GOSUB18:IFNB=1THENNB=0:GOTO46ELSEXC%=83:YC%=27:B=1:GOSUB26
98 GOTO46
99 XC%=43:GOSUB30:RETURN
100 XC%=55:GOSUB30:RETURN
101 XC%=67:GOSUB30:RETURN
102 XC%=78:GOSUB30:RETURN
103 XC%=92:GOSUB30:RETURN
104 XC%=105:GOSUB30:RETURN
105 GOSUB2:PRINT@27,C$" BET":PRINT@67,"MOVES TO":PRINT@107,XT:C(WA)=C(12):C(12)=0:B=1:YC%=19:RETURN
106 GOSUB25:GOSUB33:PRINT@147,D$:PRINT@187,C$"?":PRINT@227,"";:WA=13:L=227:GOSUB18:IFNB=1THENNB=0:GOTO46ELSEXC%=124:YC%=7:B=1:GOSUB26
107 GOTO46
108 XC%=43:GOSUB31:RETURN
109 XC%=53:GOSUB31:RETURN
110 XC%=65:GOSUB31:RETURN
111 XC%=80:GOSUB31:RETURN
112 XC%=92:GOSUB31:RETURN
113 XC%=105:GOSUB31:RETURN
114 GOSUB2:PRINT@27,D$;C$:PRINT@67,"BET MOVES":PRINT@107,"TO";XT:C(WA)=C(13):C(13)=0:B=1:YC%=6:RETURN
115 GOSUB1:ONINSTR(1,"RrSs",A$)GOTO116,116,58,58:BEEP:GOTO115
116 RO=RO+1:XA=0:XB=0:IFMM=0THEN120
117 GOSUB2:PRINT@27,"MANUAL MODE":PRINT@67,"ENTER TWO":PRINT@107,"DICE NUMBERS"
118 PRINT@147,"";:INPUTXA,XB:IFXA<1ORXA>6ORXB<1ORXB>6THENBEEP:GOTO118
119 GOTO121
120 GOSUB2:XA=INT(RND(1)*6+1):GOSUB133:XB=INT(RND(1)*6)+1:GOSUB133
121 XT=XA+XB:FORND=0TO1STEP1:FORI=123TO143STEP20:LINE(I,30)-(I+11,41),ND,BF:NEXTI:NEXTND:GOSUB132:ND=0:MD=0:ONXAGOSUB122,123,124,125,126,127:MD=20:ONXBGOSUB122,123,124,125,126,127:GOTO134
122 GOSUB128:RETURN
123 GOSUB129:RETURN
124 GOSUB128:GOSUB129:RETURN
125 GOSUB129:GOSUB130:RETURN
126 GOSUB124:GOSUB130:RETURN
127 GOSUB125:GOSUB131:RETURN
128 LINE(128+MD,35)-(129+MD,36),ND,B:RETURN
129 LINE(124+MD,31)-(125+MD,32),ND,B:LINE(132+MD,39)-(133+MD,40),ND,B:RETURN
130 LINE(124+MD,39)-(125+MD,40),ND,B:LINE(132+MD,31)-(133+MD,32),ND,B:RETURN
131 LINE(124+MD,35)-(125+MD,36),ND,B:LINE(132+MD,35)-(133+MD,36),ND,B:RETURN
132 ND=1:FORI=1TO4:MD=0:FORJ=1TO2:ND=ND+1:ONIGOSUB122,124,126,123:NEXT:MD=20:FORJ=1TO2:ND=ND+1:ONIGOSUB127,125,123,122:NEXTJ,I:RETURN
133 FORI=1TO11:E=RND(1):NEXT:RETURN
134 GOSUB23:GOSUB166:FORI=6TO14:C(I)=0:NEXT:C(33)=0:CALLCA:B=1:IFC(0)THENXC%=88:YC%=59:GOSUB14ELSEIFC(1)THENXC%=71:YC%=51:GOSUB14
135 YC%=13:IFC(15)THENXC%=44:GOSUB14
136 IFC(16)THENXC%=54:GOSUB14
137 IFC(17)THENXC%=66:GOSUB14
138 IFC(18)THENXC%=79:GOSUB14
139 IFC(19)THENXC%=91:GOSUB14
140 IFC(20)THENXC%=106:GOSUB14
141 YC%=19:IFC(21)THENXC%=43:GOSUB14
142 IFC(22)THENXC%=55:GOSUB14
143 IFC(23)THENXC%=67:GOSUB14
144 IFC(24)THENXC%=78:GOSUB14
145 IFC(25)THENXC%=92:GOSUB14
146 IFC(26)THENXC%=105:GOSUB14
147 YC%=6:IFC(27)THENXC%=43:GOSUB14
148 IFC(28)THENXC%=53:GOSUB14
149 IFC(29)THENXC%=65:GOSUB14
150 IFC(30)THENXC%=80:GOSUB14
151 IFC(31)THENXC%=92:GOSUB14
152 IFC(32)THENXC%=105:GOSUB14
153 GOSUB268:IFPTHENGOSUB159
154 IFPTHEN52ELSE42
155 C(0)=0:GOSUB2:PRINT@27,"CRAPS!":PRINT@67,S$" BET":PRINT@107,L$:GOSUB23:RETURN
156 WI=2*C(0):MB=MB+WI:C(0)=0:GOSUB2:PRINT@27,"ELEVEN":PRINT@67,G$:PRINT@107,S$" BET PAYS":PRINT@147,"$";WI:GOSUB23:RETURN
157 SP=0:WI=2*C(0):MB=MB+WI:C(0)=0:GOSUB2:PRINT@27,"SEVEN":PRINT@67,G$:PRINT@107,S$" BET PAYS":PRINT@147,"$";WI:GOSUB23:RETURN
158 Q=1:P=XT:GOSUB2:PRINT@27,"YOUR POINT IS":PRINT@67,P
159 O$=STR$(P):O$=RIGHT$(O$,LEN(O$)-1):IFP=4THENR=87ELSEIFP=5THENR=89ELSEIFP=6THENR=91ELSEIFP=8THENR=93ELSEIFP=9THENR=95ELSEIFP=10THENR=97
160 PRINT@R,CHR$(27)"p"O$CHR$(27)"q":IFQ=1THENQ=0:GOSUB23:RETURNELSERETURN
161 WI=2*C(1):MB=MB+2*WI:C(1)=0:GOSUB2:GOSUB34:PRINT@67,D$;S$:PRINT@107,"PAYS":PRINT@147,"$";WI:GOSUB23:RETURN
162 WI=C(1):MB=MB+WI:C(1)=0:GOSUB2:GOSUB34:PRINT@67,"12 "D$;S$:PRINT@107,"A STANDOFF":PRINT@147,"RETURNING":PRINT@187,"$";WI:PRINT@227,"BET":GOSUB23:RETURN
163 MB=MB+C(13):GOSUB2:GOSUB34:PRINT@67,"12 "D$;C$:PRINT@107,"A STANDOFF":PRINT@147,"RETURNING":PRINT@187,"$";C(13):PRINT@227,"BET":C(13)=0:GOSUB23:RETURN
164 C(1)=0:GOSUB2:PRINT@27,"ELEVEN":PRINT@67,D$;S$:PRINT@107,"DOESN'T PAY":GOSUB23:RETURN
165 SP=0:C(1)=0:GOSUB2:PRINT@27,"SEVEN":PRINT@67,D$;S$:PRINT@107,"DOESN'T PAY":GOSUB23:RETURN
166 ONXTGOTO0,167,171,175,182,188,195,205,212,218,225,230
167 GOSUB242:IFC(12)THENGOSUB253
168 IFC(13)THENGOSUB257
169 IFC(6)THENWA=6:Z=29:UQ$="HARD 2":GOSUB234
170 GOSUB238:GOSUB236:GOSUB239:RETURN
171 GOSUB242:IFC(12)THENGOSUB253
172 IFC(13)THENGOSUB257
173 IFC(8)THENWA=8:Z=14:UQ$="THREE":GOSUB234
174 GOSUB238:GOSUB236:GOSUB239:RETURN
175 GOSUB241:IFC(21)THENWA=21:GOSUB261
176 IFC(27)THENWA=27:GOSUB262
177 IFC(12)THENWA=21:GOSUB253
178 IFC(13)THENWA=27:GOSUB257
179 IFC(5)THENWA=5:GOSUB277:IFXA=XBTHENZ=7:UQ$="HARD 4":GOSUB234
180 IFC(15)THENWA=15:Z=9/5:Z$="9 TO 5!":UQ$="FOUR":GOSUB235
181 GOSUB236:RETURN
182 GOSUB241:IFC(22)THENWA=22:GOSUB261
183 IFC(28)THENWA=28:GOSUB262
184 IFC(12)THENWA=22:GOSUB253
185 IFC(13)THENWA=28:GOSUB257
186 IFC(16)THENWA=16:Z=7/5:Z$="7 TO 5!":UQ$="FIVE":GOSUB235
187 RETURN
188 GOSUB241:IFC(23)THENWA=23:GOSUB261
189 IFC(29)THENWA=29:GOSUB262
190 IFC(12)THENWA=23:GOSUB253
191 IFC(13)THENWA=29:GOSUB257
192 IFC(2)THENWA=2:GOSUB277:IFXA=XBTHENZ=9:UQ$="HARD 6":GOSUB234
193 IFC(17)THENWA=17:Z=7/6:Z$="7 TO 6!":UQ$="SIX":GOSUB235
194 RETURN
195 IFC(0)>0ANDSP=1THENGOSUB157ELSEIFC(1)>0ANDSP=1THENGOSUB165
196 GOSUB275:IFC(12)THENGOSUB253
197 IFC(13)THENGOSUB257
198 IFSP=0ANDC(0)>0THENGOSUB247
199 IFSP=0ANDC(1)>0THENGOSUB248
200 IFC(33)THENWA=33:Z=4:UQ$="SEVEN":GOSUB234
201 FORWA=21TO26:IFC(WA)THENGOSUB249
202 NEXT:FORWA=27TO32:IFC(WA)THENGOSUB251
203 NEXT:RS=0:FORI=15TO20:RS=RS+C(I):C(I)=0:NEXT:IFRSTHENRS=0:GOSUB2:PRINT@27,"ALL PLACE":PRINT@67,"BETS ARE":PRINT@107,"OFF!":GOSUB23
204 RETURN
205 GOSUB241:IFC(24)THENWA=24:GOSUB261
206 IFC(30)THENWA=30:GOSUB262
207 IFC(12)THENWA=24:GOSUB253
208 IFC(13)THENWA=30:GOSUB257
209 IFC(4)THENWA=4:GOSUB277:IFXA=XBTHENZ=9:UQ$="HARD 8":GOSUB234
210 IFC(18)THENWA=18:Z=7/6:Z$="7 TO 6!":UQ$="EIGHT":GOSUB235
211 RETURN
212 GOSUB241:IFC(25)THENWA=25:GOSUB261
213 IFC(31)THENWA=31:GOSUB262
214 IFC(12)THENWA=25:GOSUB253
215 IFC(13)THENWA=31:GOSUB257
216 IFC(19)THENWA=19:Z=7/5:Z$="7 TO 5!":UQ$="NINE":GOSUB235
217 GOSUB236:RETURN
218 GOSUB241:IFC(26)THENWA=26:GOSUB261
219 IFC(32)THENWA=32:GOSUB262
220 IFC(12)THENWA=26:GOSUB253
221 IFC(13)THENWA=32:GOSUB257
222 IFC(3)THENWA=3:GOSUB277:IFXA=XBTHENZ=7:UQ$="HARD 10":GOSUB234
223 IFC(20)THENWA=20:Z=9/5:Z$="9 TO 5!":UQ$="TEN":GOSUB235
224 GOSUB236:RETURN
225 IFC(0)>0ANDSP=1THENSP=0:GOSUB156ELSEIFC(1)>0ANDSP=1THENSP=0:GOSUB164
226 IFC(12)THENGOSUB253
227 IFC(13)THENGOSUB257
228 IFC(9)THENWA=9:Z=14:UQ$="ELEVEN":GOSUB234
229 GOSUB236:GOSUB239:RETURN
230 F=1:GOSUB242:IFC(12)THENGOSUB253
231 IFC(13)THENGOSUB163
232 IFC(7)THENWA=7:Z=29:UQ$="HARD 12":GOSUB234
233 GOSUB238:GOSUB236:GOSUB239:RETURN
234 GOSUB2:PRINT@27,UQ$:PRINT@67,Z"TO 1!":PRINT@107,"BET PAYS":PRINT@147,"$";(Z+1)*C(WA):GOSUB23:MB=MB+(Z+1)*C(WA):C(WA)=0:RETURN
235 GOSUB2:PRINT@27,UQ$:PRINT@67,Z$:PRINT@107,"BET PAYS":PRINT@147,"$";INT(Z*C(WA)):GOSUB23:MB=MB+INT(Z*C(WA)):RETURN
236 IFC(14)=0THENRETURNELSEWA=14:IFXT>2ANDXT<12THENZ=1ELSEZ=2
237 UQ$="FIELD":GOSUB234:RETURN
238 IFC(11)THENWA=11:Z=7:UQ$="CRAPS":GOSUB234:RETURN
239 IFC(10)THENWA=10:Z=7-((XT=11)*7):UQ$="CRAPS/11":C(10)=C(10)/2:GOSUB234:RETURN
240 RETURN
241 IFSPTHENSP=0:GOSUB158:RETURNELSEGOSUB244:RETURN
242 IFC(0)>0ANDSP=1THENSP=0:GOSUB155:RETURNELSEIFC(1)>0ANDSP=1THENSP=0ELSERETURN
243 IFF=1THENF=0:GOSUB162:RETURNELSEGOSUB161:RETURNELSERETURN
244 IFXT=PANDSP=0ANDC(0)>1THENWI=2*C(0):MB=MB+WI:C(0)=0:P=0:GOSUB2:GOSUB35:PRINT@107,S$" BET PAYS":PRINT@147,"$";WI:GOSUB23:RETURN
245 IFXT=PANDSP=0ANDC(1)>1THENC(1)=0:P=0:GOSUB2:GOSUB35:PRINT@107,D$;S$:PRINT@147,"BET "L$:GOSUB23:RETURN
246 RETURN
247 C(0)=0:P=0:GOSUB2:PRINT@27,S$" BET":PRINT@67,L$:GOSUB23:RETURN
248 WI=2*C(1):MB=MB+WI:P=0:C(1)=0:GOSUB2:PRINT@27,D$;S$:GOSUB38:PRINT@107,"PAYS":PRINT@147,"$"WI:GOSUB23::RETURN
249 IFWA<24THENLC=WA-17ELSELC=WA-16
250 C(WA)=0:GOSUB2:PRINT@27,LC:PRINT@67,C$" BET":PRINT@107,L$:GOSUB23:RETURN
251 IFWA<30THENLC=WA-23ELSELC=WA-22
252 WI=2*C(WA):MB=MB+WI:C(WA)=0:GOSUB2:PRINT@27,LC:PRINT@67,D$;C$:PRINT@107,W$:PRINT@147,"PAYS":PRINT@187,"$"WI:GOSUB23:RETURN
253 IFXT=2ORXT=3ORXT=12THENC(12)=0:GOSUB2:GOSUB36:PRINT@67,"CRAPS OUT":GOSUB23:RETURN
254 IFXT=7ORXT=11THENWI=2*C(12):MB=MB+WI:C(12)=0:GOSUB2:GOSUB36:GOSUB38:PRINT@107,"PAYS":PRINT@147,"$";WI:GOSUB23:RETURN
255 IFWA=21THENGOSUB99ELSEIFWA=22THENGOSUB100ELSEIFWA=23THENGOSUB101ELSEIFWA=24THENGOSUB102ELSEIFWA=25THENGOSUB103ELSEIFWA=26THENGOSUB104
256 RETURN
257 IFXT=7ORXT=11THENC(13)=0:GOSUB2:GOSUB37:PRINT@67,"BET "L$:GOSUB23:RETURN
258 IFXT=2ORXT=3THENWI=2*C(13):MB=MB+WI:C(13)=0:GOSUB2:GOSUB37:PRINT@67,"BET "W$:PRINT@107,"PAYS":PRINT@147,"$";WI:GOSUB23:RETURN
259 IFWA=27THENGOSUB108ELSEIFWA=28THENGOSUB109ELSEIFWA=29THENGOSUB110ELSEIFWA=30THENGOSUB111ELSEIFWA=31THENGOSUB112ELSEIFWA=32THENGOSUB113
260 RETURN
261 WI=2*C(WA):MB=MB+WI:C(WA)=0:GOSUB2:PRINT@27,"YOU MADE":PRINT@67,C$" POINT!":PRINT@107,"BET PAYS":PRINT@147,"$";WI:GOSUB23:RETURN
262 C(WA)=0:GOSUB2:PRINT@27,C$" POINT":PRINT@67,"MADE":PRINT@107,D$;C$:PRINT@147,"BET "L$:GOSUB23:RETURN
263 CLS:CLEAR512,58267:ONERRORGOTO7:LOADM"CRAPS!.CO":ONERRORGOTO7:DIMC(33):P$=CHR$(27)+"p[":Q$="]"+CHR$(27)+"q":CA=58267:L$="LOSES":S$="PASS":C$="COME":D$="DON'T ":W$="WINS!":G$="A WINNER!
264 PRINT@54,"CRAPPS.100":PRINT@98,"by":PRINT@134,"Don Zeikel":PRINT@162,"copyright 1986, all rights reserved.":PRINT@209,"PRESS [ENTER] TO BEGIN":PRINT@249,CHR$(34)"M"CHR$(34)" FOR MANUAL DICE";:PRINT@289,CHR$(34)"S"CHR$(34)" FOR MANUAL SCREEN";
265 GOSUB1:IFA$="M"ORA$="m"THENMM=1:PRINT@250,CHR$(27)"pM"CHR$(27)"q";:GOTO265
266 IFA$="S"ORA$="s"THENNN=1:PRINT@290,CHR$(27)"pS"CHR$(27)"q";:GOTO265
267 IFA$=CHR$(13)THEN41ELSEBEEP:GOTO265
268 B=0:XC%=10:IFC(2)THENYC%=5:GOSUB14
269 IFC(4)THENYC%=16:GOSUB14
271 XC%=30:IFC(3)THENYC%=5:GOSUB14
272 IFC(5)THENYC%=16:GOSUB14
274 RETURN
275 J=0:FORI=2TO7:J=J+C(I):NEXT:IFJ>0THENGOSUB2:PRINT@27,"ALL":PRINT@67,"HARD WAYS":PRINT@107,"ARE OFF":GOSUB23
276 FORI=2TO7:C(I)=0:NEXT:RETURN
277 IFXA<>XBTHENGOSUB2:PRINT@27,"HARD"XT:PRINT@67,L$:C(WA)=0:GOSUB23
278 RETURN
