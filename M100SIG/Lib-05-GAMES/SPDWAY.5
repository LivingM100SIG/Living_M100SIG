1 'CRAZY MONTY SPEEDWAY VERSION 1.5'
2 'BY SETH KAPLAN [72256,744]
3 'THIS MAY BE PRINTED ON BILLIONS'
4 'OF LEAFLETS AND DISTRIBUTED ALL'
5 'OVER THE KNOWN 17 UNIVERSES, SO'
6 'LONG AS LINES 1 TO 7 REMAIN'
7 'INTACT AND UNCHANGED IN ANY WAY'
11 CA$=CHR$(132):OPEN"RAM:SCORE.DO"FORINPUTAS1:INPUT#1,HI:CLOSE#1:ONERRORGOTO5000
14 CLS:PRINT"WELCOME TO CRAZY MONTY SPEEDWAY: VER 1.5":PRINT" USE LEFT AND RIGHT ARROW KEYS TO STEER"
15 PRINTTAB(7);"CURRENT HIGH SCORE IS:";HI
19 PRINT@187,"                    ":PRINT@172,"HOW MANY LIVES:";:INPUTT$:T=VAL(T$):IFT<1THENGOTO19
20 PRINTTAB(8);"PRESS ANY KEY TO BEGIN!"
30 IFINKEY$=""THEN30
90 X=20:A=14:B=26:W=31:CLS
105 X2=X:CA$=CHR$(132):P=0:I$=INKEY$:IFI$=CHR$(30)ANDC<150THENW=W-5
107 IFI$=CHR$(31)ANDC<150THENW=W+5
108 IFW<11ANDC<150THENW=11
109 IFW>91THENW=91
110 IFI$=CHR$(29)THENX=X-1
120 IFI$=CHR$(28)THENX=X+1
121 IFI$=CHR$(65)THENX=X-1
122 IFI$=CHR$(76)THENX=X+1
125 IFX=AORX=BTHENCR=CR+1:IFCR=TTHENGOTO177
127 IFX=AORX=BANDCR<>TTHENCA$="BANG!"
130 R=INT(RND(2)*10+1):R2=INT(RND(3)*3+1):IFR2<>2THENIFR/2=INT(R/2)THENP=1ELSEP=-1
150 A=A+P:B=B+P:IFX<9THENX=X+1
152 IFX>36THENX=X-1
153 IFX<ATHENX=A+2
154 IFX>BTHENX=B-2
155 IFA<2THENA=A+1:B=B+1
156 IFB>38THENB=B-1:A=A-1
157 C=C+1:FORL=1TOW:NEXTL:IFC=36THENA=A+1:W=W-5
160 IFC=30THENB=B-1
161 IFC=50THENA=A+1:W=W-5
162 IFC=80THENB=B-1:W=W-5
163 IFC=100THENA=A+1
164 IFC=120THENB=B-1
165 IFC=151THENB=B-1:W=1:PRINT@160,"SCORE GREATER THAN 150: TURBO ACTIVATION"
166 IFC=155ORC=159THENPRINT@160,"SCORE GREATER THAN 150: TURBO ACTIVATION"
167 IFC=190THENPRINT@160,"WARNING: ROADS NARROW AHEAD!-CRAZY MONTY"
168 IFC=200THENB=B-1:A=A+1:PRINT@160,"     SCORE 200:DANGER! ROADS NARROW"
169 IFC=215THEN179
172 PRINT@200,"          ":WHU=240+X2:PRINT@240,"SPEED:";101-W
173 IFCA$<>"BANG!"THENPRINT@WHU," ":IFP=0THENPRINT"SCORE:";C;TAB(A);CHR$(239);TAB(X);CA$;TAB(B);CHR$(239)"
174 IFP=1THENPRINT"SCORE:";C;TAB(A);CHR$(236);TAB(X);CA$;TAB(B);CHR$(236)
175 IFP=-1THENPRINT"SCORE:";C;TAB(A);CHR$(235);TAB(X);CA$;TAB(B);CHR$(235)
176 GOTO105
177 BEEP:BEEP:BEEP:PRINTTAB(14);"YOU CRASHED!":PRINTTAB(12);"YOUR SCORE IS:";C:GOTO184
179 BEEP:PRINTTAB(X-4);CHR$(255);"FINISH!";CHR$(255)
180 IFT<>1THEN184ELSEBEEP:PRINT "  BRAVO! YOU ARE NOW AN OFFICAL CRAZY"
181 PRINTTAB(9);"MONTY RACE CAR DRIVER"
184 IFC>HIANDT=1THENPRINTTAB(14);"NEW HI SCORE!":HI=C
185 OPEN"RAM:SCORE.DO"FOROUTPUTAS1:PRINT#1,HI:CLOSE1:GOTO5010
5000 PRINT"ERROR#";ERR;"AT LINE#";ERL:STOP
5010 PRINT:IFT>1THEN6000
5015 IFC<31THENPRINT"RATING: FLUNKED CRAZY MONTY DRIVING TEST"
5020 IFC>30ANDC<60THENPRINT"RATING: THIS IS RACING, NOT DEMOLITION!!"
5030 IFC>59ANDC<90THENPRINT"  RATING: OKAY SPORT! KEEP ON TRYING!"
5040 IFC>89ANDC<120THENPRINT"    RATING: GOOD! KEEP ON CRUISING!"
5050 IFC>119ANDC<150THENPRINT"RATING: NOW WE'RE MOVING! WHAT A DRIVER!"
5060 IFC>149ANDC<180THENPRINT" RATING: A TURBO MAN! GO FOR THE 200!"
5070 IFC>179ANDC<214THENPRINT" RATING: TOP NOTCH DRIVING! CONGRATS!"
5080 IFC=214THENPRINT"RATING: PERFECT DRIVING, GO FOR THE 215!"
5090 IFC=215THENPRINT"RATING: YOU ARE AN OFFICIAL CRAZY MONTY!"
6000 END
