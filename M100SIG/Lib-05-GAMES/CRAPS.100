0 REM CRAPS.100 by Don Zeikel[75775,1430],1985
10 CLS: CM=4697:EM=3718:FM=3516:GM=3134:A#=2636:CH=2348
12 FORI=0TO240STEP40:PRINT@I,STRING$(40,229);:NEXT: PRINT@126,CHR$(27);"p";" LUCK BE A LADY, TONIGHT ! ";CHR$(27);"q"
15 FORI=1TO2:SOUNDGM,15:SOUNDFM,8:SOUNDEM,8:SOUNDFM,10:SOUNDGM,12:SOUNDA#,10:SOUNDGM,30:FORJ=1TO100:NEXT:NEXT
17 SOUNDGM,15:SOUNDFM,10:FORI=1TO2:SOUNDEM,10:SOUNDFM,10:SOUNDGM,10:SOUNDFM,10:NEXT:SOUNDEM,10:SOUNDFM,10:SOUNDGM,10:FORI=1TO100:NEXT
19 SOUNDCH,15:SOUNDGM,10:SOUNDFM,10:SOUNDGM,10:FORI=1TO2:SOUNDCM,10:NEXT:SOUNDCM,25
20 CLS: CLEAR: PRINTSTRING$(2,255)"CRAPS"STRING$(33,255);
25 DIMA(12)
30 FORI=1TOINT(((VAL(RIGHT$(TIME$,2))))*((VAL(MID$(TIME$,4,2)))/2)*(VAL(MID$(DATE$,4,2)))/100):I3=RND(1):NEXT
35 C=0:PASS=0:BEEP:FORI=40TO160STEP40:PRINT@I,"                    ":NEXT:PRINT@120,"Type your pass bet!":INPUTPASS:IFPASS=0THEN35
37 T=T-PASS:PRINT@120,"                   ":PRINT@160,"Pass bet is $";PASS:GOSUB9000
40 COUNT=COUNT+1:PRINT@20," Roll #";COUNT:C=C+1:FORI=121TO181STEP60:LINE(I,11)-(I+50,61),1,BF:NEXT
43 FORI=1TO11:DUMMY=RND(1):NEXT
45 FORAD=0TO10STEP10
50 R%=RND(1)*10:IFR%=0ORR%>6THEN50
55 IFAD=0THENLETE=R%
57 IFAD=10THENLETF=R%
60 IFR%=1THENGOSUB1000
70 IFR%=2THENGOSUB3000
80 IFR%=3THENGOSUB1000:GOSUB3000
90 IFR%=4THENGOSUB2000:GOSUB3000
100 IFR%=5THENGOSUB2000:GOSUB3000:GOSUB1000
110 IFR%=6THENGOSUB2000:GOSUB3000:GOSUB4000
120 NEXT
130 G=E+F
135 GOSUB13000
140 IFC=1AND(G=7ORG=11)THENPRINT@120,G;" a winner!     ":GOSUB10000:T=T+2*PASS:PRINT@160,"Pass pays $";2*PASS;"   ":GOSUB9000:GOSUB8000:GOTO35
150 IFC=1AND(G=2ORG=3ORG=12)THENPRINT@120,"Craps you lose!     ":GOSUB11000:GOSUB8000:GOTO35
160 IFC=1THENLETP=G:PRINT@120,"Your point is ";P:FORI=1TO200:NEXT:GOSUB12000:GOTO40
170 IFG=7THENPRINT@120,"7 you lose!       ":GOSUB14000:GOSUB11000:GOSUB8000:GOTO35
180 IFG=PTHENPRINT@120,"Winner!            ":GOSUB10000:T=T+2*PASS:PRINT@160,"Pass pays $";2*PASS;"   ":GOSUB9000:GOSUB8000:GOTO35
190 GOSUB8000:GOTO40
1000 PRINT@184+AD," ":RETURN
2000 PRINT@107+AD," ":PRINT@261+AD," ":RETURN
3000 PRINT@101+AD," ":PRINT@267+AD," ":RETURN
4000 PRINT@181+AD," ":PRINT@187+AD," ":RETURN
8000 BEEP:PRINT@280,"** PRESS ANY KEY **";
8010 DUMMY=RND(1):A$=INKEY$:IFA$=""THEN8010ELSEPRINT@280,SPACE$(19);:RETURN
9000 IFT>0THENPRINT@200,"You are ahead       ":PRINT@240,"$";T;"        "
9010 IFT<0THENPRINT@200,"You are behind      ":PRINT@240,"$";ABS(T);"        "
9020 IFT=0THENPRINT@200,"You are even        ":PRINT@240,"              "
9030 RETURN
10000 FORI=1TO10:SOUND2348,3:SOUND2092,3:NEXT:RETURN
11000 FORI=1TO2:SOUND9952,10:SOUND10544,10:SOUND11172,10:SOUND11836,10:SOUND12538,10:NEXT:RETURN
12000 PRINT@80,"                  ":SIDE$="":BEEP:PRINT@40,"SIDE BET?(TYPE Y/N)"
12010 SIDE$=INKEY$:IFSIDE$=""GOTO12010
12020 IFSIDE$<>"Y"ANDSIDE$<>"y"THENPRINT@40,"                   ":PRINT@80,"                    ":RETURN
12030 BEEP:PRINT@40,"WHAT NUMBER         ":INPUTSIDE
12035 IFSIDE<2ORSIDE>12ORSIDE=7THENPRINT@80,"             ":GOTO12030
12040 BEEP:PRINT@80,"                 ":PRINT@40,"HOW MUCH            ":INPUTBET
12050 T=T-BET:A(SIDE)=A(SIDE)+BET:BET=0:SIDE=0:GOSUB9000:GOTO12000
13000 IFA(G)THENT=T+INT(A(G)*5/4)ELSERETURN
13010 GOSUB10000:PRINT@40,"SIDE BET PAYS $";INT(A(G)*5/4)
13015 GOSUB9000
13020 FORI=1TO1000:NEXT:PRINT@40,"                    ":RETURN
14000 PRINT@40,"ALL SIDE BETS":PRINT@80,"ARE OFF":FORI=2TO12:A(I)=0:NEXT:RETURN
