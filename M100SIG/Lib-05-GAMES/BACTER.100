1 ' BACTERIA
2 '  By Eiji Miura   [71216,1564]
3 '  August 1, 1984
4 ' Original idea from T. Saito's
5 ' "BACTERIA" for NEC PC-8201.
6 ' ------------------------------
10 CLEAR:SCREEN0,0:DEFINTA-R:DEFSTRQ-Z:DIMP1(269)
11 CLS:GOSUB31:N=0
12 LINE(58,0)-(58,63):LINE(186,0)-(186,63):LINE(187,16)-(239,16):LINE(0,16)-(57,16):LINE(187,46)-(239,46)
13 PRINT@1,"Hi-Score":PRINT@43,USINGQ;M:PRINT@34,"Score":PRINT@275,"Time"
14 PRINT@A,T;:PRINT@A+D,S;:PRINT@B*C+10,U;:PRINT@B*C+30,V;
15 PRINT@73,USINGQ;N:K=K-1:PRINT@313,USINGQ;K;:IFK=0THEN26
16 X=INKEY$:IFX<>""THENONINSTR(Y,X)GOTO20,20,21,21,22,23,24
17 H=RND(1)*4-2:I=RND(1)*4-2:O=O+H:P=P+I:IFO<51ORO>69THENO=O-H
18 IFP<0ORP>5THENP=P-I
19 G=P*C+O:P1(G)=1:PRINT@G,W;:GOTO15
20 B=B-1:IFB<1THENB=B+1:GOTO15ELSEPRINT@B*C+50,Z;:PRINT@B*C+70,Z;:GOTO14
21 B=B+1:IFB>6THENB=B-1:GOTO15ELSEPRINT@B*C-30,Z;:PRINT@B*C-10,Z;:GOTO14
22 A=A-1:IFA<11THENA=A+1:GOTO15ELSEPRINT@A+1,Z;:PRINT@A+F,Z;:GOTO14
23 A=A+1:IFA>29THENA=A-1:GOTO15ELSEPRINT@A-1,Z;:PRINT@A+E,Z;:GOTO14
24 N=N-1:J=B*C+A:PRINT@J,R;:SOUND500,1:SOUND1000,1:IFP1(J)=1THENSOUND1500,1:N=N+10
25 P1(J)=0:PRINT@B*C+A,Z;:GOTO15
26 LINE(90,31)-(155,31):PRINT@175,Z2" GAME OVER "Z3:SOUND10000,90:LINE(58,0)-(186,15),1,BF:LINE(0,16)-(239,63),1,BF
27 IFN>MTHENM=N:SOUND1000,1:SOUND1500,1:PRINT@43,USINGQ;M
28 PRINT@175,Z2"Play again?"Z3;
29 IFINKEY$<>""THEN29
30 Q1=INKEY$:IFQ1=""THEN30ELSEIFQ1="Y"ORQ1="y"THEN11ELSEMENU
31 A=20:B=3:C=40:D=280:E=279:F=281:K=501:O=60:P=3:Q="######":R="*":S=CHR$(152)
32 T=CHR$(153):U=CHR$(154):V=CHR$(155):W=CHR$(144):Y="AaZz,. ":Z=" ":Z1=CHR$(27):Z2=Z1+"p":Z3=Z1+"q"
33 FORA1=1TOVAL(RIGHT$(TIME$,2)):A2=RND(1):NEXT
34 PRINTTAB(15)Z2" BACTERIA "Z3:PRINT:PRINTTAB(4)"Kill Bacterias with Cross Raser.":PRINT
35 PRINTTAB(6)"[A]":PRINT"   [,]   [.]     [SPACE BAR] to fire":PRINTTAB(6)"[Z]":PRINTTAB(8)"<Press any key to start>";
36 IFINKEY$=""THEN36ELSECLS:RETURN

