10 ' BINGO by Forrest C. Hudspeth Copyright 1986
20 ' Written on 23 Feb 1986
200 DEFSTR A-C:DEFINT W-Z:DIM A(75),C(75),Z(75):W=0
210 'Initialize the random number generator using the last 2
220 'digits of the clock
230 CLS:Y=VAL(RIGHT$(TIME$,2)):FOR X=1 TO Y:R=RND(1):NEXT X
240 'Initialize array with Bingo values
250 FOR X=1 TO 75:READ A(X):NEXT X
260 CLS 'Bingo loop
270 X=INT(RND(1)*100):IF X>75 GOTO 270
280 IF Z(X)=X GOTO 270
290 W=W+1:Z(X)=X:C(W)=A(X):BEEP:PRINT @ 92,"====> ";A(X);" <===="
300 B=" ":PRINT:PRINT"Hit ENTER for the next number"
310 PRINT" S to stop":PRINT" L to list all numbers called"
320 INPUT" C to check a number";B
330 IF B="S"THEN GOTO 450 ELSE IF B="L"THEN GOTO 340 ELSE IF B="C" THEN GOTO 350 ELSE GOTO 260
340 CLS:PRINT:FOR Y=1TOW:PRINT C(Y);" ";:NEXT Y:PRINT:INPUT"Hit ENTER to continue";B:CLS:GOTO 300
350 CLS:INPUT"Your number to check ";B
360 FOR Y=1TOW:IF B=C(Y) THEN PRINT:PRINT"That number has been called":PRINT:GOTO 380 ELSE NEXT Y
370 PRINT:PRINT"That number has not been called":PRINT
380 PRINT"Hit ENTER to check another"
390 INPUT" S to stop checking ";B:IF B="S"THEN CLS:GOTO 300 ELSE GOTO 350
400 DATA B1,B2,B3,B4,B5,B6,B7,B8,B9,B10,B11,B12,B13,B14,B15
410 DATA I16,I17,I18,I19,I20,I21,I22,I23,I24,I25,I26,I27,I28,I29,I30
420 DATA N31,N32,N33,N34,N35,N36,N37,N38,N39,N40,N41,N42,N43,N44,N45
430 DATA G46,G47,G48,G49,G50,G51,G52,G53,G54,G55,G56,G57,G58,G59,G60
440 DATA O61,O62,O63,O64,O65,O66,O67,O68,O69,O70,O71,O72,O73,O74,O75
450 MENU
