0 REM FROGER.BA V1 BY Nigel Hackett     March27th 1985
1 CLEAR1000:Q=31:Z=32:E=147:SCO=0:TIM=180:LIV=4:POKE63038,175
2 CLS:PRINT:PRINT"Press prefered keys for movement or":PRINT"     <ENTER> for arrow keys":PRINT"","left  ";
3 AA$=INPUT$(1):IFAA$=CHR$(13) THEN GOTO300 ELSE PRINTAA$:PRINT"","right ";:AB$=INPUT$(1):PRINTAB$:PRINT"","up    ";
4 AC$=INPUT$(1):PRINTAC$:PRINT"","down  ";:AD$=INPUT$(1):PRINTAD$
5 CLS:FORX=120 TO 159:PRINT@X,"#";:NEXT
7 CAR$="   "+CHR$(132):FLY$="  "+CHR$(133)+" "
8 C$="":N$="": FORX=1 TO10:C$=C$+CAR$:N$=N$+FLY$:NEXT:E$=C$:O$=N$
12 G$="":PRINT@0,CHR$(239);CHR$(239);:F$="  "+STRING$(5,239):FORX=1TO5:G$=G$+F$:NEXT:PRINTG$;"  ";CHR$(239);
13 X=19:Y=6:A$="":GOTO60
15 H=0:V=0:A$=INKEY$
16 IFA$="" THEN34 ELSE E=(E XOR 7)
20 IF A$=AA$ THEN X=X-1:H=1 ELSE IFA$=AB$ THEN X=X+1:H=-1 ELSE IF A$=AC$ THEN Y=Y-1:V=1 ELSE IF A$=AD$ THEN Y=Y+1:V=-1
25 IF X>39 THEN X=39:H=0 ELSE IF X<0 THEN X=0:H=0
30 IF Y>6 THEN Y=6:V=0 ELSEIF Y<0 THENY=0
34 IFQ>5THENQ=0:GOTO35ELSEQ=Q+1:GOTO40
35 PRINT@40,C$;:D$=RIGHT$(C$,1):C$=D$+LEFT$(C$,39):PRINT@160,E$:D$=LEFT$(E$,1):E$=RIGHT$(E$,39)+D$
36 PRINT@80,N$;:D$=LEFT$(N$,1):N$=RIGHT$(N$,39)+D$:PRINT@200,O$:D$=RIGHT$(O$,1):O$=D$+LEFT$(O$,39)
40 IFA$="" THEN 50 ELSE PRINT@X+H+(Y+V)*40,CHR$(Z);:Z=PEEK(65024+X+40*Y):IFZ<>32AND Z<>35 THEN100ELSE60
50 T=PEEK(65024+X+40*Y):IFT<>147ANDT<>148ANDT<>32THEN100
60 PRINT@280,"SCORE=";SCO:PRINT@310,"TIME=";TIM;:PRINT@X+40*Y,CHR$(E);:IFY=0THEN80
61 IFLIV<>0THENPRINT@295,"";:FORB=0TOLIV-1:PRINTCHR$(147);:NEXT
65 W=VAL(RIGHT$(TIME$,2)):IFW=JTHEN70ELSEJ=W:TIM=TIM-1:IFTIM=0 THEN200
70 GOTO15
80 SCO=SCO+10:TIM=TIM+10:SOUND2793,12:SOUND2484,12:SOUND2348,12:PRINT@X-1,STRING$(3,239);:X=19:Y=6
85 IF SCO/60=INT(SCO/60)THENGOSUB150:GOTO12ELSE13
100 Z=32:SOUND11172,50:LIV=LIV-1:PRINT@295,"      ";:IFLIV=-1THEN200ELSE13
150 IF SCO/240=INT(SCO/240) THEN LIV=LIV+1
160 IFLIV>5 THEN LIV=5:SCO=SCO+120:TIM=TIM+30
170 RETURN
200 CLS:PRINT@50,"SCORE=";SCO:PRINT@130,"PLAY AGAIN <Y/N>?";
210 A$=INPUT$(1):IFA$<>"y"AND A$<>"Y"THEN MENUELSERUN
300 AA$=CHR$(29):AB$=CHR$(28):AC$=CHR$(30):AD$=CHR$(31):GOTO5

