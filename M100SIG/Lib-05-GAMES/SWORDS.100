1 REM WRITTEN BY KRIS LEDOUX
2 REM COMPUSERVE # 73127,1572
3 REM MAY 4 1986
4 CLS
8 GOTO20
10 FORX=1TO500:NEXTX:GOTO3000
11 FORX=1TO450:NEXTX:GOTO430
12 FORX=1TO650:NEXTX:GOTO1250
13 FORX=1TO450:NEXTX:GOTO830
14 FORX=1TO450:NEXTX:GOTO2000
20 GOSUB250:GOTO400
30 SW=1:GA=0:S1=8:E=1:MH=15:P=42:LE=1:GP=0:EX=10:HP=15:CP=5:EL=1
40 PRINT@P,CHR$(147)
41 PRINT@27,"level    ";LE
42 PRINT@67,"gold ";GP
43 PRINT@107,"EXP  ";EX
44 PRINT@147,"healing  ";CP
45 PRINT@187,"exp level";EL
46 PRINT@227,"H Points ";HP
48 IFHP<-1THEN1500
50 A$=INKEY$:IFA$=""THEN50
51 BEEP:BEEP
60 PRINT@P,CHR$(239)
62 O=P
70 IFA$=CHR$(28)THENP=P+4
80 IFA$=CHR$(29)THENP=P-4
90 IFA$=CHR$(30)THENP=P-120
100 IFA$=CHR$(31)THENP=P+120
101 IFA$=CHR$(104)THEN10000
110 IFP<42ORP>182THENP=O
120 IFP>62ANDP<162THENP=O
130 IFA$=CHR$(100)THENLE=LE+1:FORX=2000TO3000STEP100:SOUNDX,7:NEXTX
140 IFA$=CHR$(117)THENLE=LE-1:FORX=3000TO2000STEP-100:SOUNDX,7:NEXTX
141 IFLE=0THENLE=1
142 IFLE>4THENLE=4
150 PRINT@P,CHR$(147)
160 IFLE=1THEN170
161 IFLE=2THENCLS:GOTO179
162 IFLE=3THENCLS:GOTO189
163 IFLE=4THEN195
170 IFL1>18THENPRINT"level one has been emptied.":GOTO11
171 R=3:GOSUB550:IFA1<0THEN174
172 IFA1<5THEN177
173 IFA1<10THENCLS:PRINT:PRINT"You are being attacked by TWO ORCS!!":FORX=1TO350:NEXTX:L1=L1+1:AR=2:RA=AR:EH=14:EM=14:GOTO1250
174 CLS:PRINT:PRINT"You unlucky dog you get a troll.   hope you have cash":FORX=1TO350:NEXTX:L1=L1+1:TR=1:EH=42:GOTO1250
175 CLS:LINE(8,8)-(232,55),1,B:LINE(28,28)-(212,35),1,BF:PRINT@86,"You find 1000 GP.":GP=GP+1000:EX=EX+1000:CP=CP+1
176 GOTO11
177 CLS:PRINT:PRINT"You are being attacked by one Orc.":FORX=1TO350:NEXTX:AR=1:RA=AR:EH=8:EM=8:L1=L1+1:GOTO1250
179 IFL2>15THENCLS:PRINT:PRINT"level two has been emptied>":GOTO11
180 L2=L2+1:R=4:GOSUB550:IFA2<7THEN174
181 IFA2<10THEN173
182 IFA2<20THENPRINT:PRINT"You are being attacked by 3 orcs.":AR=3:RA=AR:EH=18:EM=18:CP=CP+1:GOTO12
183 IFA2=20THEN175
189 IFL3>14THENCLS:PRINT:PRINT:PRINT"level three is empty.":GOTO11
190 L3=L3+1:R=4:GOSUB 550:IFA2<7THEN174
191 IFA2<14THENGA=1:EH=41:PRINT:PRINT"Just your luck,  a Gargoyle wants YOU!!!":CP=CP+1:GOTO12
192 IFA2<16THENAR=4:RA=AR:EH=26:EM=26:PRINT:PRINT" 4 Orcs sucker":GOTO12
193 IFA2<21AND SW=1THENPRINT:PRINT"You have found a +2 +4 Sword! Now it's easy to kill Gargoyles.":SW=2:GOTO11
194 IFA2<21ANDSW=2THENPRINT:PRINT" A BIG 500 GP and a healing potion":CP=CP+1:GP=GP+500:EX=EX+500:GOTO11
195 IFL4>12THENCLS:PRINT:PRINT"You have cleaned out level four":GOTO11
196 L4=L4+1:R=4:GOSUB550:IFA2<5THEN191
197 IFA2<10 THEN174
198 IFA2<12THENAR=6:RA=AR:EH=40:EM=40:CLS:PRINT:PRINT"You get 6 orcs to play with.":CP=CP+1:GOTO12
199 IFA2<18THENMI=1:EH=76:PRINT:PRINT"It's time you get to fight the Minotaur":GOTO12
200 IFA2<21THENCLS:BU=1:EH=76:PRINT"You have to fight a bulette. (LANDSHARK SUPREME) THEY KILL FAST.":FORX=1TO500:NEXTX:GOTO12
210 GOTO430
250 FORX=1000TO4000STEP200
251 SOUNDX,5
252 NEXTX
254 FORX=4000TO1000STEP-200
255 SOUNDX,4
256 NEXTX
257 CLS:PRINT:PRINT"Do you need instructions. y/n"
258 O$=INKEY$:IFO$=""THEN258
259 IFO$="n"ORO$="N"THENRETURN
260 CLS:PRINT@40,"  The object of this game is to clear   the dungeon of hostile creatures and    find as much gold as possible."
261 PRINT"In order to do this you must survive and gain experience, this makes combat with stronger creatures possible." 
262 P$=INKEY$:IFP$=""THEN262
263 CLS:PRINT@40,"  The cursor control keys will move you from room to room. To go down to the next level press d "
264 PRINT"To go up press u . Press h to take a healing potion for 15 points of healing."
265 PRINT"Your maximum HP goes up as your level increases."
266 P$=INKEY$:IFP$=""THEN266
267 CLS:PRINT@40,"  The only time you can take a healing  potion is when the maze is shown on the screen or after using "
268 PRINT"a rope trick spell.  This will use up one of your healing potions."
269 U$=INKEY$:IFU$=""THEN269
270 CLS:PRINT@40,"  As you go up in experience you get more spells, and also more powerful ones. Unfortunatly once you"
271 PRINT" use your spells you don't get more until you go up a level, so judicial use of spells is a good idea."
272 U$=INKEY$:IFU$=""THEN272
273 CLS:PRINT@40,"  You will get to second level at 5000  exp.   Good LUCK and GO FOR IT."
274 U$=INKEY$:IFU$=""THEN274
275 RETURN
299 FORX=1TO1000:NEXTX:GOTO250
300 GOTO430
301 S=VAL(RIGHT$(TIME$,2)):FORN=1TOS:D=RND(1):NEXTN
302 R=4:GOSUB550:IFA2>11THEN307
303 CLS:PRINT@40,A2;"attack1  missed":FORX=1TO250:NEXTX:GOTO310
307 Z8=INT(RND(1)*44+5)
308 PRINT@40,A2"He bites for";Z8;"damage":HP=HP-Z8:IFHP<0THEN1500
309 FORX=1TO500:NEXTX
310 FORX=1TO2:R=4:GOSUB550:IFA2<12THEN320
311 R=3:GOSUB550
312 PRINTA2;  A1+4;"points of damage from feet":HP=HP-(A1+4):IFHP<0THEN1500
313 NEXTX
314 GOTO12
320 PRINTA2;"missed":NEXTX
321 GOTO12
399 GOTO430
400 CLS:CLEAR2000
401 FORL=1TO6
402 FORI=1TO25
403 IFL<4THEN READN:X$=X$+CHR$(N)
404 IFL>3THENREADN:Y$=Y$+CHR$(N)
405 NEXTI
406 X$=X$+CHR$(10)+STRING$(25,8)
407 IFL<4THEN409
408 Y$=Y$+CHR$(10)+STRING$(25,8)
409 NEXTL
410 PRINT@1,X$:PRINT@121,Y$
415 GOTO30
430 CLS: PRINT@1,X$:PRINT@121,Y$
431 AR=0:GA=0:TR=0:MI=0:BU=0
432 IFL1>18ANDL2>15ANDL3>14ANDL4>12THEN9000
439 A$=""
440 GOTO40
549 END
550 S=VAL(RIGHT$(TIME$,2)):FORN=1TOS:D=RND(1):NEXTN
560 ONRGOTO600,620,640,660
600 A4=RND(1):A4=INT(A4*4+1)
610 RETURN
620 A6=RND(1):A6=INT(A6*6+1)
630 RETURN
640 A10=RND(1):A10=INT(A10*10+1)
650 RETURN
660 A20=RND(1):A20=INT(A20*20+1)
670 RETURN
700 CLS:PRINT@40,"YOU have elected to try and bribe your  way out of this.":FORX=1TO500:NEXTX:CLS
701 IFAR>0THEN720
703 IFTR>0THEN735
704 IFGA>0THEN780
705 IFMI>0THEN780
706 IFBU>0THEN750
720 PRINT"They want 500 gold pieces"
721 INPUT"Will you give it to them y/n";B$
722 IFB$="y"THEN728
723 PRINT"He attacks you first. You lose 3 hp":HP=HP-3:GOTO12
728 GP=GP-500:IFGP<0THEN723
729 GOTO430
735 PRINT"It wants 6000 gold pieces"
736 INPUT"are you willing to pay. y/n";B$
737 IFB$="y"THEN745
738 PRINT"He attacks you first. You lose 7 hp":HP=HP-7:GOTO12
745 GP=GP-6000:IFGP<0GOTO738
746 GOTO430
750 CLS:PRINT:PRINT"A Bulette wants only to eat you.":PRINT"BE PREPARED TO DIE!!":FORX=1TO500:NEXTX:GOTO12
780 IFMI>0THENCLS:PRINT: PRINT"Minotaur's cannot be bribed!":GOTO12
781 IFGA>0THENCLS:PRINT:PRINT"Gargoyles don't want money, He wants your soul!!":GOTO12
800 EM=EH:CLS:PRINT@80,"You have elected to fight. You can fight       with your sword or magic."
801 PRINT@200,"<1> Sword   <2>Magic   <3>Spellbook
804 D$=INKEY$:IFD$=""THEN804
805 X=VAL(D$):ONXGOTO808,910,1100
808 IFSW=1ANDGA>0THENGOTO2500
810 CLS:PRINT@130,"You swing your sword!"
811 R=4:GOSUB550
812 PRINT@215,"You rolled a ";A2:FORX=1TO350:NEXTX:A2=A2+1:IFSW=1THENA2=A2+1
813 IFSW=2THENA2=A2+2
814 IFEL=1ANDA2>15THEN820
815 IFEL=2ANDA2>13THEN820
816 IFEL=3ANDA2>11THEN820
817 IFEL=4ANDA2>9THEN820
818 IF EL>4ANDA2>7THEN820
819 R=0:CLS:PRINT@90,"Sorry.  You MISSED!":FORX=1TO350:NEXTX:ONEGOTO830,936,936,826
820 CLS:R=3:GOSUB550:IFSW=1THENA1=A1+2
821 IFSW=2THENA1=A1+4
822 PRINT@80,"You hit him for ";A1;"points of damage!":EH=EH-A1:FORX=1TO350:NEXTX:IFEH<0THEN827
823 IFAR=0THEN825
824 T1=A1+A1:IFT1>EM/ARTHENAR=AR-1:PRINT"YOU GOT ONE":T1=0:IFEH>0ANDAR=0THENPRINT"BUT HE'S NOT DEAD"
825 FORX=1TO290:NEXTX:ONEGOTO826,936,936,826
826 R=0:IFEH>0THEN830
827 PRINT:PRINT"You killed him with that one.":FORX=1TO350:NEXTX:GOTO2000
830 CLS
834 IFAR>0THEN842
835 IFAR=0ANDEH<1THEN827
837 IFBU=1THEN301
838 IFGA=1THEN875
839 IF MI=1THEN890
840 IFTR=1THEN855
842 PRINT@89,"The orc or orcs attack now.":FORX=1TO300:NEXT
843 FORX=1TOAR:R=4:GOSUB550
844 PRINTA2;:IFA2>14THEN847
845 PRINT"missed!":FORY=1TO300:NEXTY:NEXTX
846 GOTO850
847 R=2:GOSUB550
848 PRINT"You take";A6;"hit points of damage"
849 HP=HP-A6:FORY=1TO300:NEXTY:NEXTX
850 IFHP<0THEN1500
851 GOTO1250
855 CLS:PRINT@80,"The Troll attacks!!! !!"
856 FORX=1TO2:R=4:GOSUB550
857 PRINTA2;
858 IFA2>11THEN861
859 PRINT"Missed!"
860 NEXTX:GOTO864
861 R=2:GOSUB550
862 DA=INT(A6/2+1):PRINT"You take";DA;"hit points damage!#!":HP=HP-DA:IF HP<0THEN1500
863 NEXTX
864 R=4:GOSUB550
865 PRINTA2;:IFA2>11THEN868
866 PRINT"missed":FORY=1TO180:NEXTY:GOTO1250
868 R=3:GOSUB550
869 DA=A1+1:PRINT"You take";DA;"points of damage":HP=HP-DA:IFHP<0THEN1500
870 FORX=1TO1000:NEXTX:GOTO1250
875 CLS:IFA2>8THEN877
876 PRINT:PRINT"The Gargoyle smiles a fanged smile.":PRINT"Then he attacks!!**#@!!":GOTO878
877 CLS:PRINT:PRINT"The Gargoyle grins a bloody grimace":PRINT"Then he attacks!"
878 FORX=1TO4:R=4:GOSUB550
879 PRINTA2;:IFA2>7THEN884
880 PRINT"Missed":FORY=1TO150:NEXTY:NEXTX
881 GOTO1250
884 R=1:GOSUB550
886 PRINT"You take";A4;"hit points damage"
887 HP=HP-A4:IFHP<0THEN1500
888 FORY=1TO400:NEXTY: NEXTX:GOTO1250
890 CLS:PRINT:PRINT"The Minotaur attacks!*!!"
892 FORX=1TO2:R=4:GOSUB550:PRINTA2;
894 IFA2>8THEN898
895 PRINT"Missed?":FORY=1TO150:NEXTY:NEXTX:GOTO1250
898 R=3:GOSUB550:A1=A1+2
899 PRINT"You take";A1;"points of damage!"
900 HP=HP-A1:IFHP<0THEN1500
901 FORY=1TO300:NEXTY:NEXTX:GOTO1250
910 CLS:PRINT@40,"<1>MAGIC MISSILE  <2>SLOW
911 PRINT"<3>FLAMING SPHERE <4>ROPE TRICK"
912 PRINT"<5>FIREBALL":PRINT:PRINT"S1=";S1;"  S2=";S2;"    S3=";S3
913 U$=INKEY$:IFU$=""THEN913
915 SP=VAL(U$)
917 ONSPGOTO920,933,940,945,960
920 IFS1<1THENPRINT"no 1st level spells left":FORX=1TO100:NEXTX:GOTO1250
921 IFEL>2THEN925
922 S1=S1-1:R=1:GOSUB550
923 A4=A4+1:CLS:PRINT:PRINT"You hit him for";A4;"points of damage":FORX=1TO290:NEXTX:EH=EH-A4:IFEH<1THENPRINT"HE'S DEAD":GOTO2000
924 GOTO13
925 IFEL>4THEN930
926 S1=S1-1:R=1:GOSUB550
927 W=A4*2+2:PRINT:PRINT"IT TOOK";W;"HIT POINTS DAMAGE":EH=EH-W:IFEH<1THENPRINT"IT'S DEAD":GOTO14
928 FORX=1TO300:NEXTX:GOTO830
930 S1=S1-1:R=1:GOSUB550
931 W=A4*3+3:PRINT:PRINT"IT TOOK";W;"HITS OF DAMAGE":EH=EH-W:IFEH<1THENPRINT"IT'S DEAD":FORX=1TO300:NEXTX:GOTO2000
932 FORX=1TO300:NEXTX:GOTO830
933 IFS1<1THENPRINT"YOU HAVE NO FIRST LEVEL SPELLS LEFT":GOTO12
934 S1=S1-1:CLS:PRINT:PRINT"This spell allows you to make 3 attacks":E=2
935 FORX=1TO280:NEXTX:GOTO808
936 E=E+1:GOTO808
940 IFEL<3THENCLS:PRINT:PRINT"Not high enough":GOTO12
941 IFS2<1THENPRINT"out of 2nd spells":GOTO12
942 S2=S2-1:R=3:GOSUB550:A1=A1+6
943 PRINT:PRINT"YOU GAVE HIM";A1;"POINTS OF DAMAGE":EH=EH-A1:IFEH<1THENPRINT"YOU KILLED HIM":GOTO14
944 FORX=1TO300:NEXTX:GOTO830
945 IFS2<1THENPRINT"OUT OF 2ND SPELLS":FORX=1TO300:NEXTX:GOTO1250
946 IFEL<3THENPRINT"NOT HIGH ENOUGH FOR THIS YET":FORX=1TO300:NEXTX:GOTO1250
947 CLS:S2=S2-1:PRINT"You have entered an extradimensional":PRINT"space. While here you can heal yourself WITH POTIONS."
948 PRINT"<1>HEAL","<2>RETURN"
949 E$=INKEY$:IFE$=""THEN949
950 I=VAL(E$):ONIGOTO952,958
951 GOTO1250
952 HP=HP+15:CP=CP-1:IFHP>MHTHENHP=MH
953 PRINT@250,"HIT POINTS";HP;"POTIONS";CP:IFCP=0THEN1250
954 GOTO949
958 GOTO830
960 IFEL<5THENPRINT"You're not high enough":GOTO12
961 IFS3<1THENPRINT"You're out of 3rd spells":FORX=1TO300:NEXTX:GOTO1250
962 S3=S3-1:CLS:R=3:GOSUB550:IFA1<2THENPRINT:PRINT"The spell did'nt work":GOTO13
963 W=0:R=2:FORX=1TO8:GOSUB550
964 W=W+A6+1:NEXTX
965 LINE(5,5)-(234,58),1,B
966 PRINT@163,"He gets";W;"points of damage"
967 EH=EH-W:IFEH<1THENPRINT@270,"DEATH":GOTO14
968 GOTO13
1100 CLS:PRINT"These are the spells available to you":FORX=1TO350:NEXTX
1101 CLS
1110 PRINT"FIRST LEVEL"
1111 PRINT"MAGIC MISSILES    SLOW
1112 IFEL<3THEN1124
1115 PRINT"SECOND LEVEL"
1116 PRINT"FLAMING SPHERE    ROPE TRICK"
1118 IFEL<5THEN1124
1120 PRINT"THIRD LEVEL"
1121 PRINT"FIREBALL"
1124 PRINT@255,"<enter>"
1125 Z$=INKEY$:IFZ$=""THEN1125
1126 GOTO1250
1200 CLS:PRINT@80,"You are a coward. As you turn to run you get attacked."
1201 FORX=1TO700:NEXTX:CLS:IFBU>0THEN1265
1202 IFMI=1THEN1215
1203 S=VAL(RIGHT$(TIME$,2))
1204 FORN=1TOS:D=RND(1):NEXTN
1205 B1=INT(RND(1)*100+1):IFB1>37THEN1230
1209 LINE(12,5)-(190,58),1,B:PRINT@130,"Missed you!":FORX=1TO400:NEXTX:IFGA>0ORTR>0THEN1212
1210 IFMI=1THEN1215
1211 GOTO430
1212 IFB1>37THENPRINT"It blocks your escape!!":GOTO12
1213 PRINT"So you still manage to get away":FORX=1TO500:NEXTX:GOTO430
1215 PRINT"The Minotaur blocks your escape!!":GOTO12
1230 B2=INT(RND(1)*6+1):HP=HP-B2
1231 PRINT@121,"You've been hit for ";B2;" hit points":FORX=1TO600:NEXTX:IFGA>0ORTR>0THEN1212
1232 GOTO430
1250 CLS:PRINT@80,"You have three choices of action."
1251 PRINT@160,"<1>fight","<2>run","<3>bribe"
1252 PRINT@250,"Hit points";HP
1255 C$=INKEY$:IFC$=""THEN1255
1256 X=VAL(C$)
1260 ONXGOTO800,1200,700
1261 GOTO1255
1265 CLS:PRINT:PRINT"You can't outrun a bulette!":GOTO13
1500 CLS:PRINT"Although you tried."
1501 PRINT"You obviusly failed."
1502 PRINT"Better luck next time."
1503 PRINT"YOUR DEAD! DEAD!!  DEAD!!!"
1504 PRINT"enemy hit points were ";EH: PRINT"<ENTER>"
1505 IFEH>45THENPRINT"He is virtually unscathed'"
1506 Z$=INKEY$:IFZ$=""THEN1506
1510 GOTO20
1950 DATA239,239,239,224,252,239,254,224,238,232,237,224,252,232,239,224,237,232,238,224,252,239,254,232,238
1951 DATA238,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239
1952 DATA236,239,251,224,253,239,251,224,231,239,239,224,239,231,236,224,236,239,233,224,235,239,236,231,231
1953 DATA224,238,254,224,252,239,239,224,224,239,224,224,239,252,237,227,224,239,233,224,224,239,224,239,254
1954 DATA239,239,239,232,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239,239
1955 DATA239,239,239,224,253,239,251,224,239,239,239,224,239,239,231,231,239,239,239,224,239,239,239,239,251
2000 IFRA=1THEN2020
2002 IFRA=2THEN2024
2003 IFRA=3THEN2025
2004 IFTR=1THEN2026
2005 IFRA=4THEN2028
2006 IFGA=1THENGB=2000:EA=400:CLS:PRINT:PRINT"the Gargoyle had 2000 GP and ahealing potion":GOTO10
2007 IFRA=6THENGB=2000:EA=200:CP=CP+1:PRINT:PRINT"2000 GP and a healing potion":GOTO10
2008 IFMI=1THENGB=3500:EA=1000:PRINT:PRINT"3500 GP":GOTO10
2009 IFBU=1THENGB=10000:EA=800:PRINT:PRINT"The bulette was tough 10000gp+2 potions":CP=CP+2:GOTO10
2020 GB=420:EA=220:CLS:PRINT:PRINT"He had 420 gold pieces.":GOTO10
2024 CP=CP+1:GB=700:EA=440:CLS:PRINT:PRINT"They had 700 gold pieces and a healing potion":GOTO10
2025 GB=1100:EA=660:CLS:PRINT:PRINT"They had 1100GP":GOTO10
2026 GB=1800:EA=520:CP=CP+1:CLS:PRINT:PRINT"He had 1800 GP and a healing potion.":GOTO10
2028 GB=1520:EA=880:CLS:PRINT:PRINT"1520 GP":GOTO10
2500 CLS:PRINT:PRINT"You swing, but a +2 sword is required to hit a gargoyle.":GOTO13
3000 GP=GP+GB:EX=EX+GB+EA:OP=EL:IFEX<8000THENEL=1
3001 AR=0:TR=0:GA=0:MI=0:RA=0:BU=0
3002 IFEX>5001THENEL=2:MH=25
3003 IFEX>10999THENEL=3:MH=35
3004 IFEX>19000THENEL=4:MH=45
3005 IFEX>26000THENEL=5:MH=55
3006 IFEX>32000THENEL=5:MH=70
3007 IFEL=OP+1ANDEL=2THENS1=S1+7
3008 IFEL=OP+1ANDEL=3THENS1=S1+7:S2=S2+4
3009 IFEL=OP+1ANDEL=4THENS1=S1+7:S2=S2+5
3010 IFEL=OP+1ANDEL=5THENS1=S1+8:S2=S2+6:S3=S3+1
3011 IFMH=70THENS1=S1+.5:S2=S2+.5:S3=S3+.25
3012 IFEX>40000THENS1=S1+1:S2=S2+.5:S3=S3+.5:MH=80:EL=6
3013 IFS3>3THENS3=3
3014 IF S1>8THENS1=9
3015 IFS2>5THENS2=6
3017 GOTO430
9000 CLS
9001 LINE(5,5)-(231,57),1,BF
9002 LINE(5,5)-(231,57),0,BF
9003 PRINT@171,"YOU WON!!!":PRINT"Gold pieces       ";GP:PRINT"Experience points ";EX
9004 PRINT"The dungeon is empty"
9005 SOUND1234,24:PRINT"Play again y/n"
9006 I$=INKEY$:IFI$=""THEN9006
9007 IFI$="y"THENGOTO1
9008 MENU
10000 IFCP<1THEN430
10002 IFCP<0THEN430
10004 CP=CP-1:HP=HP+15:IFHP>MHTHENHP=MH
10006 GOTO430

