0 REM FLIP.100 BY Don Zeikel [75775,1430], 1985
1 CLS:GOTO70
2 A$=INKEY$:DU=RND(1):IFA$=""THEN2ELSERETURN
3 FORI=1TO9:PRINT@P(I),I:FORJ=1TO30:NEXTJ,I:RETURN
4 PRINT@0,S$:RETURN
5 GOSUB2:IFASC(A$)<49ORASC(A$)>57THEN5ELSEGOSUB4:RETURN
6 FORI=1TO11:DU=RND(1):NEXT:RETURN
8 CLS:PRINT@120+ZE,P$" FLIP.100 "Q$;:RETURN
10 X=INT(RND(1)*6+1):GOSUB6:Y=INT(RND(1)*6)+1:GOSUB6:Z=X+Y
12 FORN=0TO1STEP1:FORI=105TO125STEP20:LINE(I,30)-(I+11,41),N,BF:NEXTI,N:GOSUB40
14 N=0:M=0:ONXGOSUB21,22,23,24,25,26:M=20:ONYGOSUB21,22,23,24,25,26:RETURN
16 IF(X3=1)OR(X3=2)THENPRINT@P(Z),P$;Z;Q$:NE(Z)=1:RETURN
17 IFRND(1)<.8THENPRINT@P(Z),P$;Z;Q$:NE(Z)=1ELSEPRINT@P(X3),P$;X3;Q$:NE(X3)=1:PRINT@P(Y3),P$;Y3;Q$:NE(Y3)=1:RETURNELSERETURN
20 RETURN
21 GOSUB30:RETURN
22 GOSUB31:RETURN
23 GOSUB30:GOSUB31:RETURN
24 GOSUB31:GOSUB32:RETURN
25 GOSUB23:GOSUB32:RETURN
26 GOSUB24:GOSUB33:RETURN
30 LINE(110+M,35)-(111+M,36),N,B:RETURN
31 LINE(106+M,31)-(107+M,32),N,B:LINE(114+M,39)-(115+M,40),N,B:RETURN
32 LINE(106+M,39)-(107+M,40),N,B:LINE(114+M,31)-(115+M,32),N,B:RETURN
33 LINE(106+M,35)-(107+M,36),N,B:LINE(114+M,35)-(115+M,36),N,B:RETURN
40 N=1:FORI=1TO4:M=0:FORJ=1TO2:N=N+1:ONIGOSUB21,23,25,22:NEXT:M=20:FORJ=1TO2:N=N+1:ONIGOSUB26,24,22,21:NEXTJ,I:RETURN
70 FORI=91TO107STEP2:J=J+1:P(J)=I:NEXT:P$=CHR$(27)+"p":Q$=CHR$(27)+"q":DIMNE(13):S$=SPACE$(39)
75 ZE=14:GOSUB8:PRINT@207,"by Don Zeikel [75775,1430]":FORI=1TO250:NEXT:FORI=1TOINT(((VAL(RIGHT$(TIME$,2))))*((VAL(MID$(TIME$,4,2)))/2)*(VAL(MID$(DATE$,4,2)))/100):I3=RND(1):NEXT
85 CH=28:PH=28:PI=-1:CI=-1:ZE=0:GOSUB8:LINE(65,12)-(180,48),1,B:LINE(64,11)-(181,49),1,B:LINE(63,10)-(182,50),1,B:LINE(65,25)-(180,25):GOSUB400
90 GOSUB4:FORI=1TO12:NE(I)=0:NEXT:GOSUB3
110 UQ=0:FORI=1TO9:UQ=UQ+NE(I):NEXT:IF(UQ=9)OR((UQ=8)AND(NE(1)=0))THENGOSUB4:GOTO134ELSEPRINT@0,"Press any key to roll dice.
120 GOSUB2:GOSUB4:GOSUB10:IF(NE(Z)=0)AND(Z<10)THENGOTO140ELSEFORI=1TO5:IFNE(I)=1ANDI<>5THENNEXTELSEIFNE(I)=1ANDI=5THEN134
132 FORJ=(I+1)TO9:IF(NE(J)=0)AND(I+J=Z)THENGOTO140ELSENEXTJ,I
134 PR=0:FORI=1TO9:IFNE(I)=0THENPR=PR+I
136 NEXT:GOSUB4:PRINT@0,"Your score this round:"PR:PT=PT+PR:GOSUB500:FORI=1TO1500:NEXT:GOTO300
140 PRINT@0,"Press one or two numbers."
145 X1=0:Y1=0:GOSUB5:X1=VAL(A$):IFNE(X1)=1THEN210ELSEIF(Z=X1)AND(NE(X1)=0)THENPRINT@P(Z),P$;Z;Q$:NE(Z)=1:GOTO110
180 GOSUB5:Y1=VAL(A$):IF(Z=X1+Y1)AND(NE(X1)=0)AND(NE(Y1)=0)THENPRINT@P(X1),P$;X1:PRINT@P(Y1),Y1;Q$:NE(X1)=1:NE(Y1)=1:GOTO110
210 BEEP:PRINT@0,"INVALID ENTRY!  TRY AGAIN.":GOTO145
300 FORI=1TO12:NE(I)=0:NEXT:GOSUB3:GOSUB4:PRINT@0,"Now it's MY turn!"
310 TR=0:TS=0:UQ=0:FORI=1TO9:UQ=UQ+NE(I):NEXT:IF(UQ=9)OR((UQ=8)AND(NE(1)=0))THEN318ELSEGOSUB10:FORI=1TO900:NEXT:IF(NE(Z)=0)AND(Z<10)THENTR=1
311 FORI=5TO1STEP-1:IFNE(I)=1ANDI<>1THENNEXTELSEIFNE(I)=1ANDI=1THEN314
312 X3=0:Y3=0:FORJ=(I+1)TO9:IF(NE(J)=0)AND(I+J=Z)THENTS=1:X3=I:Y3=J:GOTO314ELSENEXTJ,I
314 IF(TR=1)AND(TS=0)THENPRINT@P(Z),P$;Z;Q$:NE(Z)=1ELSEIF(TR=0)AND(TS=1)THENPRINT@P(X3),P$;X3;Q$:NE(X3)=1:PRINT@P(Y3),P$;Y3;Q$:NE(Y3)=1ELSEIF(TR=1)AND(TS=1)THENGOSUB16ELSEGOTO318
316 GOTO310
318 CR=0:FORI=1TO9:IFNE(I)=0THENCR=CR+I
319 NEXT:GOSUB4:PRINT@0,"My score this round:"CR:CT=CT+CR:GOSUB520:FORI=1TO1500:NEXT
330 GOSUB4:IF((PT>44)AND(CT<45))OR((PT>44)AND(CT>44)AND(PT>CT))THENPRINT@0,"I WIN!":CG=CG+1ELSEIF((PT<45)AND(CT>44))OR((PT>44)AND(CT>44)AND(PT<CT))THENPRINT@0,"YOU WIN!":PG=PG+1ELSEIF(PT>44)AND(CT>44)AND(PT=CT)THENPRINT@0,"TIE!":TG=TG+1ELSEGOTO90
343 FORI=1TO1000:NEXT
345 IFTG=1THENQR$=""ELSEQR$="S
350 CLS:PRINT@90,"YOU HAVE WON"PG:PRINT@130,"I HAVE WON"CG:PRINT@170,"WITH"TG"TIE"QR$
360 PRINT@250,"Play again? (Y/N)? ";:GOSUB2:IFA$="Y"ORA$="y"THENPT=0:CT=0:GOTO85ELSEIFA$="N"ORA$="n"THENMENUELSEGOTO360
400 PRINT@240,"YOU";:PRINT@280,"ME";:FORJ=0TO6STEP6:FORI=52TO55STEP3:LINE(28,I+J)-(208,I+J):NEXTI,J:FORI=32TO208STEP4:LINE(I,53)-(I,54):LINE(I,59)-(I,60):NEXT:FORI=275TO315STEP40:PRINT@I,0;:NEXT:RETURN
500 IFPT>45THENTP=45ELSETP=PT
510 FORJ=PHTO((TP*4)+28)STEP4:FORI=53TO54:LINE(28,I)-(J,I):NEXT:PI=PI+1:PRINT@275,PI;:FORK=1TO25:NEXTK,J:PH=TP*4+28:PI=TP-1:IFPT>45THENFORI=46TOPT:PRINT@275,I;:FORK=1TO25:NEXTK,I:RETURNELSERETURN
520 IFCT>45THENTC=45ELSETC=CT
530 FORJ=CHTO((TC*4)+28)STEP4:FORI=59TO60:LINE(28,I)-(J,I):NEXT:CI=CI+1:PRINT@315,CI;:FORK=1TO25:NEXTK,J:CH=TC*4+28:CI=TC-1:IFCT>45THENFORI=46TOCT:PRINT@315,I;:FORK=1TO25:NEXTK,I:RETURNELSERETURN
