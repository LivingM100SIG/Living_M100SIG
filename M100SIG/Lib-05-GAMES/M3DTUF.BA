0 REM M3DTUF.BA adapted by Tim Peters [72227,2416] from Tim Ekdom's [72575,1473] MAZE3D.BA, in turn based on Caves of Ice by Robert Tsuk published in COMPUTE!
1 GOTO70
2 P1=I:P2=J:P3=K
3 IFD=1THENOK=P2<Z:P2=P2+1ELSEIFD=2THENOK=P2>0:P2=P2-1ELSEIFD=3THENOK=P1<Z:P1=P1+1ELSEIFD=4THENOK=P1>0:P1=P1-1ELSEIFD=5THENOK=P3<Z:P3=P3+1ELSEOK=P3>0:P3=P3-1
4 RETURN
5 CLS:LINE(41,15)-(96,50),1,B:LINE(27,1)-(41,15):LINE(96,15)-(110,1):LINE(28,63)-(41,50):LINE(96,50)-(109,63):LINE(114,1)-(115,63),1,B:LINE(22,1)-(23,63),1,B:RETURN
6 LINE(60,50)-(76,24),1,BF:FORB=0TO5:LINE(64-B,46+B)-(72+B,46+B),2:NEXT:LINE(61,25)-(63,27),2:LINE(75,25)-(73,27),2:LINE(64,46)-(72,28),2,BF:RETURN
7 RETURN
8 LINE(109,15)-(109,63):LINE(108,16)-(104,20):LINE(104,21)-(108,58),1,BF:LINE(105,59)-(108,62),2:RETURN
9 LINE(28,15)-(28,63):LINE(29,16)-(33,20):LINE(29,21)-(33,58),1,BF:LINE(29,62)-(32,59),2:RETURN
10 FORB=0TO5:LINE(61-B,7-B)-(75+B,7-B):NEXT:LINE(62,6)-(74,3),2,BF:RETURN
11 FORB=0TO5:LINE(61-B,58+B)-(75+B,58+B):NEXT:LINE(62,59)-(74,62),2,BF:RETURN
12 A=INKEY$:IFA=""THEN12ELSEIFA<"a"ORA>"z"THENRETURNELSEA=CHR$(ASC(A)-32):RETURN
13 R=RND(1):GOSUB14:?@222,A1;K$:?@262,A2;K$:FORB=1TO900:NEXT:?@222,K$:?@262,K$:RETURN
14 IFR<.3THENA1="Sorry, you can't":A2="go that way.":RETURN
15 IFD<5THENIFR<.6THENA1="You can't walk":A2="through walls!!":RETURNELSEIFR<.9THENA1="Oops! You bumped":A2="into a wall!":RETURN
16 IFR<.9ANDD=5THENA1="There's no way":A2="to go up!!":RETURN
17 IFR<.9ANDD=6THENA1="There's no door":A2="on the floor!":RETURN
18 A1="Getting":A2="frustrated?":RETURN
19 IFF=1THEND$="North"ELSEIFF=2THEND$="South"ELSEIFF=3THEND$="East"ELSED$="West
20 ?@62,"Facing: "P$" "D$" "Q$:RETURN
21 ?@142,H$:RETURN
22 K=I+I-2:?@142+K,P$MID$(H$,K+1,1)Q$:FORB=1TO40:NEXT:RETURN
23 ?@222,"Position: "STR$(P1+1)STR$(P2+1)STR$(P3+1):RETURN
24 ?@262,"Exit:     "STR$(E1+1)STR$(E2+1)STR$(E3+1):RETURN
25 ?@62,"Face which way?":GOSUB21
26 GOSUB12:IFA=CHR$(13)THENRETURN
27 I=INSTR("NSEW",A):IFITHENF=I:GOTO22ELSEBEEP:GOTO26
50 F=1
52 GOSUB5:FORN=1TO6:IFM(P1,P2,P3)ANDP(N)THENONF(F,N)GOSUB6,7,8,9,10,11
54 NEXT:GOSUB19:GOSUB21
56 IFPTHENGOSUB23
58 IFEXTHENGOSUB24
60 GOSUB12:I=INSTR("NSEWUD?*F",A):IFITHENGOSUB22ELSEBEEP:GOTO60
62 D=I:IFD<7THENIFM(P1,P2,P3)ANDP(D)THENGOSUB3:IFOKTHEN52ELSE86ELSEBEEP:GOSUB13:GOSUB21:GOTO56
64 IFD=7THENEX=NOTEX:IFEXTHENGOSUB24ELSE?@262,K$
66 IFD=8THENP=NOTP:IFPTHENGOSUB23ELSE?@222,K$
68 IFD=9THENGOSUB25:GOTO52ELSEGOSUB21:GOTO60
70 DEFINTA-Z:DEFSNGR:DEFSTRA:CLS:DIMD,B,P1,P2,P3:Z1=5:INPUT"Maze size";Z1:Z3=Z1^3:Z=Z1-1:M=Z3/3+40:DIMM(Z,Z,Z),I(M),J(M),K(M),P(6),D(5),F(4,6):A=CHR$(27):FORI=1TO6:P(I)=2^I:NEXT
72 K$=A+"K":P$=A+"p":Q$=A+"q":GOSUB5:GOSUB6:GOSUB8:GOSUB9:GOSUB10:GOSUB11:KEY(4)ON:KEY(8)ON:ONKEYGOSUB,,,94,,,,92:?@65,P$" MAZE 3D "Q$:LINE(150,7)-(203,7):LINE(152,5)-(205,5)
74 LINE(205,6)-(205,13):?@143,"Initializing...":?@223,"Timing starts":?@263,"at the BEEP!";:FORN=1TO4:FORI=1TO6:READF(N,I):NEXT:NEXT
76 FORB=1TO(FRE(0)+VAL(RIGHT$(TIME$,2)))MOD200:R=RND(1):NEXT:B=1:FORG=1TOZ3:?@182,G"of"Z3:D=RND(1)*B:B=B-1:I=I(D):I(D)=I(B):J=J(D):J(D)=J(B):K=K(D):K(D)=K(B):N=0:FORD=1TO6:GOSUB2
78 IFOKTHENM=M(P1,P2,P3):IFMAND128THEND(N)=D:N=N+1ELSEIFM=0THENM(P1,P2,P3)=1:I(B)=P1:J(B)=P2:K(B)=P3:B=B+1
80 NEXT:D=D(RND(1)*N):M(I,J,K)=P(D)OR128:IFNTHENGOSUB2:M(P1,P2,P3)=M(P1,P2,P3)ORP((D-1XOR1)+1)
82 NEXT:E1=RND(1)*Z1:E2=RND(1)*Z1:E3=RND(1)*Z1:D=RND(1)*6+1:IFD=1THENE2=ZELSEIFD=2THENE2=0ELSEIFD=3THENE1=ZELSEIFD=4THENE1=0ELSEIFD=5THENE3=ZELSEE3=0
84 M(E1,E2,E3)=M(E1,E2,E3)ORP(D):P1=RND(1)*Z1:P2=RND(1)*Z1:P3=RND(1)*Z1:BEEP:AB=TIME$:H$="N S E W U D ? * F ":GOTO50
86 CLS:A=TIME$:I=VAL(RIGHT$(A,2))-VAL(RIGHT$(AB,2)):J=VAL(MID$(A,4))-VAL(MID$(AB,4)):IFI<0THENI=I+60:J=J-1
88 K=1:?@250,"Time to win ="STR$(J-60*(J<0))":"RIGHT$(STR$(100+I),2):A=" YOU WIN!! ":FORB=1TO29:SOUND(7750+K*B*250),5:IFK<0THEN?@174,AELSE?@174,P$A;Q$
90 K=-K:NEXT:CLS:?@125,"Play again? (Y/N)";:GOSUB12:IFA="Y"THENRUN
92 MENU
94 RUN:DATA1,2,3,4,5,6,2,1,4,3,5,6,4,3,1,2,5,6,3,4,2,1,5,6
