1 'OFFICIAL YODERHAY BARDICE, VERSION BARDCE.101
2 'by DON ZEIKEL [75775,1430] 1984
3 GOTO10
4 A$=INKEY$:IFA$=""THEN4ELSERETURN
5 PRINT@285,CHR$(27)"p PRESS ANY KEY TO CONTINUE "CHR$(27)"q";:GOSUB4:CLS:RETURN
10 CLEAR:CLS:DEFINTA-Z
11 N$="NOTHING in":P$="Press any key to roll.":Y$="You have":R$=" roll"
12 U=0:V=0:W=239:X=63:FORI=1TO5:LINE(U,V)-(W,X),1,B:U=U+5:V=V+5:W=W-5:X=X-5:NEXT
15 GOSUB9000
27 GOSUB7000
30 GOTO800
31 FORI=1TO6:C(I)=0:NEXT:RETURN
32 E=0:F=0:G=0:L=0:M=0:N=0:H=0:R=0:RETURN
36 FORI=1TO3000:NEXT:RETURN
37 PRINT@200,SPACE$(119);:RETURN
38 FORI=2TO6:IFC(1)+C(I)=5THENI=6:GOSUB3000:GOSUB36:GOTO2000ELSENEXT:RETURN
39 RETURN
40 GOSUB4
45 Z=0
50 PRINT@0,SPACE$(200);:R=R+1:FORI=1TO5-H:DT=6*RND(1)+1
55 C(DT)=C(DT)+1
60 FORJ=1TO9:DUM=RND(1):NEXT
65 FORRK=1TO6:ONRKGOSUB100,300,500,200,400,600:NEXT
70 ONDTGOSUB100,200,300,400,500,600
80 Z=Z+7
90 NEXT:IFC(1)=5THEN8000ELSERETURN
100 GOSUB700:PRINT@(40+Z),CHR$(245)SPACE$(5)CHR$(245):GOSUB720:PRINT@(120+Z),CHR$(245)SPACE$(5)CHR$(245):GOSUB710:RETURN
200 GOSUB700:GOSUB730:GOSUB770:GOSUB740:GOSUB710:RETURN
300 GOSUB700:GOSUB730:GOSUB720:GOSUB740:GOSUB710:RETURN
400 GOSUB700:GOSUB750:GOSUB770:GOSUB760:GOSUB710:RETURN
500 GOSUB700:GOSUB750:GOSUB720:GOSUB760:GOSUB710:RETURN
600 GOSUB700:GOSUB750:PRINT@(80+Z),CHR$(245)CHR$(157)SPACE$(3)CHR$(157)CHR$(245):GOSUB760:GOSUB710:RETURN
700 PRINT@(0+Z),CHR$(240)STRING$(5,241)CHR$(242):RETURN
710 PRINT@(160+Z),CHR$(246)STRING$(5,241)CHR$(247):RETURN
720 PRINT@(80+Z),CHR$(245)SPACE$(2)CHR$(157)SPACE$(2)CHR$(245):RETURN
730 PRINT@(40+Z),CHR$(245)CHR$(157)SPACE$(4)CHR$(245):RETURN
740 PRINT@(120+Z),CHR$(245)SPACE$(4)CHR$(157)CHR$(245):RETURN
750 PRINT@(40+Z),CHR$(245)CHR$(157)SPACE$(3)CHR$(157)CHR$(245):RETURN
760 PRINT@(120+Z),CHR$(245)CHR$(157)SPACE$(3)CHR$(157)CHR$(245):RETURN
770 PRINT@(80+Z),CHR$(245)SPACE$(5)CHR$(245):RETURN
800 CLS:PRINT@85,"You go first.":PRINT@165,P$:TR=0:PL=1:CO=0:PH=0:CH=0:GOSUB40:GOTO870
810 CLS:PRINT@85,"Bring them back!":PRINT@165,P$:TR=0:PL=1:CO=0:GOSUB40
870 IFC(1)=0ANDR<3THENPRINT@200,N$;R:PRINTP$;:GOSUB31:GOSUB40:GOTO870
880 IFC(1)=0ANDR=3THENGOSUB37:PRINT@200,"NOTHING ALL DAY!":PRINT"That's easy to beat!";:E=0:F=0:G=3:GOSUB36:GOTO2000
885 IFC(1)>0ANDR=3ANDTR=0THENGOSUB3000:GOSUB36:GOTO2000
887 IFTR=0ANDR<3ANDC(1)>0THENGOSUB38
890 IFTR=0ANDR<3ANDC(1)>0THENGOSUB37:PRINT@200,"[F]arm,":PRINT"[S]tand, or":PRINT"[K]eep the one's ?";ELSEGOTO970
900 GOSUB4
910 IFA$="S"ORA$="s"THENGOSUB3000:GOSUB36:GOTO2000
920 IFA$="K"ORA$="k"THENGOSUB37:PRINT@200,Y$;C(1)"dots.":PRINTP$:H=C(1):FORI=2TO6:C(I)=0:NEXT:GOSUB40:GOTO885
930 IFA$="F"ORA$="f"THENGOSUB37:PRINT@200,"Which number do you want to farm?":GOTO940ELSEGOTO900
940 B$=INKEY$:IFB$=""THEN940
950 B=VAL(B$):IFB>6ORB<2THEN940
960 IFC(B)=0THEN940
970 IFTR=0THENH=C(1)+C(B):Z=0:E=H:F=B:G=R:GOSUB37:PRINT@200,Y$;E;F"'s in"G:PRINTP$;:TR=1:GOSUB40
980 IFTR=1THENGOSUB37:E=C(1)+C(F):G=R:GOSUB37:PRINT@200,Y$;E;F"'s in"G:IFE=5ORR=3THENGOSUB36:GOTO2000
982 IFTR=1ANDR<3THENPRINT"[R]oll again or":PRINT"[S]tand";ELSE2000
985 GOSUB4
990 IFA$="S"ORA$="s"THENGOTO2000
995 IFA$="R"ORA$="r"THENH=C(1)+C(F):GOSUB45:GOTO980
997 GOTO985
2000 PL=0:CO=1:H=0:R=0:GOSUB37
2010 GOSUB31
2015 IFE=0THENPRINT@200,"All I need is ONE LITTLE DOT!":PRINT"Here goes a roll.":GOSUB36:GOSUB45:GOSUB3000:GOTO4000
2030 IFE=0ANDC(1)=0ANDR=3GOTO6000
2040 IFE=0ANDC(1)>0THENGOSUB3000:GOTO4000
2050 IFE>0ANDL=0THENPRINT@200,"I must beat"E;F"'s in"G:PRINT"Here goes a"R$"!";:GOSUB36:GOSUB45:GOSUB3000:GOTO4000
2060 IFE>0ANDL>0ANDR<GTHENGOSUB37:PRINT@200,"I will"R$" again":PRINT"to beat"E;F"'s in"G:GOSUB36:H=L:GOSUB31:GOSUB45:L=L+C(1)+C(M):N=N+1:GOSUB3020:GOTO4000
3000 Q=0:RH=0:IFCO=1ANDC(1)=0THENGOSUB37:PRINT@200,N$;R:L=0:M=0:N=R:IFCC=1ANDE=0THENCC=0:GOSUB36:GOTO6000ELSERETURN
3002 CC=0:IFPL=1ANDC(1)=0THENGOSUB37:PRINT@200,N$;R:RETURN
3003 FORI=2TO6:IFC(I)>=QTHENRH=I:Q=C(I)
3005 NEXT
3010 IFPL=1THENE=C(RH)+C(1):F=RH:G=RELSEL=C(RH)+C(1):M=RH:N=R
3020 GOSUB37:IFPL=1THENPRINT@200,Y$;E;F"'s in"GELSEPRINT@200,"I have"L;M"'s in"N
3025 IFCO=1ANDN<GANDE=5ANDM<FTHENPRINT"I will save my dots and"R$" again";:L=0:M=0:H=C(1):FORI=2TO6:C(I)=0:NEXT:GOSUB36:GOSUB36:GOSUB45:GOTO3000
3030 RETURN
4000 GOSUB36:GOSUB37:IFL>EOR(L=EANDM>F)THENPH=PH+1:PRINT@200,"A horse upon you!":GOSUB36:GOTO5000
4020 IF(E>LOR(E=LANDF>M))ANDR=GTHENCH=CH+1:PRINT@200,"A horse upon me!":GOSUB36:GOTO5000
4025 IFE=LANDF=MANDG=NGOTO6000
4030 GOTO2010
5000 IFPH=>2THENCW=CW+1:GOSUB37:PRINT@200,"I WIN THIS ONE!";
5010 IFCH=>2THENPW=PW+1:GOSUB37:PRINT@200,"YOU WIN THIS ONE!";
5012 IFPH<2ANDCH<2THEN5020
5015 GOSUB36:CLS:PRINT@45,Y$" won"PW"games":PRINT@85,"I have won"CW"games":PRINT:PRINT@165,"[P]lay again or":PRINT@205,"[Q]uit?
5017 GOSUB4
5018 IFA$="P"ORA$="p"THENCLS:GOSUB31:GOSUB32:GOTO800
5019 IFA$="Q"ORA$="q"THENMENUELSE5017
5020 GOSUB31:GOSUB32:GOTO810
6000 GOSUB37:PRINT@200,"TIE !!!":PRINT"One flop each!":PRINTP$;:GOSUB31:GOSUB32:PL=1:CO=0:GOSUB40:GOSUB3000:GOSUB36:CC=1:GOTO2000
7000 CLS:PRINT@85,"Instructions ? (Y/N) ";
7010 GOSUB4
7020 IFA$="N"ORA$="n"THENRETURN
7030 IFA$="Y"ORA$="y"THENCLSELSEGOTO7010
7040 PRINT@14,"INSTRUCTIONS":PRINT" This is a dice game popular in bars     throughout Northeast Wisconsin, ";
7045 PRINT"where   the clank of the dice cup can be heard
7050 PRINT" amid cries of":PRINT" "CHR$(34)"How about them Packers!?!"CHR$(34):GOSUB5
7060 PRINT"In this version, you are playing againstme, and you always roll first.  You have5 dice and up to 3 ";
7065 PRINT"rolls to get the besthand.  You must get at least one ace to form a hand.  Otherwise, roll all dice  again.":GOSUB5
7070 PRINT"Aces may equal any other number you     roll.  Thus, 2 aces, one 3, and two 4's could be ";
7075 PRINTCHR$(34)"4 4's"CHR$(34)" or "CHR$(34)"3 3's."CHR$(34)" Since high numbers beat low, ";
7077 PRINT"and high counts beat  low counts, the first choice would be   the best.
7080 GOSUB5:PRINT"Examples: 4 4's beat 3 4's.  4 4's beat 4 3's.    5 2's beat 4 4's.  I get up toas many rolls ";
7085 PRINT"as you had; one flop each on the tie.  If you have less than 3    rolls and at least one ace you can";
7090 PRINT":     farm a number, save your aces only and  roll again, or stand.":GOSUB5
7095 PRINT"Five dots is an automatic loss.  Do not press a button unless told to.          ";
7100 PRINT"The loser of a round gets a "CHR$(34)"horse"CHR$(34)" and two horses loses a game.":PRINT"HAPPY PLAYING, HEY!"
7110 :GOSUB 5:RETURN
8000 GOSUB37:PRINT@200,"5 DOTS!!  AUTOMATIC LOSS!!";:GOSUB36
8010 IFPL=1THENPH=2:GOTO5000
8020 CH=2:GOTO5000
9000 PRINT@96,"OFFICIAL":PRINT@135,CHR$(27);"p YODERHAY ":PRINT@176,CHR$(27);"qBAR DICE"
9010 FM=3516:DM=3718:SM=4184:UM=2636:HM=2793:KM=2348:GM=3134:TM=3321
9020 FORII=1TO2:SOUNDFM,6:SOUNDDM,6:SOUNDFM,10:SOUNDSM,20:NEXT:SOUNDFM,6:SOUNDDM,6
9025 SOUNDFM,10:SOUNDUM,13:SOUNDKM,16:SOUNDUM,9:SOUNDUM,9
9030 SOUNDHM,20:SOUNDHM,6:SOUNDHM,6:SOUNDHM,10:SOUNDUM,13:SOUNDKM,16:SOUNDUM,9:SOUNDUM,9:SOUNDHM,20
9040 PRINT@251," by Don Zeikel, hey. ":SOUNDGM,12:SOUNDFM,12:SOUNDTM,12:SOUNDGM,12:SOUNDHM,12:SOUNDUM,25
9050 FORI=1TOINT((VAL(RIGHT$(TIME$,2)))*(VAL(MID$(TIME$,4,2)))*(VAL(MID$(DATE$,4,2)))/100):I3=RND(1):NEXT:RETURN
