10 'Grid-Runner 
12 DEFINTA-Z:TRUE=NOTFALSE:CLS:PRINT@55,"Grid-Runner":PRINT@121,"One player or Two (1/2):"; 
14 A$=INPUT$(1):IFA$="1"THENPRINT:PRINT:PRINT" The GridBug will play A. You are B.":A=1:ONKEYGOSUB,,,,,,,226:KEYON:goto16 
15 IFA$<>"2"THENRUNELSEA=0:ONKEYGOSUB218,220,222,224,,,,226:KEYON 
16 GOSUB20'randomstart 
18 IFNOTDNTHENGOSUB38:GOTO18ELSECLS:IFCRTHENC1=C1+100-C/3ELSEC0=C0+100-C/3 
19 DN=FALSE:PRINT@57,"Scores":PRINT@241,"A:";C0;:PRINT@271,"B:";C1:A$=INPUT$(1):IFA$=CHR$(13)THEN16ELSE19 
20 'randomstart 
22 T$=TIME$ 
24 X=RND(1):IFT$=TIME$THEN24ELSET$=TIME$ 
25 X=RND(1):IFT$=TIME$THEN25ELSEIFRND(1)>.151THENCLS 
26 P=RND(1)*240:Q=RND(1)*64 
28 R=RND(1)*240:S=RND(1)*64 
30 PD=RND(1)*4:RD=RND(1)*4 
32 PSET(P,Q):PSET(R,S) 
34 PRESET(P,Q):FORI=0TO100:NEXTI:SOUND10,10:PSET(P,Q):FORI=0TO100:NEXTI:A$=INKEY$:IFA$=""THEN34ELSESOUND5000,20:C=0:RETURN 
36 PRINTCHR$(27)+"P";:PRINT@INT(X/6)+40*INT(Y/8),;:Z=SGN((2^(YMOD8))ANDPEEK((XMOD6)-20)):PRINTCHR$(27)+"Q";:RETURN'ppoint 
38 'turns 
39 C=C+1 
40 GOSUB48'B: 
42 IFDNTHENRETURN 
44 IFATHENGOSUB84ELSEGOSUB192'A: 
46 RETURN 
48 'B: 
50 K$=INKEY$ 
52 IFK$=CHR$(29)THENDP=3 
54 IFK$=CHR$(28)THENDP=1 
56 IFK$=CHR$(30)THENDP=0 
58 IFK$=CHR$(31)THENDP=2 
60 ONDP+1GOTO62,64,66,68 
62 Q=Q-1:GOTO70 
64 P=P+1:GOTO70 
66 Q=Q+1:GOTO70 
68 P=P-1:GOTO70 
70 IFP<0THENDN=TRUE:CR=0:GOTO80 
72 IFP>239THENDN=TRUE:CR=0:GOTO80 
74 IFQ<0THENDN=TRUE:CR=0:GOTO80 
76 IFQ>63THENDN=TRUE:CR=0:GOTO80 
78 X=P:Y=Q:GOSUB36:IFZ=0THENDN=TRUE:CR=0 
80 IFDNTHENSOUND10000,50ELSEPSET(P,Q) 
82 RETURN 
84 'A:-automatic 
85 IFRND(I)<.05THENDR=INT(RND(I)*4) 
86 ONDR+1GOTO88,90,92,94 
88 S=S-1:GOTO96 
90 R=R+1:GOTO96 
92 S=S+1:GOTO96 
94 R=R-1:GOTO96 
96 IFR<0THENGOSUB144:IFDNTHEN106ELSE84 
98 IFR>239THENGOSUB156:IFDNTHEN106ELSE84 
100 IFS<0THENGOSUB168:IFDNTHEN106ELSE84 
102 IFS>63THENGOSUB180:IFDNTHEN106ELSE84 
104 X=R:Y=S:GOSUB36:IFZ=0THENGOSUB110 
106 IFDNTHENSOUND6000,50:CR=1ELSEPSET(R,S) 
108 RETURN 
110 'collisionavoidance 
112 ONDR+1GOTO116,128,140,142 
114 DN=TRUE:RETURN 
116 S=S+1 
118 IFR=0THENDR=1:R=R+1:X=R:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSERETURN 
120 IFR=239THENDR=3:R=R-1:X=R:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSERETURN 
122 X=R+1:Y=S:GOSUB36:IFZ=0THENDR=3:R=R-1:X=R:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSERETURN 
124 X=R-1:Y=S:GOSUB36:IFZ=0THENDR=1:R=R+1:RETURNELSEIFRND(1)>.5THENDR=1:R=R+1:RETURNELSEDR=3:R=R-1:RETURN 
126 RETURN 
128 R=R-1 
130 IFS=0THENDR=2:S=S+1:X=R:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSERETURN 
132 IFS=63THENDR=0:S=S-1:X=R:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSERETURN 
134 X=R:Y=S+1:GOSUB36:IFZ=0THENDR=0:S=S-1:X=R:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSERETURN 
136 X=R:Y=S-1:GOSUB36:IFZ=0THENDR=2:S=S+1:RETURNELSEIFRND(1)>.5THENDR=2:S=S+1:RETURNELSEDR=0:S=S-1:RETURN 
138 RETURN 
140 S=S-1:GOTO118 
142 R=R+1:GOTO130 
144 'leftwall 
146 R=0:IFS=0THENDR=2:RETURN 
148 IFS=63THENDR=0:RETURN 
150 X=R:Y=S-1:GOSUB36:IFZ=0THENDR=2:RETURN 
152 X=R:Y=S+1:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSEIFRND(1)>.5THENDR=0ELSEDR=2 
154 RETURN 
156 'rightwall 
158 R=239:IFS=0THENDR=2:RETURN 
160 IFS=63THENDR=0:RETURN 
162 X=R:Y=S-1:GOSUB36:IFZ=0THENDR=2:RETURN 
164 X=R:Y=S+1:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSEIFRND(1)>.5THENDR=0ELSEDR=2 
166 RETURN 
168 'top 
170 S=0:IFR=0THENDR=1:RETURN 
172 IFR=239THENDR=3:RETURN 
174 X=R-1:Y=S:GOSUB36:IFZ=0THENDR=1:RETURN 
176 X=R+1:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSEIFRND(1)>.5THENDR=1ELSEDR=3 
178 RETURN 
180 'bottom 
182 S=63:IFR=0THENDR=1:RETURN 
184 IFR=239THENDR=3:RETURN 
186 X=R-1:Y=S:GOSUB36:IFZ=0THENDR=1:RETURN 
188 X=R+1:Y=S:GOSUB36:IFZ=0THENDN=TRUE:RETURNELSEIFRND(1)>.5THENDR=1ELSEDR=3 
190 RETURN 
192 'A:-manual 
194 ONDR+1GOTO196,198,200,202 
196 S=S-1:GOTO204 
198 R=R+1:GOTO204 
200 S=S+1:GOTO204 
202 R=R-1:GOTO204 
204 IFR<0THENDN=TRUE:CR=1:GOTO214 
206 IFR>239THENDN=TRUE:CR=1:GOTO214 
208 IFS<0THENDN=TRUE:CR=1:GOTO214 
210 IFS>63THENDN=TRUE:CR=1:GOTO214 
212 X=R:Y=S:GOSUB36:IFZ=0THENDN=TRUE:CR=1 
214 IFDNTHENSOUND8000,50ELSEPSET(R,S) 
216 RETURN 
218 DR=3:RETURN 
220 DR=1:RETURN 
222 DR=0:RETURN 
224 DR=2:RETURN 
226 menu 
