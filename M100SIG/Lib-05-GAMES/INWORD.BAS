0 '   INWORD.BA        (C) 3/26/88
1 '   Wilson Van Alst  [76576,2735]
2 '   Requires 26 dictionary files
3 '   in the form "DICT-L.DO" (where
4 '   "L" is an alphabet letter) on
5 '   disk.  Multiple disks are OK.
6 '   Words in those files must be
7 '   all lower-case.  You must also
8 '   have a DOS that will access the
9 '   disk files with a "0:" prefix.
10 CLS:MAXFILES=2:DEFSTRA-F:DEFINTG-Z:E=CHR$(27)
12 PRINT@85,"Output File: ":PRINT@104,".DO":PRINT@98,"";:LINEINPUTB:OPENBFOROUTPUTAS2
14 CLS:PRINT@82,"Word <lwr. case>:";:LINEINPUTA
16 L=LEN(A):G=59-L/2:H=3*(39-L):CLS:LINE(H-18,5)-(H+6*L+20,18),1,B:PRINT@G,E"p "A" "E"q";:G=G+81
18 PRINT#2,"Words inside *** ";A;" ***"
20 FORZ=97TO122
22 IFINSTR(A,CHR$(Z))THENGOSUB26
24 NEXT:GOTO72
26 GOSUB66
28 IFEOF(1)THENRETURN
30 INPUT#1,D
32 PRINT@G,E"K"D;
34 IFLEN(D)>LEN(A)THEN28
36 FORI=2TOLEN(D):C=MID$(D,I,1)
38 IFINSTR(A,C)=0THEN28
40 NEXT
42 FORI=1TOLEN(D)-1
44 C=MID$(D,I,1)
46 IFINSTR(MID$(D,I+1),C)=0THENNEXT:GOTO64
48 R=0:FORJ=1TOLEN(A)
50 IFMID$(A,J,1)=CTHENR=R+1
52 NEXT:IFR<2THEN28
54 Q=0:FORJ=1TOLEN(D)
56 IFMID$(D,J,1)=CTHENQ=Q+1
58 NEXT
60 IFQ>RTHEN28
62 NEXT
64 SOUND800,1:PRINT#2,D:GOTO28
66 CLOSE#1:ONERRORGOTO68:F="0:DICT-"+CHR$(Z):OPENFFORINPUTAS1:SOUND800,1:SOUND700,2:SOUND800,3:RETURN
68 IFERR=52THENFORI=1TO10:SOUND10800,1:NEXTI:PRINT:PRINT@200,"CHANGE DISK":PRINT"(Then press any key to cont.)"
70 B="":B=INKEY$:IFB=""THEN70ELSEPRINT@G,E"J":RESUME
72 FORI=1TO10:SOUND10800,5:SOUND5400,2:NEXT:MAXFILES=1:MENU
