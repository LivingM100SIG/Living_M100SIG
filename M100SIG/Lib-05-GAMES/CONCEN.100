1 ' Concentration
2 ' 03/85 Rick LaBanca
3 ' Match 'em up on the 100!
10 CLS
14 DIM A(16),A$(16)
15 CLS
16 PRINT "           * Concentration *"
17 PRINT@80, "            by Rick LaBanca"
18 PRINT:PRINT:PRINT"Please wait....";CHR$(14);
19 FOR T=1 TO VAL(MID$(TIME$,7,2)+MID$(TIME$,4,2))/10:R=RND(1):NEXT T
20 GOSUB 500
30 TR=0:IT=8
100 FS=0:SS=0
105 GOSUB 1000
108 GOSUB 2000
110 PRINT@240,"  Your first guess  :";
120 LINEINPUT FS$
125 IF FS$="Menu" THEN 3000
130 FS=INT(VAL(LEFT$(FS$,2)))
140 IF FS<1 OR FS>16 THEN BEEP:GOTO 108
150 IF A(FS)=0 THEN BEEP:GOTO 108
155 GOSUB 1000
158 GOSUB 2000
160 PRINT@240,"  Your second guess :";
170 LINEINPUT SS$
175 IF SS$="Menu" THEN 3000
180 SS=INT(VAL(LEFT$(SS$,2)))
190 IF SS<1 OR SS>16 THEN BEEP:GOTO 158
200 IF A(SS)=0 THEN BEEP:GOTO 158
210 IF SS=FS THEN BEEP:GOTO 158
215 GOSUB 1000
220 IF A(FS)<>A(SS) THEN GOSUB 2000:PRINT@240,"  Sorry, no match there!":SOUND 10000,80:TR=TR+1:GOTO 100
225 GOSUB 2000
230 PRINT@240,"  A Match!! You win the ";A$(A(SS));"!"
240 FOR T=1000 TO -1000 STEP -40
250 SOUND ABS(T),1:SOUND ABS(T)+100,1:NEXT T
260 A(FS)=0
270 A(SS)=0
280 IT=IT-1:TR=TR+1
290 IF IT<>0 THEN 100
300 CLS
310 PRINT:PRINT:PRINT
320 PRINT"You have completed the board"
330 PRINT"in";TR;"tries!"
340 PRINT:PRINT
350 PRINT"<ENTER>=Another game, <F8>=Menu :";
360 LINEINPUT A$:IF A$<>"Menu" THEN RUN
370 MENU
500 FOR G=1 TO 2
505 FOR T=1 TO 8
510 R=INT(RND(1)*16)+1
520 IF A(R)<>0 THEN 510
530 A(R)=T
535 IF G=1 THEN READ A$(T)
540 NEXT T
545 NEXT G
590 RETURN
600 DATA "Car!","Boat!","T.V.","Hat"
610 DATA Chair,M-100,Phone,Bike
1000 PRINT@80,;
1010 FOR Y=0 TO 12 STEP 4
1015 PRINT"  ";
1020 FOR X=1 TO 4
1030 IF Y+X=FS OR Y+X=SS THEN PRINT"<<";A$(A(Y+X));STRING$(5-LEN(A$(A(Y+X))),32);">>";:GOTO 1060
1040 IF A(X+Y)<>0 THEN PRINT"<< ";:PRINT USING"##";X+Y;:PRINT"  >>";:GOTO 1060
1050 PRINT"<<----->>";
1060 NEXT X:PRINT:NEXT Y
1070 RETURN
2000 PRINT@240,STRING$(40,32);
2010 RETURN
3000 CLS
3010 PRINT:PRINT:PRINT
3020 PRINT"Quit to menu (Y/N)?";
3030 A$=(INPUT$(1))
3040 IF A$="y"OR A$="Y" THEN PRINT"Yes":MENU
3050 PRINT"No"
3055 CLS:PRINT"           * Concentration *"
3060 GOTO 100

