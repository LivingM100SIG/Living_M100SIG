5 'ASCII HEX to BINARY Conversion Program. Copyright A.Ryan June 1990
10 CLS:HEX$="0123456789ABCDEF":INPUT"Enter Data File Name:";F$:OPENF$FORINPUTAS1:INPUT#1,TP,EN,EX:CLS:AD=TP:CHK=TP+EN+EX:IFHIMEM>TPTHEN120
20 IFNOTEOF(1)ANDAD<=ENTHENINPUT#1,Z$ELSE40
30 FORI=1TOLEN(Z$)STEP2:HI$=MID$(Z$,I,1):LO$=MID$(Z$,I+1,1):V=(16*(INSTR(HEX$,HI$)-1))+(INSTR(HEX$,LO$)-1):PRINT@87,"Data:";V;"  ":PRINT@99,"Address:";AD:POKE AD,V:CHK=CHK+V:AD=AD+1:NEXT:GOTO20
40 IFNOTEOF(1)THENINPUT#1,Z$ELSE130
50 SUM=VAL(Z$):IFCHK<>SUMTHEN140
60 IFFRE(0)<6+(EN-TP)THENEND
70 PRINT"Do you wish to save program?"
80 Z$=INKEY$:IFZ$=""THEN80
90 IFZ$<>"Y"ANDZ$<>"y"THENEND
100 INPUT"Output File Name:";F$
110 SAVEM F$,TP,EN,EX:END
120 CLS:PRINT"Top  :";TP:PRINT"End  :";EN:PRINT"Exe  :";EX:PRINT"HIMEM:";HIMEM:PRINT"Memory Conflict. Reset HIMEM":END
130 PRINT"Unexpected End Of File":END
140 PRINT"Checksum Error. File Unusable!":END
