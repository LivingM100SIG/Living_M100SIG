1 'WORPR2.NEC translated to NEC dialect  from Eiji Miura's WORPRO.EM2
2 'by Murray Cohn 75166,2263.  Thanx Eiji!
10 GOSUB30:PRINTTAB(17)"WORPRO"
11 CLEAR2000:SCREEN0,0:MAXFILES=2:POKE64173!,0:DEFINTA-Z:DEFSTRW-Y:ONERRORGOTO140:GOSUB132:PRINTXA"V":GOTO32
12 K=0:IFRIGHT$(X,1)>" "ORLEN(X)<=M(0)THENRETURN
13 X=LEFT$(X,LEN(X)-1):K1=M(1)-LEN(X):IFK1=0THENRETURN
14 FORJ=1TOLEN(X)
15 XE=MID$(X,J,1):IFXE=" "THENNEXT
16 XJ=RIGHT$(X,LEN(X)-(J-1))
17 K2=1
18 IFK2=LEN(XJ)THEN17
19 XG=MID$(XJ,K2,1):IFXG<>" "THEN22
20 XU=LEFT$(XJ,K2):YU=RIGHT$(XJ,LEN(XJ)-LEN(XU)):XJ=XU+" "+YU:K=K+1:K2=K2+1:IFK=K1THEN23
21 IFK2=LEN(XJ)THEN17
22 K2=K2+1:GOTO18
23 X=SPACE$(J-1)+XJ:RETURN
24 XC=WC(BB):J=J-LEN(WC(BB)):RETURN
25 GOSUB30:PRINTTAB(10)"Printout in progress":RETURN
26 CLS:O$="":INPUT"Output file name";O$:IFO$=""THEN35ELSEIFLEN(O$)>6THEN26
27 RETURN
28 IFD=0ANDE=0THENB=0ELSEB=1
29 RETURN
30 CLS:BEEP:PRINT:PRINT:PRINT:RETURN
31 FORI=1TO1500:NEXT:RETURN
32 CLS:PRINTXA"p Files in memory "XA"q":FILES
33 LOCATE0,7:PRINT"File to format:";:LOCATE22,7:PRINT".DO";:LOCATE16,7:PRINT"";:LINEINPUTI$:IFI$=""THENMENU
34 OPENI$FORINPUTAS1
35 CLS:PRINT"1. "XS" (L/R/T/B)":PRINT"2. "XF:PRINT"3. Line spacing":PRINT"4. Output"
36 PRINT"5. Page numbering":PRINT"6. Pause between pages":PRINT"7. Print":PRINT"8. Exit";
37 FORI=23TO223STEP40:LOCATE(IMOD40),(I\40):PRINT":";:NEXT
38 FORI=1TO4:LOCATE20+I*4,0:PRINTUSINGXZ;M(I-1):NEXT:LOCATE24,1:PRINTF:LOCATE25,2:PRINTS:LOCATE25,3:PRINTYA(A)XK:LOCATE25,4:PRINTYB(B):LOCATE25,5:PRINTYC(C)
39 ONVAL(INKEY$)GOTO101,110,112,114,116,129,40,131:GOTO39
40 CLS:PRINT"1. Continue":PRINT"2. "XR" to menu"
41 ONVAL(INKEY$)GOTO42,35:GOTO41
42 CLS:XT="":PRINT"Title:  [ENTER] if no title":INPUTXT
43 IFLEN(XT)>M(1)-M(0)THENBEEP:PRINT:PRINT"Too long":GOSUB31:GOTO42ELSEIFXT=""THENT=0ELSET=1
44 CLS:IFA=0THENO$="LPT:"
45 IFA=1THENO$="COM:57I1E"
46 IFA=2THENO$="LCD:"
47 IFA=3THENGOSUB26
48 IFA=4THENGOSUB26:O$="CAS:"+O$
49 PRINTXA"W"
50 OPENO$FOROUTPUTAS2:IFA=3ORA=4THENGOSUB30:PRINTTAB(10)"Processing the file"
51 IFA<2THENGOSUB25ELSEN=1
52 W=SPACE$(M(0)):X=W:U=P
53 V=1
54 PRINT#2,"":V=V+1:IFV=3AND((U=PANDD=1)OR(U>PANDE=1))THENPRINT#2,SPACE$(M(1)-LEN(STR$(U)))U:V=V+1
55 IFV<=M(2)THEN54
56 IFT=1ANDU=PTHENPRINT#2,SPACE$(((M(1)-M(0))-LEN(XT))/2+M(0))XT;:FORI=1TO3:PRINT#2,"":V=V+1:NEXT
57 IFV>M(3)THEN75
58 FORJ=LEN(X)TOM(1):XC=INPUT$(1,1):IFEOF(1)THENCLOSE1:O=1:GOTO61
59 AA=INSTR(XL,XC):ONAAGOTO87,88,95,96,98,99
60 BB=INSTR(XM,XC):IFBB<>0THENGOSUB24
61 X=X+XC:IFRIGHT$(X,1)=CHR$(10)THEN66
62 NEXT
63 IFMID$(X,J,1)=" "THEN65
64 J=J-1:GOTO63
65 Y=MID$(X,J+1,M(1)):X=MID$(X,1,J):GOTO67
66 X=LEFT$(X,LEN(X)-2)
67 IFMID$(X,M(0),3)=SPACE$(3)THEN70
68 IFMID$(X,M(0),1)=" "THENX=MID$(X,2,LEN(X)):GOTO68
69 IFG=1THENPRINT#2,SPACE$(((M(1)-M(0))-LEN(RIGHT$(X,LEN(X)-M(0))))/2)X:V=V+1:G=0:GOTO72
70 IFR=1THENGOSUB12
71 PRINT#2,X"":V=V+1
72 IFO=1THEN75
73 IFS=2THENPRINT#2,"":V=V+1
74 X=W+Y:Y="":GOTO57
75 PRINT#2,"":V=V+1:IFV=M(3)+2AND((U=PANDD=2)OR(U>PANDE=2))THENPRINT#2,SPACE$(((M(1)-M(0))/2)-(LEN(STR$(U))/2)+M(0))U:V=V+1
76 IFV=F+1THEN77ELSE75
77 IFO=1THENCLOSE2:GOTO80
78 IFC=1ANDA<2THENGOSUB85
79 U=U+1:GOTO53
80 NN=NN+1:IFNN<NTHEN83
81 CLS:PRINT"1. Continue":PRINT"2. Print another page":PRINT"3. Exit"
82 ONVAL(INKEY$)GOTO11,83,84:GOTO82
83 OPENI$FORINPUTAS1:O=0:GOTO44
84 MENU
85 CLS:GOSUB30:PRINTTAB(6)"Press [ENTER] for next page"
86 IFINKEY$=CHR$(13)THENGOSUB25:RETURNELSE86
87 PRINT#2,"":V=V+1:IFV>M(3)THEN100ELSE87
88 WA=""
89 WB=INPUT$(1,1)
90 IFWB="-"THEN93
91 IFWB=CHR$(129)THEN94
92 WA=WA+WB:GOTO89
93 M(0)=VAL(WA):GOTO88
94 M(1)=VAL(WA):W=SPACE$(M(0)-1):GOTO100
95 G=1:GOTO100
96 IFS=1THENS=2ELSES=1
97 GOTO100
98 R=0:GOTO100
99 R=1
100 XD=INPUT$(1,1):IFXD=CHR$(10)THENX=W:GOTO57ELSE100
101 CLS:PRINTXS:PRINT"1. "X(0):PRINT"2. "X(1):PRINT"3. "X(2):PRINT"4. "X(3):PRINT"5. "X(1)" justify":PRINT"6. "XR
102 FORI=57TO217STEP40:LOCATE(IMOD40),(I\40):PRINT":":NEXT:FORI=0TO3:LOCATE(I*40+58)MOD40,(I*40+58)\40:PRINTUSINGXZ;M(I):NEXT
103 LOCATE19,5:PRINTYD(R)
104 Q=VAL(INKEY$):ONQGOTO105,105,105,105,107,109:GOTO104
105 Q=Q-1:LOCATE0,7:PRINTX(Q);:INPUTN(Q):IFN(Q)<1THEN101
106 M(Q)=N(Q):GOTO101
107 IFR=1THENR=0ELSER=1
108 GOTO103
109 IF(M(3)=>F)ORM(0)=>M(1)ORM(2)=>M(3)THENGOSUB30:PRINTTAB(14)XS" error":GOSUB31:GOTO101ELSE35
110 CLS:PRINTXF;:INPUTL:IFL<=M(3)THENBEEP:PRINT:PRINT"Shorter than "X(3)XS:GOSUB31:L=FELSEF=L
111 GOTO35
112 IFS=1THENS=2ELSES=1
113 LOCATE25,2:PRINTS:GOTO39
114 IFA=4THENA=0ELSEA=A+1
115 LOCATE25,3:PRINTYA(A)XK:GOTO39
116 CLS:PRINT"Page numbering":PRINT"1. First page":PRINT"2. Rest of pages":PRINT"3. Starting page #":PRINT"4. "XP:PRINT"5. "XR
117 FORI=60TO180STEP40:LOCATEIMOD40,I\40:PRINT":":NEXT
118 LOCATE24,1:PRINTY(D)XK:LOCATE24,2:PRINTW(E)XK:LOCATE23,3:PRINTUSINGXZ;P:LOCATE23,4:PRINTUSINGXZ;N
119 ONVAL(INKEY$)GOTO120,122,124,126,128:GOTO119
120 IFD=2THEND=0ELSED=D+1
121 GOTO118
122 IFE=2THENE=0ELSEE=E+1
123 GOTO118
124 LOCATE0,7:PRINT"Page #";:INPUTH:IFH>0THENP=H
125 GOTO116
126 LOCATE0,7:PRINTXP;:INPUTM:IFM>0THENN=M
127 GOTO116
128 GOSUB28:GOTO35
129 IFC=1THENC=0ELSEC=1
130 LOCATE25,5:PRINTYC(C):GOTO39
131 CLS:BEEP:PRINT"Sure?";:XQ=INPUT$(1):IFXQ="Y"ORXQ="y"THENMENUELSE35
132 M(0)=10:M(1)=70:M(2)=6:M(3)=60:F=66:S=1:A=0:C=0:D=2:E=2:R=1:P=1:N=1:Z=0
133 X(0)="Left":X(1)="Right":X(2)="Top":X(3)="Bottom ":XR="Return":XF="Form length":XS="Margin"
134 XA=CHR$(27):XK=XA+"K":XN="No   ":XH="Yes  ":XZ="###":XL=CHR$(128)+CHR$(129)+CHR$(132)+CHR$(135)+CHR$(147)+CHR$(148)
135 XV="Printer  ":YA(0)=XV+"<P>":YA(1)=XV+"<S>":YA(2)="LCD":YA(3)="RAM":YA(4)="Cassette":YB(0)=XN:YB(1)=XH:YC(0)=XN:YC(1)=XH
136 Y(0)=XN:Y(1)=XH+X(2):Y(2)=XH+X(3):W(0)=XN:W(1)=Y(1):W(2)=Y(2):YD(0)=XN:YD(1)=XH:XP="Number of copies"
137 IFZ=0THEN139
138 A1=A1+1:READB1,B2:XM=XM+CHR$(B1):FORI=1TOB2:READB3:WC(A1)=WC(A1)+CHR$(B3):NEXT:IFA1<ZTHEN138
139 L=F:GOSUB28:RETURN
140 GOSUB30
141 IFERL=34AND(ERR=52ORERR=55)THENPRINTTAB(13)"File not found":GOSUB31:RESUME32
142 IFERL=50AND(ERR=53ORERR=55)THENPRINTTAB(13)"Bad file name":GOSUB31:RESUME44
143 IFERL=58ANDERR=54THENGOSUB30:PRINTTAB(15)"Empty file":GOSUB31:RESUME11
144 PRINT"ERROR #"ERR" IN LINE"ERL:STOP
200 'Define DATA for printer codes here
