1 'JOG.BA by P Cooperberg, Sep 27,1984
5 CLS
7 DEFINT T,M,D 'dt1$=first day,df=#of day1,t=tot miles, m=miles,d=day of year,td=today,ml=today's miles,mw=week's miles
10 MAXFILES=2
20 PRINT@40,"How many miles did you run today";:INPUTML
25 DATA0,31,59,90,120,151,181,212,243,273,304,334
30 DIMD1%(12)
35 FORI=1TO12:READD1%(I):NEXT
40 MO=VAL(LEFT$(DATE$,2)):DA=VAL(MID$(DATE$,4,2)):TD=D1%(MO)+DA
42 ON ERROR GOTO500
43 OPEN "ram:jogdat.do" FOR INPUT AS 1
44 INPUT#1,DT1$
45 MO=VAL(LEFT$(DT1$,2)):DA=VAL(MID$(DT1$,4,2)):DF=D1%(MO)+DA
50 CLS
55 FORZ=1TO11STEP5:READP:PRINT@P,USING"##";Z;:LINE(12,64-Z*3.5)-(13,64-Z*3.5):NEXT
56 DATA280,200,120
57 LINE(14,63)-(14,23)
60 LINE(TD-DF+16,63-ML*3.5)-(TD-DF+16,63)
70 OPEN"ram:jogtmp.do"FOR OUTPUT AS 2
86 M=ML
88 PRINT#2,DT1$
90 INPUT#1,D,M:IFM>0THEN GOTO400
93 IFD<>399 THEN90
95 CLOSE#1
100 D=TD:M=ML:PRINT#2,D;M:PRINT#2,399;-6:CLOSE#2
150 T=T+ML
160 PRINT@0,"Total=";T;"miles since "DT1$"(av="INT(T/(TD-DF+1)*10)/10") and "MW+ML" miles this past week"
200 KILL"jogdat.do":NAME"ram:jogtmp.do"AS"ram:jogdat.do"
210 END
400 ' DRAW MILES AND AVERAGES AND SAVE        DATA IN JOGTMP
401 IF(TD-D)<7THENMW=MW+M
403 T=T+M
405 DD=D-DF+16:LINE(DD,63-M*3.5)-(DD,63)
407 AV=T/(D-DF+1)
408 PRESET(DD,63-3.5*AV)
410 PRINT#2,D;M
420 GOTO90
500 ' IF NO JOGDAT.DO FILE -- OPEN ONE 
505 IFERR=52 THEN OPEN "RAM:JOGDAT.DO" FOR OUTPUT AS 1:DT1$=DATE$:PRINT#1,DT1$:PRINT#1,399;-6:CLOSE#1:RESUME 
510 PRINT"Error code";ERR;"in line";ERL:STOP
