0 ' (c)1987 RONALD D. RICHE TypeII C-TYPE
1 MAXFILES=2:DEFINTA-Z
2 CLS:CLOSE:SCREEN 0,0:CLEAR800:ONERRORGOTO64
3 VR$="-ronnie-":PR$=""
4 L=66:OD$="0":S$="S":R=75:Z=5:OU$="Y":U$="Y":AU=1:I$="P":RAM$="TYPE.DO":UP$="4":SX$="SNGLspace":O=0:E$=CHR$(27):BK$=CHR$(8):TB$=CHR$(9):RT$=CHR$(13):ND$="K<":UN=2
5 AE$="###,###,###.":D$=AE$+"##":OPR$="NOP"
6 PRINT" TypeII  v2.1c  (c)1987 Ronald Riche"
7 KEYON:GOSUB37
8 PRINT@40,E$+"p Send output to printer? *Y/N "+E$+"q"+E$+"K";:TMP$=INPUT$(1)
9 IFTMP$=RT$THENOU$="Y":GOTO12
10 IFTMP$="Y"ORTMP$="y"THENOU$="Y":GOTO12
11 IFTMP$="N"ORTMP$="n"THENOU$="N":GOTO48:ELSESOUND1234,18:GOTO8
12 PRINT@40,E$+"p Position paper.. Press ANY KEY "+E$+"q";:AN$=INPUT$(1):IF(INP(187)AND6)=2THENPRINT@0,E$+"K":GOTO46:ELSESOUND1174,18:PRINT@0," Printer not ready!!..Fix printer"+E$+"K":GOTO8
13 IFS$="S"ORS$="s"THENL=L-1:GOTO19
14 L=L-2:GOTO19
15 PRINT@40,E$+"p New left margin or ENTER: "+E$+"q"+E$+"K ";:INPUTLE$:IFLE$=""THEN16:ELSEZ=VAL(LE$):IFZ>RORZ<0THENSOUND1234,18:GOTO15
16 PRINT@40,E$+"p Out(+)/In(-)dent#: "+E$+"q ENTER= ";VAL(OD$);E$+"K ";:INPUTOD$:O=VAL(OD$):IFZ-O<0ORZ-O>RTHENSOUND1234,18:OD$="":O=0:ELSEGOTO20
17 CLS
18 GOSUB37
19 PRINT@121,RIGHT$(L$,37)+E$+ND$
20 IFL<1THENSOUND2345,18:GOTO53:ELSE:PRINT@1,RIGHT$(OLD$,33)+E$+"K":PRINT@35,O:PRINT@40,E$"p TYPE:";L;"Lines to go      COLUMN="+E$+"q"+E$+"K"
21 PRINT@201,SX$+SPACE$(19)+"LPT:on ";:PRINT@259,Z;:PRINT@264,R;:PRINT@240," (c)1987 "+VR$;:IFOU$="N"OROU$="n"THENPRINT@233,"off"+E$+"K";
22 IFU$="Y"ORU$="y"THENPRINT@269,CHR$(34)+LEFT$(RAM$,9);:ELSEPRINT@269,E$+"K";
23 KEYON:PRINT@80,FM$+E$+"K"
24 Z$=SPACE$(Z-O):PRINT@75,LEN(Z$)+LEN(L$)-AD;
25 XX$=INKEY$:IFXX$=""THEN25:ELSEXX=ASC(XX$):IFXX>31ANDXX<127THENL$=L$+XX$:GOTO60:ELSEIFXX=13THEN26:ELSEIFXX=8THEN57:ELSEIFXX=27THEN50:ELSEIFXX=127THEN58:ELSEIFXX=9THEN59:ELSESOUND300,18:GOTO25
26 PRINT@80,E$"K Printing... ":PRINT@160,E$+"K"
27 GOSUB61:PRINT@120,E$+"K":IFOU$="Y"OROU$="y"THENLPRINTPR$;Z$;L$:IFS$<>"S"ANDS$<>"s"THENLPRINT
28 IFU$="Y"ORU$="y"THEN29ELSE32
29 IFL$=""THENPRINT#1,PR$:ELSEPRINT#1,PR$;Z$;L$
30 IFS$<>"S"ANDS$<>"s"THENPRINT#1,""
31 'END PRINT;CLEARVAR
32 L$="":PR$="":O=0:AD=0:FM$="":FN$="qwqwq":PRINT@80,E$+"K";
33 GOTO13
34 PRINT@40,E$+"p (R)estart (M)enu: "+E$+"q"+E$+"K";:AN$=INPUT$(1)
35 IFAN$="R"ORAN$="r"THENSOUND765,4:RUN0:ELSEIFAN$="M"ORAN$="m"THEN36:ELSESOUND1234,18:GOTO34
36 CLOSE:CLEAR:MENU
37 PRINT@280,E$+"p Go  Page Outp Spce Left Rght Cent Exit"+E$+"q";:ONKEYGOSUB17,53,8,40,15,43,38,34:RETURN
38 PRINT@40,E$+"p Enter text to be centered: "+E$+"q"+E$+"K":PRINT@121,E$+"K";:LINEINPUTCEN$:IFLEN(CEN$)>R-2THENBEEP:GOTO18
39 C=INT(LEN(CEN$)/2):L$=SPACE$(INT((R-Z)/2)-C)+CEN$:GOTO26
40 PRINT@40,E$+"p (S)ingle or (D)ouble spacing: "+E$+"q"+E$+"K";:TMP$=INPUT$(1):IFTMP$=RT$THEN42
41 S$=TMP$:IFS$="S"ORS$="s"THENSX$="SNGLspace"ELSESX$="DUBLspace"
42 GOTO20
43 PRINT@40,E$+"p New right margin or ENTER: "+E$+"q"+E$+"K ";:INPUTRI$:IFRI$=""THEN45
44 R=VAL(RI$):IFR<Z-OORR<ZTHENSOUND1234,18:RI$="":GOTO43
45 PRINT@161,E$+"K":GOTO20
46 PRINT@40,E$+"p Select RAM/DISK file?  Y/N* : "+E$+"q"+E$+"K";:U$=INPUT$(1):IFU$=RT$THENU$="N":ELSEIFU$="Y"ORU$="y"THENGOTO48
47 IFU$="N"ORU$="n"THENCLOSE#1:GOTO20:ELSESOUND1234,18:GOTO46
48 KEYOFF:CLOSE1:U$="Y":CLS:FILES:PRINT"Free memory=";:PRINTFRE(0):PRINT"[Name of file]  <ENTER> == "+RAM$:INPUTRAM$
49 OPENRAM$FORAPPENDAS1:GOTO17
50 PRINT@40,E$+"p DEL He Qk Ti Up Vw XY0 1/9 : "+E$+"q"+E$+"K";:AN$=INPUT$(1):IFAN$=CHR$(8)THEN67:ELSEAN=INSTR("1234567890UuTtVvQqXxYyHh",AN$)
51 IFAN=0THENSOUND1234,18:GOTO19:ELSEONANGOTO93,94,95,96,97,98,99,100,101,102,65,65,68,68,69,69,70,70,90,90,91,91,103,103
52 GOTO18
53 PRINT@0,E$+"K":PRINT@40,E$+"p (N)ew page or (S)et line count:"+E$+"q"+E$+"K";
54 AN$=INPUT$(1):IFAN$="N"ORAN$="n"THENL=66:GOTO20
55 IFAN$="S"ORAN$="s"THENPRINT@0,E$+"K Enter new line count..":PRINT@40,E$+"K Current count=";L;:INPUTLN$:L=VAL(LN$):GOTO20
56 SOUND1234,18:GOTO53
57 IFLEN(L$)=0THENSOUND300,6:GOTO25:ELSEL$=LEFT$(L$,LEN(L$)-1):ZZ$="":GOTO60
58 L$="":AD=0:GOTO60
59 PRINT@40,E$+"p Tab how many characters? 1/9 "+E$"q"+E$+"K";:TB$=INPUT$(1):TB=VAL(TB$):L$=L$+SPACE$(TB):IFLEN(L$)+Z>RTHENBEEP:GOTO19:ELSE19
60 PRINT@75,LEN(L$)+LEN(Z$)-AD;:PRINT@121,RIGHT$(L$,37)+E$+ND$;:IFLEN(L$)>(R+AD-LEN(Z$))THENBEEP:GOTO25:ELSEGOTO25
61 IFL$<>""THENOLD$=L$:RETURN:ELSERETURN
62 PRINT@80,E$"K Printing... ";:IFOU$="Y"OROU$="y"THENFORX=1TOUP:LPRINT:NEXT
63 IFU$="Y"ORU$="y"THENFORX=1TOUP:PRINT#1,"":NEXT:GOTO20:ELSE20
64 SOUND1678,24:IFERR=55THENRESUME48ELSECLS:PRINT@0,E$+"K Error ";ERR;" in line ";ERL;:PRINT@40,E$+"p Press any key to continue... "+E$+"q"+E$+"K";:TMP$=INPUT$(1):RESUME18
65 PRINT@0," ENTER (0) to exit"+E$+"K":PRINT@40,E$+"p How many lines up "+E$+"q Default= ";VAL(UP$);E$+"K";
66 INPUTUP$:UP=VAL(UP$):IFUP>LTHENSOUND1234,8:GOTO50:ELSEIFUP<1THEN20ELSEL=L-UP:GOTO62
67 PRINT@40,E$+"p Erase how many characters? "+E$+"q"+" ENTER= 0 "+E$+"K";:CK$=INPUT$(1):CK=VAL(CK$):IFCK>LEN(L$)THENSOUND900,3:GOTO19:ELSEL$=LEFT$(L$,LEN(L$)-CK):GOTO19
68 L$=L$+DAY$+"   "+DATE$+"  "+TIME$:GOTO19
69 FM$=" "+LEFT$(OLD$,33):GOTO19
70 GOTO86 'Calculator
71 PRINT@1,OPR$:PRINT@15,USINGD$;Y;:PRINTE$+"K"
72 PRINT@55,USINGD$;X;:PRINTE$+"K":RETURN
73 M2=M1:M1=Y:Y=X:GOSUB71:RETURN
74 QC=1:SC=0:GOSUB71:RETURN
75 X=X+Y:OPR$="X+Y":GOSUB74:RETURN
76 X=Y-X:OPR$="Y-X":GOSUB74:RETURN
77 X=X*Y:OPR$="X*Y":GOSUB74:RETURN
78 IFX=0THENBEEP:RETURNELSEX=Y/X:OPR$="Y/X":GOSUB74:RETURN
79 X=Y^X:OPR$="Y^X":GOSUB74:RETURN
80 X=VAL(B$):PRINT@55,USINGD$;X;:PRINTE$+"K";
81 A$=INKEY$:IFA$=""THEN81ELSEIFINSTR("0123456789.-",A$)=0THEN83
82 IFSC=1THENSC=0:B$=A$:GOTO80ELSEIFQC=1THENQC=0:B$=A$:GOSUB73:GOTO80ELSEIFLEN(B$)=15THENBEEP:GOTO81ELSEB$=B$+A$:GOTO80
83 IFA$=CHR$(8)ANDLEN(B$)>0THENB$=LEFT$(B$,LEN(B$)-1):GOTO80ELSEIFA$=RT$THENSC=1:GOSUB73:GOTO81:ELSESOUND1234,6:GOTO81
84 PRINT@40,E$+"p #dec 1/5: "+E$+"q";:F$=INPUT$(1):IFF$=""THEN84ELSEIFASC(F$)<48ORASC(F$)>53THENSOUND1234,8:GOTO84ELSED$=AE$+STRING$(VAL(F$),"#"):GOSUB71:PRINT@40,E$+"p Quick Calc "+E$+"q";:RETURN
85 M2=0:M1=0:Y=0:X=0:B$="":OPR$="NOP":GOSUB71:RETURN
86 SCREEN0,0
87 ONKEYGOSUB75,76,77,78,79,84,85,89:PRINT@280,E$+"p +   Y-X   *   Y/X  Y^X  #Dec Clr  Retn"+E$+"q";
88 PRINT@0,SPACE$(13)+E$+"KY=":PRINT@40,E$+"p Quick Calc "+E$+"q X="+E$+"K";:PRINT@160,E$+"K":GOSUB71:GOTO81
89 GOSUB37:GOTO19
90 PRINT@160,E$+"K X=";:PRINTUSINGD$;X;:GOTO18
91 PRINT@160,E$+"K Y=";:PRINTUSINGD$;Y;:GOTO18
92 'CUSTOM ROUTINES
93 FM$=" Default":Z=5:R=75:O=0:GOTO19
94 FM$=" Indents set":Z=10:R=70:O=0:GOTO19
95 FM$=" Default, outdent 3":Z=8:R=75:O=3:GOTO19
96 FM$=" Description":Z=30:R=75:O=25:GOTO19
97 FM$=" Rolodex card":Z=5:R=35:L=10:O=0:GOTO19
98 GOTO19
99 GOTO19
100 GOTO19
101 GOTO19
102 T=(1-(1+(Y/M1))^(-M1*M2))/(Y/M1):T=X/T:PRINT@160,E$+"K Payment/period = $";:PRINTUSINGD$;T;:GOTO19 'calculator routine
103 Z=5:R=75:O=0:L$="Ronald Riche, 1700 Aycock St, Arabi, LA  70032, CIS# 70337,3727":GOTO19
