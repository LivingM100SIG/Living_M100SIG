4 CLS:OPEN"RAM:GAS.DO"FOR APPEND AS 1:CLOSE:GOSUB 10010
10 CLS:PRINT:PRINT SPACE$(14);"MILEAGE CALC"
20 PRINT:PRINT TAB(10)"1) ENTER MILEAGE DATA"
30 PRINT TAB(10);"2) REVIEW MILEAGE DATA"
40 PRINT TAB(10);"3) PRINT MILEAGE DATA"
50 PRINT TAB(10);"4) END PROGRAM"
60 GOSUB 10000
70 I=VAL(I$):ON I GOSUB100,200,300,400
80 GOTO10
100 CLS:PRINT:PRINT:PRINT TAB(15);"GAS UNITS?"
105 PRINT:PRINT TAB(10);"<G>ALLONS  <L>ITERS"
110 GOSUB10000:IF I$="G" THEN GU$="GALLONS" ELSE IF I$="L" THEN GU$="LITERS" ELSE 100
120 CLS:PRINT:PRINT:PRINT"        ENTER ";GU$;" ADDED";:INPUT"";G1
125 CLS:PRINT:PRINT:INPUT"     ENTER ODOMETER READING ";OD
130 IF I$="L" THEN G1=G1/3.785
135 MT=OD-EM:CMPG=MT/G1:OPEN"RAM:GAS.DO" FOR APPEND AS 1:PRINT#1,USING"######.#";OD;G1;CMPG:CLOSE:GOSUB10010
140 CLS:PRINT:PRINT:PRINT TAB(9);USING"######.#";MT,:PRINT"     MILES"
141 PRINT TAB(13);USING"##.#";G1,:PRINT"     GALLONS":PRINT TAB(13);USING"##.#";CMPG,:PRINT"     M.P.G."
145 GOSUB10100:GOSUB10010:GOSUB10200:RETURN
200 OPEN"RAM:GAS.DO"FOR INPUT AS 1
205 CLS:C=0:PRINT"ODOMETER";TAB(15);"GALLONS";TAB(30);"M.P.G."
210 IF EOF(1) THEN CLOSE:GOSUB10100:GOSUB10200:RETURN
212 INPUT#1,I1,I2,I3
215 PRINTI1;TAB(15);I2;TAB(30);I3:C=C+1
220 IF C=5 AND EOF(1) THEN GOSUB10100:GOSUB10200:CLOSE:RETURN
225 IF C=5 THEN GOSUB10100:GOTO205
230 GOTO210
300 CLS:PRINT@136,"PRINTING":LPRINT"MILES","GALLONS","LITERS","MPG"
310 OPEN"RAM:GAS.DO"FOR INPUT AS 1
320 IF EOF(1) THEN CLOSE:GOTO360
330 INPUT#1,I1,I2,I3:L=I2*3.785
340 LPRINTI1,I2,USING"##.#";L;:LPRINT,I3
350 GOTO320
360 LPRINT:LPRINT:LPRINT:LPRINT"TOTAL MILES DRIVEN   ";EM-SM
370 LPRINT"TOTAL GAL OF GAS     ";G
380 LPRINT"AVERAGE M.P.G.        ";USING"##.#";MPG
390 RETURN
400 MENU
10000 I$=INKEY$:IF I$="" THEN 10000 ELSE RETURN
10010 N=1:G=0:MPG=0:OPEN"RAM:GAS.DO"FOR INPUT AS 1
10015 IF EOF(1) THEN CLOSE:GOTO10060
10020 INPUT#1,I1,I2,I3:IF I3=0 THEN SM=I1
10040 G=G+I2:MPG=MPG+I3:N=N+1:IF EOF(1) THEN10050 ELSE 10020
10050 CLOSE:EM=I1:TM=EM-SM:MPG=MPG/(N-2):RETURN
10060 CLS:PRINT:PRINT:INPUT"   STARTING MILEAGE?";SM
10070 OPEN"RAM:GAS.DO"FOR APPEND AS 1:PRINT#1,SM;",";0;",";0:CLOSE:EM=SM:RETURN
10100 PRINT:PRINT TAB(11);"<ANY KEY> TO CONT.";:GOSUB10000:RETURN
10200 CLS:PRINT TAB(15);"STATISTICS":PRINT
10210 PRINT TAB(5);"AVERAGE FILLUP ";USING"###.#";G/(N-2);:PRINT" GALLONS"
10215 PRINT TAB(5);"TOTAL GAS USED ";G;"GALLONS"
10220 PRINT TAB(5);"TOTAL MILES    ";EM-SM
10230 PRINT TAB(5);"AVERAGE MPG     ";:PRINT USING"##.#";MPG
10240 GOSUB10100:RETURN

