20 'INVENT.BA
40 DEFINTA-Z
60 DIMN$(20),Q(20),WP(20),DT$(20)
80 E$=CHR$(27):RS$=E$+"p":NS$=E$+"q"
100 CLS:PRINT@8,RS$"INVENTORY UPDATE & REVIEW"NS$
120 PRINT@87,"1-UPDATE EXISTING INVENTORY
140 PRINT@127,"2-ADD NEW TO INVENTORY
160 PRINT@167,"3-DISPLAY SUMMARY
180 PRINT@207,"9-EXIT
200 PRINT:INPUT" ENTER SELECTION NUMBER";A:ONAGOTO220,460,660,,,,,,780
220 CLS:PRINT@11,RS$"INVENTORY UPDATE"NS$:GOSUB800
240 PRINT"SELECT FROM PRODUCT LIST:
260 FORI=1TOK:PRINTI"-> "N$(I):NEXT
280 INPUT"ENTER SELECTION NUMBER";I
300 CLS:PRINT@8,RS$"UPDATE ITEM #"I"- "N$(I)NS$
320 PRINT@44,"LAST TOTAL & DATE ="Q(I)" ON  "DT$(I)
340 PRINT:INPUT"REVISED TOTAL QUANITY";Q(I)
360 INPUT"UNIT VALUE";WP(I)
380 INPUT"DATE";DT$(I):GOSUB880
400 PRINT"PRESS ANY KEY TO CONTINUE";
420 A$=INKEY$:IFA$=""THEN420
440 GOTO100
460 CLS:PRINT@6,RS$"ADD A NEW PRODUCT TO INVENTORY"NS$
480 PRINT:INPUT"PRODUCT NAME";N$
500 INPUT"STARTING QUANITY";Q
520 INPUT"UNIT VALUE";WP
540 INPUT"DATE";DT$
560 OPEN"INVDTA"FORAPPENDAS1
580 PRINT#1,N$","Q","WP","DT$:CLOSE
600 PRINT"PRESS ANY KEY TO CONTINUE";
620 A$=INKEY$:IFA$=""THEN620
640 GOTO100
660 CLS:PRINT@12,RS$"INVENTORY SUMMARY"NS$:GOSUB800
680 PRINTRS$" ITEM     QTY    UNIT    DATE    VALUE "NS$
700 FORI=1TOK
720 PRINTN$(I);TAB(9)Q(I);TAB(17)WP(I);TAB(25)DT$(I);TAB(33);Q(I)*WP(I):NEXT
740 A$=INKEY$:IFA$=""THEN740
760 GOTO100
780 MENU
800 OPEN"INVDTA"FORINPUTAS1
820 FORI=1TO999:INPUT#1,N$(I),Q(I),WP(I),DT$(I)
840 SOUND4000,1:K=I:IFEOF(1)THENGOTO860ELSENEXT
860 CLOSE:RETURN
880 OPEN"INVDTA"FOROUTPUTAS1
900 FORI=1TOK:SOUND3000,1
920 PRINT#1,N$(I)","Q(I)","WP(I)","DT$(I):NEXT:CLOSE:RETURN
