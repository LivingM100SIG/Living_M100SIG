0 'APPTR2.MPC 11/19/88 16:41
1 'TMPC Utility program 2 of 2
2 'by Jean-Luc Crevier CIS [73720,2456]
3 'See TMPCUT.DOC for documentation.
4 ONERRORGOTO39
5 CLEAR2000:MAXFILES=2:DEFINTA-Z:DIMAM$(50)
6 CLS:PRINT"TMPC Appnt/Memo Report Generator-Part 2":PRINT
7 PRINT"Resetting today's date!"
8 OPEN"TODAY"FORINPUTAS1
9 LINEINPUT#1,TD$:DATE$=TD$
10 LINEINPUT#1,TF$:CLOSE1
11 PRINT"Recovering safety task file!"
12 KILLTF$:NAME"SAFETY.DO"ASTF$
13 PRINT:PRINT"Sorting Diary"
14 I=1
15 OPEN"DIARY"FORINPUTAS1:PRINT@280,"Reading";
16 IFEOF(1)THEN20
17 PRINT@290,I;:LINEINPUT#1,AM$(I)
18 I=I+1
19 GOTO16
20 F=I-1:CLOSE1
21 PRINT:LINEINPUT"Report dev:filename? ";FO$:IFFO$=""THENFO$="LPT:"
22 CLS:OPENFO$FOROUTPUTAS1
23 PRINT@174,"Sorting"
24 I=I-1
25 X=0
26 X=X+1
27 IFX>ITHEN35
28 IFAM$(X)=CHR$(255)THEN26
29 FORY=1TOI
30 IFAM$(Y)<AM$(X)THENX=Y
31 NEXTY
32 S=S+1:PRINT#1,AM$(X):PRINT@182,X;:PRINT@214,S;"of"F;
33 AM$(X)=CHR$(255)
34 GOTO25
35 CLOSE1
36 CLS:PRINT@176,"Completed":BEEP:BEEP
37 KILL"TODAY.DO"
38 MENU
39 PRINT"Error"ERR"in line"ERL:SOUND12538,10
40 END
