1 CLEAR1000:MAXFILES=2:GOTO3010
1000 R=2
1010 OPEN"lcd:"FOROUTPUTAS2:ONERRORGOTO3050
1020 OPEN"cj"FORINPUTAS1
1025 IFEOF(1)GOTO1210
1027 GOSUB5500:INPUT#1,A$,B$,C$,D$,E$,F,G:IF C$="SB"THENSB=G-F:GOTO1025
1040 IFR=1ANDE$<>""GOTO1025
1050 FSUM=FSUM+F:GSUM=GSUM+G:IFLC>0GOTO1110
1065 IFLC=0GOTO1025
1070 IFF>0THENLPRINTTAB(43)F
1080 IFG>0THENLPRINTTAB(55)G
1100 GOTO1025
1110 LPRINTTAB(3)LEFT$(A$,8);TAB(12)D$;TAB(18)LEFT$(B$,25);TAB(43);:IFF>0THENLPRINTUSINGS$;F;
1140 IFG>0THENLPRINTTAB(53);USINGS$;G;
1150 LPRINT:LC=LC+1:IFLC>54THENLPRINTCHR$(12):LC=1
1160 GOTO1025
1210 IFLC>0ANDR=1GOTO2040
1212 IFLC>50THENLPRINTCHR$(12):LC=1
1215 IFLC=0THENCLS
1216 IFR=0GOTO1218
1217 IFR=2THENBAL=SB+GSUM-FSUM:GSUM=0:FSUM=0:R=1:GOTO1745
1218 PRINT#2,"":PRINT#2,"Start Balance =";USINGT$;SB:PRINT#2,"Total Checks  =";USINGS$;FSUM:PRINT#2,"Total Deposits=";USINGS$;GSUM:PRINT#2,"":PRINT#2,"     Balance  =";:PRINT#2,USINGT$;SB+GSUM-FSUM:CLOSE:IFLC>0THENLPRINTCHR$(12)
1300 INPUT"Press <CR> to Continue";Z$:GOTO3010
1450 GOSUB5000
1460 ONERRORGOTO1610:OPEN"cj"FORINPUTAS1:CLOSE:OPEN"cj"FORAPPENDAS1
1480 CLS:PRINT:PRINT:PRINTTAB(12)"C]heck Input":PRINTTAB(12)"D]eposit Input":PRINTTAB(12)"R]eturn to Menu"
1490 ONINSTR("  CcDdRr",INKEY$)GOTO1490,1490,1495,1495,1500,1500,3010,3010:GOTO1490
1495 Q=1:CLS:PRINT"CHECK (DEBIT)":GOSUB5800:GOTO1503
1500 Q=0:CLS:PRINT"DEPOSIT (CREDIT)":GOSUB5800
1503 PRINT@240,"Date";:AP$=""
1505 AP$=AP$+INKEY$:IFAP$=CHR$(13)GOTO1513
1509 PRINT@246,AP$:IFLEN(A$)=8ANDLEN(AP$)=1THENA$=""
1510 IFLEN(AP$)<2GOTO1505
1511 A$=A$+AP$:AP$="":PRINT@246,SPACE$(8):GOSUB5800
1512 IFLEN(A$)<8THENA$=A$+"/":GOTO1503
1513 PRINT@240,"Description";:INPUTB$:GOSUB5800
1514 PRINT@240,"Category";:INPUTC$:GOSUB5800
1515 PRINT@240,"Number";:INPUTD$:GOSUB5800:DM$=D$:AM$=A$
1520 PRINT@240,"Amount";:INPUTI:GOSUB5800
1530 IFQ=1THENF=IELSEG=I
1535 IFI=0GOTO8000
1540 PRINT@240,"<CR>Input or C]ancel":ONINSTR("Cc",INPUT$(1))GOTO8000,8000
1545 IFI=0GOTO8000
1550 GOTO7000
1560 PRINT#1,A$;",";B$;",";C$;",";D$;",";E$;",";F;",";G:GOSUB5500:D$=STR$(1+VAL(DM$)):A$=AM$
1570 GOTO1480
1610 A$=DATE$:B$="Starting Balance":C$="SB":E$="X":F=0:CLS:INPUT"Starting Balance";G:INPUT"Date of Starting Balance";A$:OPEN"CJ"FORAPPENDAS1:GOTO1560
1730 CLS:ONERRORGOTO3050:OPEN"cj"FORINPUTAS1:CLOSE:OPEN"lpt:"FOROUTPUTAS2
1745 IFR=1THENLPRINTTAB(28)"Bank Reconciliation":GOTO1760
1750 LPRINT:LPRINTTAB(30)"Cash Journal"
1760 LPRINTTAB(29)"As Of ";DATE$:LPRINT:LPRINT:LPRINT:LPRINTTAB(48)"Checks";TAB(56)"Deposits":LC=7:IFR=1THENCLOSE
1780 GOTO1020
1790 ONERRORGOTO3050:OPEN"cj"FORINPUTAS1:OPEN"temp"FOROUTPUTAS2
1820 GOSUB5500:IFEOF(1)THENGOTO2000
1850 INPUT#1,A$,B$,C$,D$,E$,F,G:IFE$<>""GOTO1960
1900 CLS:IFD$<>" "THENPRINT:PRINT"Ck #";D$;" "B$
1910 IFF<>0THENPRINTUSINGS$;F:INPUT"Correct Check Amount";Z
1920 IFZ<>0THENF=Z:Z=0
1930 IFG<>0THENPRINTUSINGS$;G:INPUT"Correct Deposit Amount";Z
1940 IFZ<>0THENG=Z:Z=0
1950 INPUT"Enter X for Cleared";E$
1960 PRINT#2,A$;",";B$;",";C$;",";D$;",";E$;",";F;",";G:GOTO1820
2000 CLOSE:KILL"cj.do":NAME"temp.do"AS"cj.do":GOTO3010
2040 IFLC>50THENLPRINTCHR$(12):LC=0
2100 LPRINT:LPRINT"Check Balance       =";USINGT$;BAL:LPRINT"Outstanding Deposits=";USINGS$;GSUM:LPRINT"Outstanding Checks  =";USINGS$;FSUM:LPRINT
2110 LPRINT"Bank Balance Should =";USINGT$;BAL+FSUM-GSUM:LPRINTCHR$(12):CLOSE:BAL=0:FSUM=0:GSUM=0:CLS:INPUT"Press <CR> to Continue";Z$
3010 CLEAR:S$="########.##":T$="        $$########.##":CLS:PRINT"SELECT:":PRINTTAB(10)"I]nput Data":PRINTTAB(10)"C]heck Balance":PRINTTAB(10)"P]rint Journal":PRINTTAB(10)"S]tatement Check":PRINTTAB(10)"R]econciliation":PRINTTAB(10)"E]xit"
3040 ONINSTR("  IiCcEePpSsRr",INKEY$)GOTO3040,3040,1450,1450,1010,1010,3045,3045,1730,1730,1790,1790,1000,1000:GOTO3040
3045 CLOSE:MAXFILES=1:MENU:END
3050 CLS:PRINT:PRINT:PRINT:PRINT"     THERE IS NO CHECK JOURNAL":INPUT"     HIT <CR> TO CONTINUE";Z$:GOTO1
5000 CLS:PRINT@135,"WAIT":N=1:OPEN"wtb"FORINPUTAS2
5010 IFEOF(2)GOTO5050
5020 LINEINPUT#2,Z$:N=N+1:GOTO5010
5050 CLOSE#2
5100 DIMU$(N):N=1:OPEN"wtb"FORINPUTAS2
5120 IFEOF(2)GOTO5150
5130 INPUT#2,U$(N),V$,U,V,W,X,Y
5140 N=N+1:GOTO5120
5150 CLOSE#2:RETURN
5500 A$=" ":B$=" ":C$=" ":D$=" ":E$=" ":F=0:G=0:I=0:RETURN
5800 PRINT@40,"Date: "A$"       "
5810 PRINT@80,"Description: ";B$
5820 PRINT@120,"Category: ";C$
5830 PRINT@140,"Number: ";D$"     "
5840 PRINT@160,"Amount";USINGS$;I
5850 PRINT@240,SPACE$(39)
5860 RETURN
7000 FORM=1TON
7010 IFU$(M)=C$GOTO1560
7020 NEXT
7030 CLS:PRINTA$;" ";B$;D$
7040 PRINT"Cat#";C$:C$=""
7050 IFF>0THENPRINTUSINGT$;F
7060 IFG>0THENPRINTTAB(20)USINGT$;G
7070 PRINT"  INVALID CATEGORY NUMBER"
7075 PRINT"<CR> to Cancel or"
7080 INPUT"  ENTER CORRECTION";C$:IFC$=""GOTO8000
7090 CLS:GOTO7000
8000 CLS:PRINT@135,"CANCELED":FORM=1TO100:NEXT:GOSUB5500:D$=DM$:A$=AM$:GOTO1480
