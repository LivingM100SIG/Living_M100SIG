0 ' BIBPRT.BA v1.3 (c)1/29/89,R.W.Hess
1 ' Requires BIBFYL.DO data file made
2 ' using BIBLIO.100, BIBPWR.100 or
3 ' BIBLIO.200.  This will print 
4 ' fully formated cross-refs and direc
5 ' tories by source with labels for
6 ' disks and tapes and their jackets.
7 ' YOU MUST enter the 80,96,&132 colum
8 ' codes for YOUR printer in lines
9 ' 112,114 & 116 or THIS PROGRAM
10 'WON'T DO YOU ANY GOOD! The program
11 'has come to you set up for RS/DMP
12 'printers. (If you like BOLD print
13 'or any other enhancements please
14 'feel free to add that code too.)
15 'Please note that Line 18 sets
16 'E$=CHR$(27). "Wksht" prnts comments
18 CLS:MAXFILES=1:CLEAR1500:DEFSTRA,B:CLS:E$=CHR$(27):IF(INP(187)AND6)<>2THENBEEP:BEEP:PRINT@130,"PRINTER NOT READY!!":END
20 CLS:GOSUB108:PRINT@127,"<L>abels"
22 PRINT@142,"<D>irectory":PRINT@246,"<X>-ref    <W>ksht    <M>enu"
24 A=INKEY$:IFA=""THEN24ELSEIFINSTR("Xx",A)THEN138ELSEIFINSTR("Mm",A)THENGOSUB112:BEEP:MENU:ELSEIFINSTR("Dd",A)THEN26ELSEIFINSTR("Ll",A)THEN26ELSEIFINSTR("Ww",A)THEN176ELSE24
26 CLS:GOSUB108:PRINT@169,"<C>assettes   <D>isks"
28 B=INKEY$:IFB=""THEN28ELSEIFINSTR("Cc",B)THENB$="c":N$="Cassette"ELSEIFINSTR("Dd",B)THENB$="d":N$="Disk"ELSE28
30 IFINSTR("Ll",A)THEN42
32 CLS:GOSUB108:A="":PRINT@168, "<8>0c   <9>6c    <1>32c"
34 A=INKEY$:IFA="8"THEN36ELSEIFA="9"THEN38ELSEIFA="1"THEN40ELSE34
36 GOSUB112:GOSUB60:GOTO20
38 GOSUB114:GOSUB60:GOTO20
40 GOSUB116:GOSUB60:GOTO20
42 CLS:GOSUB108:GOSUB88:PRINT:PRINTN$" ID's: ";
44 FORZ=2TOLEN(O$)STEP4:IFMID$(O$,Z+3,1)=B$THENPRINTMID$(O$,Z,3)" ";:NEXTELSENEXT:PRINT
46 PRINT:PRINT:PRINTTAB(10);"Which "N$;:LINEINPUT"? ";ID$
48 IFID$=""THEN20ELSEID$=MID$(ID$,1,3):ID$=SPACE$(3-LEN(ID$))+ID$:FORZ=2TOLEN(O$)STEP4:IFMID$(O$,Z,3)<>ID$THENNEXT:BEEP:GOTO42
50 IFB$="d"THEN56
52 GOSUB116:GOSUB118:LPRINT:LPRINT"Case label:":LPRINTSTRING$(60,"_"):T=6:GOSUB92:T=6:L=15:FY=1:DY=1:SY=0:NY=1:GOSUB120
54 LPRINT:LPRINT"Tape label:":LPRINT:LPRINTSTRING$(60,"_"):GOSUB92:L=4:FY=0:DY=0:SY=0:NY=0:GOSUB120:GOTO42
56 GOSUB114:GOSUB118:T=4:LPRINT:LPRINT"Sleeve label:":LPRINTSTRING$(40,"_"):GOSUB92:L=20:FY=1:SY=1:DY=1:NY=1:GOSUB120
58 GOSUB116:LPRINT:LPRINT"Disk label:":LPRINTSTRING$(40,"_"):GOSUB92:L=12:T=4:DY=1:SY=1:FY=1:NY=0:GOSUB120:GOTO42
60 GOSUB172:CLS:LPRINTN$;" Directory printed ";DATE$:LPRINTSTRING$(EL,"="):D$="":Z=2:DL=2:PG=1
62 CLS:GOSUB108:GOSUB88:GOSUB78:PRINT@166,"Sorting files of "N$": "S$
64 IFEOF(1)THEN72ELSELINEINPUT#1,PT$:GOSUB106:IFMID$(PT$,11,3)=S$THENC=C+1:GOTO66ELSE64
66 LF$=MID$(PT$,15,5)+" "
68 IFF<TTHENA$(N)=A$(N)+LEFT$(PT$,10)+LF$:F=F+1:VL=VL+VAL(LF$):GOTO64
70 N=N+1:F=0:GOTO68
72 CLS:GOSUB108:PRINT@174,"Printing...":DL=DL+N+4:GOSUB82:LPRINT
74 LPRINTN$": "S$:FORI=1TON:LPRINTA$(I):NEXT:LPRINTC"files, updated ";DT$;
76 IFB$="c"THENLPRINT:GOTO62ELSELPRINT"; aprox.";100000-VL;"bytes free.":LPRINT:GOTO62
78 IFMID$(O$,Z,3)=""THEN20ELSEIFMID$(O$,Z+3,1)<>B$THENZ=Z+4:GOTO78
80 S$=MID$(O$,Z,3):DT$=MID$(D$,(Z*2)-2,8):Z=Z+4:RETURN
82 IFDL+2=>66THEN84ELSERETURN
84 S=66-DL+N+4:FORX=1TOS:LPRINT:NEXT:PG=PG+1:LPRINTN$" Directory; Page"PG;
86 LPRINTTAB(EL-8);DATE$:LPRINTSTRING$(EL,"-"):DL=N+6:RETURN
88 CLOSE:FORI=1TO10:A$(I)="":NEXT:OPEN"BIBFYL.DO"FORINPUTAS1:LINEINPUT#1,O$:LINEINPUT#1,D$
90 L=0:P$="":PT$="":FF=0:F=0:N=1:C=0:VL=0:RETURN
92 CLS:GOSUB108:GOSUB102:PRINT@166,"Sorting files of "N$": "ID$
94 IFEOF(1)THENRETURNELSELINEINPUT#1,PT$:GOSUB106:IFMID$(PT$,11,3)=ID$THENC=C+1:ELSE94
96 IFB$="d"THENLF$=MID$(PT$,15,5)
98 IFF<TTHENA$(N)=A$(N)+LEFT$(PT$,9)+" ":F=F+1:VL=VL+VAL(LF$):GOTO94
100 N=N+1:F=0:GOTO98
102 FORZ=2TOLEN(O$)STEP4:IFMID$(O$,Z,3)<>ID$THENNEXT
104 DT$=MID$(D$,(Z*2)-2,8):RETURN
106 J=1-J:PRINT@299,CHR$(147+J);:RETURN
108 PRINT@15,"-BIBPRT-":SOUND(1108+(VAL(RIGHT$(TIME$,1))/2*400)),1:RETURN
110 BEEP:MENU
112 LPRINTE$;CHR$(19):REM 80 chr
113 T=5:EL=79:RETURN
114 LPRINTE$;CHR$(23):REM 96 chr
115 T=6:EL=95:RETURN
116 LPRINTE$;CHR$(20):REM 132 chr
117 T=8:EL=127:RETURN
118 GOSUB172:LPRINT"Sleeve and media labels for "N$": "ID$:LPRINT:LPRINT:RETURN
120 CLS:GOSUB108:PRINT@174,"Printing...":IFNY=1THENLPRINTN$": "ID$
122 IFN=>LTHENN=L:LN=0:ELSELN=L-N
124 FORI=1TON:LPRINTA$(I):NEXT
126 IFLN-(NY+2)>0THENFORI=1TOLN-(NY+2):LPRINT:NEXT
128 IFFY=1THENLPRINTC"files"
130 IFSY=1THENLPRINT" "100000-VL;"bytes free; ";
132 IFDY=1THENLPRINT"updated ";DT$
134 LPRINTSTRING$((T*10),"-")
136 RETURN
138 CLS:GOSUB108:E$=CHR$(27):PRINT@168,"<8>0c   <9>6c    <1>32c"
140 A=INKEY$:IFA="8"THENGOSUB112:EL=73:TB=4:GOTO148
142 IFA="9"THENGOSUB114:EL=88:TB=4:GOTO148
144 IFA="1"THENGOSUB116:EL=118:TB=7:GOTO148
146 GOTO140
148 GOSUB172:CLS:GOSUB88:GOSUB108:PRINT@174,"PRINTING...":LPRINTTAB(TB);"Cross-reference printed "DATE$
150 LPRINTTAB(TB);STRING$(EL,"="):LPRINT
152 LPRINTTAB(TB);"Source ID's with last update:":S$="":S=0:Z=2
154 FORX=2TOLEN(O$)STEP(T*4):FORN=1TOT:F=F+1:S$=S$+MID$(O$,Z,4)+" "+MID$(D$,(Z*2)-2,8)+"  ":Z=Z+4:NEXT
156 LPRINTTAB(TB);S$:S$="":S=S+1:NEXT
158 LPRINTTAB(TB);S$:S=S+1
160 F=0:LPRINTTAB(TB)"Files:"
162 GOSUB168:FORN=1TOT:IFEOF(1)THEN166ELSELINEINPUT#1,PT$:C=C+1:P$=P$+LEFT$(PT$,13)+"  ":NEXT
164 LPRINTTAB(TB);P$:FF=FF+1:P$="":GOTO162
166 LPRINTTAB(TB);P$:LPRINTTAB(TB);C" files cataloged.":GOTO20
168 IFFF<>56-STHENRETURN
170 FORX=1TO5:LPRINT:NEXT:LPRINTTAB(TB);"Cross-reference (cont.)":LPRINTTAB(TB);STRING$(EL,"-"):LPRINT:FF=0:S=-3:RETURN
172 CLS:PRINT@166,"PAPER READY?  PRESS ANY KEY."
174 A=INKEY$:IFA=""THEN174ELSERETURN
176 GOSUB112:GOSUB88:CLS:PRINT@174,"Printing..."
180 LPRINT:LPRINT"BIBFYL Worksheet @ ";DATE$:LPRINT:FORZ=2TOLEN(O$)STEP4:LPRINT" "MID$(O$,Z,4);" ";MID$(D$,(Z*2)-2,8);"  ";:NEXT
181 LPRINT:LPRINT:LPRINT"Files:":LPRINT:LP=2*(57-((Z-2)\16)):PG=1
183 CLS:PRINT@174,"Printing..."
184 FORX=1TOLP
185 IFEOF(1)THENBEEP:RUN
186 LINEINPUT#1,A$:LPRINTMID$(A$,1,39);STRING$(39-LEN(A$),"_");" ";:NEXT
190 FORX=1TO5:LPRINT:NEXT:PG=PG+1:LPRINTSPACE$(35);"- Page ";PG;"-":LPRINT:LP=120:GOTO183
