10 'TVTIME III BY TERRY IRVING
20 'THE LATEST UPDATED, IMPROVED PROGRAM FOR TV JOURNALISTS TO RECORD NOTES
30 'ON A M100 AND AUTOMATICALLY RECORD TIME-OF-DAY OR TIMECODE TO MATCH 
40 'VIDEOTAPES.  JUST RUN IT AND START WRITING, IT WILL WRITE A "NOTE.DO" 
50 'FILE AND INCLUDE THE TIME.  YOU CAN RESET THE TIME OFF THE MAIN MENU...
60 'STOP THE TIMER TO MATCH A TAPE MACHINE, OR RESET TIMER TO ZERO.
70 'IF YOU WRITE OVER THE SCREEN, IT WILL DUMP THE NOTE AND GIVE YOU A FRESH SCREEN.  IF YOU HIT ENTER TO START
80 'THE NOTE, IT WILL TIME THE EVENT FOR YOU.
90 'REMOVE THESE LINES AND IT IS QUITE SMALL IN MEMORY.
300 CLS:CLEAR600:PRINT@41,"F1=REZERO CLOCK":PRINT@81,"F2=RESET CLOCK"
305 KEYON:ONKEYGOSUB500,700,,,900,,,800
310 PRINT@121,"F8=MENU":PRINT@161,"F5=TAPE STOP":PRINT@241,"ENTER TO TIME NOTE"
350 T$=TIME$:PRINT@148,"TVTIME III":PRINT@229,T$
355 K$=INKEY$
362 IFK$=""THEN350
365 IFK$=CHR$(13)THENK$="":J$="Y"
367 ST$="":IFJ$="Y"THENST$=TIME$
368 R$="("+T$+")"+SPACE$(04)
380 CLS:CALL16959:PRINT@80,STRING$(230,95):PRINT@17,"Record"
381 PRINTR$;:PRINT@82,K$:L=0
382 S$=INPUT$(1)
383 IFS$=CHR$(8)THENS$="_":G=LEN(N$):N$=LEFT$(N$,G-1):L=L-1:PRINT@83+L,S$:GOTO382
384 PRINT@83+L,S$
387 IFS$=CHR$(13)THEN400
388 N$=N$+S$:S$="":L=L+1
390 IFLEN(N$)<230GOTO382
392 IFP=1THENR$=""
393 OPEN"NOTE.DO"FORAPPENDAS1
394 PRINT#1,R$+K$+N$;:CLOSE1:P=1
395 L=0:N$="":K$=""
396 GOTO380
400 OPEN"NOTE.DO"FORAPPENDAS1
405 IFP=1THENR$=""
410 PRINT#1,R$+K$+N$:PRINT#1,""
411 IFJ$="Y"THEN412ELSE420
412 SR$="":SR$=TIME$:G=VAL(RIGHT$(ST$,2)):H=VAL(RIGHT$(SR$,2))
413 I=VAL(MID$(ST$,4,2)):J=VAL(MID$(SR$,4,2)):K=G+(60*I):L=H+(60*J):SE=L-K
414 PRINT#1,"TIME="SE" sec"
420 CLOSE1:RUN
500 LINEINPUT"RESET TIME TO ZERO? (Y OR N)";Y$
510 IFY$="Y"ORY$="y"THEN530
520 GOTO600
530 TIME$="00:00:00"
600 RUN
700 LINEINPUT"Present Time is?--HH:MM:SS  ";TI$
710 IFTI$=""THENRUN
720 TIME$=TI$
730 RUN
800 MENU
900 TI$=TIME$:CLS:PRINT"   TAPE STOP"
910 PRINT "HIT ENTER TO CONTINUE":PRINT@241,TI$
920 W$=INKEY$
930 IFW$<>CHR$(13)THEN920
940 TIME$=TI$:RUN
