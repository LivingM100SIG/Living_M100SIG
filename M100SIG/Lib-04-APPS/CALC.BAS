0 'copyright e.b.irving  all rights reserved
1 CLS:PRINT"If you like this program","support my programming:
2 PRINT"Please send $10 (or whatever) to","Terry Irving
3 PRINT"9618 Parkwood Drive","Bethesda, MD 20814
4 FORA=1TO2000:NEXT
5 CLEAR
6 ONKEYGOSUB17,18,19,20,16,33,35,48:KEYON
7 CLS:GOSUB25:X=0:IFB$=""THENB$="+"
8 GOSUB49
9 R$=INKEY$:IFXTHEN7
10 IFR$=""THEN9
11 IFR$=CHR$(8)THENA$=LEFT$(A$,LEN(A$)-1):PRINT@127,A$+" ":GOTO9
12 IFR$=CHR$(13)THEN22
13 IFINSTR(" .1234567890",R$)=0THEN9
14 A$=A$+R$
15 PRINT@127,A$:GOTO9
16 RUN5
17 B$="+":BB=0:GOTO21
18 B$="-":BB=1:GOTO21
19 B$="*":BB=2:GOTO21
20 B$="/":BB=3:GOTO21
21 CLS:GOSUB25:GOSUB49:RETURN
22 IFNTHENGOSUB37ELSEA=VAL(A$)
23 IFA=0THEN7
24 GOSUB28:A$="":GOTO7
25 PRINT@40,"Total=";:IFUTHENPRINTUSING"$$#####.##";Z:RETURN
26 IFNTHENGOSUB47:PRINTH%":"M%":"S%:A$="":RETURN
27 PRINTZ:RETURN
28 B=Z:ONINSTR("-*/+",B$)GOSUB29,30,31,32:RETURN
29 Z=B-A:RETURN
30 Z=B*A:RETURN
31 Z=B/A:RETURN
32 Z=B+A:RETURN
33 IFUTHENU=0:X=1:RETURN
34 U=1:N=0:X=1:RETURN
35 IFNTHENN=0:X=1:RETURN
36 N=1:U=0:X=1:RETURN
37 R=LEN(A$):ONRGOTO38,39,40,41,42,43:IFR>6THEN44
38 F$=A$:A$="00:00:0"+F$:GOTO45
39 F$=A$:A$="00:00:"+F$:GOTO45
40 F$=LEFT$(A$,1):G$=RIGHT$(A$,2):A$="00:0"+F$+":"+G$:GOTO45
41 F$=LEFT$(A$,2):G$=RIGHT$(A$,2):A$="00:"+F$+":"+G$:GOTO45
42 F$=LEFT$(A$,1):G$=MID$(A$,2,2):H$=RIGHT$(A$,2):A$="0"+F$+":"+G$+":"+H$:GOTO45
43 F$=LEFT$(A$,2):G$=MID$(A$,2,2):H$=RIGHT$(A$,2):A$=F$+":"+G$+":"+H$:GOTO45
44 A$="00:00:00"
45 S=VAL(RIGHT$(A$,2)):M=VAL(MID$(A$,4,2)):H=VAL(LEFT$(A$,2))
46 A=S+(60*M)+(60*(60*H)):RETURN
47 H%=Z/3600:HH=Z-(H%*3600):M%=HH/60:SS=HH-(M%*60):S%=SS:RETURN
48 MENU
49 PRINT@240+BB*5,"////"
50 PRINT@280,"ADD  SUBT MULT DIV  CLR  $$$$ TIME MENU";
51 PRINT@120,"INPUT";:RETURN
