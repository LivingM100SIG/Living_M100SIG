10 REM COMPUTE BEARINGS
20 REM FOR THE TANDY MODEL 100
30 REM ADAPTED FROM A PUBLIC DOMAIN PROGRAM
40 REM BY PAUL MACDONALD V1.2 01/27/85
50 REM EPSON GENEVA VERSION IN PCS-19 DL6
60 CLS
70 PRINT "COMPUTE BEARINGS PROGRAM"
80 PRINT "------------------------":PRINT
90 R=3953*12*5280*.0000254
100 PI=4*ATN(1)
110 K=1
120 KK=2
130 FOR J=K TO KK
140 Z$="POINT B"
150 IF J=1 THEN Z$="POINT A"
160 PRINT"";Z$;" LAT  (DEGS, MINS)";
170 BEEP
180 INPUT A(J),X
190 A(J)=PI*((A(J)+X/60)/180)
200 IF A(J)=0 THEN 280
210 PRINT" N OR S";
220 BEEP
230 INPUT S$
240 S$=LEFT$(S$,1)
250 IF S$="N" OR S$="n" THEN 280
260 IF S$="S" OR S$="s" THEN A(J)=-A(J):GOTO 280
270 GOTO 210
280 PRINT Z$;" LONG (DEGS, MINS)";
290 BEEP
300 INPUT B(J),X
310 B(J)=PI*((B(J)+X/60)/180)
320 IF B(J)=0 OR B(J)=PI THEN 400
330 PRINT" E OR W";
340 BEEP
350 INPUT S$:PRINT
360 S$=LEFT$(S$,1)
370 IF S$="E" OR S$="e" THEN 400
380 IF S$="W" OR S$="w" THEN B(J)=-B(J):GOTO 400
390 GOTO 330
400 NEXT J
410 C=COS(A(2))
420 X=C*COS(B(2))
430 C=C*SIN(B(2))
440 D=SIN(A(2))
450 H=SIN(A(1))
460 G=COS(B(1))
470 J=SIN(B(1))
480 K=COS(A(1))
490 W=(G*X)+(J*C)
500 E=(H*W)-(K*D)
510 F=(G*C)-(J*X)
520 G=(K*W)+(H*D)
530 IF ABS(ABS(G)-1)<.00001 THEN 560
540 W=1-G*G
550 IF W>0 THEN H=ATN(G/SQR(W)):GOTO 570
560 H=G*PI/2
570 IF ABS(ABS(G)-1)<=.00001 OR W<=0 THEN PRINT"ANY ANGLE OK ";:GOTO 680
580 IF E>0 THEN X=ATN(F/E):GOTO 640
590 X=PI/2
600 IF F<=0 THEN X=-X
610 IF E>=0 THEN 640
620 X=(ATN(F/E))-PI
630 IF F>=0 THEN X=X+2*PI
640 D=180*(PI-X)/PI
650 C=INT(D+.5)
660 IF C=360 THEN C=0
670 PRINT:PRINT "ANGLE "C" DEGREES ";
680 PRINT"AND "
690 D=R*(.5*PI-H)
700 C=INT(D+.5)
710 PRINT"RANGE "C" KILOMETERS OR"
720 PRINT C*.6215" MILES."
730 BEEP
740 INPUT"NEW POINT A (Y/N)";Y$(1)
750 BEEP
760 INPUT"NEW POINT B (Y/N)";Y$(2)
770 IF Y$(1)<>"Y" AND Y$(2)<>"Y" THEN GOTO 790
780 GOTO 800
790 IF Y$(1)<>"y" AND Y$(2)<>"y" THEN 870
800 K=1
810 KK=2
820 IF Y$(1)<>"Y" AND Y$(1)<>"y" THEN K=2
830 IF Y$(2)<>"Y" AND Y$(2)<>"y" THEN KK=1
840 CLS:PRINT "COMPUTE BEARINGS PROGRAM"
850 PRINT "------------------------":PRINT
860 GOTO 130
870 CLS:PRINT:PRINT "PROGRAM TERMINATED ON REQUEST."
880 END
