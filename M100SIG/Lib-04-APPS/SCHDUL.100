1 '''''''''SCHEDULER 100`````````````
2 ' by  ROGER L. STRAND 71675,335
3 ' NOTE.DO MUST BE CREATED BEFORE USING
10 CLEAR1000:MAXFILES=2:DEFINTA-Z:DEFDBLM,L:DIM D$(50),A$(50),MD(50)
20 CLS:KEYOFF
30 T$="SCHEDULER 100"
40 PRINT@140-(LEN(T$)/2),T$:FORT=1TO1000:NEXTT:GOSUB1000
45 RESTORE:CLS:PRINT
50 FOR T=1TO5:READT$:PRINTTAB(6);T$:NEXTT
60 GOSUB7200:A=VAL(I$)
70 ONAGOSUB 2000,3000,4000,5000,6000
80 GOTO45
1000 '   READ APPOINTMENTS
1020 OPEN"RAM:NOTE.DO"FORINPUTAS1
1025 IFEOF(1)THENCLOSE:RETURN
1030 FORN=1TO50:INPUT#1,D$(N),A$(N)
1040 IF EOF(1)THENCLOSE:N=N+1:RETURN
1050 NEXTN
2000 '   DISPLAY APPOINTMENTS
2020 C1=1:C2=6
2030 CLS:FORC=C1TOC2
2040 PRINTD$(C);"   ";LEFT$(A$(C),28)
2045 NEXTC
2050 PRINT:PRINT"          <ENTER> TO CONTINUE";
2060 GOSUB7200
2070 IFD$(C)=""THENRETURN
2080 C1=C1+6:C2=C2+6:GOTO2030
3000 '   ADD APPOINTMENTS
3020 OPEN"RAM:NOTE.DO"FORAPPENDAS2
3030 CLS:PRINT:PRINTTAB(9);"ENTER MM/DD/YY,ACTION":PRINT:PRINT"<GRPH P> FOR PHONE   <GRPH C> FOR VISIT":PRINT
3040 INPUT"";D$(N),A$(N)
3050 PRINT#2,D$(N);",";A$(N)
3060 CLOSE:GOSUB1000:RETURN
4000 '   DELETE RECORDS
4020 CLS:PRINT@14,"DELETE RECORDS":PRINT:PRINT
4030 PRINTTAB(7);"1  DELETE OLD RECORDS"
4040 PRINTTAB(7);"2  SEARCH AND DELETE"
4050 PRINTTAB(7);"3  RETURN TO MAIN MENU"
4060 GOSUB7200:T1=VAL(I$)
4070 ONT1 GOSUB4100,4200:IFT1=3THEN45
4080 GOTO4020
4100 CLS:PRINT:PRINT:INPUT"MOST RECENT DATE TO RETAIN";R$
4105 CLS:PRINT@136,"DELETING"
4110 MS=VAL((MID$(R$,7,2))+(MID$(R$,1,2))+(MID$(R$,4,2)))
4115 GOSUB7100
4120 OPEN"RAM:NOTE.DO"FOROUTPUTAS2
4130 FORT=1TON-1
4140 IFMD(T)>=MSTHEN4190
4170 NEXTT
4180 CLOSE:GOSUB7000:GOSUB1000:RETURN
4190 PRINT#2,D$(T);",";A$(T):GOTO4170
4200 ' SEARCH AND DELETE
4205 C1=1:C2=6
4210 CLS:FORT=C1TOC2:PRINTT;D$(T);"  ";LEFT$(A$(T),26):NEXTT
4220 T1=0:PRINT:INPUT"    ENTER NO. OR 0 FOR MORE CHOISES";T1
4230 IF T1=0 THENC1=C1+6:C2=C2+6:IFD$(C2-5)=""THENRETURNELSE4210
4240 IF T1<C1 OR T1>C2 THEN GOTO4210
4245 CLS:PRINT@136,"DELETING"
4250 FORT=T1TON:D$(T)=D$(T+1):A$(T)=A$(T+1):NEXTT
4260 OPEN"RAM:NOTE.DO"FOROUTPUTAS2
4270 FORT=1TON-2:PRINT#2,D$(T);",";A$(T):NEXTT
4280 CLOSE:GOSUB7000:GOSUB1000:RETURN
5000 '   SORT FILE
5020 CLS:PRINT@133,"   SORTING   "
5025 OPEN"RAM:NOTE.DO"FOROUTPUTAS2
5030 GOSUB 7100
5040 LD=991231:FORT1=1TON-1:FORT=1TON-1
5050 IFMD(T)<LDTHENLD=MD(T):PI=T
5055 NEXTT
5060 IFLD=991231THEN5150
5070 PRINT#2,D$(PI);",";A$(PI):MD(PI)=999999:LD=991231
5080 NEXTT1
5150 CLOSE:GOSUB7000:GOSUB1000:RETURN
6000 CLOSE:CLS:MENU
7000 FORT=1TON:D$(T)="":A$(T)="":NEXTT:RETURN
7100 FOR T=1TON-1:MD(T)=VAL((MID$(D$(T),7,2))+(MID$(D$(T),1,2))+(MID$(D$(T),4,2))):NEXTT:RETURN
7200 I$=INKEY$:IFI$=""THEN7200ELSERETURN
10000 DATA 1  DISPLAY APPOINTMENTS,2  ADD AN APPOINTMENT,3  DELETE RECORDS,4  SORT FILE (by date),5  END PROGRAM

