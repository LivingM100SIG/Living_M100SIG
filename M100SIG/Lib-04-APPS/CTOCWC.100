1 'CTOCWC.BA
2 'City TO City Weather Comparitor
3 'BY: Ron VanBenthusen 70721-3223
4 'DATA COMPANION FILE: CITIES.DO
5 'RUN 1000 TO CREATE/DELETE FILES
10 POKE63038,175
20 SY=55:EM=12:EC=5:BS=5:BM=2
100 CLOSE#1:OPEN"CITIES"FORINPUTAS#1
110 INPUT#1,TC,TI
120 DIMC$(TC),P$(2),D(13,2)
200 CLS:FORC=1TOTC:INPUT#1,C$(C)
210 PRINT@(C*20)-20,USING"(##) \          \";C,C$(C):NEXTC
220 PRINT@281,"CITIES (A,B)";:INPUTA,B
230 A=INT(A):B=INT(B)
240 IFA>TCORB>TCORA<1ORB<1ORA=BTHEN500
270 P$(1)=C$(A):P$(2)=C$(B)
300 FORI=1TOTI:INPUT#1,I$:X=0
400 FORC=1TOTC
410 IFA=CTHENN=1:GOSUB700:GOTO440
420 IFB=CTHENN=2:GOSUB700:GOTO440
430 D$="":LINEINPUT#1,D$
440 NEXTC:GOSUB800:NEXTI
500 PRINT@308,"A)GAIN Q)UIT":BEEP
510 K$=INKEY$:IFK$=""THEN510
520 IFK$="A"ORK$="a"THENRUN
530 IFK$="Q"ORK$="q"THENMENUELSE510
700 FORM=1TO12:INPUT#1,D:D(M,N)=D:T=T+D
710 IFI=1THEN740
720 PRINT@(80*N)+26+M,"-";CHR$(154)
740 IFX=0ANDM=1THENL=D:H=D
750 IFD<LTHENL=D
760 IFD>HTHENH=D
770 NEXTM:IFH=LTHENH=H+.01
780 D(13,N)=T/12:T=0:X=X+1:RETURN
800 CLS:PRINT@280,"!J!F!M!A!M!J!J!A!S!O!N!D!Y"
810 PRINT@309,USING"(## OF ##)";I,TI
820 LINE(160,8)-(165,14),1,B
830 LINE(160,40)-(165,46),1,BF
840 PRINT@147,USING"\           \";I$
850 PRINT@27,USING"------####.##";H
860 PRINT@267,USING"------####.##";L
870 FORN=1TO2
890 PRINT@(N*160)-92,USING"\          \";P$(N)
900 FORM=1TO13:D=D(M,N):GOSUB980
910 IFN=1THENLINE(X,SY)-(X+BS,Y),1,B
920 IFN=2THENLINE(X,SY)-(X+BS,Y),1,BF
930 PRINT@(80*N)+33,USING"####.##";D
940 NEXTM:NEXTN:RETURN
980 X=(M*EM)+(N*EC)-(EM+EC)+BM
990 Y=SY-(SY*((D-L)/(H-L))):RETURN
1000 'CREATE/DELETE
1010 POKE63038,175
1020 DATA"CREATE CITY","DELETE CITY","CREATE ITEM"
1030 MAXFILES=2
1040 CLOSE#1:OPEN"CITIES"FORINPUTAS#1
1050 INPUT#1,TC,TI
1060 DIMC$(TC),O$(3)
1070 FORC=1TOTC:INPUT#1,C$(C):NEXTC
1080 CLOSE#1:OPEN"CITIES"FORINPUTAS#1
1090 CLOSE#2:OPEN"TEMP"FOROUTPUTAS#2
1095 INPUT#1,TC,TI
1100 CLS:FORN=1TO3:READO$(N)
1120 PRINT@50+(N*40),N,O$(N):NEXTN
1130 K$=INKEY$:IFK$=""THEN1130
1140 K=VAL(K$):K=INT(K)
1150 IFK>3ORK<1THENRUN
1360 ONKGOSUB2000,3000,4000
1800 KILL"CITIES.DO"
1810 NAME"TEMP.DO"AS"CITIES.DO":RUN
2000 PRINT#2,TC+1","TI
2060 GOSUB7100:GOSUB7800
2080 GOSUB7300:INPUTC$:PRINT#2,C$
2100 GOSUB7600:IFEOF(1)THENRETURN
2200 GOSUB7800:CLS:PRINT@1,C$" / "I$
2230 GOSUB7000:GOTO2100
3000 PRINT#2,TC-1","TI
3010 GOSUB7100:GOSUB7300:INPUTA
3090 IFA<>INT(A)ORA>TCORA<1THENRUN
3120 GOSUB7800
3160 GOSUB7600:IFEOF(1)THENRETURN
3180 GOSUB7800:GOTO3160
4000 PRINT#2,TC","TI+1
4060 GOSUB7100:GOSUB7800
4100 FORI=1TOTI:GOSUB7600
4120 GOSUB7800:NEXTI
4140 GOSUB7300:INPUTI$:PRINT#2,I$
4200 FORC=1TOTC
4210 CLS:PRINT@1,I$" / "C$(C)
4230 GOSUB7000:NEXTC:GOSUB7600:RETURN
7000 FORM=1TO12
7010 PRINT@(M*20)+20,USING"[##]";M
7015 NEXTM
7020 FORM=1TO12:PRINT@(M*20)+24," ";
7030 INPUTE$:I=VAL(E$):IFI=0THENE$=""
7040 IFM<12THENE$=E$+","
7050 TE$=TE$+E$:E$="":NEXTM
7060 PRINT#2,TE$:TE$="":RETURN
7100 CLS:FORC=1TOTC
7110 PRINT@(C*20)-20,USING"(##) \          \";C,C$(C)
7120 NEXTC:RETURN
7300 PRINT@281,O$(K);:RETURN
7600 INPUT#1,I$
7610 PRINT@306,USING"\           \";I$
7620 PRINT#2,I$:RETURN
7800 FORC=1TOTC:LINEINPUT#1,D$
7810 IFC<>ATHENPRINT#2,D$:GOTO7830
7820 PRINT@(C*20)-15,"--DELETING--"
7830 NEXTC:RETURN
