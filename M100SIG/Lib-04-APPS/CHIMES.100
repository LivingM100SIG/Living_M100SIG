2 ' CHIME
4 ' BY: PAUL CARNELL, JUNE 1983
6 '     [70320,174]
8 '
10 CLS:LINE (91,4)-(153,58),1,B:LINE (93,6)-(151,16),1,B:LINE (93,19)-(151,56),1,BF
20 PRINT @256,CHR$(27);CHR$(112);"REGULATOR";CHR$(27);CHR$(113);
30 TIME$ ON:ON KEY GOSUB ,,,,,,,500:KEY ON:PRINT @315,"MENU";
40 C=4697:D=20:F=-1:P=122
50 H=VAL(MID$(TIME$,4,5))
60 IF H<15 THEN M=1
70 IF H=>15 THEN M=2
80 IF H=>30 THEN M=3
90 IF H=>45 THEN M=4:H=1:ELSE H=0
100 H=H+VAL(LEFT$(TIME$,2))
110 IF H>23 THEN H=0
120 GOSUB 1100
130 H$=TIME$
140 IF H$=TIME$ THEN 140
150 IF H$<>TIME$ THEN H$=TIME$:F=F/(-1)
160 P=P+F:IF P<117 OR P>127 THEN P=P-F
170 LINE (P-F,20)-(P-F,41),1:LINE (P-F-1,42)-(P-F+1,44),1,BF
180 LINE (P,20)-(P,41),0:LINE (P-1,42)-(P+1,44),0,BF
190 PRINT @57,TIME$;
200 GOTO 150
500 TIME$ OFF:MENU
1000 RESTORE
1010 FOR I=1 TO M
1020 READ T:SOUND T,D:SOUND 0,D
1030 READ T:SOUND T,D:SOUND 0,D
1040 NEXT I
1050 IF H>12 THEN HT=H-12 ELSE IF H=0 THEN HT=12 ELSE HT=H
1060 IF M=4 THEN FOR I=1 TO HT:SOUND 0,D*2:SOUND C,D:NEXT I
1070 M=M+1:IF M>4 THEN M=1
1080 IF M=4 THEN H=H+1:IF H>23 THEN H=0
1100 IF H<10 THEN H$="0"+RIGHT$(STR$(H),1) ELSE H$=RIGHT$(STR$(H),2)
1110 IF M=4 THEN M$="00" ELSE M$=RIGHT$(STR$(M*15),2)
1120 ON TIME$=H$+":"+M$+":00" GOSUB 1000
1130 RETURN
2000 DATA 3728,4697,4184,6269,6269,4184,3728,4697

