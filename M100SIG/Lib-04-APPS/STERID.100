1 'STERID.BA
2 'By Ron Vanbenthusen (70721,3223)
10 CLS:PRINT@135,"STERID.BA":POKE63038,175:OD=2400:ID=2.5:U$="yd"
20 C=50:F=900:N=800:FL=35:FC=50:FP=125:P=3.14159265358979
30 DATA"Normal minimum","Moderately low","Moderate","Moderately high","High"
35 FORA=1TO5:READH$(A):NEXTA
40 DATA3,5,10,20,30
45 FORA=1TO5:READHA(A):IA=HA(A):GOSUB3900:H(A)=HF:NEXTA
50 DATA"yd-Yards","mt-Meters","ft-Feet", "cm-Centimeters","in-Inches","mm-Milimeters"
55 FORA=1TO6:READU$(A):NEXTA
60 OD$="Object distance":ID$="Inter-lens distance":IA$="Inter-lens angle":HF$="Hyperstereo factor"
62 C$="Closest ground detail":HD$="Description of angle":F$="Far":N$="Near"
64 FP$="Print size":FL$="Lens focal length":FC$="Format focal length"
100 A=1:H$=H$(A):HA=HA(A):H=H(A):SF=OD/ID
500 CLS:PRINT@14,"Main menu"
510 PRINT@84,"1)Normal ";ID$:PRINT@124,"2)Close-up ";ID$
525 PRINT@164,"3)";IA$;" calculation":PRINT@204,"4)Quit"
530 Q$=INKEY$:IFQ$=""THEN530
540 Q=VAL(Q$):IFQ<1ORQ>4THEN530
550 ONQGOTO600,700,800,9900
600 CLS:PRINT@0,"1)";C$:PRINT@26,USING"#######.### \\";C,U$
610 PRINT@40,"2)Hyperstereo:":PRINT@55,USING"\             \";H$
620 PRINT@83,USING"Angle=###.####\\   Factor=####.###";HA,CHR$(166),H
640 GOSUB1000:CF=FL/FC:I=C/(H*CF):GOSUB1200
690 ONQGOSUB2000,3000,7000,8000,9000:GOTO600
700 CLS:PRINT@0,"1)";OD$;": ";F$:PRINT@26,USING"#######.### \\";F,U$
710 PRINT@59,N$:PRINT@66,USING"#######.### \\";N,U$
720 PRINT@80,"2)";FP$:PRINT@93,USING"#### mm";FP:FV=FL*(FP/FC)
730 PRINT@102,USING"Viewer fl  #### mm";FV
750 GOSUB1000:CF=FL/FC:VF=FV/FL:I=(((F-N)/3)+N)*VF/24*CF:GOSUB1200
790 ONQGOSUB5000,6000,7000,8000,9000:GOTO700
800 CLS:PRINT@4,IA$;" calculation"
805 PRINT@80,"1)";OD$:PRINT@106,USING"#######.### \\";OD,U$
810 PRINT@120,"2)";ID$:PRINT@146,USING"#######.### \\";ID,U$
815 IA=90-(ATN(OD/ID)/P)*180
820 PRINT@160,"3)";IA$:PRINT@189,USING"###.####\\";IA,CHR$(166)
825 HF=OD/ID:PRINT@200,"4)";HF$:PRINT@229,USING"####.###";HF:GOSUB1300
845 ONQGOSUB860,870,880,890,9000:GOTO800
860 P$=OD$:GOSUB9500:INPUTOD:RETURN
870 P$=ID$:GOSUB9500:INPUTID:RETURN
880 P$=IA$:GOSUB9500:INPUTIA:GOSUB3900:ID=OD/HF:RETURN
890 P$=HF$:GOSUB9500:INPUTHF:ID=OD/HF:RETURN
1000 PRINT@120,"3)";FL$:PRINT@153,USING"#### mm";FL
1100 PRINT@160,"4)";FC$:PRINT@193,USING"#### mm";FC:RETURN
1200 PRINT@203,ID$;" =":PRINT@227,USING"######.### \\";I,U$
1210 SI=I*SF:PRINT@243,USING"Stereo infinity = ############.### \\";SI,U$
1300 PRINT@280,"5)Unit of measure  6)Main menu  7)Quit"
1400 Q$=INKEY$:IFQ$=""THEN1400
1500 Q=VAL(Q$):IFQ=6THEN500ELSEIFQ=7THEN9900ELSEIFQ<1ORQ>7THEN1400ELSERETURN
2000 P$=C$:GOSUB9500:INPUTC:RETURN
3000 CLS:PRINT@10,HF$;"s"
3010 PRINT@43,"Description        Angle     Factor"
3020 FORA=1TO5:PRINT@40+A*40,USING"#)\               \ ###.####\\ ####.###";A,H$(A),HA(A),CHR$(166),H(A):NEXTA
3025 PRINT@280,"6)Other"
3030 Q$=INKEY$:IFQ$=""THEN3030
3040 Q=VAL(Q$):IFQ=6THEN3100ELSEIFQ<1ORQ>6THENRETURN
3050 H$=H$(Q):HA=HA(Q):H=H(Q):RETURN
3100 P$=HD$:GOSUB9500:INPUTH$
3110 P$=IA$:GOSUB9500:INPUTIA:GOSUB3900:HA=IA:H=HF:RETURN
3900 HF=TAN(((90-IA)*P)/180):RETURN
5000 P$=F$+" "+OD$:GOSUB9500:INPUTF
5010 P$=N$+" "+OD$:GOSUB9500:INPUTN:RETURN
6000 P$=FP$:GOSUB9500:INPUTFP:RETURN
7000 P$=FL$:GOSUB9500:INPUTFL:RETURN
8000 P$=FC$:GOSUB9500:INPUTFC:RETURN
9000 CLS:PRINT@48,"Unit of measurements"
9010 FORA=1TO6:PRINT@102+(A*20),USING"#)\              \";A,U$(A):NEXTA
9020 Q$=INKEY$:IFQ$=""THEN9020
9030 Q=VAL(Q$):IFQ<1ORQ>6THEN9020
9040 U$=LEFT$(U$(Q),2):RETURN
9500 PRINT@280,SPACE$(40)
9510 PRINT@280,USING"\                    \";P$;:RETURN
9900 MENU
