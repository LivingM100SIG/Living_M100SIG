0 'TODO.PJP by Phil Pearsall [70476,1476] 10/14/84
10 MAXFILES=2:CLEAR1000:DEFSTRA-M:B="note.do":OPENBFORAPPENDAS1:CLOSE:E=LEFT$(DATE$,5):H=CHR$(27)+"p":I=CHR$(27)+"q":P=42:CLS
15 GOSUB90:PRINT@280,"view entr phon alrm calc caln reln menu";:PRINT@1,H;"  Appointments & To-Dos  ";I
16 LINE(6,7)-(155,56),1,B:KEYON:ONKEYGOSUB100,200,300,400,500,600,700,80
20 IFEOF(1)THEN30ELSEIFP>242THEN30ELSELINEINPUT#1,L:IFINSTR(L,E)<>0THEN25ELSEIFINSTR(L,"TO-DO")<>0THEN25ELSE20
25 PRINT@P,MID$(L,7,23):P=P+40:GOTO20
30 A="alarm.do":LINE(161,0)-(233,56),1,B:PRINT@27,H;"  Today is  ":PRINT@147,"The Time is ";:PRINT@227,"   Alarm    ";I
33 PRINT@109,DATE$:IFDAY$="Sun"THENC="Sunday"ELSEIFDAY$="Mon"THENC="Monday"ELSEIFDAY$="Fri"THENC="Friday"
34 PRINT@70,C:IFDAY$="Tue"THENF="Tuesday"ELSEIFDAY$="Wed"THENF="Wednesday"
35 IFDAY$="Thu"THENF="Thursday"ELSEIFDAY$="Sat"THENF="Saturday"
36 PRINT@69,F
40 M=TIME$:K=LEFT$(TIME$,5):E=LEFT$(DATE$,5):IFEOF(1)THENCLOSE:OPENAFORINPUTAS1
45 LINEINPUT#1,L:IFINSTR(L,"  ***")=0THEN50ELSEIFINSTR(L,K)=0THEN50ELSE55
50 IFINSTR(L,E)=0THEN65ELSEIFINSTR(L,K)=0THEN65
55 CLS:PRINT@207,H;" Press Any Key To Silence ";I
60 J=INKEY$:FORY=2000TO1000STEP-50:SOUNDY,1:NEXTY:SOUND9999,25:N=N+1:IFN=99THENRUNELSEIFJ=""THEN60ELSEFORR=1TO25000:NEXT:RUN
65 IFM=TIME$THEN65ELSEIFINSTR(L,E)<>0ORINSTR(L,"  ***")<>0THENPRINT@270,MID$(L,8,5)
70 S=VAL(LEFT$(TIME$,2)):IFS>12THENPRINT@187,STR$(S-12);RIGHT$(M,6)ELSEIFS<10THENPRINT@187,STR$(S);RIGHT$(M,6)ELSEPRINT@188,M;
75 IFS>11THENPRINT@196,"PM";ELSEIFS<12THENPRINT@196,"AM";
77 IFTIME$="00:00:01"THENRUNELSE40
80 MENU
90 CLOSE:OPENBFORINPUTAS1:RETURN
100 GOSUB90:OPEN"t.do"FORAPPENDAS2:PRINT@57,H;" Select: "I:PRINT@97," 1)Scan  ":PRINT@137," 2)MM/DD "
101 PRINT@177," 3)Today ":PRINT@217," 4)TO-DO ":PRINT@257,STRING$(8,32):PRINT@260,"";:LINE(101,7)-(155,56),1,B
105 J=INKEY$:N=VAL(J):IFN<1ORN>4THEN105ELSEN=N:ONNGOSUB110,115,120,125
110 K="S":GOTO135
115 GOTO130
120 K=E:GOTO135
125 K="TO-DO":GOTO135
130 LINEINPUT"";K
135 IFEOF(1)THENCLOSE:KILLB:NAME"t.do"ASB:RUNELSELINEINPUT#1,L:IFINSTR(L,K)<>0ORK="S"THEN140ELSEPRINT#2,L:GOTO135
140 CLS:N=LEN(L):IFN<40THENPRINT@80+(40-N)/2,LELSEPRINT@80,L
150 PRINT@253,H;" Delete (Y/N) "I;:J=INPUT$(1):IFINSTR("Yy",J)>0THEN135ELSEPRINT#2,L:CLS:GOTO135
200 PRINT@171,H;"   Select:< >  "I:PRINT@211," <T>o-Do      ":PRINT@251," <A>ppointment"
201 LINE(66,39)-(155,56),1,B:CLOSE:OPENBFORAPPENDAS1
205 J=INKEY$:IFJ="a"ORJ="A"THEN210ELSEIFJ="t"ORJ="T"THEN220ELSE205
210 CLS:PRINT@12,"Today is "E:PRINT@52,"Time is "K:PRINT@131,"Date: Time: Appointment:":PRINT@171,;
215 LINEINPUT"";M:PRINT#1,M:CLS:PRINT@129,H" Another Appointment? "I;:J=INPUT$(1):IFINSTR("Yy",J)>0THEN210ELSE230
220 CLS:PRINT@131,;:LINEINPUT"To-Do:";M:PRINT#1,"TO-DO "M:CLS:PRINT@132,H" Another To-Do? "I;
225 J=INPUT$(1):IFINSTR("Yy",J)>0THEN220
230 GOSUB90:DIMA(100):CLS:PRINT@169,H" Reorganizing File "I:N=1:O=11:P=0
235 P=P+1:LINEINPUT#1,A(P):IFEOF(1)THEN240ELSE235
240 CLOSE1:GOSUB245:KILLB:OPENBFOROUTPUTAS1:FORQ=1TOP:PRINT#1,A(Q):NEXT:RUN
245 U=P
250 U=INT(U/2):IFU=0THENRETURNELSER=1:S=P-U
255 Q=R
260 T=Q+U:IF(MID$(A(Q),N,(O-N)+1))<(MID$(A(T),N,(O-N)+1))THEN270
265 G=A(Q):A(Q)=A(T):A(T)=G:Q=Q-U:IFQ<1THEN270ELSE260
270 R=R+1:IFR>STHEN250ELSE255
300 CLOSE:OPEN"ADRS.DO"FORINPUTAS1:PRINT@171,H;"     Name      "I:PRINT@211,STRING$(15,32):PRINT@251,STRING$(15,32)
301 LINE(66,39)-(155,56),1,B:PRINT@212,"";:LINEINPUT"";J:IFJ=""THEN301
302 FORP=1TOLEN(J):Q=ASC(MID$(J,P,1)):IFQ>96ANDQ<123THENMID$(J,P,1)=CHR$(Q-32)
303 NEXTP
304 CLS:PRINT@60,"Home      Work"
305 IFEOF(1)THEN315ELSELINEINPUT#1,L:IFINSTR(L,J)<>0THEN310ELSE305
310 PRINTL:GOTO305
315 PRINT@252,H;" Another No.? "I;:J=INPUT$(1):IFINSTR("Yy",J)>0THEN300ELSECLOSE:RUN
400 C=STRING$(14,32):PRINT@12,H;" ** Alarm **  "I:PRINT@52,"   <S>et      ":PRINT@92,"   <C>ancel   "
401 PRINT@132,C:PRINT@172,C:PRINT@212,C:PRINT@252,C:LINE(72,7)-(155,56),1,B
405 J=INKEY$:IFJ="s"ORJ="S"THEN410ELSEIFJ="c"ORJ="C"THEN430ELSE405
410 CLOSE:OPENAFORAPPENDAS1:PRINT@55,"<D>aily":PRINT@95,"<S>ingle"
415 J=INKEY$:IFJ="d"ORJ="D"THEN420ELSEIFJ="s"ORJ="S"THEN425ELSE415
420 PRINT@176,"Time:":PRINT@216,"";:LINEINPUT"";D:PRINT#1,"  ***  "D:CLOSE:RUN
425 PRINT@173,"Date:  Time:":PRINT@213,"";:LINEINPUT"";D:PRINT#1,D:CLOSE:RUN
430 CLOSE:OPENAFORINPUTAS1:OPEN"temp.do"FORAPPENDAS2
435 IFEOF(1)THENCLOSE:KILLA:NAME"temp.do"ASA:RUNELSELINEINPUT#1,L
440 PRINT@173,LEFT$(L,12):PRINT@252,H;" Cancel (Y/N) "I:J=INKEY$:IFJ=""THEN440ELSEIFJ="y"ORJ="Y"THEN435ELSEPRINT#2,L:GOTO435
500 CLS:N=0:A=" TOTAL =":M=" memory=":ONERRORGOTO554
502 PRINTH;" ABACUS "I;" to Printer:Y/N?";:C=INPUT$(1):GOSUB532:IFC="Y"ORC="y"THENR=1:PRINT"(PRINTER ON-LINE)":GOSUB552
506 LINEINPUT" Enter # ";G:IFG="M"ORG="m"THENP=NELSEP=VAL(G)
508 F=" ":X=P:T=P:GOSUB532:GOSUB550
510 PRINT"Function:":PRINT" <+><-><X></> <S><^><%>  <N>ew <E>nd"
511 PRINTH;" memory   <P>lus <M>inus <C>ancel   "I:PRINT@240,"Select:<>"
512 F=INKEY$:IFF=""THEN512
514 IFF="P"ORF="p"THENN=N+T:GOTO530ELSEIFF="M"ORF="m"THENN=N-T:GOTO530
516 IFF="C"ORF="c"THENN=0:GOTO530ELSEIFF="N"ORF="n"THEN534:ELSEIFF="E"ORF="e"THENRUN
517 IFF="%"ORF="5"THEN538ELSEIFF="S"ORF="s"THEN542ELSEIFF="^"ORF="6"THEN546
518 IFF="X"ORF="x"ORF="*"ORF="8"THENF="x":GOTO520ELSEIFF="/"ORF="-"THEN520ELSEIFF="+"ORF="="ORF=CHR$(13)THENF="+"
519 GOTO520ELSEGOSUB532:GOTO510
520 GOSUB532:PRINT@248,"Enter # or M ("H;F;" memory "I;")":PRINT@88,F;:LINEINPUTG:IFG="M"ORG="m"THENP=NELSEP=VAL(G):GOSUB550
524 IFF="x"THENT=X*PELSEIFF="/"THENIFP=0THEN530ELSEIFX=PTHENT=1ELSET=X/PELSEIFF="-"THENT=X-PELSEIFF="+"THENT=X+P
530 GOSUB532:GOSUB552:GOTO510
532 CLS:X=T:PRINTH;M;I;N:PRINTA;T:RETURN
534 GOSUB532:GOSUB552:PRINT" <NEW>  Save ";H" memory"I":Y/N?";:K=INPUT$(1):PRINTK:T=0:IFK="N"ORK="n"THEN500ELSE502
538 CLS:X=T:P=0:GOSUB532:PRINTT"%";:INPUT" of what #";P:Q=T:T=(T/100)*P:IFP=0THENT=X
540 IFR=1THENLPRINTQ"% OF";P;"=";T:GOSUB532:GOTO510ELSE530
542 P=T:T=SQR(T):GOSUB532:PRINT@87,"(SQ. ROOT OF";P;")":IFR=1THENLPRINTT;"= SQ. ROOT OF";P:GOSUB552:GOTO510ELSE510
546 P=T:T=T*T:GOSUB532:PRINT@87,"(SQUARE OF";P;")":IFR=1THENLPRINTT;"= SQUARE OF";P:GOSUB552:GOTO510ELSE510
550 IFR=1THENLPRINT"   ";F;P:RETURN
552 IFR=1THENLPRINT"TOT=";T;" MEM=";N:RETURNELSERETURN
554 CLS:PRINT@137,"OVERFLOW":SOUND1500,100:GOTO500
600 KEYOFF:D="":GOSUB608
602 Y=-INT((14-W)/12):Z=X-32074+INT(1461*(V+4800+Y)/4):Z=Z+INT(367*(W-2-12*Y)/12):N=Z-INT(3*INT((V+4900+Y)/100)/4):RETURN
604 DATAJanuary,31,February,28,March,31,April,30,May,31,June,30,July,31,August,31
605 DATASeptember,30,October,31,November,30,December,31,EOF,-1
606 LINE(6,47)-(137,56),1,B:PRINT@241,H;" Month/Year "I"         ";:PRINT@253,"";:INPUT"";D
608 GOSUB602:RESTORE604:IFD=""THEND=DATE$:O%=VAL(MID$(D,4,2)):D=LEFT$(D,3)+RIGHT$(D,2)
610 P%=INSTR(D,"/"):IFP%<2ORP%>3ORP%=LEN(D)THENBEEP
612 W=VAL(LEFT$(D,P%-1)):V=VAL(RIGHT$(D,LEN(D)-P%)):X=1
614 IFW<1ORW>12ORV<0THENBEEP:GOTO606ELSEIFV<100THENV=V+1900
616 GOSUB602:FORP%=1TOW:READA,Q%:NEXTP%:IFW=2THENQ%=Q%-(0=VMOD4)+(0=VMOD100)-(0=VMOD400)
618 CLS:PRINT@280,"               next prev any  tody retn";:KEYON:ONKEYGOSUB,,,634,630,606,600,700
620 LINE(6,0)-(137,7),1,BF:PRINT@2,H;A;:PRINT@17,V;I;
621 PRINT@42,"Su Mo Tu We Th Fr Sa":R%=N-7*INT(N/7):PRINTTAB(1);SPACE$(3*R%);
622 FORP%=1TOQ%:IFP%=O%THENPRINT" ";:PRINTH;ELSEPRINT" ";
624 PRINTUSING"##";P%;:IFP%=O%THENPRINTI;
626 R%=R%+1:IFR%>6ANDP%<>Q%THENPRINT:PRINTTAB(1);:R%=0
628 NEXTP%:LINE(6,7)-(137,56),1,B:GOSUB30
630 C=STR$(VAL(LEFT$(D,2))-1):C=RIGHT$(C,2)+"/"+RIGHT$(D,2):D=C:O%=0
632 IFVAL(LEFT$(D,2))=0THENC=STR$(12)+"/"+RIGHT$(STR$(VAL(RIGHT$(D,2))-1),2):D=RIGHT$(C,5)
633 GOTO608
634 C=STR$(VAL(LEFT$(D,2))+1):C=RIGHT$(C,2)+"/"+RIGHT$(D,2):D=C:O%=0
636 IFVAL(LEFT$(D,2))=13THENC=STR$(1)+"/"+RIGHT$(STR$(VAL(RIGHT$(D,2))+1),2):D=RIGHT$(C,5)
638 GOTO608
700 RUN
