0 'TVTIME V.4 COPYRIGHT 1987 BY TERRY IRVING
1 CLS:PRINT"IF YOU LIKE THE PROGRAM","SUPPORT MY PROGRAMMING"
2 PRINT"SEND $20 TO"," TERRY IRVING","9618 PARKWOOD DRIVE"
3 PRINT"BETHESDA MD 20814
4 FORAA=1TO2000:NEXTAA
5 CLS:CLEAR600:KEYON:ONKEYGOSUB43,46,64,,49,54,52,66:DEFSTRA-N:DEFINTP-Z
6 P=0:'IF P=1, MAKE IT ZERO.           IF P=0 MAKE IT 1...HIT F8 THEN F4
7 PRINT"F1=REZERO CLOCK","F2=RESET CLOCK
8 IFPTHENPRINT"F3=CLOCK ON(OFF)"ELSEPRINT"F3=CLOCK OFF(ON)
9 PRINT"F5=TAPE STOP",,"F6=EDIT GRPH/2-5","F7=EDIT FILENAME"
10 PRINT"F8=MENU",,"ESC=BRACKETS    ENTER=TIME BITE";
11 PRINT@144,"MEMORY="FRE(0)
12 IFD=CHR$(13)THEND="":Z=1:B=TIME$:GOTO15
13 PRINT@226,TIME$:D=INKEY$:IFD=""THEN13
14 RR=ASC(D):IFRR>155ANDRR<160THENGOSUB57
15 IFD=CHR$(13)THEND="":Z=1:B=TIME$:GOTO18
16 IFRR=27THENGOSUB55
17 IFRR>155ANDRR<160THENGOSUB57
18 IFPTHENC=TIME$+SPACE$(2):GOTO21
19 C="CNTL/A FOR TIMECODE
20 IFRR>155ANDRR<160THENGOSUB58
21 CLS
22 PRINT@43+Y,D:IFD=CHR$(13)THENW=1:GOTO24
23 PRINT@1,C;:PRINT@42,D:Y=LEN(D)-1:E=D
24 D=INPUT$(1)
25 IFD=CHR$(8)THEND=" ":X=LEN(E):E=LEFT$(E,X-1):Y=Y-1:PRINT@43+Y,D:GOTO24
26 RR=ASC(D)
27 IFRR>155ANDRR<160THENGOSUB57
28 IFRR=27THENGOSUB55
29 IFRR=1THENGOTO65
30 PRINT@43+Y,D:IFD=CHR$(13)THENW=1:GOTO32
31 E=E+D:Y=Y+LEN(D):D="":IFLEN(E)<197GOTO24
32 IFVTHENC="
33 IFINSTR(C,"CN")THENC="
34 OPEN"RAM:NOTES"FORAPPENDAS1:PRINT#1,C+A+E;:V=1:Y=0:E="":A=""
35 'CHANGE THE WORD AFTER RAM: LEAVE QUOTES..HIT F8..F4
36 IFWTHENW=0:PRINT#1,"":PRINT#1,"":GOTO38
37 CLOSE:GOTO21
38 IFZ=0THEN42
39 J=TIME$:X=VAL(RIGHT$(J,2)):U=VAL(RIGHT$(B,2))
40 T=VAL(MID$(J,4,2)):S=VAL(MID$(B,4,2)):R=X+(60*T):Y=U+(60*S):Q=R-Y
41 PRINT#1,"TIME="Q" sec
42 CLOSE:RUN5
43 INPUT"ZERO TIME (Y/N)";F
44 IFINSTR("Yy",F)THENTIME$="00:00:00
45 RUN5
46 LINEINPUT"Present Time (HH:MM:SS)";G
47 IFG=""THENRUN5
48 TIME$=G:RUN5
49 G=TIME$:CLS:PRINT"TAPE STOP","ENTER TO CONTINUE":PRINT@241,G
50 H=INKEY$:IFH<>CHR$(13)THEN50
51 TIME$=G:RUN5
52 CLS:EDIT34-35
53 D="{":TT=1:RETURN
54 EDIT59-63
55 IFTT=1THEND="}":TT=0:RETURN
56 D="{":TT=1:RETURN
57 P=ASC(D)-155:ONPGOSUB59,60,61,62
58 RETURN
59 D="FAST ONE ":RETURN:'GRPH/2
60 D="FAST TWO ":RETURN:'GRPH/3
61 D="FAST 3   ":RETURN:'GRPH/4
62 D="FAST 4   ":RETURN:'GRPH/5
63 'CHANGE PHRASES INSIDE THE QUOTES...HIT F8...THEN F4
64 EDIT6
65 INPUT"T-CODE";O:C=STR$(O)+SPACE$(2):CLS:PRINTC:E=E+SPACE$(1):PRINT@42,E:GOTO24
66 MENU
