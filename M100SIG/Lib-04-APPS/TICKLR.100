0 'TICKLR.100 adapted by John Kupersmith from REMIND.100 by Don Corbitt and CLKCAL.IPL by Howard Benner.
1 'Set up "NOTE.DO" file with notes and dates.  Dates must be in MM/DD format (January 9 is "01/09"); year is optional.
2 'TICKLR will look for notes with current date; if none, it will go to clock/calendar.
3 'If a note is displayed, hit ENTER to move on.
4 'Change "84"s in line 12 to "85"s on 01/01/85 to maintain datebug fix.
5 CLS:MAXFILES=1:CLOSE:OPEN"note.do"FORINPUTAS1
6 IFEOF(1)THEN12ELSELINEINPUT#1,L$:Z$=LEFT$(DATE$,5)
7 IFINSTR(L$,Z$)=0THEN6ELSEPRINT:PRINTL$;:FORJ=1TO6:SOUND555,1:SOUND444,1:NEXT:LINEINPUTL$:GOTO6
12 MAXFILES=0:CLEAR1024:IFRIGHT$(DATE$,2)<>"84"THENDATE$=LEFT$(DATE$,6)+"84"
13 OP$="G":GOSUB3040:GOTO110
22 Y=-INT((14-W)/12):Z=X-32074+INT(1461*(V+4800+Y)/4):Z=Z+INT(367*(W-2-12*Y)/12):D=Z-INT(3*INT((V+4900+Y)/100)/4):RETURN
23 DATAJanuary,31,February,28,March,31, April,30,May,31,June,30
24 DATAJuly,31,August,31,September,30, October,31,November,30,December,31,EOF,-1
110 LINE(157,4)-(219,58),1,B:LINE(159,6)-(217,16),1,B:LINE(159,19)-(217,56),1,BF
111 PRINT@268,CHR$(27);CHR$(112);"- ";DAY$;" -";CHR$(27);CHR$(113);:TIME$ON
122 ONKEYGOSUB,,,,,,,600:KEY(8)ON
140 C=4697:D=20:F=-1:P=192:H=VAL(MID$(TIME$,4,5)):IFH<15THENM=1ELSEM=2:IFH>29THENM=3:IFH>44THENM=4:H=1
200 H=H+VAL(LEFT$(TIME$,2)):IFH>23THENH=0
220 H$=TIME$
240 IFH$=TIME$THENGOTO240
250 IFH$<>TIME$THENH$=TIME$: F=F/(-1):TYMP$=" "+TIME$
260 P=P+F:IFP<183ORP>193THENP=P-F
270 LINE(P-F,20)-(P-F,41),1:LINE(P-F-1,42)-(P-F+1,44),1,BF:LINE(P,20)-(P,41),0:LINE(P-1,42)-(P+1,44),0,BF
280 T2=VAL(LEFT$(TIME$,2)):IFT2>12THENT3=T2-12:TYMP$=STR$(T3)+RIGHT$(TIME$,6):IFT3<10THENTYMP$=TYMP$+" "
281 IFT2<10THENTYMP$=STR$(T2)+RIGHT$(TIME$,6)+" "
290 PRINT@67,TYMP$;:OP$=INKEY$:IFOP$<>""THENGOTO3040
300 GOTO250
600 TIME$OFF:MENU
3010 CLEAR1024
3030 CLS:PRINT@170," ";:INPUT"Month/year";D$
3040 GOSUB22:RESTORE23:IFD$=""THEND$= DATE$: DA%=VAL(MID$(D$,4,2)):D$=LEFT$(D$,3)+RIGHT$(D$,2)
3050 I%=INSTR(D$,"/"):IFI%<2ORI%>3ORI%=LEN(D$)THENBEEP:RUN3010
3060 W=VAL(LEFT$(D$,I%-1)):V=VAL(RIGHT$(D$,LEN(D$)-I%)):X=1
3061 IFW<1ORW>12ORV<0THENBEEP:GOTO3030 ELSEIFV<100THENV=V+1900
3100 GOSUB22:FORI%=1TOW:READM$,MD%:  NEXTI%:IFW=2THENMD%=MD%-(0=VMOD4)+(0=VMOD100)-(0=VMOD400)
3130 CLS:PRINTTAB(3);M$;:PRINT@18,V: PRINT"   Su Mo Tu We Th Fr Sa":D%=D-7*INT(D/7):PRINTTAB(2);SPACE$(3*D%);
3131 FORI%=1TOMD%:IFI%=DA%THENPRINT" ";:PRINTCHR$(27);"p";ELSEPRINT" ";
3132 PRINTUSING"##";I%;:IFI%=DA%THEN PRINT CHR$(27);"q";
3133 D%=D%+1:IFD%>6ANDI%<>MD%THENPRINT: PRINTTAB(2);:D%=0
3210 NEXTI%:IFOP$="G"THENRETURN
3211 PRINT@108,"<N>ext";:PRINT@148,"<P>revious";:PRINT@188,"<O>ther";
3212 PRINT@228,"<T>oday";:PRINT@268,"<M>enu";
3220 PRINT@28,DATE$;:PRINT@68,TIME$;: C$=INKEY$:IFC$=""THEN3220ELSEIF C$="M"ORC$="m"THENMENU
3221 IFC$="o"ORC$="O"THENRUN3010ELSEIFC$ ="T"ORC$="t"THENRUN12ELSEIFC$="P"ORC$= "p"THEN3225ELSEIFC$="N"ORC$="n"THEN3227
3222 BEEP:GOTO3220
3225 TD$=STR$(VAL(LEFT$(D$,2))-1):TD$= RIGHT$(TD$,2)+"/"+RIGHT$(D$,2):D$=TD$: DA%=0
3226 IFVAL(LEFT$(D$,2))=0THENTD$=STR$(12)+"/"+RIGHT$(STR$(VAL(RIGHT$(D$,2))-1),2):D$=RIGHT$(TD$,5):GOTO3040ELSEGOTO3040
3227 TD$=STR$(VAL(LEFT$(D$,2))+1):TD$=RIGHT$(TD$,2)+"/"+RIGHT$(D$,2):D$=TD$:DA%=0
3228 IFVAL(LEFT$(D$,2))=13THENTD$=STR$(1)+"/"+RIGHT$(STR$(VAL(RIGHT$(D$,2))+1),2):D$=RIGHT$(TD$,5)
3229 GOTO3040
