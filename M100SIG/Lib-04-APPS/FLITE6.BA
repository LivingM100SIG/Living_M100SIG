0 REM FLITE6.BA - 10/9/89 - Ver 2.00
10 CLS:CLEAR3000:RV$=CHR$(27)+"p":NV$=CHR$(27)+"q":MN$=CHR$(13)+CHR$(27):ES$="<ESC> to Quit, <ENTER> to Continue":F$="Fuel"
40 W$="Weight":BL$="Balance":E$="Empty":VL$="Values"
61 GOTO1520
70 RUN"FLIGHT.BA"
1520 CLS:PRINT@11,W$" & "BL$:PRINT@53,RV$;E$" "VL$;NV$:PRINTE$" "W$" ";EW:PRINTE$" Moment/X ";EM:PRINT"X = ";X
1530 PRINT@92,"";:INPUTEW:PRINT@93,SPACE$(9):PRINT@93,EW:PRINT@134,"";:INPUTEM:PRINT@135,SPACE$(9):PRINT@135,EM:PRINT@163,"";:INPUTX:PRINT@164,SPACE$(9):PRINT@164,X
1540 CLS:PRINT@11,W$" & "BL$:PRINT@53,RV$;"Arm "VL$;NV$:PRINT"Front Seat ";AF:PRINT@100,"Rear Seat ";AR
1550 PRINT"Baggage ";AB:PRINT@140,"Oil ";AO:PRINT"Main "F$;AM:PRINT@180,"Aux. "F$;AA
1560 PRINT@90,"";:INPUTAF:PRINT@91,SPACE$(8):PRINT@91,AF
1570 PRINT@109,"";:INPUTAR:PRINT@110,SPACE$(8):PRINT@110,AR
1580 PRINT@127,"";:INPUTAB:PRINT@128,SPACE$(9):PRINT@128,AB
1590 PRINT@143,"";:INPUTAO:PRINT@144,SPACE$(9):PRINT@144,AO
1600 PRINT@169,"";:INPUTAM:PRINT@170,SPACE$(9):PRINT@170,AM
1610 PRINT@189,"";:INPUTAA:PRINT@190,SPACE$(9):PRINT@190,AA
1620 PRINT@53,RV$;W$" "VL$;NV$:PRINT@91,SPACE$(8):PRINT@91,WF:PRINT@90,"";:INPUTWF:PRINT@91,SPACE$(8):PRINT@91,WF
1630 PRINT@110,SPACE$(8):PRINT@110,WR:PRINT@109,"";:INPUTWR:PRINT@110,SPACE$(8):PRINT@110,WR
1640 PRINT@128,SPACE$(9):PRINT@128,WB:PRINT@127,"";:INPUTWB:PRINT@128,SPACE$(9):PRINT@128,WB
1650 PRINT@140,SPACE$(19)
1660 PRINT@160,SPACE$(39):PRINT@171,RV$;F$" & Oil Gallons";NV$
1670 PRINT"Main Fuel Tanks ";MG:PRINT@215,"";:INPUTMG:PRINT@216,SPACE$(4):PRINT@216,MG
1680 PRINT@220,"Aux Fuel Tanks ";AG:PRINT@234,"";:INPUTAG:PRINT@235,SPACE$(4):PRINT@235,AG
1690 PRINT"Oil ";OG:PRINT@243,"";:INPUTOG:PRINT@244,"   ":PRINT@244,OG
1700 WT=EW+WF+WR+WB+(MG*6)+(AG*6)+(OG*7.5):MT=((EM*X)+(AF*WF)+(AR*WR)+(AB*WB)+(AO*(OG*7.5))+(AM*(MG*6))+(AA*(AG*6)))/X
1710 CLS:CLS:PRINT@11,W$" & "BL$:PRINT@80,"Total Loaded "W$" =";WT:PRINT@160,"Total Moment/";X;"=";MT:PRINT@240,"Center of Gravity =";INT((((MT*X)/WT)+.05)*10)/10:PRINT@283,ES$;
1720 GOSUB2750:ONINSTR(MN$,IK$)GOTO1520,70
1730 GOTO1720
2750 IK$=INKEY$:IFIK$=""THEN2750
2760 RETURN
