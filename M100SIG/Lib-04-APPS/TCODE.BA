0 'TCODE.BA   Time Code Calculator with   Memory  -  Fred Edelkamp  [70506,223]
1 'Type in Time code number, the numeric keypad works with NUM on or off, Plus  dosn't need to be shifted
2 'Press [-] Minus, [+] Plus, [C] Clear, [ENTER] Memory
3 'type in number, Press [=] Equals, [C] Clear, or [ENTER] Memory
4 'Total shown, Press any key to         continue. Total is  carried forward,    Press [C], [-], [+] or [ENTER] Memory
5 ' MEMORY
6 ' [C] Clear Memory, [R] Memory Recall, [-] Memory Minus, [+] Memory Plus
7 ' BKSP - correct last number entry
8 ' ESC  - Menu
9 '
10 CLS:PRINT@6,"*** Time Code Calculator ***":P=45:U=1800:W=30:Z=108000:H$=":":Q$="00:00:00:00":M$=Q$:V=2592000:M=V
20 PRINT@P,"HH:MM:SS:FF"
30 A$="00000000":B$=Q$
40 PRINT@P+40,B$
50 GOSUB470
70 IFI$=CHR$(13)THEN270
80 IFI$=CHR$(8)THENA$="0"+LEFT$(A$,7):GOTO180
90 IFI$>"/"ANDI$<H$THEN170
100 I=INSTR(1,"MmJjKkLlUuIiOoCc-_+=",I$):IFI=0THENBEEP:GOTO50
105 I=(I-1)\2:IFI<7THEN160
110 IFI=7THEN30
120 IFI=8THENIFC$=""THENC$="-"
140 IFI=9THENIFC$=""THENC$="+"
150 GOTO210
160 I$=RIGHT$(STR$(I),1)
170 A$=RIGHT$(A$,7)+I$
180 J=1:FORX=1TO8:MID$(B$,J,1)=MID$(A$,X,1):J=J+1:IFX\2=X/2THENJ=J+1
190 NEXT
200 GOTO40
210 GOSUB480:IFE$>""THEN30
220 G$=B$:GOSUB360
230 IFL=0THEN250ELSEIFC$="-"THEN236ELSET=L+T:IFT=>VTHENT=T-V
234 GOTO240
236 T=T-L:IFT<0THENT=V-2-T
240 C$="":L=T:GOSUB370:GOTO400
250 G$=B$:GOSUB360:L=T
260 PRINT@P+75,"  "C$"  ":P=P+40:GOTO30
270 G$=B$:GOSUB360
280 PRINT@60,"MC MR M- M+":G$=B$:GOSUB360:GOSUB470
290 I=(INSTR(1,"CcRr-_+=",I$)+1)\2:ONIGOTO310,320,330,340
300 BEEP:GOTO280
310 M$=Q$:M=V:PRINT@60,SPACE$(20):PRINT@100,SPACE$(20):GOTO40
320 B$=M$:PRINT@60,"   Memory  ":GOTO410
330 M=M-T:IFM<0THENM=V-2-M
335 GOTO345
340 M=T+M
345 IFM=>VTHENM=M-V
350 T=M:GOSUB370:M$=T$:PRINT@60,"   Memory  ":PRINT@100,M$:GOTO40
360 F=VAL(RIGHT$(G$,2)):H=VAL(LEFT$(G$,2)):N=VAL(MID$(G$,4,2)):S=VAL(MID$(G$,7,2)):T=F+(S*W)+(N*U)+(H*Z):IFT=0THENT=V
365 RETURN
370 IFT=VTHENT=0
375 H=INT(T/Z):T=T-H*Z:N=INT(T/U):T=T-N*U:S=INT(T/W):F=T-S*W
380 T$=RIGHT$(STR$(H),2)+H$+RIGHT$(STR$(N),2)+H$+RIGHT$(STR$(S),2)+H$+RIGHT$(STR$(F),2)
385 FORX=1TO10:IFMID$(T$,X,1)=" "THENMID$(T$,X,1)="0"
390 NEXT:RETURN
400 L=0:PRINT@165,STRING$(11,"-"):PRINT@205,T$:B$=T$:GOSUB470:P=45
410 J=1:FORX=1TO8:MID$(A$,X,1)=MID$(B$,J,1):J=J+1:IFX\2=X/2THENJ=J+1
420 NEXT:PRINT@120,SPACE$(100):GOTO40
470 I$=INKEY$:IFI$=""THEN470ELSEIFI$=CHR$(27)THENMENUELSERETURN
480 IFMID$(B$,10,2)>"29"THENE$="Frames":GOTO530
490 IFMID$(B$,7,2)>"59"THENE$="Seconds":GOTO530
500 IFMID$(B$,4,2)>"59"THENE$="Minutes":GOTO530
510 IFMID$(B$,1,2)>"23"THENE$="Hours":GOTO530
520 E$="":RETURN
530 PRINT"Error in "E$:BEEP:FORX=1TO500:NEXT:C$="":PRINT@P+75,SPACE$(20):RETURN

