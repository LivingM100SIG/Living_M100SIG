10 CLS:GOSUB200
20 PRINT"MARKET-100":PRINT"A Portfolio Management Program
30 PRINT"by Edwin Dethlefsen":PRINT:GOSUB200
40 PRINT"Copyright (c)1983 by Edwin Dethlefsen
50 FORI=1TO2000: NEXT I
60 CLS:INPUT"DATE(MMDDYY)";K
70 DIMA$(15),B(15),A(15),C(15),D(18),E$(15), K(18)
80 CLS:GOSUB200:PRINT"MENU:";
90 PRINTTAB(5);"(1) Create File (6) End
100 PRINTTAB(5);"(2) Update File (7)
110 PRINTTAB(5);"(3) Chart Trend/Review$$
120 PRINTTAB(5);"(4) Portfolio Summary
130 PRINTTAB(5);"(5) Tickers
140 GOSUB200: INPUT"ENTER CHOICE NUMBER";Z
150 ONZGOTO340,490,710,1130,270,160,1450
160 CLS:END
170 CLS:OPEN"TICKER"FOR INPUTAS1:RETURN
180 OPENA$(I)FOR INPUTAS1:RETURN
190 INPUTZ$:GOTO710
200 PRINTSTRING$(39,158):RETURN
210 INPUT"Press <ENTER> to continue"; Z$
220 II=0:CLS:RETURN
230 C(I)=A(I)*D(I):CC(I)=A(I)*B(I):RETURN
240 BB=BB+CC(I):DD=DD+C(I)
250 IFBB<DDTHENE$(I)="gain"ELSEE$(I)="loss"
260 RETURN
270 I=0:GOSUB200:GOSUB170
280 INPUT#1,A$(I)
290 PRINTA$(I);" ";:IFI=7THENPRINT
300 IFEOF(1)GOTO320
310 I=I+1:GOTO280
320 CLOSE:PRINT:PRINT"These are your current active files.":GOSUB200:GOSUB210
330 GOTO80
340 CLS
350 GOSUB200
360 PRINTTAB(8);"Enter ticker symbol";:INPUTA$
370 PRINTTAB(8);"Enter cost/share";:INPUTB
380 PRINTTAB(8);"Enter # shares purchased";:INPUTA
390 GOSUB200:PRINT"You own ";A;"shares ";A$;", Correct? (Y/N)"
400 INPUT"(Enter 'M' for Master Menu)";Z$
410 IFZ$="M"GOTO80
420 IFLEFT$(Z$,1)<>"Y"GOTO340
430 GOSUB200
440 OPENA$FOROUTPUTAS1
450 PRINT#1,B;A;:CLOSE
460 OPEN"TICKER"FORAPPENDAS1
470 PRINT#1,A$:CLOSE
480 PRINT"File created for";A$:GOSUB200:GOSUB210:GOTO80
490 I=0:GOSUB170
500 INPUT#1,A$(I)
510 IFEOF(1)GOTO530
520 I=I+1:GOTO500
530 CLOSE
540 CLS:GOSUB200:INPUT"Enter ticker ('M'for Menu)";A$(I)
550 ONERRORGOTO80
560 IFA$(I)="M"GOTO80
570 GOSUB180
580 INPUT#1,B(I),A(I)
590 IFEOF(1)GOTO600
600 CLOSE:GOSUB200
610 CLS:GOSUB200:PRINT"Current price of "A$(I);:INPUT" is ";D(I)
620 GOSUB690:GOSUB230
630 IFB(I)<D(I)THENE$="gain"ELSEE$="loss
640 PRINT"Current val of ";A$(I);" is";C(I)
650 PRINT"You paid";B(I);"/shr"
660 PRINT"This is a ";E$;" of ";D(I)-B(I);"/shr
670 PRINT"Current ";E$;" is ";INT(((D(I)-B(I))/B(I))*100);"%
680 GOSUB200:GOSUB210:GOTO540
690 OPENA$(I)FORAPPENDAS1
700 PRINT#1,K;D(I);:CLOSE:RETURN
710 CLS:GOSUB200
720 PRINTTAB(5);"MENU"
730 PRINTTAB(7);"(1) Review Dates/Prices
740 PRINTTAB(7);"(2) Chart Trend
750 PRINTTAB(7);"(3) Return to Master Menu":GOSUB200
760 INPUT"Enter number of your choice";Z
770 ONZGOTO780,920,80
780 CLS:J=1:T=40:GOSUB200
790 INPUT"Ticker symbol for price file";A$
800 ONERRORGOTO710
810 CLS:OPENA$FORINPUTAS1
820 INPUT#1,B,A
830 PRINT@1,"Date";:PRINT@8,"Price";:PRINT@21,"Date";:PRINT@28,"Price
840 INPUT#1,K(J),D(J)
850 PRINT@T,K(J);
860 PRINTUSING"###.## ";D(J);
870 T=T+20:J=J+1
880 IFEOF(1)GOTO900
890 GOTO840
900 CLOSE
910 GOTO190
920 CLS:J=0:L=279:N=6:X=0
930 INPUT"Enter ticker symbol ('M' for Menu)";A$
940 IFA$="M"GOTO710
950 OPENA$FORINPUTAS1
960 INPUT#1,B,A
970 CLS:FORM=40TO240STEP40:N=N-1
980 PRINT@M,N:NEXTM
990 J=J+1:X=X+12.5
1000 PRINT@0,A$;" Time Trend
1010 INPUT#1,K(J),D(J)
1020 L=L+2
1030 IFJ<10THENPRINT@L,J;ELSEPRINT@L,"$";
1040 IFJ<2THENGOSUB1110
1050 Y=D(J):IFY>60THENY=Y/2
1060 LINE-(X,62-Y)
1070 IFEOF(1)GOTO1090
1080 GOTO990
1090 CLOSE
1100 GOTO190
1110 LINE(X,54)-(235,7),1,B
1120 LINE(X,54-Y)-(X,54-Y):RETURN
1130 J=1:C=0:CC=0:BB=0:DD=0:GOSUB170
1140 INPUT#1,A$(J)
1150 IFEOF(1)GOTO1180
1160 J=J+1
1170 GOTO1140
1180 CLOSE
1190 FORI=1TOJ
1200 GOSUB180
1210 INPUT#1,B(I),A(I)
1220 IFEOF(1)GOTO1250
1230 INPUT#1,K(I),D(I)
1240 GOTO1220
1250 CLOSE
1260 GOSUB230
1270 GOSUB240
1280 PRINTUSING"####";A(I);
1290 PRINT" Shares ";
1300 PRINTUSING"\ \ ";A$(I);
1310 PRINT"show "; E$(I);" of";
1320 PRINTUSING"#####.##";C(I)-CC(I)
1330 II=II+1
1340 IFII=7THENGOSUB210
1350 NEXT
1360 GOSUB210:CLS:PRINT:GOSUB200
1370 PRINT"Total portfolio investment=";
1380 PRINTUSING"######.##";BB
1390 PRINT"Total portfolio value =";
1400 PRINTUSING"######.##";DD
1410 IFBB<DDTHENE$=" gain "ELSEE$=" loss
1420 PRINT"Total portfolio";E$;" =";
1430 PRINTUSING"######.##";DD-BB
1440 GOSUB200:GOSUB210:GOTO80
1450 CLS:GOSUB200:PRINT"Enter ticker of file to delete,
1460 INPUT"or 'M' to return to menu";A$
1470 ONERRORGOTO1450
1480 IFA$="M"GOTO80
1490 INPUT"Are you sure";Z$
1500 IFLEFT$(Z$,1)<>"Y"GOTO1450
1510 A$=A$+",DO
1520 KILL A$:PRINTA$;" FILE DELETED"
1530 PRINT"Remember to delete from ticker file
1540 FORI=1TO1500:NEXT:GOTO80
