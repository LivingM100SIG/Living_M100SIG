1'DUAT.100 (c)1990 FredM.Gray
2'(CIS71420,425)RequiresCONTEL-DUAT
3'Access. Please Read DUAT100.DOC.
4'Gets precise WX products from DUAT
5'without long dialogue.
6'Replace AC$ with access code, and
7'replace PW$ with password. (Line 10)
10 MAXFILES=4:CLEAR1000:DEFINT I-M:DEFSNG N:DEFSTR A-E:AC$="********":PW$="********":GOTO 40
20 LINE INPUT#1,B:IFINSTR(B,"--Press")THEN220
30 PRINT#3,B:PRINT"ã";:GOTO 20
40 K=0:A="18007679989<= ^M?:?:"+AC$+"^M?:"+PW$+"^M?$?N^M?:1^M?:3^M?:N6482J^M?:>":N=VARPTR(A):N=PEEK(N+1)+256*PEEK(N+2):CL=STRING$(40,241):CM=STRING$(7,239)
50 CLS:OPEN"WXRQ.DO"FOROUTPUTAS1:PRINT CM" Enter Location Identifier "CM:PRINT@40,CL;:PRINT@120,CL;
60 PRINT@160,"Use 3-letter identifier. If error, then":PRINT"Hit backspace. <ENTER> When done.":PRINT@240,CL;:CALL17020,0,4355
70 SOUND 800,5:PRINT"LOCID ";:CALL16989:LO$="":FORI=0TO2:AA=INPUT$(1):IFAA=CHR$(13)THENPRINT#1,:CLS:GOTO90ELSEIFAA=CHR$(8)THENCLS:GOTO60ELSEPRINTAA;:LO$=LO$+AA:NEXT
80 PRINT#1,LO$;" ";:CALL17020,0,4355:PRINT"Next ";:FORI=0TO500:NEXT:GOTO 70
90 PRINTCM;"  Select Weather Product ";CM:PRINTCL:PRINT"F1 - Hourly SA"TAB(21)"F5 - Pireps":PRINT"F2 - Term. F'cst"TAB(21)"F6 - Rdr. Summary":PRINT"F3 - Winds aloft"TAB(21)"F7 - Winds (12 hr)":PRINT"F4 - Notams"TAB(21)"F8 - End Requests";
100 KEYON:ON KEY GOSUB 120,125,130,140,150,160,165,180
110 SOUND8000,1:FORJ=0TO900:NEXT:GOTO110
120 PRINT#1,"SA ";:GOTO170
125 PRINT#1,"FT ";:GOTO170
130 PRINT#1,"FD1 ";:GOTO170
140 PRINT#1,"NO ";:GOTO170
150 PRINT#1,"UA ";:GOTO170
160 PRINT#1,"SD ";:GOTO170
165 PRINT#1,"FD2 ";
170 KEY OFF:SOUND4000,3:GOSUB290:GOTO100
180 PRINT#1,:KEY OFF:PRINT#1,"0000":CLOSE1
190 CLS:GOSUB290:PRINT"Thank you. Stand by":CALL28473:CALL28326,0,785:CALL28171:CALL21200:CALL 21293,0,N
200 OPEN"MDM:7I1E"FORINPUTAS1:OPEN"MDM:7I1E"FOROUTPUTAS2:OPEN"WX.DO"FOROUTPUTAS3:CLS:PRINT"LOADING DATA"
210 OPEN"WXRQ.DO"FORINPUTAS4:PRINT:FORI=0TO2:LINEINPUT#4,DA:PRINT#2,DA;CHR$(13);:NEXT:CLOSE4:CALL 28190:FORI=0TO2000:NEXT:CALL28171:FORI=0TO6:LINEINPUT#1,B:NEXT:GOTO20
220 FORI=0TO1:PRINT#2,"Q":NEXT:PRINT#2,"Y":CLOSE2:CALL 21179:CLOSE:KILL"WXRQ.DO":CLS:PRINT@40,CL;:PRINT@88,"To view the weather data":PRINT"press <SPACEBAR>.":PRINT"Then press again to view next"
230 PRINT"        screen. Or <ENTER> now to quit":PRINTCL;
240 AN=INKEY$:IFAN=""THEN240ELSEIFAN<>" "THEN280
250 OPEN"WX.DO" FOR INPUT AS 1
260 CLS:FORI=0TO6:IF EOF(1)THEN280:ELSELINEINPUT#1,AB
270 PRINTAB:SOUND2000,1:NEXT:IFEOF(1)THEN280ELSELINEINPUT#1,AB:PRINTAB;:AN=INPUT$(1):GOTO260
280 PRINT"Bye!":CLOSE:END
290 CALL17020,0,4355:CALL16989:RETURN
