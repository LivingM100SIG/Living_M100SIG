5 O$="###,###.##":SCREEN0,1
10 KEYON:KEY(6)OFF:KEY(7)OFF
20 KEY 1,"Rate":KEY2,"Term":KEY3,"Prin":KEY4,"Per1":KEY5,"Per2":KEY6,"":KEY7,""
30 PV=55100:I=10.75:N%=30:J%=1:K%=12
40 GOSUB70
50 ONKEYGOSUB1020,1040,1000,1060,1080,5,5,1100
60 GOTO50
70 PM=PV*I/1200/(1-(1+I/1200)^(-N%*12))
72 PP%=J%-1:GOSUB2000
74 B1=BA:PP%=K%:GOSUB2000
76 IN=BA-B1+(K%-J%+1)*PM
80 CLS:PRINT@14,"Mortgage Loan":PRINT:PRINT@81,"Rate(%,yr):";I
82 PRINT@121,"Term(yrs): ";N%:PRINT@161,"Prin.:  ";PV
84 PRINT@201,"Payment:";USINGO$;PM
86 PRINT@101,"Per 1: ";J%:PRINT@141,"Per 2: ";K%
90 PRINT@181,"Int.: ";USINGO$;IN
120 RETURN
1000 CLS:PRINT@161,"Input principal";:INPUTPV:GOSUB70
1010 RETURN
1020 CLS:PRINT@161,"Input the annual rate (%)";:INPUTI:GOSUB70
1030 RETURN
1040 CLS:PRINT@161,"Input the term of the loan (years)";:INPUTN%:GOSUB70
1050 RETURN
1060 CLS:PRINT@161,"Input the starting period";:INPUTJ%:GOSUB70
1070 RETURN
1080 CLS:PRINT@161,"Input the ending period";:INPUTK%:GOSUB70
1090 RETURN
1100 KEY1,"Files"+CHR$(13):KEY2,"Load "+CHR$(34):KEY3,"Save "+CHR$(34)
1200 KEY4,"Run"+CHR$(13):KEY5,"List ":KEY6,"Edit ":KEY7,"Kill "+CHR$(34):MENU
2000 PO=(1+I/1200)^-PP%
2010 BA=(PM*(PO-1)/I*1200+PV)/PO:RETURN

