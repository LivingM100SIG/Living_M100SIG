0 REM FLITE1.BA - 10/9/89 - Ver. 2.00
10 CLS:CLEAR3000:DIMS$(9),S(9):RV$=CHR$(27)+"p":NV$=CHR$(27)+"q":MN$=CHR$(13)+CHR$(27):ES$="<ESC> to Quit, <ENTER> to Continue"
20 DG$=CHR$(166):ST$="Statute":NA$="Nautical":MI$=" Miles":K$=" Kilometers":HM$="How Many"
50 FORZ=1TO8:READS$(Z):NEXT
60 FORZ=1TO8:READS(Z):NEXT
61 GOTO100
70 RUN"FLIGHT.BA"
100 CLS:PRINTTAB(12);"CONVERSIONS MENU":GOSUB2530
110 CLS:ONS3GOTO130,150,170,240,260,280,300,370,70
120 GOTO100
130 CLS:PRINT@16,DG$;"F -> ";DG$;"C":PRINT@120,HM$" ";DG$;"F";:INPUTTF:TC=(TF-32)/1.8:TC=INT((TC+.05)*10)/10:PRINT@120,TF;DG$;"F =";TC;DG$;"C  ":GOSUB2490
140 GOTO130
150 CLS:PRINT@4,ST$MI$" -> "NA$MI$:PRINT@120,HM$" "ST$MI$;:INPUTSM:NM=.8689762*SM:NM=INT((NM+.05)*10)/10:PRINT@120,SM;"Stat"MI$" =";NM;"Naut"MI$:GOSUB2490
160 GOTO150
170 CLS:PRINT@9,MI$" ->"K$:PRINT@120,ST$" or "NA$MI$" (S/N)?";
180 GOSUB2750:ONINSTR("SsNn",IK$)GOTO200,200,200,200
190 GOTO180
200 PRINT@120,SPACE$(39):PRINT@120,HM$MI$;:INPUTMI:IFIK$="N"ORIK$="n"THENM1=MI:GOTO210ELSEM1=MI*.8689762
210 KM=1.852*M1:KM=INT((KM+.05)*10)/10:IFIK$="N"ORIK$="n"THENPRINT@120,MI;NA$;:ELSEPRINT@120,MI;ST$;
220 PRINTMI$" =";KM;K$:GOSUB2490
230 GOTO170
240 CLS:PRINT@14,"Mph -> Knots":PRINT@120,HM$" Mph";:INPUTSM:NM=.8689762*SM:NM=INT((NM+.05)*10)/10:PRINT@120,SM;"Mph =";NM;"Knots":GOSUB2490
250 GOTO240
260 CLS:PRINT@16,DG$;"C -> ";DG$;"F":PRINT@120,HM$" ";DG$;"C";:INPUTTC:TF=TC*1.8+32:PRINT@120,TC;DG$;"C =";TF;DG$;"F  ":GOSUB2490
270 GOTO260
280 CLS:PRINT@4,NA$MI$" -> "ST$MI$:PRINT@120,HM$" "NA$MI$;:INPUTNM:SM=NM/.8689762:SM=INT((SM+.05)*10)/10:PRINT@120,NM;"Naut"MI$" =";SM;"Stat"MI$:GOSUB2490
290 GOTO280
300 CLS:PRINT@9,K$" ->"MI$:PRINT@120,ST$" or "NA$MI$" (S/N)?";
310 GOSUB2750:ONINSTR("SsNn",IK$)GOTO330,330,330,330
320 GOTO310
330 PRINT@120,SPACE$(39):PRINT@120,HM$K$;:INPUTKM:MI=KM/1.852:IFIK$="N"ORIK$="n"THEN340ELSEMI=MI/.8689762
340 MI=INT((MI+.05)*10)/10:PRINT@120,KM;K$" =";MI;:IFIK$="N"ORIK$="n"THENPRINTNA$MI$ELSEPRINTST$MI$
350 GOSUB2490
360 GOTO300
370 CLS:PRINT@14,"Knots -> Mph":PRINT@120,HM$" Knots";:INPUTNM:SM=NM/.8689762:SM=INT((SM+.05)*10)/10:PRINT@120,NM;"Knots =";SM;"Mph":GOSUB2490
380 GOTO370
2490 PRINT@283,ES$;
2500 GOSUB2750:ONINSTR(MN$,IK$)GOTO2520,100
2510 GOTO2500
2520 RETURN
2530 FORZ=1TO8:PRINT@S(Z),S$(Z):NEXTZ:S3=1:PRINT@283,ES$;
2540 PRINT@S(S3),RV$;S$(S3);NV$
2550 GOSUB2750:ONINSTR(MN$,IK$)GOTO2600,70
2560 IK=ASC(IK$)-29:IFIK<0THEN2550ELSEONIKGOTO2580,2590
2570 GOTO2550
2580 PRINT@S(S3),S$(S3):S3=S3-1:IFS3<1THENS3=8:GOTO2540ELSE2540
2590 PRINT@S(S3),S$(S3):S3=S3+1:IFS3>8THENS3=1:GOTO2540ELSE2540
2600 RETURN
2750 IK$=INKEY$:IFIK$=""THEN2750
2760 RETURN
2780 DATA"Temp F->Temp C","Stat mi->Naut mi","Mi->Km","Mph->Knots","TempF<-TempC","Stat mi<-Naut mi","Mi<-Km","Mph<-Knots"
2840 DATA80,120,160,200,100,140,180,220
