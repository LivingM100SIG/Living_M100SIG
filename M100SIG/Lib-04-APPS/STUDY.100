0 ' STUDY.BA v2.0 "The Scholar Reviews"
1 ' Copyright 1987 by David Dunn Thomas
2 ' All rights reserved
3 ' From GEnie's Laptops RoundTable
4 ' Shareware: Contribution appreciated
5 ' Freely distribute, with NO changes.
6 ' DATA lines and above remarks are not to be removed prior distribution.
7 GOTO14
8 DATA 32,84,104,101,32,83,99,104,111,108,97,114,32,82,101,118,105,101,119,115,32,97,114,101,32,97,32,115,104,97,114,101,119,97,114,101,32,32,32,32,32,112,114,111,100,117,99,116,32,99,111,112,121,114,105,103,104,116,32,98,121,32,68,97,118,105,100
9 DATA 32,68,117,110,110,32,84,104,111,109,97,115,32,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,241,32,83,101,110,100,32,36,36,32,116,111
10 DATA 58,32,50,51,48,56,32,67,104,101,116,119,111,111,100,32,67,105,114,99,108,101,44,32,49,48,51,32,32,32,32,32,32,32,32,32,32,32,32,32,32,32,84,105,109,111,110,105,117,109,44,32,77,68,32,50,49,48,57,51
11 FORJ=1TO8:CALL17909:NEXT:FORIP=1TOLEN(P$):POKE64984+IP,ASC(MID$(P$,IP,1)):NEXT:FORJ=1TOLEN(PV$):POKE64783+J,ASC(MID$(PV$,J,1)):NEXT:RETURN
12 CLS:PRINT@55,R$" Correct! "N$:C=C+1:RETURN
13 CALL21795:CALL16974:C=C-1:RETURN
14 CLEAR2000:DEFINTA-Z:DIMP(320):E$=CHR$(27):R$=E$+"p":N$=E$+"q":Z$=CHR$(124):ZT$=CHR$(126):P$=SPACE$(23)+"Press any key ->":CLS:PRINT@89,R$" T H E  S C H O L A R "N$:PRINT@134,"v2.0  6/3/87":PRINT@207,CHR$(171)" 1987 by David Dunn Thomas":ONERRORGOTO46
15 FORI=81TO271:READP(I):NEXT:FORT=1TO2500:NEXT
16 CLS:PRINT@45,"Welcome to "R$" Scholar's "N$" reviews!":PRINT@86,"Multiple Choice or True/False"
17 C=0:PRINT@162,"Which lesson do you wish to review?":PRINT@202,"";:LINEINPUTF1$:IFLEN(F1$)>6ORMID$(F1$,2,1)=":"THENPRINT"  Try again, filename only: ";:LINEINPUTF1$
18 PRINT@252,"[R]am or [D]isk ";
19 D$=INPUT$(1):IFD$<>"D"ANDD$<>"d"ANDD$<>"R"ANDD$<>"r"THEN19
20 IFD$="D"ORD$="d"THENF$="0:"+F1$+".DO"ELSEF$=F1$
21 OPENF$FORINPUTAS1:INPUT#1,X$,Q,T$:IFX$<>Z$THEN44
22 CLS:PRINT@47,"Scholar is ready to Review:":PRINT@(40-LEN(T$))/2+120,R$" "T$" "N$:FORI=1TOQ:INPUT#1,A,X$,Q$:IFX$<>Z$ANDX$<>ZT$THEN44
23 IFX$=ZT$THEN32
24 FORJ=1TO4:INPUT#1,A$(J):NEXT:INPUT#1,V,V$,PV$:GOSUB11:CLS:PRINT@41,Q$":":PRINT:FORK=1TO4:PRINT" "RIGHT$(STR$(K),1)") "A$(K)
25 NEXTK:PRINT@281,R$"?"N$" ";
26 A$=INPUT$(1):AN=VAL(A$):IFASC(A$)=27THENCLOSE:GOTO43
27 IFAN>4THEN26
28 IFAN=ATHENGOSUB12:GOTO30
29 GOSUB13:GOTO26
30 IFVTHENPRINT@(40-LEN(A$(A)))/2+120,A$(A):PRINT@(40-LEN(V$))/2+200,V$:GOTO39
31 PRINT@(40-LEN(Q$))/2+120,Q$:PRINT@(40-LEN(A$(A)))/2+200,A$(A)".":GOTO39
32 IFA=1THENAN$="F"ELSEIFA=2THENAN$="T"ELSE44
33 INPUT#1,V,V$,PV$:GOSUB11:CLS:PRINT@81,Q$".":PRINT@171,"[T]rue or [F]alse?":PRINT@219,R$"?"N$" ";
34 A$=INPUT$(1):A$=CHR$(ASC(A$)AND223):IFASC(A$)=27THENCLOSE:GOTO43
35 IFA$=AN$THENGOSUB12:GOTO37
36 GOSUB13:GOTO34
37 IFVTHENPRINT@(40-LEN(V$))/2+120,V$".":GOTO39
38 PRINT@(40-LEN(Q$))/2+120,Q$"."
39 IFI<>QTHEN41
40 FORT=1TO2000:NEXT:CLOSE:GOTO42
41 NEXT
42 CLS:PRINT@243,"Review another lesson? ";:A$=INPUT$(1):IFA$="Y"ORA$="y"THENCLS:GOTO17
43 CLS:PRINT@81,"Without use of reviews, you were able   to get"C"out of"Q"correct.":FORI=1TO40:POKE64703+I,229:NEXT:FORI=41TO80:POKE64703+I,231:NEXT:FORI=81TO271:POKE64703+I,P(I):NEXT:FORI=281TO320:POKE64703+I,230:NEXT:CLS:CALL21795:CALL16974:MENU
44 CLS:PRINT@41,"File "F$" is not properly formatted":PRINT" and, very likely, is not a "R$" Scholar "N$"    file, which requires a specific format.";:PRINT:PRINT" Be sure to see SCHOLAR DOCUMENTATION.":PRINT@280,P$;
45 A$=INKEY$:IFA$=""THEN45ELSEMENU
46 IFERR=54THEN44
47 IFERR=52THENCLS:PRINT@41,"Sorry, unable to locate "F$".":PRINT@81,"File MUST be a .DO file in Scholar's    format. See SCHOLAR DOCUMENTATION.":PRINT@161,"Use GOTO15 command at 'Ok` prompt to    try again!":END
48 IFERR=18ORERR=55ORERR=68THENCLS:PRINT@41,"Disk drive not on, or DOS not loaded.":PRINT:PRINT" Is disk in the drive?":END
49 CLS:PRINT"Error"ERR" in line"ERL:END
