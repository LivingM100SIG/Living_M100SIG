0 'MAILING LIST
1 'Eiji Miura [71216,1564]  06/27/88
10 CLS:CLEAR2000:SCREEN0,0:MAXFILES=1:POKE63056,128:GOSUB22:GOTO56
11 CLS:PRINTM$(M,0)
12 X=M1(M)
13 FORL=M1(M)TOM2(M):IFX=LTHENPRINT@L*40,R$;
14 PRINT@L*40,M$(M,L);N$;:NEXT
15 A$=INKEY$
16 IFA$=CHR$(13)THENRETURN
17 IFA$<>CHR$(30)ANDA$<>CHR$(31)THEN15
18 PRINT@X*40,M$(M,X);
19 IFA$=CHR$(30)THENX=X-1:IFX<M1(M)THENX=M2(M)
20 IFA$=CHR$(31)THENX=X+1:IFX>M2(M)THENX=M1(M)
21 PRINT@X*40,R$;M$(M,X);N$;:GOTO15
22 FL$="ADRS.DO"
23 B$=CHR$(27):R$=B$+"p":N$=B$+"q":C$=B$+"K"
24 M1(1)=1:M2(1)=4:M$(1,0)="MAILING LIST SYSTEM MENU":M$(1,1)=" FILER  SUBSYSTEM ":M$(1,2)=" SEARCH SUBSYSTEM ":M$(1,3)=" PRINT  SUBSYSTEM ":M$(1,4)=" EXIT"+SPACE$(13)
25 M1(2)=1:M2(2)=3:M$(2,0)="PRINT SUBSYSTEM":M$(2,1)=" SYSTEM MENU   ":M$(2,2)=" WHOLE LIST    ":M$(2,3)=" SELECTED LIST "
26 M1(3)=1:M2(3)=3:M$(3,0)="SEARCH SUBSYSTEM":M$(3,1)=M$(2,1):M$(3,2)=M$(2,2):M$(3,3)=M$(2,3)
27 M1(4)=1:M2(4)=3:M$(4,0)="PRINT OPTION":M$(4,1)=M$(1,3):M$(4,2)=" ON LABEL"+SPACE$(9):M$(4,3)=" LISTING"+SPACE$(10)
28 M1(5)=1:M2(5)=5:M$(5,0)="SELECT FIELD":M$(5,1)=" "+M$(2,0)+" ":M$(5,2)=" LAST  NAME"+SPACE$(6):M$(5,3)=" FIRST NAME"+SPACE$(6):M$(5,4)=" CITY"+SPACE$(12):M$(5,5)=" STATE"+SPACE$(11)
29 M1(6)=0:M2(6)=7:M$(6,1)=M$(5,2):M$(6,2)=M$(5,3):M$(6,3)=" TELEPHONE #     ":M$(6,4)=" ADDRESS"+SPACE$(9):M$(6,5)=M$(5,4):M$(6,6)=M$(5,5):M$(6,7)=" ZIP CODE"+SPACE$(8):M$(6,0)=" CONTINUE"+SPACE$(8)
30 M1(7)=1:M2(7)=3:M$(7,0)="FILER SUBSYSTEM":M$(7,1)=" SAVE  NAME ":M$(7,2)=" EDIT  NAME ":M$(7,3)=" CLEAR NAME "
31 M1(8)=1:M2(8)=5:M$(8,0)=M$(5,0)+SPACE$(5):M$(8,1)=" "+M$(3,0)+" ":M$(8,2)=M$(5,2)+" ":M$(8,3)=M$(5,3)+" ":M$(8,4)=M$(5,4)+" ":M$(8,5)=M$(5,5)+" "
32 M1(9)=1:M2(9)=5:M$(9,0)="SEARCH OPTION":M$(9,1)=M$(1,2):M$(9,2)=" NAME"+SPACE$(13):M$(9,3)=" NAME & TEL #     ":M$(9,4)=" NAME & ADDRESS   ":M$(9,5)=" ALL"+SPACE$(14)
33 M1(10)=1:M2(10)=5:M$(10,0)=M$(5,0):M$(10,1)=M$(8,1):M$(10,2)=M$(8,2):M$(10,3)=M$(8,3):M$(10,4)=M$(8,4):M$(10,5)=M$(8,5):RETURN
34 FORI=1TO7:INPUT#1,T$(I):IFLEFT$(T$(I),1)>CHR$(127)THENCLOSE1:ED=1:RETURN
35 NEXT
36 FORI=1TO2
37 FORJ=10TO1STEP-1
38 IFMID$(T$(I),J,1)<>" "THENJ=1:GOTO40
39 T$(I)=LEFT$(T$(I),J-1)
40 NEXTJ,I
41 T$(3)=LEFT$(T$(3),LEN(T$(3))-1):T$(3)=RIGHT$(T$(3),LEN(T$(3))-1):RETURN
42 SL=1
43 IFX=2THENK=1
44 IFX=3THENK=2
45 IFX=4THENK=5
46 IFX=5THENK=6
47 PRINT@280,"Search String: ";:PRINT@295,"";:LINEINPUTSC$(K):IFSC$(K)=""THENSL=0
48 RETURN
49 PRINT@62,"";:LINEINPUTLN$:IFLEN(LN$)>10THENBEEP:PRINT@62,"Too long"+C$:FORT=1TO300:NEXT:PRINT@62,C$:GOTO49ELSERETURN
50 PRINT@102,"";:LINEINPUTFN$:IFLEN(FN$)>10THENBEEP:PRINT@102,"Too long"+C$:FORT=1TO300:NEXT:PRINT@102,C$:GOTO50ELSERETURN
51 PRINT@142,"";:LINEINPUTTL$:RETURN
52 PRINT@182,"";:LINEINPUTAD$:RETURN
53 PRINT@222,"";:LINEINPUTCT$:RETURN
54 PRINT@262,"";:LINEINPUTST$:IFLEN(ST$)>2THENBEEP:PRINT@262,"2 letters"+C$;:FORT=1TO300:NEXT:PRINT@262,C$;:GOTO54ELSERETURN
55 PRINT@302,"";:LINEINPUTZP$:RETURN
56 M=1:GOSUB11
57 ONERRORGOTO140
58 ONXGOTO118,85,60,59
59 POKE63056,0:MENU
60 M=2:SL=0:GOSUB11
61 ONXGOTO56,65
62 M=5:GOSUB11
63 ONXGOTO60
64 GOSUB42
65 M=4:GOSUB11
66 ONXGOTO60
67 CLS:PRINT" Printout in progress...":OPENFL$FORINPUTAS1
68 IFEOF(1)THENCLOSE1:GOTO60
69 GOSUB34
70 IFED=1THENED=0:GOTO60
71 IFSL=0THEN73
72 IFINSTR(T$(K),SC$(K))=0THEN68
73 IFX=2THEN80
74 LPRINTTAB(0)T$(1)", "T$(2);
75 LPRINTTAB(24)T$(4)",";
76 LPRINTTAB(45)T$(5)",";
77 LPRINTTAB(66)T$(6)"   "T$(7)
78 LPRINTTAB(24)T$(3)
79 GOTO68
80 LPRINTTAB(2)T$(1)", "T$(2)
81 LPRINTTAB(2)T$(4)
82 LPRINTTAB(2)T$(5)", "T$(6)"  "T$(7)
83 LPRINT:LPRINT:LPRINT
84 GOTO68
85 M=3:SL=0:GOSUB11
86 ONXGOTO56,90
87 M=10:GOSUB11
88 ONXGOTO85
89 GOSUB42
90 M=9:GOSUB11
91 ONXGOTO85,92,93,94,95
92 TT=0:AA=0:GOTO96
93 TT=1:AA=0:GOTO96
94 TT=0:AA=1:GOTO96
95 TT=1:AA=1
96 OPENFL$FORINPUTAS1
97 CLS:PRINT" Searching...":IFEOF(1)THENCLOSE1:GOTO114
98 GOSUB34
99 CLS:IFED=1THENED=0:GOTO114
100 IFSL=0THEN102
101 IFINSTR(T$(K),SC$(K))=0THEN97
102 PRINT@0,T$(1)", "T$(2)
103 IFTT=0ANDAA=0THEN109
104 IFTT=1THENPRINT@42,T$(3)
105 IFAA=0THEN109
106 PRINT@82,T$(4)
107 PRINT@122,T$(5)
108 PRINT@162,T$(6)"  "T$(7);
109 PRINT@240,CHR$(234)R$"N"N$"ext":PRINT@280,CHR$(234)R$"M"N$"enu";
110 A$=INKEY$:IFA$=""THEN110
111 IFA$="N"ORA$="n"THEN97
112 IFA$="M"ORA$="m"THENCLOSE1:GOTO85
113 GOTO110
114 PRINT@0," End of File"
115 PRINT@280,"Press [ENTER] to continue";
116 A$=INKEY$:IFA$<>CHR$(13)THEN116
117 GOTO85
118 CLS:PRINT"FILER SUBSYSTEM         [ENTER] for Menu"
119 LN$="":FN$="":TL$="":AD$="":CT$="":ST$="":ZP$=""
120 PRINT@40," LAST  NAME (Max 10):";:GOSUB49
121 IFLN$=""THEN56ELSEPRINT@24,C$;
122 PRINT@80," FIRST NAME (Max 10):";:GOSUB50
123 PRINT@120," TELEPHONE NUMBER   :";:GOSUB51
124 PRINT@160," ADDRESS            :";:GOSUB52
125 PRINT@200," CITY               :";:GOSUB53
126 PRINT@240," STATE (2 Letters)  :";:GOSUB54
127 PRINT@280," ZIP CODE           :";:GOSUB55
128 CLS:PRINT@62,LN$:PRINT@102,FN$:PRINT@142,TL$:PRINT@182,AD$:PRINT@222,CT$:PRINT@262,ST$:PRINT@302,ZP$;
129 M=6:GOSUB12
130 IFX=0THEN133
131 ONXGOSUB49,50,51,52,53,54,55
132 GOTO128
133 M=7:GOSUB11
134 ONXGOTO135,128,118
135 CLS:IFLEN(LN$)<10THENLN$=LN$+" ":GOTO135
136 IFLEN(FN$)<10THENFN$=FN$+" ":GOTO136
137 TX$=LN$+","+FN$+",:"+TL$+"<>:,"+AD$+","+CT$+","+ST$+","+ZP$
138 OPENFL$FORAPPENDAS1
139 PRINT#1,TX$:CLOSE1:GOTO118
140 BEEP:IFERR=52THENRESUME56ELSEPRINT"Error #"ERR" in Line "ERL:STOP
