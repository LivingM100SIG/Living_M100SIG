0 'DSKLBL.BA v1.5 100/200 Disk label utility (c)1989,R.HESS using FYLDOS.100/200(c)1989,Wilson Van Alst with permission. All rights reserved
2 GOSUB17:IFFRE(0)<12000THENBEEP:PRINT"NEED 12000 FREE!":ENDELSECLEAR9000:MAXFILES=1:DEFSTRA-F:DEFINTG-Y:B="ZZ"+CHR$(0)+CHR$(26)+SPACE$(24)+"F":E=CHR$(27):F=SPACE$(10):DIMD(15,52)
4 PRINT@15,"Loading...":FORX=1TO15:FORY=0TO52:D(X,Y)=F:NEXT:NEXT:GOSUB28:PRINT@14,E"p DSKLBL.BA "E"q":LINE(0,38)-(239,38)
6 FORX=1TO15:GOSUB18:PRINT@85-LEN(STR$(X)),E"KDisk"X"Name: ______   (<Cr> to Prt)":PRINT@96,"";:LINEINPUTD:IFD=""THENT=X-1:GOTO14ELSEMID$(D(X,1),1,6)=MID$(D,1,6):PRINT@96,E"K"MID$(D,1,6):PRINT#1,BCHR$(1)CHR$(158):J=8:GOTO10
8 PRINT@108,CHR$(135)F:PRINT#1,BCHR$(2)CHR$(157)
10 V=VARPTR(F):CALLG,,PEEK(V+1)+256*PEEK(V+2):IFASC(F)THENJ=J+1:GOSUB38:MID$(D(X,J),2,8)=MID$(F,1,8):GOTO8ELSEMID$(D(X,2),1,LEN(STR$(J-8)))=STR$(J-8):MID$(D(X,2),5,6)="files;"
12 MID$(D(X,3),1,LEN(STR$(1280*PEEK(S))))=STR$(1280*PEEK(S)):MID$(D(X,3),7,4)="free":MID$(D(X,4),2,8)=DATE$:FORP=49TO52:MID$(D(X,P),1,10)=STRING$(10,45):NEXT:MID$(D(X,52),10)=" ":PRINT@192+X*8,E"q"MID$(D,1,6);:NEXT:T=15
14 GOSUB20:IFPS>2THENFORH=1TOTSTEP3:FORL=0TO12:FORM=HTOH+2:FORN=1+L*4TO4+L*4:LPRINTD(M,N);:NEXT:LPRINT"   ";:NEXT:LPRINT:NEXT:NEXT:GOTO16
15 FORH=1TOT:FORN=1TO4:LPRINTD(H,N);:NEXT:LPRINT:FORM=9TO48STEP4:FORO=0TO3:LPRINTD(H,M+O);:NEXT:LPRINT:NEXT:LPRINT:NEXT
16 GOSUB17:PRINT@126,"Print label set again? Y/N ";:AG=INPUT$(1):IFINSTR("Yy",AG)THEN14ELSEGOSUB60:MENU
17 CLS
18 ONERRORGOTO50:SOUND1108,1:RETURN
20 CLS:IF(INP(187)AND6)-2THENBEEP:BEEP:PRINT@120,"PRINTER NOT READY! Fix & press any key.";:AA=INPUT$(1):GOTO20
22 GOSUB17:PRINT@42,"<S>tandard: 8 1/2 by 11  (3 across)"SPACE$(44)"<L>abel: 1 15/16 by 2 3/4  (1 across)"SPACE$(51)"Select page size: ";:PS=INSTR("LlSs",INPUT$(1)):IFPS=0THEN22
23 GOSUB17:PRINT@161," When paper is ready press any key.";:AA=INPUT$(1):CLS:PRINT@96,"Printing..."
24 LPRINTCHR$(31);'132 column code
26 RETURN
28 IFPEEK(1)=171THEN32ELSEGOSUB66:RESTORE30:G=-832:S=-788:OPEN"COM:98N1D"FOROUTPUTAS1:FORI=-832TO-792:READR:POKEI,R:Q=Q+R:NEXT:IFQ-5322THEN36ELSERETURN'100
30 DATA 205,126,109,205,126,109,14,9,205,126,109,119,35,13,194,200,252,14,16,205,126,109,13,194,211,252,33,238,252,14,4,205,126,109,119,43,13,194,223,252,201
32 GOSUB64:RESTORE34:G=-1936:S=-1878:OPEN"COM:98N1DNN"FOROUTPUTAS1:FORI=-1936TO-1896:READR:POKEI,R:Q=Q+R:NEXT:IFQ-4615THEN36ELSERETURN'200
34 DATA 205,25,133,205,25,133,14,9,205,25,133,119,35,13,194,120,248,14,16,205,25,133,13,194,131,248,33,172,248,14,4,205,25,133,119,43,13,194,143,248,201
36 BEEP:CLS:PRINT"Bad Data!":END
38 FORI=1TO6:IFASC(MID$(F,I,1))AND128THENMID$(F,I,1)="^"
40 NEXT:FORI=8TO10:MID$(F,I-1)=CHR$(ASC(MID$(F,I))OR32):NEXT:RETURN
50 BEEP:IFLEN(DM)THENGOSUB60
52 CLS:PRINT"Err"ERR"Erl"ERL:END
60 IFPEEK(1)=171THENFORZ=1TO7:POKE61243+Z,ASC(MID$(DM,Z)):NEXT:RETURN
62 FORZ=1TO5:POKE63066+Z,ASC(MID$(DM,Z)):NEXT:RETURN
64 FORZ=61244TO61250:DM=DM+CHR$(PEEK(Z)):NEXT:RETURN
66 FORZ=63067TO63071:DM=DM+CHR$(PEEK(Z)):NEXT:RETURN
