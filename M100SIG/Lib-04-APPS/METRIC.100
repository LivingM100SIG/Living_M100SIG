1 'English/Metric Conversion
2 'Program for TRS-80 Model 100 Portable Computer
3 'by: W.H. Templeton
4 '    78 Larkstone Ct.                     (2715 bytes - .BA file)
5 '    Danville, CA 94526                   (3406 bytes - .DO file)
6 'phone: (415) 820-3777,(408) 496-0800
7 'email: [75655,1056]
8 '
9 CLS:LINE(0,0)-(239,63),1,B:LINE(3,3)-(236,60),1,B
10 RV$=CHR$(27)+"p":NO$=CHR$(27)+"q":PRINTRV$:PRINT@89," M E T R I C . 1 0 0 ":PRINTNO$:PRINT@134,"Version 3.3
20 PRINT@174," (c) - 1983":PRINT@250,"by: Wm. H. Templeton":FORT=1TO1000:NEXTT
30 CLS:PRINTRV$:PRINT@43," choose one: ":PRINTNO$:PRINT@130,"(E)nglish to metric":PRINT@170,"(M)etric to english"
35 PRINTRV$:PRINT@255," press ENTER for MENU...";:PRINTNO$
40 A$=INPUT$(1)
50 IFA$="E"ORA$="e"THEN80
60 IFA$="M"ORA$="m"THEN90
70 IFA$=CHR$(13)THENMENU ELSE40
80 PRINT@129,">":FOR T=1TO300:NEXT T:GOTO100
90 PRINT@169,">":FOR T=1TO300:NEXT T:GOTO700
100 CLS:PRINTRV$:PRINT@3," choose one: ":PRINTNO$:PRINT@50,"(I)nches to cm":PRINT@90,"(Y)ards to meters":PRINT@130,"(M)iles to Km
105 PRINT@170,"(G)allons to liters":PRINT@210,"(P)ounds to Kg":PRINTRV$:PRINT@254," press ENTER to return...";:PRINTNO$
110 A$=INPUT$(1)
120 IFA$="I"ORA$="i"THEN200
130 IFA$="Y"ORA$="y"THEN300
140 IFA$="M"ORA$="m"THEN400
150 IFA$="G"ORA$="g"THEN500
160 IFA$="P"ORA$="p"THEN600
170 IFA$=CHR$(13)THEN GOTO30 ELSE110
200 PRINT@49,">":FOR T=1TO300:NEXTT
210 CLS:I=0:PRINT@128,"Input Inches =";:INPUTI
220 C=I*2.54
230 PRINT@169,"Centimeters = ";:PRINTC
240 PRINTRV$;:PRINT:INPUT" ENTER";T:PRINTNO$;:GOTO100
300 PRINT@89,">":FOR T=1TO300:NEXTT
310 CLS:Y=0:PRINT@128,"Input Yards =";:INPUTY
320 M=Y*.9144
330 PRINT@173,"Meters = ";:PRINTM:GOTO240
400 PRINT@129,">":FOR T=1TO300:NEXTT
410 CLS:M=0:PRINT@128,"Input Miles =";:INPUTM
420 K=M*1.6093439
430 PRINT@169,"Kilometers = ";:PRINTK:GOTO240
500 PRINT@169,">":FOR T=1TO300:NEXTT
510 CLS:G=0:PRINT@128,"Input Gallons =";:INPUTG
520 L=G*3.7854118
530 PRINT@175,"Liters = ";:PRINTL:GOTO240
600 PRINT@209,">":FOR T=1TO300:NEXTT
610 CLS:P=0:PRINT@128,"Input Pounds =";:INPUTP
620 K=P*.4535924
630 PRINT@171,"Kilograms = ";:PRINTK:GOTO240
700 CLS:PRINTRV$:PRINT@3," choose one: ":PRINTNO$:PRINT@50,"(C)m to inches":PRINT@90,"(M)eters to yards":PRINT@130,"(K)m to miles
705 PRINT@170,"(L)iters to gallons":PRINT@209,"k(G) to pounds":PRINTRV$:PRINT@254," press ENTER to return...";:PRINTNO$
710 A$=INPUT$(1)
720 IFA$="C"ORA$="c"THEN800
730 IFA$="M"ORA$="m"THEN900
740 IFA$="K"ORA$="k"THEN1000
750 IFA$="L"ORA$="l"THEN1100
760 IFA$="G"ORA$="g"THEN1200
770 IFA$=CHR$(13)THEN GOTO30 ELSE710
800 PRINT@49,">":FOR T=1TO300:NEXTT
810 CLS:C=0:PRINT@128,"Input Cm =";:INPUTC
820 I=C*.3937
830 PRINT@170,"Inches = ";:PRINTI
840 PRINTRV$;:PRINT:INPUT" ENTER";T:PRINTNO$;:GOTO700
900 PRINT@89,">":FOR T=1TO300:NEXTT
910 CLS:M=0:PRINT@128,"Input Meters =";:INPUTM
920 Y=M*1.0936133
930 PRINT@175,"Yards = ";:PRINTY:GOTO840
1000 PRINT@129,">":FOR T=1TO300:NEXTT
1010 CLS:KM=0:PRINT@128,"Input Km =";:INPUTKM
1020 M=KM*.6213712
1030 PRINT@171,"Miles = ";:PRINTM:GOTO840
1100 PRINT@169,">":FOR T=1TO300:NEXTT
1110 CLS:L=0:PRINT@128,"Input Liters =";:INPUTL
1120 G=L*.264172
1130 PRINT@173,"Gallons = ";:PRINTG:GOTO840
1200 PRINT@209,">":FOR T=1TO300:NEXTT
1210 CLS:KG=0:PRINT@128,"Input Kg =";:INPUTKG
1220 P=KG*2.2046226
1230 PRINT@170,"Pounds = ";:PRINTP:GOTO840
