1 ' A Time and Billing Program
2 ' for Lawyers and Professionals
3 ' on the go.
4 ' Copyright (C) April 1985
5 ' David Huntington Williams
6 ' Feel free to copy and use but
7 ' not for resale!!!
9 CLEAR 256
10 CLS:CLOSE
20 PRINT"Baby's little timekeeper"
30 PRINT"you may..(f1) enter start time"
40 PRINT".........(f2) enter end time"
50 PRINT".........(f3) print to screen"
60 PRINT".........(f4) send to file"
70 KEY ON
80 ON KEY GOSUB 100,200,500,700
92 PRINT@205,"take your pick"
94 FOR X=1 TO 300:NEXT
96 PRINT@205,"              "
97 IF INKEY$<>""THEN MENU
98 GOTO 92
100 CLS:CLOSE
112 PRINT@165,"client's name ";:INPUT CLIENT$
114 PRINT@165,"              "
115 LINE INPUT "activity - ";ACT$
116 BILL$=TIME$:CLOSE
117 OPEN"Time.do"FOR INPUT AS 1:INPUT #1,P$:FOR X=1 TO 3:INPUT #1,A$:NEXT
118 IF A$<>"DONE" THEN CLS:PRINT@320,P$;" STILL OPEN ":FOR X=1 TO 1000:NEXT:CLOSE:GOTO 10
119 CLOSE:OPEN "time.do"FOR OUTPUT AS 1
120 PRINT #1, CLIENT$
122 PRINT #1, ACT$
124 PRINT #1, BILL$
125 PRINT #1,"OPEN"
126 CLOSE #1
128 GOTO 10
130 RETURN
200 CLS
210 OPEN "time.do"FOR INPUT AS 1
220 INPUT #1,CLIENT$
230 LINE INPUT #1,ACT$
235 INPUT #1,BILL$
236 INPUT#1,Q$:IF Q$="DONE" THEN 900
240 PRINT@165,"working on - ";CLIENT$
245 PRINT;ACT$
248 PRINT "you started at - ";BILL$
250 INPUT "are you finished ";T$
251 IF T$<>"Y"ANDT$<>"y" THEN CLOSE:GOTO 10
254 PAY$=TIME$
255 CLOSE:OPEN"time.do"FOR OUTPUT AS 1
256 PRINT #1,CLIENT$:PRINT#1,ACT$:PRINT#1,BILL$:PRINT #1,"DONE"
257 CLOSE
258 OPEN "log.do" FOR APPEND AS 1
260 CLS
265 PRINT@165, "logging ";CLIENT$;" to file!"
268 FOR X=1 TO 500:NEXT
272 PRINT #1,DATE$
275 PRINT #1,CLIENT$:PRINT #1,ACT$
280 PRINT #1,"begin time":PRINT #1,BILL$
290 PRINT #1,"end time":PRINT #1,PAY$
300 HR=VAL(LEFT$(PAY$,2))-VAL(LEFT$(BILL$,2))
310 IF VAL(LEFT$(PAY$,2))<VAL(LEFT$(BILL$,2))THEN HR=(24+VAL(LEFT$(PAY$,2))-VAL(LEFT$(BILL$,2)))
320 MIN=MIN+(60*HR)+(VAL(MID$(PAY$,4,2)))-(VAL(MID$(BILL$,4,2)))
350 PRINT #1,"minutes"
355 PRINT #1,MIN
360 CLS:GOTO 10
370 RETURN
500 CLS:CLOSE
510 PRINT@165,"screen display of time"
520 INPUT "what is your billing rate - ";RATE :IF RATE=0 THEN 10
530 INPUT "client name - ";CLIENT$
540 OPEN "log.do"FOR INPUT AS 1
542 CLS:RATE=RATE/60
544 IF EOF(1) THEN 599
546 INPUT #1,NOW$
547 INPUT #1,TEST$:IF EOF(1) THEN 599
548 IF INSTR(TEST$,CLIENT$)=0 THEN FOR T=1 TO 7:INPUT#1, JUNK$:NEXT:GOTO 544
550 LINE INPUT #1,ACT$
552 INPUT #1,B$
554 INPUT #1,BILL$
556 INPUT #1,P$
558 INPUT #1,PAY$
560 INPUT #1,M$
562 INPUT #1,MIN
564 DUE=RATE*MIN
565 IF MIN=0 THEN DUE=0
566 CLS
570 PRINT,NOW$
572 PRINT,TEST$
574 PRINT:PRINT
576 PRINT ACT$
578 PRINT B$,BILL$
580 PRINT P$,PAY$
582 PRINT "time = ";MIN;" amount due = ";:PRINT USING "$###.##";DUE
590 PRINT:INPUT "shall we continue to end ";Y$
592 IF Y$="Y" OR Y$="y" THEN 544
594 INPUT "check another client ";Y$
596 IF Y$="Y" OR Y$="y" THEN CLOSE:GOTO 500
598 GOTO 10
599 CLOSE:CLS:GOTO10:RETURN
700 CLS:CLOSE
701 MAXFILES=2
710 PRINT@165,"Welcome to total up"
712 INPUT "Name of Bill File (6 letters) ";WHO$
713 WHO$=WHO$+".do"
714 OPEN WHO$ FOR APPEND AS 2
720 INPUT "what's your billing rate - ";RATE:IF RATE=0 THEN 10
722 PRINT #2,"THE BILLING RATE IS $";RATE
730 INPUT "client name - ";CLIENT$
740 OPEN "log.do"FOR INPUT AS 1
742 CLS:RATE=RATE/60
744 IF EOF(1) THEN 799
746 INPUT #1,NOW$
747 INPUT #1,TEST$:IF EOF(1) THEN 799
748 IF INSTR(TEST$,CLIENT$)=0 THEN FOR X=1 TO 7:INPUT#1,JUNK$:NEXT:GOTO 744
749 T=1
750 LINE INPUT #1,ACT$
752 INPUT #1,B$
754 INPUT #1,BILL$
756 INPUT #1,P$
758 INPUT #1,PAY$
760 INPUT #1,M$
762 INPUT #1,MIN
763 IF MIN=0 THEN DUE=0
764 DUE=RATE*MIN
765 AMT=AMT+DUE
770 CLS
772 PRINT@320,"Logging ";TEST$;" to ";WHO$;" file!!"
778 PRINT #2,NOW$:PRINT #2,TEST$
779 PRINT #2,ACT$:PRINT #2,BILL$:PRINT #2,PAY$
780 PRINT #2, USING"$####.##";DUE
782 GOTO 744
799 IF T=0 THEN 850
800 PRINT:PRINT
810 PRINT "all done now"
815 PRINT #2,"--------------"
816 PRINT #2,USING "$####.##";AMT
818 PRINT #2," "
820 AMT=0:CLOSE
822 INPUT "shall we do another client ";Y$
824 IF Y$="Y" OR Y$="y" THEN T=0:GOTO 700
826 GOTO 10
830 RETURN
850 CLOSE:CLS:PRINT@320,"no such client"
854 GOTO 822
900 PRINT@320,"-no current open file-":FORX=1 TO 300:NEXT:GOTO 10

