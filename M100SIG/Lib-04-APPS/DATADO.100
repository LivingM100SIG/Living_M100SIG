DATADO.BA BY CARMEN PAONE [72677,42]

	I am indebted to champion bytefigher Gene Nestro for BETA testing the 100 version of DATADO. His suggestions on how to make the program better were well-appreciated. Among the suggestions Gene gave me was: ""Don't pack the lines of your program; leave space between the lines to let the other guy have room to improve on your program.''
	I also would like to thank Mo Budlog for his excellent TXTCAL.100 program which made DATADO.100 an even handier program
---------------------------------------
	Need a simple little database program to keep track of your clients' telephone numbers, addresses, etc.? 
DATADO.BA will help you quickly retrieve that and other kinds of information from customized DO files on your Model 100/102
	When the menu of DATADO appears on your screen, you will see the following:

1) Enter 	   2) Find data

3) Edit            4) Delete

5) Filename	   6) MENU

	The first selection will enable you to enter data into a DO file. Be careful here because a string over 39 characters will prompt an error message of "Too long." Each time you enter data, strike enter and you will be return to the DATADO menu.
	By pressing No. 2 on your keyboard, you will be able to ask DATADO to seek a string of information from a DO file. It is only necessary to enter a partial string. Once the screen clears, hit the spacebar until the desired data appears. Another strike of the spacebar at the end of the process will return you to the DATADO menu.
	Selection No. 3 will take you right to the DO file for some quick editing. When you press F8, you will be sent back to the DATADO menu
	WARNING: DATADO clears space for the TXTCAL.100 routine; as a result it will knock out some ML programs that don't protect HIMEM, such as HXFER.100. String space for programs like HXFER  or XMDPW5 can be restored by changing line 360 to read ClearXXXXXX, (starting address of program) MAXRAM:MENU
	FLOPPY, however, won't allow the editing functon of DATADO to work because it won't let DATADO's clear statement go into action. I abandoned FLOPPY a year ago and went to POWR-DOS, which can be found in DL9 of this forum
	Strings of information can be deleted from a DO file by hitting No. 4 on your keyboard. The different strings will flash on the screen and you can delete them by typing "Y" or "y" or save them by typing "N" or "n"
	Filenames can be changed with the No. 5 option. A submenu will direct you to the way in which you can change both filenames in the program. The change of both is necessary to make the editing function work.
	The first option will be take you to the top of the program listing by the BASIC EDIT function. Type in the name of the new file. Then press the F8 key and then the F7 key, which will be programmed by DATADO, and you will be back in DATADO.
	Strike No. 5 again and select Option 2, which will enable you to change the name of the FN$ filename. Hit F8 after you make the name change and then F7 _ and you will be back in DATADO.
	No. 6 puts you back on the 100 menu.
--------------------------------------
	Send comments to me via the M100SIG message center or EasyPlex.
--------------------------------------

	Use the select, cut, and paste features of your computer to place the program in a separate DO file and run the file through BASIC.

--------------------------------------

0 REM DATADO.100 BY CARMEN PAONE [72677,42] COPYRIGHT, AUG. 10, 1989
10 F$="TASK.DO":OPENF$FORAPPENDAS1:CLOSE1:DIMD$(100)
20 ONERRORGOTO40
30 CLS:PRINT@45,"D":PRINT@51,"A":PRINT@57,"T":PRINT@63,"A":PRINT@69,"D":PRINT@75,"O":PRINT@127,"1) Enter":PRINT@143,"2) Find":PRINT@167,"3) Edit":PRINT@183,"4) Delete":PRINT@207,"5) Filenames":PRINT@223,"6) MENU"
40 IFERR=10THENOTO3
50 I$=INPUT$(1):F=INSTR(1,"123456",I$):IFF=0THEN50
60 ONFGOTO240,70,350,160,310,360
70 CLS:PRINT@91," Find: ????":PRINT@160,STRING$(39,95):PRINT@160,"";
80 LINEINPUTS$:IFLEN(S$)>39THENPRINT,"TOO LONG":FORT=1TO500:NEXT:CLS:GOTO70
90 IFS$=""THENRUN
100 FORG=1TOLEN(S$):P=ASC(MID$(S$,G,1)):IFP>96ANDP<123THENMID$(S$,G,1)=CHR$(P-32)
110 NEXT:OPENF$FORINPUTAS1:CLS:PRINT
120 IFEOF(1)THEN150
130 LINEINPUT#1,T$:IFINSTR(1,T$,S$)<>0THENPRINTT$
140 PRINT@91,"Spacebar"CHR$(32):I$=INPUT$(1):GOTO120
150 CLOSE1:PRINT@211,"Spacebar for menu":I$=INPUT$(1):GOTO30
160 CLS:D=0:OPENF$FORINPUTAS1
170 IFEOF(1)THENCLOSE1:GOTO210
180 D=D+1:LINEINPUT#1,D$(D):PRINT@120,LEFT$(D$(D),39);SPACE$(39-LEN(D$(D))):PRINT@222,"Kill? _"CHR$(8)
190 I$=INPUT$(1):IFI$="Y"ORI$="y"THEND$(D)=""ELSEIFI$="N"ORI$="n"THEN200ELSE190
200 GOTO170
210 CLS:PRINT@131,"Redoing file ":KILLF$:OPENF$FOROUTPUTAS1:FORT=1TOD:IFD$(T)=""THEN230
220 PRINT#1,D$(T)
230 NEXT:CLOSE1:GOTO30
240 CLS:PRINT@91,"Enter data on line":PRINT@160,STRING$(39,95)CHR$(13);:LINEINPUTA$
250 CLS:PRINT@120,"";:IFLEN(A$)>39THENPRINT"Too long.":FORT=1TO500:NEXT:CLS:GOTO240
260 IFA$=""THENGOTO30
270 FORY=1TOLEN(A$):IFASC(MID$(A$,Y,1))>96ANDASC(MID$(A$,Y,1))<123THENMID$(A$,Y,1)=CHR$(ASC(MID$(A$,Y,1))-32)
280 NEXT:OPENF$FORAPPENDAS1:PRINT#1,A$:CLOSE1:GOTO30
290 KEY7,"RUN"+CHR$(34)+"Datado.BA"+CHR$(13):EDIT10
300 KEY7,"RUN"+CHR$(34)+"Datado.BA"+CHR$(13):EDIT400
310 CLS:PRINT@81,"NEW FILE NAMES (SELECT 1 OR 2)":PRINT@201,"REMINDER:BOTH NAMES MUST BE CHANGED"
320 I$=INPUT$(1):F=INSTR(1,"12",I$):IFF=0THEN320
330 ONFGOTO290,300
340 REM
350 GOSUB370
360 CLEAR0,MAXRAM:MENU
370 REM
380 CLEAR100,62750
390 GOSUB460
400 FN$="TASK.do"
410 OPENFN$FORAPPENDAS1
420 CLOSE1
430 CALLTX,,VARPTR(FN$)
440 RUN30
450 END
460 REM 
470 TX=62750
480 FORA=TXTOTX+65
490 READN:POKEA,N:NEXTA
500 RETURN
510 DATA229,213,197,245,126,35,94,35,86,235
520 DATA95,205,11,76,205,175,32,202,237,20
530 DATA35,94,35,86,42,157,251,229,33,0
540 DATA0,34,231,246,57,34,157,251,213,62
550 DATA1,33,77,245,195,113,95,225,225,34
560 DATA157,251,205,156,108,175,50,80,246,61
570 DATA50,173,250,195,237,20
580 REM END OF DATA
