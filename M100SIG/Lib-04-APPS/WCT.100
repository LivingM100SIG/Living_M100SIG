10 'STANLEY KENDALL [73136,3055]
20 'To SIG-100 11/29/84
40 'WCT' 4/84 (from U.S.ARMY work).
50 T$="   WINDCHILL TEMP. = ###.## deg. F":GOTO 100
70 PRINT E;CHR$(112):PRINT@240,:INPUT"      PRESS 'ENTER' TO CONTINUE...";U:PRINT E;CHR$(113):CLS:RETURN
100 CLS:BEEP:DEFSTR E:E=CHR$(27)
110 LINE(40,10)-(200,50),1,B
120 BEEP:LINE(40,10)-(200,50),1,BF
130 LINE(44,14)-(194,46),0,B
140 BEEP:PRINT@128," WINDCHILL TEMPERATURE ";:PRINT CHR$(147)
150 PRINT@173," by S.Kendall "
155 LINE(0,0)-(40,50):LINE(200,50)-(239,0)
160 FOR A=3000TO 10000STEP 100
170 SOUND A,2:NEXT
180 FOR A=10000TO 1000STEP -100
190 SOUND A,1:NEXT:GOSUB 70
200 CLS:PRINT@86,;:INPUT"Enter Temp, Deg F ";T
210 PRINT:INPUT"  Enter Wind - MPH ";V
220 IF V<4THEN 310
230 B=10.45+6.686*SQR(V)-.44*V
240 C=33-5*(T-32)/9:D=33-B*C/22.03405
250 W=D*9/5+32:BEEP:PRINT:PRINTUSING T$;W:GOSUB 70
260 PRINT@128," DO AGAIN YES=1 NO=4"
270 G=VAL(INKEY$)
280 IF G=1THEN 200
290 IF G=4THEN MENU
300 IF G<>1 OR G<>4THEN270
310 BEEP:PRINT:PRINTUSING T$;T:GOSUB 70:GOTO 260
320 '....................
330 'THE ABOVE IS SELF EXPLANATORY - SIMPLY RUN AND USE.

