10 'ALMCLK
20 '(c)1983 by BCS Software
30 '4138 Ocean View Blvd.
40 'Montrose, CA 91020
50 '21 Dec 83 by Mike Berro [75765,374]
60 'Use "ESC" to exit.
70 'Program your own sounds at lines        460 thru 469.
80 'Delete line 430 if cassette control     is not desired.
90 DEFINTA-Z:DIMA$(3):CALL16959:C$=":"
100 A$(0)=CHR$(235):A$(1)=CHR$(236):A$(2)=CHR$(238):A$(3)=CHR$(237):GOTO370
110 PRINT@311,TIME$:GOSUB350:K=K+1:IFK>3THENK=0
120 PRINT@CU,A$(K):FORI=1TO40:NEXTI
130 A$=INKEY$:IFA$=""THEN110
140 IFA$=CHR$(27)THENA=10:RETURN
150 IFA$="0"THENA=0:RETURN
160 A=VAL(A$):IFA=0THEN110ELSERETURN
170 GOSUB110:IFA=10 THENRETURN
180 IFA>2 THEN170
190 GOSUB340:SE$=A$:H=A
200 GOSUB110:IFA=10THENRETURN
210 IFA>3ANDH>1 THEN200
220 GOSUB340:SE$=SE$+A$+C$:GOSUB330
230 GOSUB110:IFA=10THENRETURN
240 IFA>5 THEN230
250 GOSUB340:SE$=SE$+A$
260 GOSUB110:IFA=10THENRETURN
270 GOSUB340:SE$=SE$+A$+C$:GOSUB330
280 GOSUB110:IFA=10 THENRETURN
290 IFA>5THEN280
300 GOSUB340:SE$=SE$+A$
310 GOSUB110:IFA=10 THENRETURN
320 GOSUB340:SE$=SE$+A$:RETURN
330 A$=C$
340 PRINT@CU,A$;:CU=CU+1:RETURN
350 PRINT@281,DATE$:RETURN
360 CLS:LINE(0,54)-(239,54),1:RETURN
370 GOSUB360:SE$="":PRINT@80,"Enter Alarm Time(HH:MM:SS): ":CU=108:GOSUB170
380 IFA=10ANDSE$=""THEN520ELSEIFA=10THEN370
390 GOSUB360:PRINT@80,"Alarm time = ";SE$:LINE(73,14)-(129,24),1,B
400 PRINT@311,TIME$:GOSUB350:IFTIME$=SE$THEN420
410 A$=INKEY$:IFA$=CHR$(27)THEN370ELSE400
420 GOSUB360:CALL17001:PRINT@120,SPACE$(120):PRINT@170,"<<<<<<< ALARM >>>>>>>":CALL17006
430 MOTORON
440 PRINT@42,"Press SPACE for snooze, ESC to Reset":LINE(7,6)-(231,16),1,B
450 PRINT@311,TIME$:GOSUB350:A$=INKEY$:IFA$=" "THEN480
460 ' +++++ Your Sounds Here
461 SOUND2000,1:SOUND4000,2:SOUND1000,2:SOUND16000,3
470 IFA$=CHR$(27) THENMOTOROFF:GOTO370ELSEGOTO450
480 MOTOROFF
490 H=VAL(LEFT$(SE$,2)):M=VAL(MID$(SE$,4,2)):S$=RIGHT$(SE$,2):M=M+10:IFM<60THEN510
500 M=M-60:H=H+1:IFH=24THENH=0
510 SE$=RIGHT$("0"+RIGHT$(STR$(H),1+H/10),2)+C$+RIGHT$("0"+RIGHT$(STR$(M),1+M/10),2)+C$+S$:GOTO390
520 MOTOROFF:MENU

