1 ' PROGRAM TO READ A COMMA AND CR DELIMITED .DO FILE AND CONVERT IT TO TEXT DIF FORMAT COPR DAVE LAPINSKI 1987
2 MAXFILES=3
3 DEFSTR Q :QU=CHR$(34):Q2=QU+QU
5 PRINT"PROGRAM TO CREATE DIF FILE FROM INPUT TEXT FILE"
10 INPUT"ENTER NAME OF INPUT FILE";IN$
12 IF IN$="Files" THEN FILES: GOTO10
20 INPUT"ENTER NAME OF OUTPUT DIF FILE";OU$
30 IF OU$="" THEN OU$="DIF.DO"
40 OPEN IN$ FOR INPUT AS 1
49 PRINT" DETERMINING LENGTH AND BREADTH OF ";IN$
50 IF EOF(1) THEN 100
60 A$="":A$= INPUT$(1,1): IF A$="," THEN CC=CC+1: GOTO 50
70 IF A$=CHR$(13) THEN 90 ELSE 50
80 CLOSE
90 IF CC>CM THEN CM=CC
92 KV=KV+1
94 CC=0
96 GOTO50
100 KV=KV+1:CM=CM+1:PRINT"ROWS:";CM:PRINT"COLS:";KV
110 CLOSE
120 OPENIN$FOR INPUT AS 1
130 OPEN OU$FOR OUTPUT AS 2
132 PRINT#2,"TABLE":PRINT#2,"0,1":PRINT#2,Q2
133 PRINT#2,"VECTORS":PRINT#2,"0,";CM:PRINT#2,Q2
134 PRINT#2,"TUPLES":PRINT#2,"0,";KV:PRINT#2,Q2
135 PRINT#2,"DATA":PRINT#2,"0,0":PRINT#2,Q2
136 PRINT#2,"-1,0":PRINT#2,"BOT"
139 CC=0:KK=0
140 IF EOF(1) THEN 300
150 A$="":A$=INPUT$(1,1): IF A$=","  THEN GOSUB200:GOTO140
160 IF A$=CHR$(13) THEN GOSUB 220:GOTO140
165 IF ASC(A$)<32 THEN 180
170 B$=B$+A$
180 GOTO140
200 PRINT#2,"1,0"
205 PRINT#2,QU+B$+QU
206 PRINT"1,0 ";QU+B$+QU
208 CC=CC+1
210 B$="":RETURN
220 GOSUB200
225 GOSUB250
230 PRINT#2,"-1,0":PRINT#2,"BOT"
231 PRINT"-1,0 ";"BOT"
240 RETURN
250 IF CC=CM THEN CC=0:RETURN 
260 IF CC>CM THEN PRINT"ERROR CC>CM":CC=0:RETURN
270 FOR I= CC+1 TO CM:PRINT#2,"1,0":PRINT#2,Q2:PRINT"*1,0 ";Q2;:NEXT I:CC=0:RETURN
300 GOSUB 250
310 PRINT#2,"-1,0":PRINT#2,"EOD"
311 PRINT"-1,0 ";"EOD"
400 CLOSE
410 END

