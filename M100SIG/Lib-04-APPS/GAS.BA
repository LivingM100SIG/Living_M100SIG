1 REM CREATE TEXT FILE BEFORE RUNNING   NAME IT MPG.DO
2 REM INPUT 2 ENTRIES. 1) YOUR NORMAL   MPG (26 for ex.), 2) the digit 1.
3 REM BY DAN HAMILTON [70625,1237]
4 REM MDIFIED FROM MOD III VERSION
55 CLEAR100
60 CLS
70 FORX=0TO6
80 PRINT@48,"FUEL MILEAGE COMPUTER"
90 FORZ=0TO55:NEXTZ
100 CLS
110 FORZ=0TO55:NEXTZ
120 NEXTX
130 PRINT@48,"FUEL MILEAGE COMPUTER"
140 PRINT"TO CALCULATE MILES DRIVEN ENTER 'HELP'":INPUT"OR PRESS ENTER TO CONTINUE";E$
150 IFE$="HELP"THENGOSUB480ELSE160
160 CLS:PRINT:PRINT"CONVERTS LITERS TO GALLONS & COMPUTES":PRINT"MILEAGE AND COST PER MILE.";:PRINT"ALSO AVERAGES FUEL CONSUMPTION TO DATE."
170 GOSUB530
190 REM M=MILES DRIVEN
200 IFX=5GOTO220ELSEPRINT:INPUT"MILES DRIVEN";M
205 IFM<1THENGOTO200
210 REM D=$
220 PRINT:PRINT:INPUT"AMOUNT SPENT $";D
225 IFD<1THENCLS:GOTO220
230 REM L=LITERS
240 PRINT:INPUT"LITERS USED (0 IF GALLONS USED)";L
250 IFL<>0GOTO290
260 REM G=GALS
270 PRINT:INPUT"GALLONS USED ";G
280 IFG<>0GOTO300
290 CLS:GOSUB 420
300 REM C=MILEAGE
320 C=M/G
330 M$="YOU GOT ##.## "
340 CLS:PRINT:PRINTUSINGM$;C;:PRINT" M.P.G."
350 REM P=COST PER MILE (D/M)
360 P=D/M
370 C$="AND IT COST YOU $.##"
380 PRINTUSINGC$;P;:PRINT" PER MILE"
385 PRINT:PRINT:INPUT"Press ENTER to continue";E
390 GOTO690
400 PRINT:PRINT:INPUT"Press ENTER to run again";E
410 CLS:RUN130
420 REM LITER CONVERSION
430 G=L/3.785
440 G$="THAT IS ##.## "
450 CLS:PRINT:PRINTUSINGG$;G;:PRINT" GALLONS"
460 FORZ=0TO895:NEXTZ
470 RETURN
480 REM MILE SUBTRACT
490 CLS:INPUT"MILEAGE AT THIS FILL-UP";B
500 INPUT"MILEAGE AT LAST FILL-UP";L
510 M=B-L:X=5
520 RETURN
530 REM READS FUEL FILE
540 F$="RAM:MPG.DO"
550 REM R=MPG TOTAL TO DATE; A=NUMBER OF ENTRIES TO AVERAGE
560 OPENF$FORINPUTAS1
570 INPUT#1,A
600 INPUT#1,R
620 CLOSE
630 FORZ=0TO1552:NEXTZ
640 CLS
660 PRINT:PRINT"AVERAGE CONSUMPTION TO DATE IS":PRINTR/A" M.P.G."
665 PRINT"AVERAGED OVER"A"TANKS OF FUEL"
670 PRINT:INPUT"Press enter to continue.";E
680 CLS:GOTO 190
690 REM UPDATE MPG FILE
695 CLS:PRINT:PRINT"** UPDATING MILEAGE FILE **"
710 OPENF$FORINPUTAS1
720 INPUT#1,A
740 INPUT#1,R
745 CLOSE
780 OPENF$FOROUTPUTAS1
785 R=C+R
788 A=A+1
790 PRINT#1,A
810 PRINT#1,R
830 CLOSE
840 PRINT:INPUT"Press ENTER to run again...";E
850 CLS:RUN 130
