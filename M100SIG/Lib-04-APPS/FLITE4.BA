0 REM FLITE4.BA - 10/9/89 - Ver 2.00
10 CLS:CLEAR3000:RV$=CHR$(27)+"p":NV$=CHR$(27)+"q":PRINT@134,RV$;"Initializing";NV$:DIMS$(5),S(5):MN$=CHR$(13)+CHR$(27):ES$="<ESC> to Quit, <ENTER> to Continue":DG$=CHR$(166)
30 NM$="Number":AL$="Altitude ":AS$="Airspeed ":PR$="Pressure":DN$="Density":T$="True"
50 FORZ=1TO4:READS$(Z):NEXT
60 FORZ=1TO4:READS(Z):NEXT
61 GOTO920
70 RUN"FLIGHT.BA"
920 CLS:PRINT@10,AL$" & "AS$:GOSUB2530
930 CLS:ONS3GOTO950,1060,1120,1170
940 GOTO920
950 CLS:PRINT@16,PR$" "AL$:PRINT@80,"Altimeter Setting ";AS:PRINT@97,"";:INPUTAS:PRINT@98,SPACE$(9):PRINT@98,AS
960 PRINT@120,AL$;AL:PRINT@128,"";:INPUTAL:PRINT@129,SPACE$(9):PRINT@129,AL
970 IFAS<28.5THENCF=((29.92-AS)*910)+46.8+(60*(28.5-AS)):GOTO1030
980 IFAS<29THENCF=((29.92-AS)*910)+22.8+(50*(29-AS)):GOTO1030
990 IFAS<29.3THENCF=((29.92-AS)*910)+13.8+(40*(29.3-AS)):GOTO1030
1000 IFAS<29.7THENCF=((29.92-AS)*910)+.8+(30*(29.7-AS)):GOTO1030
1010 IFAS<29.92THENCF=((29.92-AS)*910)+.8+(20*(29.9-AS)):GOTO1030
1020 IFAS>=29.92THENCF=((29.92-AS)*910)-.2
1030 PA=AL+CF:PRINT@200,PR$" "AL$" =";PA:PRINT@283,ES$;
1040 GOSUB2750:ONINSTR(MN$,IK$)GOTO950,920
1050 GOTO1040
1060 CLS:PRINT@12,DN$" "AL$:PRINT@80,PR$" "AL$;PA:PRINT@97,"";:INPUTPA:PRINT@98,PA
1070 PRINT@120,"Temperature (";DG$;"F) ";TF:PRINT@136,"";:INPUTTF:PRINT@137,SPACE$(9):PRINT@137,TF
1080 TS=59-(3.5*PA/1000):DA=.9075*PA+66*(TF-TS)+256:DA=INT(DA+.5):TS=INT(TS+.5)
1090 PRINT@160,DN$" "AL$" =";DA:PRINT"Std. Temp. (";DG$;"F) =";TS:PRINT@283,ES$;
1100 GOSUB2750:ONINSTR(MN$,IK$)GOTO1060,920
1110 GOTO1100
1120 CLS:PRINT@14,T$" "AS$:PRINT@80,"Indicated "AS$;AI:PRINT@98,"";:INPUTAI:PRINT@99,SPACE$(9):PRINT@99,AI
1130 PRINT@120,DN$" "AL$;DA:PRINT@136,"";:INPUTDA:PRINT@137,SPACE$(9):PRINT@137,DA
1140 AT=AI*(1+(.017*(DA/1000))):AT=INT(AT+.5):PRINT@160,T$" "AS$"=";AT:PRINT@283,ES$;
1150 GOSUB2750:ONINSTR(MN$,IK$)GOTO1120,920
1160 GOTO1150
1170 CLS:PRINT@14,"Mach "NM$:PRINT@80,T$" "AS$"(Knots) ";AT:PRINT@101,"";:INPUTAT:PRINT@102,SPACE$(9):PRINT@102,AT
1180 PRINT"Temperature in ";DG$;"F ";TF;:PRINT@137,"";:INPUTTF:PRINT@138,SPACE$(9):PRINT@138,TF
1190 MN=AT/(700-((50-((TF-32)/1.8))*1.2)):MN=INT((MN+.005)*100)/100:PRINT"Mach "NM$" =";MN:PRINT@283,ES$;
1200 GOSUB2750:ONINSTR(MN$,IK$)GOTO1170,920
1210 GOTO1200
2530 FORZ=1TO4:PRINT@S(Z),S$(Z):NEXTZ:S3=1:PRINT@283,ES$;
2540 PRINT@S(S3),RV$;S$(S3);NV$
2550 GOSUB2750:ONINSTR(MN$,IK$)GOTO2600,70
2560 IK=ASC(IK$)-29:IFIK<0THEN2550ELSEONIKGOTO2580,2590
2570 GOTO2550
2580 PRINT@S(S3),S$(S3):S3=S3-1:IFS3<1THENS3=4:GOTO2540ELSE2540
2590 PRINT@S(S3),S$(S3):S3=S3+1:IFS3>4THENS3=1:GOTO2540ELSE2540
2600 RETURN
2750 IK$=INKEY$:IFIK$=""THEN2750
2760 RETURN
2800 DATA"Pressure Alt","Density Alt","True Airspeed","Mach Number"
2860 DATA80,120,100,140
