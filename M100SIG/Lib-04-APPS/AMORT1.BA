
10 CLS
20 PRINT"Loan amortization version 2.0"
30 PRINT"By Jim Gerritz & Phil Estrem"
40 FORD=1TO1500:NEXTD:CLS
42 CLS:PRINT"PD=Payment date. IP=Interest paid"
44 PRINT"PP=Principle paid. RP=Remaining"
46 PRINT"principle."
48 INPUT"Press enter to continue";XX$
49 CLS
50 AT=0:LC=0:AM=0:ZZ=0
55 CC=0
60 PN=1:PA=1
130 C=0:INPUT"Principle";P
140 INPUT"Number of payments";L
150 INPUT"interest rate";R
160 INPUT"Month of first payment (06)";MM
165 INPUT"Year of first payment (84)";Y
170 ZZ=0
180 INPUT "Do you want a printout";A$:IF LEFT$(A$,1)="Y"OR LEFT$(A$,1)="y"THENZZ=1
190 I=R/12:I=I/100
200 T=1:FORX=1TOL
210 T=T*(1+I):NEXTX:T=1/T
220 T=1-T
230 M=P*I/T
240 M=INT(M*100+.5)/100
241 CLS
242 PRINT"Your monthly payment is ";:PRINTUSING "$$######.##";M
245 INPUT "Press enter to continue";XX$
250 GOSUB 600
260 FORZ=1TOL
270 IFC<5THEN300
280 IF ZZ=1THEN300
290 INPUT"Enter to continue";ZZ$:C=0:GOSUB600
300 A=(INT(P*I*100+.5))/100
305 CC=CC+A
310 B=M-A
320 IFB>PTHENM=A+P
330 IFB>PTHENB=P
340 P=P-B:P=INT(P*100+.5)/100
350 IFP<1THENB=B+P:IFP<1THENM=M+P:IFP<1THENP=0
360 PRINT USING"##";MM;:PRINT"/";Y;
365 PRINTTAB(1)
380 PRINTUSING"$$####.##";A,
385 PRINTTAB(12):PRINTUSING"$$####.##";B;
390 PRINTTAB(19);
395 PRINTUSING"$$######.##";P
400 IFZZ<>1THEN450
410 LPRINT TAB(3);:LPRINTUSING"###";PN;:LPRINT TAB(12);:LPRINTUSING"##";MM;
420 LPRINT" /";Y;:LPRINT TAB(24);:LPRINTUSING"$$####.##";M;:LPRINT TAB(36);
430 LPRINTUSING"$$####.##";A;:LPRINT TAB(48);:LPRINTUSING"$$####.##";B;
440 LPRINT TAB(60);:LPRINTUSING"$$#####.##";P
450 AM=AM+1:LC=LC+1
460 PN=PN+1
470 MM=MM+1:IFMM=13THENGOSUB820
480 IFZZ<>1THEN510
490 IFLC<67THEN510ELSELPRINTCHR$(12)
500 PA=PA+1:GOSUB600
510 AT=AT+A
520 C=C+1:NEXTZ
530 GOSUB820
540 PRINT"Total interest paid ";:PRINTUSING"$$#######.##";AT
550 IFZZ<>1THEN599
560 LPRINT
570 LPRINT TAB(12);"Total interest paid  ";:LPRINTTAB(35);:LPRINTUSING"$$#######.##";AT
580 LPRINTCHR$(12)
599 PRINT"Any more":INPUT ZZ$:IF ZZ$="y"THEN42 ELSE MENU
600 CLS
610 PRINT" PD        IP        PP        RP"
640 IFZZ<>1THEN810
710 IFPA>1THENLC=14:IFPA>1THEN770
720 LPRINT"Amount of loan: ";:LPRINTUSING"$$#####.##";P
730 LPRINT"Interest rate: ";R;"%"
740 LPRINT"Number of payments ";L
750 LPRINT"Monthly payment ";:LPRINTUSING"$$#####.##";M;:LPRINT:LPRINT
760 LC=20
770 LPRINT "   Payment    Payment    Monthly    Interest    Principle    Remaining"
780 LPRINT "   Number     Date       Payment     Paid       Paid         Principle"
790 LPRINT:LC=LC+3
800 C=0
810 RETURN
820 PRINT"Interest paid in ";Y;"  ";:PRINT USING "$$####.##";CC
840 IFZZ<>1THEN890
850 LPRINT:LPRINTTAB(12);"Interest paid in ";Y;:LPRINTTAB(36);CC
870 LPRINT
880 LC=LC+3
890 AM=0:MM=1:Y=Y+1
892 CC=0
895 IFY=100 THENY=0
900 RETURN
