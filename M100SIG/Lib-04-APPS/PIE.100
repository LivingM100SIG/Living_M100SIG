50  DEFSNG A-Z
100 CLS:P2=3.1416*2
105 GOSUB 3000
110 GOSUB 1000
120 ANG=0:GOSUB 4000:LA=0
130 FORI=1TON-1
140 ANG=ANG+P2*DA(I)/SUM:GOSUB4000
150 GOSUB5000:NEXTI:ANG=P2:GOSUB5000
160 REM PRINT@287,"Pie Chart";
165 GOSUB 6000
170 A$=INKEY$:IFA$=""THEN170:ELSE RUN
1000 RAD=24:ANG=0:GOSUB8000
1010 GOSUB 7000:ANG=ANG+.3:IF ANG<P2 THEN 1010:ELSE RAD=24:ANG=0:GOSUB7000:RETURN
2000 X=RAD*SIN(ANG)+45:Y=RAD*COS(ANG)*.9+32:PSET(X,Y):RETURN
3000 CLS
3010 INPUT"number of entries";N
3020 DIM DA(N)
3030 SUM=0:FOR I=1 TO N
3040 PRINT"#";I;:INPUT DA(I)
3050 SUM=SUM+DA(I):NEXT I
3060 PRINT "sum = ";SUM
3070 CLS:RETURN
4000 X=24*SIN(ANG)+45:Y=24*COS(ANG)*.9+32:LINE(45,32)-(X,Y):RETURN
5000 A=LA+(ANG-LA)/2:LA=ANG
5010 X=33*SIN(A)+45:Y=33*COS(A)*.9+32
5020 PL=(Y\8)*40+(X\6)
5025 A$=CHR$(96+I)
5030 PRINT@PL,A$;
5040 RETURN
6000 PL=16:FM$="!:### ##.#%"
6005 ON ERROR GOTO 6900
6010 FOR I=1TON:W=DA(I)/SUM*100.
6020 PRINT@PL,USING FM$;CHR$(96+I);DA(I);W;
6030 PL=PL+12
6040 IF I MOD 2=0 THEN PL=PL+16
6050 NEXT I
6060 RETURN
6900 IF ERL=6020 THEN RESUME 170
6910 ERROR ERR
7000 OX=X:OY=Y:GOSUB8000:LINE(OX,OY)-(X,Y):RETURN
8000 X=24*SIN(ANG)+45:Y=24*COS(ANG)*.9+32:RETURN
