10 CLS
20 F$=" Ohms"
30 P$="Press <any key> to continue."
40 PRINT@47,"Resistor Value Calculator";
50 PRINT@98,"By"
60 PRINT@133,"Dennis Thomas"
70 LINE(40,4)-(192,32),1,B
80 PRINT@213,"1. Input Color";
90 PRINT@253,"2. Input Value";
100 PRINT@293,"Press <Enter> for Menu";
110 GOSUB 640
120 IF A$=CHR$(13) THEN MENU
130 IF A<1 OR A>2 THEN 10
140 IF A=2 THEN 320
150 CLS: PRINTTAB(0);"0. Black";TAB(10);"3. Orange";TAB(20);"6. Blue";TAB(30);"9. White"
160 PRINTTAB(0);"1. Brown";TAB(10);"4. Yellow";TAB(20);"7. Violet"
170 PRINTTAB(0);"2. Red";TAB(10);"5. Green";TAB(20);"8. Grey"
180 A1$="1st Color Band: ":GOSUB630
190 B1=A*10:IF A<0 OR A>9 THEN 150
200 PRINT A
210 A1$="2nd Color Band: ":GOSUB630
220 B1=A+B1:IF A<0 OR A>9 THEN 150
230 PRINT A
240 A1$="3rd Color Band: ":GOSUB630
250 B3=A:IF A<0 OR A>9 THEN 150
260 PRINT A
270 R = B1*10^(B3)
280 CLS:PRINT@130,USING"###,###,###,###";R;
290 PRINT F$
300 A1$=P$:PRINT@286,"";
310 GOSUB 630:GOTO10
320 CLS: INPUT" Enter value of resistor ";V$
330 Z$=LEFT$(V$,1)
340 Z1$=MID$(V$,2,1)
350 Z2$=MID$(V$,3,15)
360 A = VAL(Z$)
370 RESTORE
380 FOR X=0 TO A:READ C$:NEXT
390 A=VAL(Z1$)
400 RESTORE
410 FOR X=0 TO A:READ C1$:NEXT
420 IF Z2$ <> "" GOTO 440
430 C3$ = "None":GOTO 470
440 A=LEN(Z2$)
450 RESTORE
460 FOR X=0 TO A:READ C3$:NEXT
470 CLS:
480 PRINT@248,C$
490 PRINT@256,C1$
500 PRINT@264,C3$
510 LINE(40,45)-(210,60),1,B
520 LINE(85,45)-(85,60),1:LINE(86,45)-(86,60),1:LINE(87,45)-(87,60),1
530 LINE(133,45)-(133,60),1:LINE(134,45)-(134,60),1:LINE(135,45)-(135,60),1
540 LINE(180,45)-(180,60),1:LINE(181,45)-(181,60),1:LINE(182,45)-(182,60),1
550 PRINT@168,"Band-1"
560 PRINT@176,"Band-2"
570 PRINT@184,"Band-3"
580 PRINT@88,"Resistor Color Code Is:"
590 PRINT@128,"Value = ";V$
600 PRINT@6,"";
610 A1$=P$
620 GOSUB 630:GOTO10
630 PRINT A1$;
640 A$ = INKEY$:IF A$ = "" GOTO640
650 A = VAL(A$):RETURN
660 DATA "Black","Brown","Red","Orange","Yellow","Green","Blue","Violet","Grey","White"
