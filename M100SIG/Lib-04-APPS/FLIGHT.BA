0 REM FLIGHT.BA - 10/9/89 - Ver. 2.00 - Main Menu Module
10 CLS:MAXFILES=2:CLEAR3000:RV$=CHR$(27)+"p":NV$=CHR$(27)+"q":PRINT@134,RV$;"Initializing";NV$:DIMS$(8),S(8):MN$=CHR$(13)+CHR$(27):ES$="<ESC> to Quit, <ENTER> to Continue"
20 LA$="Lat  Deg      Min      Var":LG$="Long Deg      Min      -E+W":ID$=CHR$(8)+CHR$(13)+CHR$(30)+CHR$(31):DG$=CHR$(166)
50 FORZ=1TO7:READS$(Z):NEXT
60 FORZ=1TO7:READS(Z):NEXT
70 CLS:PRINT@10,"FLTCMP.BA---MAIN MENU":GOSUB2530
80 CLS:ONS3GOTO100,110,120,130,140,150,160,2890
90 GOTO70
100 RUN"FLITE1.BA"
110 RUN"FLITE2.BA"
120 RUN"FLITE3.BA"
130 RUN"FLITE4.BA"
140 RUN"FLITE5.BA"
150 RUN"FLITE6.BA"
160 RUN"FLITE7.BA"
2530 FORZ=1TO7:PRINT@S(Z),S$(Z):NEXTZ:S3=1:PRINT@283,ES$;
2540 PRINT@S(S3),RV$;S$(S3);NV$
2550 GOSUB2750:ONINSTR(MN$,IK$)GOTO2600,2890
2560 IK=ASC(IK$)-29:IFIK<0THEN2550ELSEONIKGOTO2580,2590
2570 GOTO2550
2580 PRINT@S(S3),S$(S3):S3=S3-1:IFS3<1THENS3=7:GOTO2540ELSE2540
2590 PRINT@S(S3),S$(S3):S3=S3+1:IFS3>7THENS3=1:GOTO2540ELSE2540
2600 RETURN
2750 IK$=INKEY$:IFIK$=""THEN2750
2760 RETURN
2770 DATA"Conversions","Std. Math","Time/Speed/Dist.","Alt & AS","Wind & Drift","Wt & Bal","Navigation"
2830 DATA80,120,160,200,100,140,180
2890 CLS:PRINT@134,"Happy Flying"
2900 SOUND2484,12:SOUND2348,6:SOUND2092,36:SOUND2348,6:SOUND2484,6:SOUND2793,6:SOUND2484,18:SOUND2348,18:SOUND2216,18:SOUND2092,18
2910 MAXFILES=0:CLEAR:MENU
