10   CLS
20   CLEAR1000
30   MAXFILES=2
40   PRINT@89,"(c) 1988 by K. Richmond";
50   PRINT@135,"Check Book"
60   PRINT@175,"[73127,1727]"
70   GOSUB 580
80   A$=INKEY$:IF A$="" THEN 70
90   CLS
100  GOSUB 580
110  PRINT@90,"ENTER CHECKS        (1)";
120  PRINT@130,"BALANCE CHECK BOOK  (2)";
130  PRINT@170,"END                 (3)";
140  PRINT@210,"CHOICE ";
150  INPUT CH
160  ON CH GOSUB 190,340,550
170  GOTO 90
180  '***** ADD CHECKS TO FILE *****
190  OPEN "CHECKS.DO" FOR APPEND AS 1
200  CLS
210  GOSUB 580
220  PRINT@85,"ENTER CHECK NUMBER";
230  INPUT N$:N$=N$+STRING$(10," "):N$=LEFT$(N$,5)
240  PRINT@125,"ENTER PAYEE";
250  INPUT P$:P$=P$+STRING$(15," "):P$=LEFT$(P$,15)
260  PRINT@165,"ENTER CHECK AMOUNT";
270  INPUT AM$:AM$=STRING$(9," ")+AM$:AM$=RIGHT$(AM$,9)
280  X$=N$+DATE$+" "+P$+" "+AM$
290  PRINT#1,X$
300  CLS:PRINT:PRINT"ENTER MORE CHECKS(Y/N)";
310  INPUT Y$
320  IF Y$="Y" OR Y$="y" THEN 200 ELSE CLOSE#1:RETURN
330  '***** BALANCE CHECK BOOK *****
340  CLS
350  X=0:AM=0:AM$="":CK$=""
360  OPEN "CHECKS.DO" FOR INPUT AS 2
370  INPUT#2,X$
380  IF EOF(2) THEN X=1
390  CK$=LEFT$(X$,5)
400  AM$=RIGHT$(X$,9)
410  AM=VAL(AM$)
420  T=T+AM
430  IF X=1 THEN 450
440  GOTO 370
450  CLS
460  GOSUB 580
470  IF T<0 THEN CB$="DEBIT  BALANCE"
480  IF T>0 THEN CB$="CREDIT BALANCE"
490  PRINT@134,CB$
500  PRINT@176,"$";T*-1
510  A$=INKEY$:IF A$="" THEN 510
520  CLOSE#2
530  RETURN
540  '***** END ROUTINE *****
550  CLS:PRINT"QUIT Y/N";:INPUTA$
560  IF A$="Y" THEN MENU ELSE RETURN
570  '***** WINDOW ROUTINE *****
580  LINE(27,11)-(218,52),1,B
590  RETURN
