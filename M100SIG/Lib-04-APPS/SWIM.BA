20 CLS:PRINT@50,"Swim Meet Scoring":PRINT@127,"Copyright 1989 Louis Self"
25 PRINT@207,"For the Tandy 100/102/200":PRINT@285,"Check scoring in lines 30-70
30 'DATA=Events,Places,Scoring,999=end
40 'DATA 1,6,4,9,7,3,1, 7,54,3,5,3,1, 55,60,4,9,7,3,1,999:'Dual
50 'DATA 1,6,6,14,10,8,6,4,2, 7,54,6,7,5,4,3,2,1, 55,60,6,14,10,8,6,4,2,999:'Division
70 DATA 1,2,6,14,10,8,6,4,2, 3,4,6,7,5,4,3,2,1, 5,6,6,14,10,8,6,4,2, 999:'Debug data
80 MAXFILES=2:DEFINTA-B,D-S,U-Z:DIM P(6),R(8),S(6),T(8),N$(8):TM=2
100 ONERRORGOTO6000:OPEN"MEET"FORINPUTAS1:INPUT#1,M$,TM:FORT=1TOTM:INPUT#1,N$(T):NEXT:CLOSE
120 PRINT:PRINT"Input info or <ENTER> to keep the same:":PRINT
130 ONERRORGOTO0:PRINT"Meet:  "M$:INPUTM$:PRINT"Number of teams ="TM;:INPUTTM:PRINT"Lane  Team
140 FORT=1TOTM:PRINTT;N$(T):INPUTN$(T):NEXT
160 OPEN"MEET"FOROUTPUTAS1:PRINT#1,M$","TM:FORT=1TOTM:R(T)=T:PRINT#1,N$(T):NEXT:CLOSE
180 OPEN"LCD:"FOROUTPUTAS1:PS=1:IFINP(187)=243ORINP(187)=194ORINP(187)=195THENOPEN"LPT:"FOROUTPUTAS2:PS=2:LPRINT,M$
200 CLS:F1=1:F2=PS:GOSUB 1400:IFPS=1THEN:PRINT"	<<No printer on line>>":PRINT
230 F=PS:GOSUB 1200
240 READEF:IFEF=999THENF1=1:F2=PS:GOSUB800:GOTO5000
260 READEL,PM:FORX=1TOPM:READS(X):NEXT
280 FORE=EFTOEL
300   A$=INKEY$:IFA$=""THENGOTO300
320	IFINSTR("Bb",A$)THENP=0:P(1)=0:PRINTCHR$(27)"M";:GOTO300
340	IFINSTR("Cc",A$)THENPRINT:PRINTCHR$(27)"A"CHR$(27)"M";:INPUT"Corect team LN#, Change";T,C:IFT<1ORT>TMTHENPRINTCHR$(27)"A"CHR$(27);"K";:P=0:P(1)=0:BEEP:GOTO300 ELSET(T)=T(T)+C:F1=1:F2=PS:PRINTCHR$(27)"A"CHR$(27)"M";:GOSUB1300:P=0:P(1)=0:GOTO300
360	IFINSTR("Hh",A$)THENP=0:P(1)=0:F=1:GOSUB1200:GOTO300
380	IFINSTR("Rr",A$)THENF1=1:F2=1:GOSUB800:GOTO300
400	 IFA$=CHR$(13)THENGOTO500
420 T=VAL(A$):IFT<1ORT>TMTHENBEEP:GOTO300
440 P=P+1:IFP>PMTHENP=PM
460 P(P)=T:PRINTCHR$(27)"A"
480 PRINTCHR$(27)"p";:F1=1:F2=1:GOSUB600
490 PRINTCHR$(27)"K";CHR$(27)"q";:GOTO300
499 '----Process event----
500 FORX=0TOP:T(P(X))=T(P(X))+S(X):NEXT:PRINTCHR$(27)"K";:F1=1:F2=PS:GOSUB1100:F1=2:F2=2:GOSUB600:PRINTCHR$(27)"q":P=0
510 NEXTE:F=PS:GOSUB1200:GOTO240
598 '---Subroutines---
599 '---PRINT Event Ranks---
600 IFF2=2ANDPS=1THENRETURN ELSEFORF=F1TOF2
604 IFF=2ANDP=0THENRETURN
606 IFF=2THENLPRINT"  ";
610 FORX=1TOP:A$=STR$(S(X)):PRINT#F," ";RIGHT$(STR$(P(X)),1);"-";RIGHT$(STR$(X),1);"=";RIGHT$(A$,LEN(A$)-1);:NEXT
620 NEXT:RETURN
799 '---Rank teams with indexed bubble sort---
800 Y=0:FORT=1TOTM-1:IFT(R(T))<T(R(T+1))THENY=1:R(0)=R(T):R(T)=R(T+1):R(T+1)=R(0)
815 :NEXT:IFY<>0THENGOTO800
899 '---Print sorted teams---
900 IFF2=2ANDPS=1THENRETURN ELSEFORF=F1TOF2
920 PRINT#F,:PRINT#F,M$:PRINT#F,"Pl LN   POINT   TEAM";:IFEF=999THENPRINT#F,"	 >>END OF MEET<<":ELSE PRINT#F,
940 FORX=1TOTM:PRINT#F,USING"## ##  ###.##  \                      \";X;R(X);T(R(X));N$(R(X)):NEXT:NEXT
960 RETURN
1099 '---Print Lane Totals---
1100 IFF2=2ANDPS=1THENRETURN ELSEFORF=F1TOF2
1110 PRINT#F,:PRINT#F,USING"##";E;:PRINT#F," Pt->";:FORT=1TOTM:PRINT#F,USING"####";T(T);:NEXT
1120 NEXT:RETURN
1199 '----Heading----
1200 PRINT:PRINTCHR$(27)"ABCHR/E"CHR$(27)"p Tm>1";:FORX=2TOTM:PRINTUSING"   #";X;:NEXT
1210 IFF=2THENLPRINT:LPRINTSTRING$(79,45):LPRINT"Lane No.->1";:FORX=2TOTM:LPRINTUSING"   #";X;:NEXT:LPRINT"		Rank Finishes":LPRINTSTRING$(4*TM+47,45);
1220 PRINTCHR$(27)"q":RETURN
1299 '----Print corrected results----
1300 IFF2=2ANDPS=1THENRETURN ELSEFORF=F1TOF2
1305 IFF=2THENLPRINT
1310 PRINT#F,"Team #"T"corrected by"C"giving"T(T);:IFF=1THENPRINT
1320 NEXT:RETURN
1399 IFPS=2THENLPRINT:LPRINT,"<<<-remove this from line 1399->>> Final Scores, "M$:'----Print totals & teams----
1400 IFF2=2ANDPS=1THENRETURN ELSEFORF=F1TOF2
1420 PRINT#F,"# Pnt Team":FORT=1TOTM:PRINT#F,USING"# ### ";T;T(T);:PRINT#F,N$(T):NEXT:PRINT
1440 NEXT:RETURN
5000 GOTO5000
6000 PRINT:PRINT"	<<Creating new DATA file>>":RESUME 120
7000 NAME"meet.do"AS"meet8.do":RUN
7002 NAME"meet.do"AS"meet8.do"
7003 NAME"meet2.do"AS"meet.do":RUN
7008 NAME"meet.do"AS"meet2.do"
7009 NAME"meet8.do"AS"meet.do":RUN
