10 ' HP-35 - 4 JAN 87 - LARRY REDDIG - 72506.15
20 CLS: PRINT @ 161, "<I> Instructions ?"
30 PRINT @ 201, "<ANY KEY> Start"
40 A$= INKEY$:IF A$="" THEN 40
50 IF A$="I" OR A$="i" THEN 60 ELSE 130
60 CLS:PRINT:PRINT"* Works like orig HP-35 RPN Calculator"
70 PRINT"  (4-stack, 1 mem, only disp X)"
80 PRINT"* <P> for (pi)     * <Q> for (quit)"
90 PRINT"* <E> for (E EX)   * <C> for (clear)"
100 PRINT"* Up-Down arrows to roll stack"
110 PRINT"* Left-Right arrows to chg func keys"
120 GOTO 40
130 CLEAR 100,MAXRAM:DEFINT F,G,I:X=0:Y=0:Z=0:T=0:F=0:G=0:I=0:P=4*ATN(1):B$="":CLS:CALL 16959
140 ON ERROR GOTO 370
150 GOSUB 730
160 PRINT @ 210,STRING$(21,"-")
170 PRINT @240,STRING$(40,"_")
180 GOSUB 430
190 A$=INKEY$:IF A$="" THEN 190
200 IF A$="" THEN 190
210 IF A$=CHR$(13) THEN GOSUB 450:GOTO190
220 IF A$=CHR$(28) OR A$=CHR$(29) THEN GOSUB 560:GOTO 190
230 IF A$=CHR$(30) THEN M=T:GOSUB 450:X=M:GOSUB 430:GOTO190
240 IF A$=CHR$(31) THEN M=X:X=Y:GOSUB 790:T=M:F=0:GOSUB 430:GOTO 190
250 IF A$="P"OR A$="p" THEN F=1:B$=STR$(P):A$=""
260 IF A$="Q"OR A$="q" THEN 390
270 IF A$="C"OR A$="c" THEN 130
280 IF A$="E" AND B$="" THEN B$="1"
290 T$=RIGHT$(B$,1)
300 IF A$="E" AND T$="E" THEN 190
310 IF A$="." AND T$="." THEN 190
320 IF F=1 THEN T=Z:Z=Y:Y=X:F=0:GOSUB 440:GOTO 190
330 IF A$="E" THEN 350
340 IF ASC(A$)<46 OR ASC(A$)>57 THEN 190
350 GOSUB 440
360 GOTO 190
370 PRINT"ERROR";ERR;" IN LINE ";ERL:FOR N=1 TO 1000:NEXT N
380 GOTO 130
390 CLS
400 CALL 16964
410 MENU
420 END
430 PRINT @ 170,SPACE$(21):PRINT @ 170,X:RETURN
440 B$=B$+A$:X=VAL(B$):GOSUB 430:RETURN
450 T=Z:Z=Y:Y=X:B$="":F=0: RETURN
460 X=Y+X:GOSUB 790:GOSUB 430:RETURN
470 X=Y-X:GOSUB790:GOSUB 430:RETURN
480 X=Y*X:GOSUB 790:GOSUB 430:RETURN
490 X=Y/X:GOSUB 790:GOSUB 430:RETURN
500 X=0:F=0:B$="":GOSUB 430:RETURN
510 S=X:F=0:B$="":GOSUB 430:RETURN
520 GOSUB 450:X=S:GOSUB 430:RETURN
530 IF F=1 THEN T=Z:Z=Y:Y=X:F=0
540 IF MID$(B$,1,1)="-" THEN MID$(B$,1,1)=" " ELSE B$="-"+B$
550 GOSUB 440:RETURN
560 IF G=0 THEN G=1:GOSUB 760:RETURN
570 IF G=1 THEN G=0:GOSUB 730:RETURN
580 I=ABS(I-1):IF I=1 THEN PRINT @ 280,CHR$(27)"pINV"CHR$(27)"q" ELSE PRINT @ 280,"INV":GOSUB 430
590 RETURN
600 IF I=1 THEN X=(ATN(X/SQR(-X*X+1)))*180/P ELSE X=SIN((X*P)/180)
610 GOSUB 800:GOSUB 430:RETURN
620 IF I=1 THEN X=(-ATN(X/SQR(-X*X+1))+P/2)*180/P ELSE X=COS((X*P)/180)
630 GOSUB 800:GOSUB 430:RETURN
640 IF I=1 THEN X=ATN(X)*(180/P) ELSE X=TAN((X*P)/180)
650 GOSUB 800:GOSUB 430:RETURN
660 X=X^Y:I=0:GOSUB 800:GOSUB 790:GOSUB 430:RETURN
670 IF I=1 THEN X=LOG(X) ELSE X=2.718281828^X
680 GOSUB 800:GOSUB 430:RETURN
690 M=Y:Y=X:X=M
700 GOSUB 800:GOSUB 430:RETURN
710 X=1/X
720 GOSUB 800:GOSUB 430:RETURN
730 PRINT @280,"  +    -    x    /   CLX  STO  RCL  CHS"
740 ON KEY GOSUB 460,470,480,490,500,510,520,530:KEY ON:B$=""
750 RETURN
760 PRINT @280,"INV  SIN  COS  TAN  x^y  e^x  x<>y  1/x"
770 ON KEY GOSUB 580,600,620,640,660,670,690,710:KEY ON:B$=""
780 RETURN
790 Y=Z:Z=T:B$="":F=1:RETURN
800 PRINT @ 280,"INV":I=0:F=1:B$="":RETURN
