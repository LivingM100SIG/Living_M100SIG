0 REM FLITE3.BA - 10/9/89 - Ver. 2.00
10 CLS:CLEAR3000:RV$=CHR$(27)+"p":NV$=CHR$(27)+"q":PRINT@134,RV$"Initializing"NV$:DIMS$(7),S(7):MN$=CHR$(13)+CHR$(27):ES$="<ESC> to Quit, <ENTER> to Continue"
30 MI$=" Miles":TI$="Time":SP$="Speed ":DS$="Distance":H$="Hours":M$="Minutes":F$="Fuel":G$="Gallons"
50 FORZ=1TO6:READS$(Z):NEXT
60 FORZ=1TO6:READS(Z):NEXT
61 GOTO550
70 RUN"FLIGHT.BA"
550 CLS:S=0:H=0:M=O:D=0:G=0:FR=0:PRINTTAB(10);TI$"/"SP$"/"DS$:GOSUB2530
560 ONS3GOTO580,640,700,750,810,870,70
570 GOTO550
580 CLS:PRINTTAB(17);SP$:PRINT@80,DS$;D:PRINT@88,"";:INPUTD:PRINT@89,SPACE$(20):PRINT@89,D
590 PRINT@120,H$;H:PRINT@125,"";:INPUTH:PRINT@126,SPACE$(20):PRINT@126,H
600 PRINT@140,M$;M:PRINT@147,"";:INPUTM:PRINT@148,SPACE$(4):PRINT@148,M
610 H1=H+M/60:S=INT(((D/H1)+.05)*10)/10:PRINT@160,SP$" =";S;"Mph/Knots":PRINT@283,ES$;
620 GOSUB2750:ONINSTR(MN$,IK$)GOTO580,550
630 GOTO620
640 CLS:PRINTTAB(16);DS$:PRINT@80,SP$;S:PRINT@85,"";:INPUTS:PRINT@86,SPACE$(30):PRINT@86,S
650 PRINT@120,H$;H:PRINT@125,"";:INPUTH:PRINT@126,SPACE$(20):PRINT@126,H
660 PRINT@140,M$;M:PRINT@147,"";:INPUTM:PRINT@148,SPACE$(9):PRINT@148,M
670 H1=H+M/60:D=INT(((S*H1)+.05)*10)/10:PRINT@160,DS$" =";D;MI$:PRINT@283,ES$;
680 GOSUB2750:ONINSTR(MN$,IK$)GOTO640,550
690 GOTO680
700 CLS:PRINTTAB(12);DS$"->"TI$:PRINT@80,DS$;" ";D:PRINT@88,"";:INPUTD:PRINT@89,SPACE$(30):PRINT@89,D
710 PRINT@120,SP$;S:PRINT@125,"";:INPUTS:PRINT@126,SPACE$(33):PRINT@126,S
720 H1=D/S:H=INT(H1):M1=H1-H:M=INT((M1*60)+.5):PRINT@160,H;H$;M;M$:PRINT@283,ES$;
730 GOSUB2750:ONINSTR(MN$,IK$)GOTO700,550
740 GOTO730
750 CLS:PRINT@(15),F$" Rate":PRINT@80,G$" Used ";G:PRINT@92,"";:INPUTG:PRINT@93,SPACE$(20):PRINT@93,G
760 PRINT@120,H$;H:PRINT@125,"";:INPUTH:PRINT@126,SPACE$(20):PRINT@126,H
770 PRINT@140,M$;M:PRINT@147,"";:INPUTM:PRINT@148,SPACE$(4):PRINT@148,M
780 H1=H+M/60:FR=INT(((G/H1)+.05)*10)/10:PRINT@160,F$" Rate =";FR;"gal./hour":PRINT@283,ES$;
790 GOSUB2750:ONINSTR(MN$,IK$)GOTO750,550
800 GOTO790
810 CLS:PRINT@15,F$" Used":PRINT@80,F$" Rate ";FR:PRINT@89,"";:INPUTFR:PRINT@90,SPACE$(29):PRINT@90,FR
820 PRINT@120,H$;H:PRINT@125,"";:INPUTH:PRINT@126,SPACE$(20):PRINT@126,H
830 PRINT@140,M$;M:PRINT@147,"";:INPUTM:PRINT@148,SPACE$(4):PRINT@148,M
840 H1=H+M/60:G=INT(((FR*H1)+.1)*10)/10:PRINT@160,F$" Used =";G;G$:PRINT@283,ES$;
850 GOSUB2750:ONINSTR(MN$,IK$)GOTO810,550
860 GOTO850
870 CLS:PRINT@14,F$" ->"TI$:PRINT@80,G$" Used ";G:PRINT@92,"";:INPUTG:PRINT@93,SPACE$(20):PRINT@93,G
880 PRINT@120,F$" Rate ";FR:PRINT@129,"";:INPUTFR:PRINT@130,SPACE$(9):PRINT@130,FR
890 H1=G/FR:H=INT(H1):M1=H1-H:M=INT((M1*60)+.5):PRINT@160,H;H$;M;M$:PRINT@283,ES$;
900 GOSUB2750:ONINSTR(MN$,IK$)GOTO870,550
910 GOTO900
2530 FORZ=1TO6:PRINT@S(Z),S$(Z):NEXTZ:S3=1:PRINT@283,ES$;
2540 PRINT@S(S3),RV$;S$(S3);NV$
2550 GOSUB2750:ONINSTR(MN$,IK$)GOTO2600,70
2560 IK=ASC(IK$)-29:IFIK<0THEN2550ELSEONIKGOTO2580,2590
2570 GOTO2550
2580 PRINT@S(S3),S$(S3):S3=S3-1:IFS3<1THENS3=6:GOTO2540ELSE2540
2590 PRINT@S(S3),S$(S3):S3=S3+1:IFS3>6THENS3=1:GOTO2540ELSE2540
2600 RETURN
2750 IK$=INKEY$:IFIK$=""THEN2750
2760 RETURN
2790 DATA"Speed","Distance","Distance -> Time","Fuel Rate","Fuel Used","Fuel -> Time"
2850 DATA80,120,160,100,140,180
