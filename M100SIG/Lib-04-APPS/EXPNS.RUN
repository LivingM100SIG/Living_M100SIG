10 ONERRORGOTO800
20 DEFSTRA-I:E=CHR$(27):ER=E+"p":EN=E+"q":H=CHR$(243):G=CHR$(241):GOSUB700
100 GOSUB500:IFI="2"THEN200
110 CLS:IT="":PRINT:INPUT"What for";IT:IFIT=""THENCLOSE:GOTO100
115 INPUT"How much";P:PRINT#1,USING"\                   \";IT:PRINT#1,USING"#####.##";P
120 PRINTE"Q":GOTO110
200 CLS:S=1
210 PRINT:FORJ=1TO6:LINEINPUT#1,A:INPUT#1,P
220 PRINT"Item"S;CHR$(8)":"TAB(9);A;TAB(30);:PRINTUSING"$$####.##";P
230 IFEOF(1)THEN260
240 S=S+1:NEXT:SS=FRE(""):PRINTER" Press <ENTER> to continue..."EN;
250 IFINKEY$=""THEN250ELSECLS:GOTO210
260 PRINTER" * * *   That was the last one   * * * "EN;
270 IFINKEY$=""THEN270
280 CLOSE1:GOTO100
300 CLS:PRINT:PRINT" Adding Expense Items...":S=1
310 IFEOF(1)THEN330ELSEINPUT#1,A,P
320 PRINT@135,"Item"S:S=S+1:T=T+P:GOTO310
330 CLOSE:PRINTSTRING$(40,G);:PRINT" Total = ";
340 PRINTUSING"$$####.##";T;:T=0
350 IFINKEY$=""THEN350ELSEGOTO100
500 CLS:PRINT@43,ER;TAB(8);CHR$(34);F;CHR$(34)TAB(19)"EXPENSE  MENU"TAB(37);
510 PRINTEN:PRINT@83,ER"   "EN;TAB(34);ER"   "EN
520 PRINT@123,ER" 1 "EN"  ADD new expenses."TAB(34)ER"   "EN
530 PRINT@163,ER" 2 "EN"  REVIEW expense items."TAB(34)ER"   "EN
540 PRINT@203,ER" 3 "EN"  SUM of Total expenses."TAB(34)ER"   "EN
550 PRINT@243,ER" 4 "EN"  CHANGE accounts."TAB(34)ER"   "EN
560 PRINT@283,ER"     Choose which function?"TAB(37)EN;
570 PRINT@284,E"P";
600 I=INKEY$:IFI=""THEN600ELSEPRINTE"Q";
610 IFI="q"ORI="Q"THENCLOSE:MENU
620 IFI=ETHENCLOSE:POWEROFF,RESUME
630 IFI="1"THENOPENFFORAPPENDAS#1:RETURN
640 IFI="2"THENOPENFFORINPUTAS#1:RETURN
650 IFI="3"THENOPENFFORINPUTAS#1:GOSUB300:RETURN
660 IFI="4"THENRUN
670 GOTO570
700 CLS:PRINTER;SPACE$(12)"EXPENSE  TRACKER"SPACE$(12);
710 PRINT" ver 2.0   by R. Scott Truesdell,  1984 "EN;
712 FORX=1TO5:PRINTSPACE$(7);CHR$(245);:NEXT
720 FORX=63930TO64141STEP11:IFPEEK(X)>191ANDPEEK(X)<255THEND="":FORY=X+3TOX+8:D=D+CHR$(PEEK(Y)):NEXT:PRINTD" "CHR$(245);
740 NEXT:PRINT@280,ER;E"V  Which Account shall we use "EN;
750 INPUTF:F=LEFT$(F,6):FORJ=1TOLEN(F):F%=ASC(MID$(F,J,1))
760 IFF%>96THENMID$(F,J,1)=CHR$(F%-32)
770 NEXT:RETURN
800 IFERR=52 THENRUN
810 IFERR=5ANDERL=750THENMENU
820 PRINT"ERROR "ERR" IN "ERL:END

