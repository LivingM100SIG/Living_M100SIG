0 'VIDLBL.BA COPYRIGHT 9/20/1989 BY WAYNE TALBOT 70156,1575
10 CLS:PRINT"This program creates or uses an existing database of video cassette recordings"
11 PRINT"and prints the labels on a printer. The printer must be capable of printing"
12 PRINT"superscript using variable length line  feeds. If you need to change printer"
13 PRINT"codes, they are located in line #310.
14 PRINT"PRESS ANY KEY TO CONTINUE";
15 Q$=INKEY$:IF Q$="" THEN GOTO 15
100 B$="1":Z=0:P=0:DIM A$(4):I$="LABELS.DO":ONERROR GOTO 990
105 CLS:PRINT:PRINT"   VIDEOTAPE CASSETTE LABEL PRINTER"
106 PRINT:PRINT"DO YOU NEED TO (C)REATE A LIST OR"
107 INPUT"               (U)SE A CURRENT LIST";Q$
108 IF LEFT$(Q$,1)="C" THEN GOSUB 2000
110 CLS:PRINT"FILES TO BE USED MUST CONSIST OF A FOUR"
111 PRINT"FIELD PER LINE FORMAT WITH EACH FIELD   SEPARATED BY COMMAS."
130 FILES:Q$="":INPUT "WHICH FILE IS THE SOURCE";Q$:IF Q$<>""THEN I$=Q$:PRINTI$
140 IF RIGHT$(I$,3)=".CO" OR RIGHT$(I$,3)=".co" OR RIGHT$(I$,3)=".ba" OR RIGHT$(I$,3)=".BA" THEN GOTO141 ELSE GOTO142
141 BEEP:CLS:PRINT"This program accepts ";CHR$(34);".DO";CHR$(34);" files only!":GOTO130
142 IF RIGHT$(I$,3)=".do" THEN I$=LEFT$(I$,(LEN(I$)-3))+".DO"
145 IF RIGHT$(I$,3)<>".DO" THEN I$=I$+".DO"
255 OPEN I$ FOR INPUT AS #1
300 CLS:PRINT:PRINT:PRINT:PRINT "        AT 12 LINES PER INCH,":PRINT "      HOW MANY LINES PER LABEL";:INPUT L
305 CLS:PRINT:PRINT:PRINT"SET THE PRINTER AT THE TOP OF THE LABEL         PRESS ENTER TO PROCEED":Q$=INPUT$(1):CLS
310 LPRINT CHR$(15);CHR$(27);CHR$(83);CHR$(1);CHR$(27);CHR$(51);CHR$(18)
320 INPUT #1,A$(1),A$(2),A$(3),A$(4):Z=Z+1
330 IF A$(1)=B$ THEN 350 ELSE GOSUB 1000
350 B$=A$(1)
360 LPRINT "TAPE# ";A$(1);"  ";A$(2);" ";A$(3);"  ";A$(4)
370 IF EOF(1) THEN GOTO 999
380 GOTO 320
990 CLS:PRINT:PRINT:PRINT:PRINT "ERROR TRAP!! CHECK FILE FOR FOUR FIELDS.":END
999 MENU
1000 FOR X=Z+1 TO L
1010 LPRINT
1020 NEXT
1030 Z=0:RETURN
2000 OPEN I$ FOR OUTPUT AS 1
2010 CLS:INPUT "TAPE NUMBER";A$(1)
2020 INPUT "SELECTION NUMBER";A$(2)
2030 INPUT "TITLE";A$(3)
2040 INPUT "COMMENTS";A$(4)
2050 IF LEN(A$(1))<4 THEN A$(1)=LEFT$("0000",4-LEN(A$(1)))+A$(1)
2051 IF LEN(A$(2))<2 THEN A$(2)=LEFT$("000",3-LEN(A$(2)))+A$(2)
2055 PRINTA$(1);",";A$(2);",";A$(3);",";A$(4)
2060 INPUT"IS THIS CORRECT (Y)ES OR (N)O";Q$
2070 IF LEFT$(Q$,1)="N" THEN GOTO2010
2075 PRINT #1,A$(1);",";A$(2);",";A$(3);",";A$(4)
2080 CLS:INPUT"(C)ONTINUE DATA ENTRY OR (P)RINT LABELS";Q$
2100 IF LEFT$(Q$,1)="P" THEN CLOSE #1:RETURN
2110 GOTO 2010

