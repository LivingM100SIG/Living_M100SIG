1 GOSUB1760
5 S$="######.##":GOTO1700
10 IFH$=""THENGOSUB1730
15 GOSUB1740:GOSUB1600:OPEN"WTB"FORINPUTAS1
20 IFEOF(1)THENCLOSE:GOTO50
30 GOSUB1110:IFA$>="500"ANDA$<"600"THENGOSUB1510
40 GOTO20
50 I1=WSUM-VSUM:I2=YSUM-XSUM:GOSUB1600:LPRINTTAB(32)"Income Statement":LPRINTTAB((80-LEN(H$))/2);H$:LPRINTTAB((80-LEN(DA$))/2);DA$:LPRINT:LPRINT
60 LPRINTTAB(30)"Year To Date"TAB(45)"Percent"TAB(55)"Current Period"TAB(70)"Percent":LPRINT"Income":LC=7:OPEN"WTB"FORINPUTAS1
70 IFEOF(1)THENEND
80 GOSUB1110:IFA$<"500"GOTO70
90 IFA$<"600"THENGOSUB1540:GOTO70
100 IFA$>="600"GOTO150
120 IFEOF(1)GOTO190
130 GOSUB1110:IFA$<"800"GOTO120
140 GOSUB1570:GOTO120
150 GOSUB1520:LPRINTTAB(6)"Total Income";
160 LPRINTTAB(30)USINGS$;WSUM-VSUM;:IFI1<>0THENLPRINTTAB(42)USINGS$;((WSUM-VSUM)*100)/I1;
170 LPRINTTAB(55)USINGS$;YSUM-XSUM;:IFI2<>0THENLPRINTTAB(67)USINGS$;((YSUM-XSUM)*100)/I2;
180 LPRINT:GOSUB1520:LPRINT:LPRINT"Expenses":GOSUB1600:GOSUB1570:GOTO120
190 GOSUB1520:LPRINTTAB(6)"Total Expenses";TAB(30)USINGS$;VSUM-WSUM;:IFI1<>0THENLPRINTTAB(42)USINGS$;((VSUM-WSUM)*100)/I1;
200 LPRINTTAB(55)USINGS$;XSUM-YSUM;:IFI2<>0THENLPRINTTAB(67)USINGS$;((XSUM-YSUM)*100)/I2;
210 LPRINT:GOSUB1520:LPRINTTAB(9)"Net Income";TAB(30)USINGS$;I1+WSUM-VSUM;:IFI1<>0THENLPRINTTAB(42)USINGS$;((I1+WSUM-VSUM)*100)/I1;
220 LPRINTTAB(55)USINGS$;I2+YSUM-XSUM;:IFI2<>0THENLPRINTTAB(67)USINGS$;((I2+YSUM-XSUM)*100)/I2;
230 LPRINT:GOSUB1530:LPRINTCHR$(12):CLOSE:GOTO1700
290 IFH$=""THENGOSUB1730
295 GOSUB1740:GOSUB1600:OPEN"WTB"FORINPUTAS1
300 GOSUB1100:IFA$<"500"GOTO300
310 GOSUB1200:GOTO300
320 CLOSE:P1=WSUM-VSUM:P2=YSUM-XSUM:GOSUB1600:LPRINTTAB(33)"Balance Sheet":LPRINTTAB((80-LEN(H$))/2)H$:LPRINTTAB((80-LEN(DA$))/2)DA$:LPRINT:LPRINTTAB(37)"ASSETS":LPRINT:LPRINT"Current Assets":LC=7:OPEN"WTB"FORINPUTAS1
330 GOSUB1100:IFA$="000"GOTO330
335 IFA$<"150"THENGOSUB1200:N=U+V-W:GOSUB1000:GOTO330
340 ST=USUM+VSUM-WSUM:LPRINTTAB(6)"Total Current Assets";:GOSUB1230:GOSUB1240:LPRINT"Other Assets":LC=LC+1:GOTO360
350 GOSUB1100
360 IFA$<"200"THENGOSUB1200:N=U+V-W:GOSUB1000:GOTO350
370 ST=-ST+USUM+VSUM-WSUM:LPRINTTAB(6)"Total Other Assets";:GOSUB1230:GOSUB1240:ST=USUM+VSUM-WSUM:LPRINTTAB(6)"Total Assets";:GOSUB1230:GOSUB1250:GOSUB1600:LPRINT:LPRINTTAB(35)"LIABILITIES":LC=LC+2:LPRINT"Current Liabilities":LC=LC+1:GOTO390
380 GOSUB1100
390 IFA$<"350"THENGOSUB1200:N=-U+W-V:GOSUB1000:GOTO380
395 ST=-USUM+WSUM-VSUM:LPRINTTAB(6)"Total Current Liabilities";:GOSUB1230:GOSUB1240:LPRINT"Long Term Liabilities":LC=LC+1:GOTO410
400 GOSUB1100
410 IFA$<"400"THENGOSUB1200:N=-U+W-V:GOSUB1000:GOTO400
420 ST=-ST-USUM+WSUM-VSUM:LPRINTTAB(6)"Total Long Term Liabilities";:GOSUB1230:GOSUB1240:ST=-USUM+WSUM-VSUM:LPRINTTAB(6)"Total Liabilities";:GOSUB1230:LPRINT:LPRINT:LPRINT"Equity":LC=LC+3:GOTO440
430 GOSUB1100
440 IFA$="410"ORB$="Retained Earnings"THENW=W+P1:Y=Y+P2
450 IFA$<"500"THENGOSUB1200:N=-U+W-V:GOSUB1000:GOTO430
460 ST=-ST-USUM+WSUM-VSUM:LPRINTTAB(6)"Total Equity";:GOSUB1230:GOSUB1240:ST=-USUM+WSUM-VSUM:LPRINTTAB(6)"Total Liabilities & Equity";:GOSUB1230:GOSUB1250:CLOSE:LPRINTCHR$(12):GOTO1700
1000 LPRINTTAB(3)B$;TAB(45);:LPRINTUSINGS$;N:LC=LC+1:IFLC>54THENLPRINTCHR$(12):LC=0
1010 RETURN
1100 IFEOF(1)GOTO320
1110 INPUT#1,A$,B$,U,V,W,X,Y:RETURN
1200 USUM=USUM+U:VSUM=VSUM+V:WSUM=WSUM+W:RETURN
1230 LPRINTTAB(55)USINGS$;ST:LC=LC+1:RETURN
1240 LPRINTTAB(55)"---------":LC=LC+1:RETURN
1250 LPRINTTAB(55)"=========":LPRINT:LC=LC+2:IFLC>25THENLPRINTCHR$(12):LC=0
1260 RETURN
1510 USUM=USUM+U:VSUM=VSUM+V:WSUM=WSUM+W:XSUM=XSUM+X:YSUM=YSUM+Y:RETURN
1520 LPRINTTAB(30)"---------"TAB(45)"------"TAB(55)"---------"TAB(70)"------":RETURN
1530 LPRINTTAB(30)"========="TAB(45)"======"TAB(55)"========="TAB(70)"======":RETURN
1540 LPRINTTAB(3)B$TAB(30)USINGS$;(W-V);:IFI1<>0THENLPRINTTAB(42)USINGS$;((W-V)*100)/I1;
1550 LPRINTTAB(55)USINGS$;(Y-X);:GOSUB1510:IFI2<>0THENLPRINTTAB(67)USINGS$;((Y-X)*100)/I2;
1560 LPRINT:RETURN
1570 LPRINTTAB(3)B$TAB(30)USINGS$;(V-W);:IFI1<>0THENLPRINTTAB(42)USINGS$;((V-W)*100)/I1;
1580 LPRINTTAB(55)USINGS$;(X-Y);:GOSUB1510:IFI2<>0THENLPRINTTAB(67)USINGS$;((X-Y)*100)/I2;
1590 LPRINT:RETURN
1600 USUM=0:VSUM=0:WSUM=0:XSUM=0:YSUM=0:RETURN
1700 CLS:PRINT"SELECT REPORT":PRINT:PRINT
1710 PRINTTAB(10)"I]ncome Statement":PRINTTAB(10)"B]alance Sheet":PRINTTAB(10)"E]xit"
1720 ONINSTR("  IiBbEe",INKEY$)GOTO1720,1720,10,10,290,290,1800,1800:GOTO1720
1730 CLS:PRINT"  Use No Commas":INPUT"Enter Company Name or Title";H$:RETURN
1740 CLS:PRINT"  Use No Commas":INPUT"Enter Date(s)";DA$:RETURN
1760 OPEN"WTB"FORINPUTAS1:GOSUB1110:CLOSE:IFA$="000"THENH$=B$
1770 RETURN
1800 MENU
