5 CLEAR1000
7 ONERRORGOTO700
10 CLS:PRINT@48,"VEHICLE EXPENSE PROGRAM"
11 PRINT@99,"by"
12 PRINT@132,"L. W. Leedy III"
13 PRINT@211,"Version 2.0, Jan 85"
14 PRINT@241,"Public Domain, Compuserve PPN 74166,51
20 FORI=1TO500:NEXT
25 DY$=DAY$+SPACE$(4)+DATE$+SPACE$(3)+LEFT$(TIME$,5)+SPACE$(3)
30 CLS:PRINT@40,"DO YOU WANT TO:"
45 PRINT@120,"ENTER A HEADING FOR THIS REPORT (H)"
50 PRINT@160,"ENTER GASOLINE EXPENSES (G)"
55 PRINT@200,"ENTER MISC. EXPENSES (M)"
57 PRINT@240,"TOTAL EXPENSES (T)
60 PRINT@280,"";:INPUT"Please Enter Your Selection";M$
70 IFM$="g"ORM$="G"THEN100
80 IFM$="m"ORM$="M"THEN400
85 IFM$="t"ORM$="T"THEN500
90 IFM$="h"ORM$="H"THEN800
95 IFM$="Menu"THENMENUELSE30
100 CLS
120 PRINT@5,"GASOLINE EXPENSES AND MILEAGE"
130 PRINT@40,"";:INPUT"ENTER STARTING MILEAGE";S$
140 IFS$=""THENGOSUB900
150 PRINT@120,"";:INPUT"ENTER ENDING MILEAGE";E
170 PRINT@200,;:INPUT"ENTER NUMBER OF GALLONS OF GAS";G
180 PRINT@280,"";:INPUT"ENTER THE TOTAL COST OF GAS";C
185 S=VAL(S$)
190 X=(E-S)/G
210 OPEN"VEHEXP"FORAPPENDAS1
220 PRINT#1,DY$;:PRINT#1,USING"#####.#";S;:PRINT#1,"       ";
221 PRINT#1,USING"#####.#";E;:
222 PRINT#1,"    ";:PRINT#1,USING"##.#";X;:PRINT#1,"    ";
223 PRINT#1,USING"##.#";G;:PRINT#1,"         ";:PRINT#1,USING"$###.##";C
230 CLOSE#1:CLS
240 PRINT@125,"YOU GOT ";:PRINTUSING"##.#";X;:PRINT" MILES PER GALLON"
300 FORI=1TO800:NEXT
305 CLS:PRINT@125,"":INPUT"Do you have another Y/N";O$
310 IFO$="y"ORO$="Y"THENCLOSE1:GOTO100
320 IFO$="n"ORO$="N"THEN30ELSE405
400 'MISC EXPENSES
403 CLS
405 PRINT@40,"Enter Mileage":INPUT"";A1
410 CLS:PRINT@80,STRING$(35,95)
420 PRINT@40,"Description of item? ";:PRINT@80,"";
425 LINEINPUTA2$:A2$=LEFT$(A2$,35)
430 LA=35-LEN(A2$)
440 A2$=A2$+SPACE$(LA)
450 CLS
460 PRINT@40,"Enter total cost":INPUT"";A3
470 OPEN"vehexp"FORAPPENDAS1
480 PRINT#1,DY$;:PRINT#1,USING"#####.#";A1;:PRINT#1,"  ";A2$;"  ";
490 PRINT#1,USING"$####.#";A3:CLOSE1
499 GOTO30
500 'TOTAL EXPENSES
510 CLS
520 OPEN"VEHEXP"FORINPUTAS1
530 LINEINPUT#1,T$
540 T$=RIGHT$(T$,6)
550 T=VAL(T$):GT=GT+T
560 IFEOF(1)THEN600ELSE530
600 PRINT@120,"Total Spent $";GT
605 CLOSE#1
610 PRINT@200,"";
620 INPUT"Do you wish to append this to the end of VEHEXP.DO Y/N";A$
630 IFA$="y"ORA$="Y"THEN650
640 IFA$="n"ORA$="N"THEN30ELSE620
650 GT$=SPACE$(57)+"Grand Total $"+STR$(GT)
660 OPEN"vehexp"FORAPPENDAS1:PRINT#1,GT$
670 GOTO30
699 END
700 'ERROR HANDLING
710 IFERR=11THENCLS:PRINT@120," Please check your figures and reenter":GOTO790
790 FORI=1TO500:NEXT:RESUME30
800 'HEADING
810 H$(1)="TRIP PURPOSE:  ":H$(2)="DESTINATION:  "
820 H$(3)="VEHICLE:  ":H$(4)="OPERATOR:  "
821 H5$=SPACE$(23) +"VEHICLE EXPENSES AND MILEAGE LOG"
825 FORI=1TO4
827 HX=LEN(H$(I)):HX=39-HX
830 CLS:PRINT@40,H$(I):PRINT@81,STRING$(HX,95):PRINT@80,"";:LINEINPUT"?";HA$(I)
831 HA$(I)=LEFT$(HA$(I),HX)
832 HY=LEN(HA$(I)):IFHY<=HXTHENHY=HX-HY
833 H$(I)=H$(I)+HA$(I)+SPACE$(HY)
835 NEXT
850 OPEN"VEHEXP"FORAPPENDAS1:PRINT#1,H5$:PRINT#1,"":
854 PRINT#1,H$(1);" ";H$(2)
855 PRINT#1,H$(3);" ";H$(4)
858 PRINT#1,""
860 PRINT#1,"DAY    DATE       TIME    BEGINING      ENDING";
865 PRINT#1,"     MPG     GALLONS      COST"
870 PRINT#1,""
899 CLOSE1:GOTO20
900 'FIND STARTING MILEAGE
910 OPEN"VEHEXP"FORINPUTAS1
920 INPUT#1,I$
930 I$=MID$(I$,41,7):S=VAL(I$)
940 IFEOF(1)THENCLOSE1:GOTO960
950 GOTO920:
960 PRINT@63,S:CLOSE1:RETURN

