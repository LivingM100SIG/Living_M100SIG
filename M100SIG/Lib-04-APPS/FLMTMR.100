10 'B&W Film Developing Timer, by Ron VanBenthusen [70721,3223]
20 CLS:PRINT@135,"FLMTMR.BA":DIMCH(7),S(7):BL$=CHR$(27)+"p":WH$=CHR$(27)+"q":U$=CHR$(241)+CHR$(154)
30 DATA3,5,2,7,4,9,2,8,3,9,1,4,2,2
40 FORD=1TO7:READC:CH(D)=C+250:READS:S(D)=S*1000:NEXTD
100 CLS:CI=0:PRINT@85,"Minutes in length";:INPUTTM:TS=TM*60
110 PRINT@165,"Second intervals";:INPUTSI
115 SI=INT(SI):IFSI=0THENSI=TSELSEIFSI<5THEN110
120 NI=TS/SI:IFNI>999THEN100
140 IFINT(NI)*SI>=TSTHENIN=INT(NI)-1ELSEIN=INT(NI)
200 CLS:PRINT@0,BL$"TIMER:"TM"Min. -"SI"Sec. Intervals"WH$
210 PRINT@40,"Intl     Started":PRINT@68,"Left   %Done":PRINT@80,"Gone     Time"
220 PRINT@125,"--- Intrval":PRINT@160,"Left     Ending":Q=1:GOSUB3000
310 FORL=0TOTM:LINE(L*(239/TM),63)-(L*(239/TM),40):NEXTL
320 FORL=0TONI:LINE(L*(239/NI),63)-(L*(239/NI),48):NEXTL:LINE(0,63)-(239,63)
350 MC$="312831303130313130313031":MC=VAL(LEFT$(DATE$,2)):DM=VAL(MID$(MC$,(MC*2)-1,2))
400 PRINT@136,BL$" Press [ENTER] to Start "WH$:BEEP
410 GOSUB900:K$=INKEY$:IFK$=""THENGOSUB920:GOTO410ELSEPRINT@136,SPACE$(24):GOSUB920
500 GOSUB1000:ST$=T$:SD=D:SH=H:SM=M:SS=S:ID=D:IH=H:IM=M:IS=S:PRINT@57,ST$
600 CD=SD:CH=SH:CM=SM:CS=SS+INT(TS):GOSUB2000:ET$=CT$:ED=CD:EH=CH:EM=CM:ES=CS:PRINT@177,ET$
650 IFED>DMTHENAD=ED-DM:RD$=DATE$:DATE$="01/01/88":GOTO500
700 CD=ID:CH=IH:CM=IM:CS=IS+SI:GOSUB2000:IT$=CT$:ID=CD:IH=CH:IM=CM:IS=CS:CI=CI+1:PRINT@137,IT$
800 GOSUB900:CD=ED-D:CH=EH-H:CM=EM-M:CS=ES-S:GOSUB2090:CE=CK:GOSUB2000
850 PRINT@186,BL$;CT$;WH$:PRINT@195,USING"###.#";((TS-CK)/TS)*100
860 CD=ID-D:CH=IH-H:CM=IM-M:CS=IS-S:GOSUB2090:IFCE<=CKTHEN880ELSEGOSUB2000
870 PRINT@146,BL$;CT$;WH$:PRINT@155,USING"###.#";((SI-CK)/SI)*100
880 IFCE<=0THEN4000ELSEIFCK<=0THENGOSUB3000:GOTO700
890 GOSUB920:GOTO800
900 GOSUB1000:IFS=PSTHENU=U+1:PRINT@105+U,U$:GOTO900
910 PS=S:PRINT@97,T$:RETURN
920 PRINT@106,SPACE$(U+1):U=0:RETURN
1000 T$=TIME$:H=VAL(LEFT$(T$,2)):M=VAL(MID$(T$,4,2)):S=VAL(RIGHT$(T$,2)):D=VAL(MID$(DATE$,4,2)):RETURN
2000 IFCS>59ORCS<0THENCM=CM+INT(CS/60):CS=CS-INT(CS/60)*60
2010 IFCM>59ORCM<0THENCH=CH+INT(CM/60):CM=CM-INT(CM/60)*60
2020 IFCH>23ORCH<0THENCD=CD+INT(CH/24):CH=CH-INT(CH/24)*24
2050 CH$=RIGHT$(STR$(CH),2):IFLEFT$(CH$,1)=" "THENCH$="0"+RIGHT$(CH$,1)
2060 CM$=RIGHT$(STR$(CM),2):IFLEFT$(CM$,1)=" "THENCM$="0"+RIGHT$(CM$,1)
2070 CS$=RIGHT$(STR$(CS),2):IFLEFT$(CS$,1)=" "THENCS$="0"+RIGHT$(CS$,1)
2080 CT$=CH$+":"+CM$+":"+CS$:RETURN
2090 CK=(((((CD*24)+CH)*60)+CM)*60)+CS:RETURN
3000 PRINT@45,USING"###";IN:PRINT@85,USING"###";CI:PRINT@165,USING"###";IN-CI:IFQ=1THENQ=0:RETURN
3010 Y1=239/NI:LINE(CI*Y1,58)-((CI*Y1)-Y1,52),1,BF
3020 FORN=1TO7:PRINT@199+40*(CI/NI),CHR$(CH(N)):SOUNDS(N),10:NEXTN:RETURN
4000 LINE(239,58)-(239-Y1,52),1,BF:Y1=239/30
4010 FORN=1TO30:LINE(N*Y1,57)-((N*Y1)-Y1,53),0,BF:SOUND12000-(N*300),5:NEXTN:IFAD=0THEN4040
4020 RY=VAL(RIGHT$(RD$,2)):RM=VAL(LEFT$(RD$,2))+1:RD=AD:AD=0:IFRM>12THENRM=RM-12:RY=RY+1
4030 CH=RM:CM=RD:CS=RY:GOSUB2050:MID$(CT$,3,1)="/":MID$(CT$,6,1)="/":DATE$=CT$
4040 PRINT@253,BL$" Again (Y/N) "WH$:BEEP
4050 GOSUB900:K$=INKEY$:IFK$="Y"ORK$="y"THEN100ELSEIFK$="N"ORK$="n"THENMENUELSEGOSUB920:GOTO4050
