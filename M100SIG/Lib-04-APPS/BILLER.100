0 ' *** TIME BILLER ***
1 ' A Time and Billing Program
2 ' for Professionals. (Reformatted for
3 ' Model 100 - John A. Levin, 3-10-86)
4 ' Copyright (C) April 1985
5 ' David Huntington Williams
6 ' Feel free to copy and use but
7 ' not for resale.
8 IPL"biller.ba"
9 CLEAR256
10 CLS:CLOSE
20 PRINT@41,"        *** TIMEBILLER 100 ***"
30 PRINT"     you may..(f1) enter start time"
40 PRINT"     .........(f2) enter end time"
50 PRINT"     .........(f3) print to screen"
60 PRINT"     .........(f4) send to file"
65 LINE(0,0)-(239,63),1,B:LINE(2,2)-(237,61),1,B:LINE(4,4)-(235,59),1,B
70 KEY ON
80 ONKEYGOSUB100,200,500,700
92 PRINT@250,"     Choose function";
97 IFINKEY$<>""THEN MENU
98 GOTO92
100 CLS:CLOSE
112 PRINT@165,"client's name ";:INPUTCLIENT$
114 PRINT@165,"              "
115 LINEINPUT"activity - ";ACT$
116 BILL$=TIME$:CLOSE
117 OPEN"Time.do"FORINPUTAS1:INPUT#1,P$:FOR X=1TO3:INPUT#1,A$:NEXT
118 IFA$<>"DONE"THENCLS:PRINT@200,P$;" STILL OPEN ":FORX=1TO1000:NEXT:CLOSE:GOTO10
119 CLOSE:OPEN "time.do"FOROUTPUTAS1
120 PRINT#1,CLIENT$
122 PRINT#1,ACT$
124 PRINT#1,BILL$
125 PRINT#1,"OPEN"
126 CLOSE#1
128 GOTO10
130 RETURN
200 CLS
210 OPEN"time.do"FORINPUTAS1
220 INPUT#1,CLIENT$
230 LINEINPUT#1,ACT$
235 INPUT#1,BILL$
236 INPUT#1,Q$:IFQ$="DONE"THEN900
240 PRINT@165,"working on - ";CLIENT$
245 PRINT;ACT$
248 PRINT"you started at - ";BILL$
250 INPUT"are you finished ";T$
251 IFT$<>"Y"ANDT$<>"y"THENCLOSE:GOTO10
254 PAY$=TIME$
255 CLOSE:OPEN"time.do"FOROUTPUTAS1
256 PRINT #1,CLIENT$:PRINT#1,ACT$:PRINT#1,BILL$:PRINT#1,"DONE"
257 CLOSE
258 OPEN"log.do"FORAPPENDAS1
260 CLS
265 PRINT@165,"logging ";CLIENT$;" to file."
268 FORX=1TO500:NEXT
272 PRINT#1,DATE$
275 PRINT#1,CLIENT$:PRINT#1,ACT$
280 PRINT#1,"begin":PRINT#1,BILL$
290 PRINT#1,"end":PRINT#1,PAY$
300 HR=VAL(LEFT$(PAY$,2))-VAL(LEFT$(BILL$,2))
310 IFVAL(LEFT$(PAY$,2))<VAL(LEFT$(BILL$,2))THEN HR=(24+VAL(LEFT$(PAY$,2))-VAL(LEFT$(BILL$,2)))
320 MIN=MIN+(60*HR)+(VAL(MID$(PAY$,4,2)))-(VAL(MID$(BILL$,4,2)))
350 PRINT#1,"minutes"
355 PRINT#1,MIN
360 CLS:GOTO10
370 RETURN
500 CLS:CLOSE
510 PRINT@165,"screen display of time"
520 INPUT "what is your billing rate - ";RATE:IF RATE=0 THEN 10
530 INPUT"client name - ";CLIENT$
540 OPEN"log.do"FORINPUTAS1
542 CLS:RATE=RATE/60
544 IFEOF(1)THEN599
546 INPUT#1,NOW$
547 INPUT#1,TEST$:IFEOF(1)THEN599
548 IFINSTR(TEST$,CLIENT$)=0THENFORT=1 TO 7:INPUT#1,JUNK$:NEXT:GOTO544
550 LINEINPUT#1,ACT$
552 INPUT#1,B$
554 INPUT#1,BILL$
556 INPUT#1,P$
558 INPUT#1,PAY$
560 INPUT#1,M$
562 INPUT#1,MIN
564 DUE=RATE*MIN
565 IFMIN=0THENDUE=0
566 CLS
570 PRINT,NOW$
572 PRINT,TEST$
576 PRINTACT$
578 PRINTB$,BILL$
580 PRINTP$,PAY$
582 PRINT"Time = ";MIN;" Amount due = ";:PRINTUSING"$###.##";DUE
590 PRINT:INPUT"    ***  See more? ***";Y$
592 IFY$="Y"ORY$="y"THEN544
594 INPUT"Check another client ";Y$
596 IFY$="Y"ORY$="y"THENCLOSE:GOTO 500
598 GOTO10
599 CLOSE:CLS:GOTO10:RETURN
700 CLS:CLOSE
701 MAXFILES=2
710 PRINT@165,"Welcome to total up"
712 INPUT"Name of Bill File (6 letters) ";WHO$
713 WHO$=WHO$+".do"
714 OPENWHO$FORAPPENDAS2
720 INPUT"what's your billing rate - ";RATE:IFRATE=0THEN10
722 PRINT#2,"THE BILLING RATE IS $";RATE
730 INPUT"client name - ";CLIENT$
740 OPEN"log.do"FORINPUTAS1
742 CLS:RATE=RATE/60
744 IFEOF(1)THEN799
746 INPUT#1,NOW$
747 INPUT#1,TEST$:IFEOF(1)THEN799
748 IFINSTR(TEST$,CLIENT$)=0THENFORX=1TO7:INPUT#1,JUNK$:NEXT:GOTO744
749 T=1
750 LINEINPUT#1,ACT$
752 INPUT#1,B$
754 INPUT#1,BILL$
756 INPUT#1,P$
758 INPUT#1,PAY$
760 INPUT#1,M$
762 INPUT#1,MIN
763 IFMIN=0THENDUE=0
764 DUE=RATE*MIN
765 AMT=AMT+DUE
770 CLS
772 PRINT@165,"*** Logging ";TEST$;" to ";WHO$;" file ***"
778 PRINT#2,NOW$:PRINT#2,TEST$
779 PRINT#2,ACT$:PRINT#2,BILL$:PRINT#2,PAY$
780 PRINT#2,USING"$####.##";DUE
782 GOTO744
799 IFT=0THEN850
800 'total time and fees
815 PRINT#2,"--------------"
816 PRINT#2,USING"$####.##";AMT
818 PRINT#2," "
820 AMT=0:CLOSE
822 INPUT"     *** Do another client";Y$
824 IFY$="Y"ORY$="y"THENT=0:GOTO700
826 GOTO10
830 RETURN
850 CLOSE:CLS:PRINT@320,"no such client"
854 GOTO822
900 PRINT@320,"-no current open file-":FORX=1TO300:NEXT:GOTO10
