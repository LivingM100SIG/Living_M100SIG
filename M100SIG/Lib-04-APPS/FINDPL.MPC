1 'FIND+.MPC 03/09/89 21:24
2 'TMPC 3-in-1 Simple Report Generator
3 'by Jean-Luc Crevier [73720,2456]
4 'See FIND+.DOC for documentation
5 MAXFILES=2:CLEAR500
6 ONERRORGOTO63
7 GOTO34 'main
8 K$=INKEY$
9 IFK$=" "THEN12
10 IFK$=CHR$(27)THENPRINT#2,"Report aborted":CLOSE1,2:BEEP:MENU
11 RETURN
12 IFINKEY$=""THEN12
13 RETURN
14 HB$=INPUT$(1,1):IFHB$=" "THENST=ST+1:GOTO17
15 CC$=INPUT$(1,1):CC=ASC(CC$)-32
16 DC$=INPUT$(CC,1):IFINSTR(DC$,T$)>0 THEN GOSUB29
17 RETURN
18 HB$=INPUT$(6,1)
19 IFHB$="      "THENST=ST+1:GOTO28
20 IFLEN(T$)=3ANDINSTR(HB$,T$)>0THENPRINT#2,DA$;" ";:CC$=INPUT$(1,1):CC=ASC(CC$)-32:DC$=INPUT$(CC,1):GOSUB29:GOTO28
21 CC$=INPUT$(1,1):CC=ASC(CC$)-32
22 DC$=INPUT$(CC,1):IFINSTR(DC$,T$)=0 THEN28
23 D$=STR$(ASC(MID$(HB$,3,1))-32):D$=RIGHT$(D$,LEN(D$)-1):IFLEN(D$)=1THEND$="0"+D$
24 IFVAL(D$)>31 THEN 65
25 M$=STR$(ASC(MID$(HB$,4,1))-32):M$=RIGHT$(M$,LEN(M$)-1):IFLEN(M$)=1THENM$="0"+M$
26 Y$=STR$(ASC(MID$(HB$,5,1))-32+80)
27 GOSUB30:GOSUB29
28 RETURN
29 PRINT#2,DC$:GOSUB8:RETURN
30 PRINT#2,M$;"/";
31 PRINT#2,D$;"/";
32 PRINT#2,RIGHT$(Y$,LEN(Y$)-1);" ";
33 GOSUB8:RETURN
34 CLS:LINEINPUT"Task file: ";FI$:IFFI$=""THENFI$="TASK.DO"
35 PRINT@11,FI$
36 OPENFI$FORINPUTAS1
37 LINEINPUT"Output file: ";FO$:IFFO$=""THENFO$="REPORT.DO"
38 PRINT@53,FO$
39 IFMID$(FO$,4,1)=":"THENOPENFO$FOROUTPUTAS2 ELSE OPENFO$FORAPPENDAS2
40 PRINT"<D>ate or <I>tem ";:DI$=INPUT$(1)
41 IFDI$="D"ORDI$="d"THENGOSUB53:GOTO43
42 PRINT:LINEINPUT"Target string: ";T$
43 CLS:PRINT#2,:PRINT#2,"TMPC Report - ";DAY$;" ";DATE$;" ";TIME$
44 IFEOF(1)THEN65
45 HD$=INPUT$(7,1):IFLEFT$(HD$,4)<>"tMpC"THEN BEEP:PRINTFI$" is not a TMPC task file!":END
46 PRINTCHR$(27);"pScanning To-dos...";CHR$(27);"q"
47 IFEOF(1)THEN65
48 IFST<4THENGOSUB14:GOTO47
49 PRINTCHR$(27);"pScanning Single Date Appointments...";CHR$(27);"q":HB$=HB$+INPUT$(5,1):GOSUB19
50 IFEOF(1)THEN65
51 IFST<6THENGOSUB18:GOTO50
52 GOTO65
53 PRINT:PRINT"Date as MM/DD/YY ";:LINEINPUT"(enter=today) ";DA$:IFDA$=""THENDA$=DATE$
54 IFINSTR(DA$,"/")=0ORLEN(DA$)<>8THENBEEP:GOTO53
55 M$=LEFT$(DA$,2):M=VAL(M$)
56 D$=MID$(DA$,4,2):D=VAL(D$)
57 Y$=RIGHT$(DA$,2):Y=VAL(Y$)
58 IFD<1ORD>31THEN62
59 IFM<1ORM>12THEN62
60 IFY<80ORM>99THEN62
61 T$=CHR$(D+32)+CHR$(M+32)+CHR$(Y-80+32):RETURN
62 BEEP:PRINT"Invalid date for TMPC":GOTO53
63 BEEP:IFERR=52THENPRINTFI$;" not found":RUN
64 PRINT"Program stopped due to error";ERR:PRINT"in line";ERL:END
65 PRINT#2,"NOTE: weekly & monthly repeating items  (if any) are not included in this report":CLOSE1,2:BEEP:END
