10 CLS:PRINT"":PRINT"This program will print a professional"
11 PRINT"looking billing invoice on a STAR "
12 PRINT"printer in the IBM mode.  It will also"
13 PRINT"work on other printers that use the"
14 PRINT"same codes" 
15 PRINT@305,"";:INPUT"Press Enter";T:CLS
16 PRINT"":PRINT"The program prints too slowly to print"
17 PRINT"all of your invoices, for best use of "
18 PRINT"this program, I print one copy and  "
19 PRINT"zerox the rest."
20 PRINT""
22 PRINT"Tom Glovier      70346,1720"
23 PRINT@305,"";:INPUT"Press Enter";T:CLS
50 CALL 16959:MAXFILES=1:DEFSTRA-M,R,V:R=CHR$(27)+"p":V=CHR$(27)+"q":CLOSE:OPEN"LPT:"FOROUTPUTAS1:PRINTR:FORI%=40TO280STEP40:PRINT@I%,SPACE$(40):NEXT
51 PRINT@0,"    Billing Invoice Designer/Printer    ":PRINT@45,"by T F Glovier    70346,1720":LINE(0,17)-(239,17),0
52 PRINT@170,R;"  Company Name  "V:PRINT@207,STRING$(24,32):PRINT@247,STRING$(24,32)
53 LINE(41,40)-(185,59),0,B:LINE(42,41)-(184,57),1,B:LINE(45,56)-(181,56),0:PRINT@248,"";:LINEINPUT"";B(1):IFB(1)=""THEN53
54 FORI%=120TO280STEP40:PRINTR;:PRINT@I%,SPACE$(40):NEXT
55 DATA Address Line 1,Address Line 2,Telephone Number
56 FORX=2TO4:READC
57 PRINT@170,R;C;V:PRINT@201,STRING$(38,32):PRINT@241,STRING$(38,32)
58 LINE(06,40)-(235,59),0,B:LINE(07,41)-(234,57),1,B:LINE(10,56)-(231,56),0:PRINT@242,"";:LINEINPUT"";B(X):IFB(X)=""THEN58
59 FORI%=120TO280STEP40:PRINTR;:PRINT@I%,SPACE$(40):NEXTI%
60 NEXT X
61 PRINT@207,V;STRING$(24,32):PRINT@247,STRING$(24,32)
62 LINE(41,40)-(185,59),0,B:LINE(42,41)-(184,57),1,B:LINE(45,56)-(181,56),0:PRINT@248,"";:PRINT"Printing your  Invoice"
100 PRINT#1,CHR$(27)"x1";CHR$(27)"G";
120 PRINT#1,CHR$(27)"U1"
150 PRINT#1,CHR$(27)"a"CHR$(1);
200 PRINT#1,CHR$(27)"h"CHR$(1);
250 PRINT#1,CHR$(27)"-1";
300 PRINT#1,B(1)
350 PRINT#1,CHR$(27)"-0";
400 PRINT#1,CHR$(27)"h"CHR$(0)
500 PRINT#1,B(2)
600 PRINT#1,B(3)
610 PRINT#1,B(4)
650 PRINT#1,CHR$(27)"a"CHR$(0)
660 PRINT#1,SPACE$(6);"Date ";STRING$(20,196);CHR$(10)
665 PRINT#1,SPACE$(6);"Name ";STRING$(63,196);CHR$(10)
670 PRINT#1,SPACE$(6);"Address ";STRING$(60,196);CHR$(10)
675 PRINT#1,SPACE$(6);"City ";STRING$(23,196);" State ";STRING$(14,196);" ZIP ";STRING$(14,196);CHR$(10)
680 PRINT#1,SPACE$(6);"Job Location ";STRING$(55,196)
700 PRINT#1,SPACE$(5);:PRINT#1,CHR$(201);:PRINT#1,STRING$(68,205);:PRINT#1,CHR$(187)
705 PRINT#1,SPACE$(5);CHR$(186);" QTY.                 DESCRIPTION                    PRICE   AMOUNT ";CHR$(186)
710 PRINT#1,SPACE$(5);CHR$(204);STRING$(6,205);CHR$(209);STRING$(45,205);CHR$(209);STRING$(5,205);CHR$(209);STRING$(9,205);CHR$(185)
750 FORI=1TO15
760 PRINT#1,SPACE$(5);CHR$(186);STRING$(6,32);CHR$(179);STRING$(45,32);CHR$(179);STRING$(5,32);CHR$(179);STRING$(9,32);CHR$(186)
765 PRINT#1,SPACE$(5);CHR$(199);STRING$(6,196);CHR$(197);STRING$(45,196);CHR$(197);STRING$(5,196);CHR$(197);STRING$(9,196);CHR$(182)
770 NEXT
775 PRINT#1,SPACE$(5);CHR$(186);STRING$(6,32);CHR$(179);STRING$(45,32);CHR$(179);STRING$(5,32);CHR$(179);STRING$(9,32);CHR$(186)
780 PRINT#1,SPACE$(5);CHR$(199);STRING$(6,196);CHR$(193);STRING$(45,196);CHR$(193);STRING$(5,196);CHR$(197);STRING$(9,196);CHR$(182)
785 PRINT#1,SPACE$(5);CHR$(186);STRING$(58,32);CHR$(179);STRING$(9,32);CHR$(186)
790 PRINT#1,SPACE$(5);CHR$(186);"  RECEIVED BY:";STRING$(32,32);CHR$(27)"j"CHR$(30);"Total Parts ";CHR$(27)"J"CHR$(30);CHR$(195);STRING$(9,196);CHR$(182)
795 PRINT#1,SPACE$(5);CHR$(186);STRING$(58,32);CHR$(179);STRING$(9,32);CHR$(186)
800 PRINT#1,SPACE$(5);CHR$(186);STRING$(46,32);CHR$(27)"j"CHR$(30);"Total Labor ";CHR$(27)"J"CHR$(30);CHR$(195);STRING$(9,196);CHR$(182)
810 PRINT#1,SPACE$(5);CHR$(186);STRING$(58,32);CHR$(179);STRING$(9,32);CHR$(186)
820 PRINT#1,SPACE$(5);CHR$(186);STRING$(54,32);CHR$(27)"j"CHR$(30);"Tax ";CHR$(27)"J"CHR$(30);CHR$(201);CHR$(8);CHR$(192);STRING$(9,205);CHR$(185)
830 PRINT#1,SPACE$(5);CHR$(186);STRING$(58,32);CHR$(186);STRING$(9,32);CHR$(186)
840 PRINT#1,SPACE$(5);CHR$(186);STRING$(35,32);CHR$(27)"j"CHR$(30);"PLEASE PAY THIS AMOUNT ";CHR$(27)"J"CHR$(30);CHR$(200);STRING$(9,205);CHR$(185)
900 PRINT#1,SPACE$(5);:PRINT#1,CHR$(200);:PRINT#1,STRING$(68,205);:PRINT#1,CHR$(188)
1000 PRINT#1,CHR$(27)"a"CHR$(1);
