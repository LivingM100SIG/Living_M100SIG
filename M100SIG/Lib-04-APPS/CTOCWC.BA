1 'CTOCWC.BA
2 'City TO City Weather Comparitor"
3 'BY: Ron Vanbenthusen 70721-3223"
4 'DATA COMPANION FILE: CITIES.DO
5 'RUN 1000 TO CREATE NEW CITY
10 POKE63038,175
20 SY=55:EM=12:EC=5:BS=5:BM=3
30 OPEN"CITIES"FORINPUTAS#1
40 INPUT#1,NF,NC
50 DIMD(12,2),L$(2),F$(NF)
60 CLS:FORF=1TONF:INPUT#1,F$(F)
70 PRINT@(F*20)+21,USING"#)\                 \";F,F$(F):NEXTF
100 PRINT@283,"CITIES (R,S)";:INPUTR,S
120 R=INT(R):IFR>NF ORR<1THEN700
130 S=INT(S):IFS>NF ORS<1ORR=STHEN700
400 CP=CP+1:R1=0:S1=0:INPUT#1,C$
500 FORF=1TONF
510 IFR=FANDR1=0THENN=1:L$(N)=F$(F):R1=1:GOTO530
515 IFS=FANDS1=0THENN=2:L$(N)=F$(F):S1=1:GOTO530
520 LINEINPUT#1,D1$:NEXTF:GOTO600
530 FORM=1TO12:INPUT#1,D:D(M,N)=D
540 IFCP>1THENPRINT@(80*N)+25+M,"-"
550 IFM=1ANDR1<>S1THENH=D:L=D:'L=0 FOR FULL RANGE
560 IFD>HTHENH=D
570 IFD<LTHENL=D
580 NEXTM:NEXTF
600 CLS:PRINT@145,USING"\             \";C$
602 PRINT@25,USING"--------####.##";H
604 PRINT@265,USING"--------####.##";L
606 PRINT@281,"J F M A M J J A S O N D"
608 PRINT@307,USING"(### OF###)";CP,NC
610 FORN=1TO2
612 IFN=1THENLINE(149,8)-(154,14),1,B
614 IFN=2THENLINE(149,40)-(154,46),1,BF
616 PRINT@(N*160)-94,USING"\            \";L$(N)
620 FORM=1TO12:D=D(M,N)
630 Y=SY-(SY*((D-L)/(H-L)))
640 X=(M*EM)+(N*EC)-(EM+EC)+BM
650 IFN=1THENLINE(X,SY)-(X+BS,Y),1,B
660 IFN=2THENLINE(X,SY)-(X+BS,Y),1,BF
670 PD=(80*N)+33:PRINT@PD,USING"####.##";D
690 NEXTM:PRINT@PD,SPACE$(7):NEXTN
695 IFNC>CPTHEN400
700 PRINT@306,"A)GAIN Q)UIT":BEEP
710 K$=INKEY$:IFK$=""THEN710
720 IFK$="A"ORK$="a"THENRUN
730 IFK$="Q"ORK$="q"THENMENUELSE710
1000 'CREATE NEW CITY
1010 MAXFILES=2
1020 OPEN"CITIES"FORINPUTAS#1
1030 OPEN"TEMP"FOROUTPUTAS#2
1040 M$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"
1100 INPUT#1,NL,NC:NN=NL+1:PRINT#2,NN","NC
1110 FORN=1TONL:INPUT#1,L$:PRINT#2,L$:NEXTN
1120 INPUT"ENTER CITY";L$:PRINT#2,L$
1130 INPUT#1,C$:PRINT#2,C$
1140 IFEOF(1)THENMENU
1200 FORN=1TONL:LINEINPUT#1,D$:PRINT#2,D$:NEXTN
1300 CLS:PRINT@1,L$" - "C$
1400 FORM=1TO12:PM$=MID$(M$,(M*3)-2,3)
1410 PRINT@(M*20)+20,PM$:NEXTM
1500 FORM=1TO12:PRINT@(M*20)+24," ";
1510 INPUTI:I$=STR$(I)
1520 IFM<12THENTI$=TI$+I$+",":NEXTM
1530 TI$=TI$+I$:NEXTM
1540 L=LEN(TI$)
1550 FORS=1TOL:TC$=MID$(TI$,S,1)
1560 IFTC$<>" "THEN1590
1570 DL$=LEFT$(TI$,S-1):DR$=RIGHT$(TI$,L-S)
1580 TI$=DL$+DR$:GOTO1540
1590 NEXTS:PRINT#2,TI$:TI$="":GOTO1130
