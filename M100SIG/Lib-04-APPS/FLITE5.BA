0 REM FLITE5.BA - 10/9/89 - Ver 2.00
10 CLS:CLEAR3000:RV$=CHR$(27)+"p":NV$=CHR$(27)+"q":PRINT@134,RV$;"Initializing";NV$:DIMS$(33),S(33):MN$=CHR$(13)+CHR$(27):ES$="<ESC> to Quit, <ENTER> to Continue":PI=4*ATN(1):DR=PI/180:RD=180/PI
30 SP$="Speed ":DS$="Distance":T$="True":C$="Course ":HD$="Heading"
50 FORZ=1TO2:READS$(Z):NEXT
60 FORZ=1TO2:READS(Z):NEXT
61 GOTO1220
70 RUN"FLIGHT.BA"
1220 CLS:PRINT@14,"Wind & Drift":GOSUB2530
1230 CLS:ONS3GOTO1250,1340
1240 GOTO1220
1250 CLS:PRINT@12,"Drift Correction"
1260 PRINT@80,DS$" Flown ";DF:PRINT@94,"";:INPUTDF:PRINT@95,SPACE$(9):PRINT@95,DF
1270 PRINT@120,DS$" Off "C$" ";;DO:PRINT@139,"";:INPUTDO:PRINT@140,SPACE$(9):PRINT@140,DO
1280 PRINT@160,DS$" to fly ";DL:PRINT@175,"";:INPUTDL:PRINT@176,SPACE$(9):PRINT@176,DL
1290 X=DO/DF:AP=ATN(X/SQR(-X*X+1)):AP=INT(AP*RD+.5)
1300 Y=DO/DL:DC=ATN(Y/SQR(-Y*Y+1)):DC=INT(DC*RD+.5)
1310 AH=AP+DC:PRINT@80,SPACE$(120):PRINT@80,"Angle to parallel";AP:PRINT"Drift Correction angle";DC:PRINT"Angle to intercept";AH:PRINT@283,ES$;
1320 GOSUB2750:ONINSTR(MN$,IK$)GOTO1250,1220
1330 GOTO1320
1340 CLS:PRINT@5,RV$;C$"/ "HD$" / Ground"SP$;NV$:PRINTT$" "C$;CT;TAB(20);T$" Airspeed ";AT
1350 PRINT@51,"";:INPUTCT:PRINT@52,SPACE$(8):PRINT@52,CT:PRINT@73,"";:INPUTAT:PRINT@74,SPACE$(5):PRINT@74,AT
1360 PRINT"Wind Direction ";WD;TAB(20);"Wind "SP$;WS
1370 PRINT@94,"";:INPUTWD:PRINT@95,SPACE$(4):PRINT@95,WD:PRINT@110,"";:INPUTWS:PRINT@111,SPACE$(8):PRINT@111,WS
1380 PRINT"Variat'n -E+W  ";CV:  PRINT@133,"";:  INPUT CV:  PRINT@135,SPACE$(9):  PRINT@135,CV
1390 MC=CT+CV:IFMC>360THENMC=MC-360
1400 IFMC<0THENMC=MC+360
1410 TR=DR*CT:WR=DR*WD:XX=WS*SIN(WR-TR)/AT:TH=TR+ATN(XX/SQR(-XX*XX+1)):TH=RD*TH:IFTH>360THENTH=TH-360
1420 IFTH<0THENTH=TH+360
1430 GS=WS*COS(WR-TR-PI)+SQR((WS*COS(WR-TR-PI))^2-(WS)^2+(AT)^2)
1440 WC=CT-TH:MH=TH+CV:IFMH>360THENMH=MH-360
1450 IFMH<0THENMH=MH+360
1460 MC=INT(MC+.5):MH=INT(MH+.5):TH=INT(TH+.5):GS=INT(GS+.5)
1470 PRINT@160,T$" "HD$" =";TH:PRINT@180,"Mag "C$" =";MC:PRINT"Mag "HD$" =";MH:PRINT@220,"Ground"SP$" =";GS
1480 PRINT@240,RV$;"Deviation ";NV$;DV:PRINT@249,"";:INPUTDV:PRINT@250,SPACE$(9):PRINT@250,DV
1490 CH=MH+DV:PRINT@260,"Comp. "HD$" =";CH:PRINT@283,RV$;ES$;NV$;
1500 GOSUB2750:ONINSTR(MN$,IK$)GOTO1340,1220
1510 GOTO1500
2530 FORZ=1TO2:PRINT@S(Z),S$(Z):NEXTZ:S3=1:PRINT@283,ES$;
2540 PRINT@S(S3),RV$;S$(S3);NV$
2550 GOSUB2750:ONINSTR(MN$,IK$)GOTO2600,70
2560 IK=ASC(IK$)-29:IFIK<0THEN2550ELSEONIKGOTO2580,2590
2570 GOTO2550
2580 PRINT@S(S3),S$(S3):S3=S3-1:IFS3<1THENS3=2:GOTO2540ELSE2540
2590 PRINT@S(S3),S$(S3):S3=S3+1:IFS3>2THENS3=1:GOTO2540ELSE2540
2600 RETURN
2750 IK$=INKEY$:IFIK$=""THEN2750
2760 RETURN
2810 DATA"Drift correction","Course / Heading / Groundspeed"
2870 DATA132,165
