0 'NOTE.PAD (C)1988 Barye Phillips
1 'A little note-taker/organizer that
2 'works like SCHEDL probably should.
3 'TAB moves the arrow down the menu.
4 'NOTE.PAD uses NOTE.DO as a working
5 'file and SAVE.DO as a semi-permanent
6 'archive file. Entries are displayed
7 'to the screen with Find, or sent to
8 'SAVE.DO with Save. Find will print
9 'its output if a printer is on line.
10 'Filing a CODE-S by itself will put
11 'a date/time line in NOTE.DO. Sort
12 'and Empty affect NOTE.DO only, and
13 'Quit allows you to stay in NOTE.DO
14 'when you're finished working. ESC
15 'gets you back to the M100 menu. The
16 'password (P$) can be changed to any
17 'four characters. Don't forget it!
18 '
19 CLS:MAXFILES=2:CLEAR2000:DEFSTRK:P$="pass":F$="NOTE.DO":W$="Wait-":S$=CHR$(154)
20 N=90:DIMN$(N):J=700:Q=40:U=1:M=Q+U:ONERRORGOTO49
21 PRINT:PRINT"  File":PRINT"  Find":PRINT"  Save":PRINT"  Sort":PRINT"  Empty":PRINT"  Quit"
22 GOSUB51:PRINT@(Q*U)+1,S$:PRINT@279-LEN(T$),T$;
23 K=INKEY$:IFK=""THEN22ELSEIFK=CHR$(13)THEN25ELSEIFK=CHR$(27)THENMAXFILES=1:MENU
24 IFK<>CHR$(9)THEN22ELSEPRINT@(Q*U)+1,CHR$(32):U=U+1:IFU=7THENU=1:GOTO22ELSEPRINT@(Q*U)+1,S$:GOTO22
25 CLS:ONUGOSUB31,34,35,53,36,26:CLS:GOTO21
26 PRINT@M,"Lock?":GOSUB47:CLS:IFK="y"THENGOSUB28:RETURN
27 POWEROFF,RESUME:RETURN
28 POWEROFF,RESUME:POKE63056,128:PRINT@M,"Password?":FORI=1TO4
29 K=INKEY$:IFK=""THEN29ELSEIFK<>MID$(P$,I,1)THENGOSUB63:CLS:PRINT@M,"Access is restricted.":GOSUB50:CLS:GOTO28
30 NEXT:POKE63056,0:RETURN
31 PRINT:PRINT" ";:LINEINPUTD$:IFD$=CHR$(169)THEND$=" --"+DAY$+" "+LEFT$(DATE$,5)+" "+LEFT$(TIME$,5)+"--"
32 OPENF$FORAPPENDAS1:PRINT#1,D$:CLOSE:CLS:PRINT@M,"Anything else?":GOSUB47:CLS:IFK="n"THENPRINT@M,"Filed in "F$".":GOSUB46:RETURN
33 GOTO31
34 C$="":PRINT:INPUT" Find what";C$:CLS:GOSUB40:RETURN
35 PRINT:INPUT" Save what";C$:O$="SAVE.DO":OPENF$FORINPUTAS1:OPENO$FORAPPENDAS2:CLS:PRINT@M,W$:GOSUB42:RETURN
36 PRINT@M,"Are you sure?":GOSUB47:IFK<>"y"THENRETURN
37 CLS:PRINT@M,"Replace with SAVE.DO?":GOSUB47:IFK="n"THENGOSUB39ELSEGOSUB52
38 RETURN
39 CLS:KILLF$:OPENF$FOROUTPUTAS1:CLOSE:PRINT@M,F$" is empty.":GOSUB46:RETURN
40 IF(INP(187)AND6)=2THENO$="LPT:"ELSEO$="LCD:"
41 OPENF$FORINPUTAS1:OPENO$FOROUTPUTAS2:PRINT
42 IFEOF(1)THENCLOSE:GOSUB46:RETURN
43 LINEINPUT#1,L$:IFL$=""THEN42
44 IFINSTR(L$,C$)=0THEN42
45 PRINT#2," "+L$;:PRINT#2,:GOTO42
46 GOSUB50:RETURN
47 K=INKEY$:IFK<>"n"ANDK<>"y"THEN47
48 RETURN
49 GOSUB63:CLS:RESUME21
50 FORI=1TOJ:NEXTI:RETURN
51 T$=STR$(FRE(0))+" "+DAY$+" "+LEFT$(DATE$,5)+" "+TIME$:RETURN
52 CLS:KILLF$:NAME"SAVE.DO"ASF$:PRINT@41,"SAVE.DO is now "F$".":GOSUB50:CLS:RETURN
53 C=0:CLS:PRINT@M,W$:OPENF$FORINPUTAS1:FORO=1TON:LINEINPUT#1,N$(O):C=C+1:IFEOF(1)THEN54ELSENEXT
54 X=C
55 X=INT(X/2):IFX=0THEN62
56 Y=1:Z=C-X
57 V=Y
58 W=V+X:IFN$(V)<=N$(W)THEN60
59 H$=N$(V):N$(V)=N$(W):N$(W)=H$:V=V-X:IFV<1THEN60ELSE58
60 Y=Y+1:IFY>ZTHEN55
61 GOTO57
62 CLOSE:OPENF$FOROUTPUTAS1:FORO=1TOC:PRINT#1,N$(O):NEXT:CLOSE:RETURN
63 SOUNDJ,8:RETURN
