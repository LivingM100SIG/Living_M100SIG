10 ONERRORGOTO900
20 CLS
30 PRINT@44, "St. Petersburg Police Department"
40 PRINT@125,"Police Report Writing Program"
50 PRINT@207,"Version 7.2  January 1985":FORI=1TO750:NEXT
100 CALL23164,0,23366:CALL27795
105 CALL16959
110 CLEAR1000:DEFINTA-Z:DEFSTRA,B,C,E,G,H,J,Q,R,S:ONERRORGOTO900
120 OPEN"AGENCY"FORINPUTAS1:INPUT#1,A1,A2,A3,A4,A5:CLOSE#1
130 B1="SUPERVISORS SIGNATURE":B2="ID#":B3="CONCURRENCE (YES) (NO)"
140 BS=B1+SPACE$(19)+B2+SPACE$(14)+B3
150 D$=LEFT$(DATE$,2)+MID$(DATE$,4,2)+RIGHT$(DATE$,2)
200 REM OPTION SCREEN
210 CLS:PRINT@11,"Caption Check List":
220 PRINT" 1-Begin   6-Missng 11-Damage 16-Bicycl"
230 PRINT" 2-Slvbty  7-Entry  12-Docmnt 17-Propty"
240 PRINT" 3-Person  8-Asslt  13-M.O.   18-Vehcle"
250 PRINT" 4-Physcl  9-Injury 14-Scene  ";:CALL17001:PRINT"19-Ending":CALL17006
260 PRINT" 5-Arrest 10-Theft  15-Eviden 20-Splmnt"
270 Y=FRE(Y):PRINT@303,Y;"Bytes free"
280 PRINT@280,"";:INPUT" (ENTER) Option #";M$:CLS:IFM$="Menu"THENMENU
285 M=VAL(M$):IFM<1ORM>20THEN100
290 GOSUB700:CLS
291 IFM<12THENONMGOSUB1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,
292 IFM>11THENM2=M-11:ONM2GOSUB2200,2300,2400,2500,2600,2700,2800,2900,3000
300 REM INPUT SCREEN
310 READL,T,C:DIMQ(T),F(T),P(T),E(T):S=SPACE$(L*80)
315 FORN=1TOT:READQ(N),F(N),P(N),E(N):NEXT
320 IFM=1THENMID$(S,1)=A1:MID$(S,35)="V-7.2"
321 IFM=3THENMID$(S,128)=A4:MID$(S,144)=A5
325 IFM=19THENMID$(S,1)=A2:MID$(S,41)=A3
329 IFM=20THENMID$(S,63)="SUPPLEMENT REPORT":MID$(S,81)=A1
330 FORN=1TOT
340 CALL17001:PRINT@0,S:CALL17006:PRINT@240,Q(N);"?":PRINT@280,STRING$(F(N),95)
350 LINEINPUT"";R:PRINT@240,SPACE$(80)
400 REM EDIT
410 IFR=""ANDN<>TTHENNEXT
415 IFR="-"ANDN<>1THENN=N-1:GOTO340
420 IFR=">"THENN=T:GOTO340
425 IFR="<"THENN=1:GOTO340
427 IFR="="ANDF(N)=3THENR=DAY$
428 IFR="="ANDF(N)=6THENR=D$
430 IFR="Menu"THENGOTO100
435 IFR="Files"THENOPEN"SECURE.DO"FOROUTPUTAS1:PRINT#1,S:MENU
440 IFR="List"THENOPEN"SECURE.DO"FORINPUTAS1:LINEINPUT#1,S:CLOSE#1:GOTO340 
445 IFR=""ANDN=TTHEN600
450 R=LEFT$(R,F(N)):LR=LEN(R):RE="":FORI=1TOLR:R1=MID$(R,I,1)
455 V=ASC(R1):IFV>96ANDV<123THENV=V-32
460 RE=RE+CHR$(V):NEXT:R=RE
470 D=F(N)-LEN(R):R=R+SPACE$(D)
500 REM EDIT
510 E1=LEFT$(E(N),1):E2=MID$(E(N),2)
520 IFE1="*"THENR=R
530 IFE1="+"THENR=R+E2
540 IFE1="-"THENR=E2+R
550 IFE1="@"ANDR="Y"THENR=E2+"YES"
560 IFE1="@"ANDR="N"THENR=E2+"NO "
590 MID$(S,P(N))=R:NEXT
600 REM PRINT FILE
603 CALL17001:PRINT@0,S:CALL17006
605 PRINT@280,"":INPUT"Print to file Y/N";J
606 IFJ="Y"ORJ$="y"THEN610
607 IFJ="N"ORJ$="n"THENPRINT@240,SPACE$(80):GOTO330
609 CLS:GOTO600
610 S(1)=LEFT$(S,79):S(2)=MID$(S,81,79):S(3)=MID$(S,161,79)
620 OPENHFORAPPENDAS1
624 IFM<9THENM$="0"+M$
625 IFM<>1ANDM<>19ANDM<>20THENG=STRING$(25,45):MID$(G,3)="("+M$+")-"+C:PRINT#1,G
629 IFM=19THENPRINT#1,STRING$(79,C)
630 FORI=1TOL
640 IFM=1ANDI=2ORM=20ANDI=3THENPRINT#1,STRING$(79,C)
650 PRINT#1,S(I)
660 IFM=19ANDI=2THEN:PRINT#1,BS:PRINT#1,STRING$(79,C)
690 NEXT:GOTO100
700 'FILE NAME
705 OPEN"FILNAM"FORINPUTAS1:INPUT#1,H:CLOSE#1
710 CLS:FILES:PRINT@297,H
720 PRINT@280,"Enter file name";:INPUT"";H:IFH="Menu"THENMENU
725 IFLEN(H)>6THENBEEP:GOTO700
730 OPEN"FILNAM"FOROUTPUTAS1:PRINT#1,H:CLOSE1
799 RETURN
900 REM ERROR HANDLING
905 IFERR=7THENBEEP:CLS:PRINT@134,"MEMORY FULL":RESUME990
910 IFERR=55THENBEEP:RESUME340
920 IFERR=52ORERR=54THENBEEP:CLS:PRINT@240,"PROBLEM WITH EITHER:"
925 IFERR=52ORERR=54THENPRINT@290,"AGENCY.DO OR FILNAM.DO":RESUME990
940 IFERR=52ANDR="List"THENRESUME340
990 FORI=1TO1500:NEXT:MENU
1100 RESTORE1100:RETURN:DATA 3,18,=:REM OPENING
1110 DATA Report Title,20,41,*,Report Year,2,62,-REPORT # 
1120 DATA Report Number,6,73,--,Location,28,81,-OCCURRED @ 
1130 DATA Crime Code,6,121,-CODE/,Zone,4,133,-ZONE/,Reporting Area,3,143,-CT/
1140 DATA (1) Day Occurred,3,161,-ON ,(1) Date Occurred,6,168,*
1150 DATA (1) Time Occurred,4,175,*,(2) Day Occurred,3,181,-/
1160 DATA (2) Date Occurred,6,186,-/,(2) Time Occurred,4,194,-/
1170 DATA Date Received,6,201,-(R),Time Received,4,211,*
1180 DATA Time Arrived,4,216,-(A),Time Completed,4,224,-(C)
1190 DATA Total Time,4,232,-(T)
1200 RESTORE1200:RETURN:DATA 2,8,SOLVABILTY
1210 DATA Was an arrest made (Y/N),1,1,@ARREST/
1220 DATA Can a suspect be named (Y/N),1,18,@NAMED SUSPECT/
1230 DATA Is there an eye witness (Y/N),1,41,@WITNESSES/
1240 DATA Was a vehicle used (Y/N),1,58,@VEHICLE/
1250 DATA Traceable stolen property (Y/N),1,81,@PROPERTY/
1260 DATA Value of stolen property,4,98,-VALUE/
1270 DATA Was this crime unusual (Y/N),1,121,@UNUSUAL/
1280 DATA Evid. linkable to suspect (Y/N),1,138,@EVIDENCE/
1300 RESTORE1300:RETURN:DATA 3,24,PERSON
1310 DATA Person Status,3,1,+/,Name,30,5,*,Race,1,37,*,Sex,1,38,-/,Age,2,41,*
1320 DATA Date of Birth,6,45,*,Sobriety,5,53,*
1330 DATA Suspect Relationship,14,66,*,Identify Suspect (Y/N),1,59,@ID/
1340 DATA Street #,6,81,*,Directional Prefix,2,88,*,Street Name,20,91,*
1350 DATA Street Type,4,112,*,Direction,2,118,*,Bldg. Apt. Lot #,6,121,*
1360 DATA City,15,128,*,State,2,144,*,Zip Code,5,147,*,Home Phone,7,153,*
1370 DATA Occupation,15,161,*,Business/School,20,178,*,Location,20,201,*
1380 DATA Business Hours,9,222,*,Business Phone,7,233,*
1400 RESTORE1400:RETURN:DATA 3,18,PHYSICAL
1410 DATA Description of,3,1,*,Race,1,5,* ,Sex,1,6,-/,Age,5,9,*
1420 DATA Height,9,15,*,Weight,7,25,*,Color Hair,3,33,*,Color Eyes,3,36,-/
1430 DATA Build,8,41,*,Hair Style,9,50,-HAI/,Facial Hair,16,64,*
1440 DATA Describe Glasses,13,81,-GLASSES/,Complexion,12,103,-SKIN/
1450 DATA General Appearance,12,121,*,Demeanor,10,134,*
1460 DATA Sobriety,5,145,-SOBRIETY/,Clothes,39,161,*,"",39,201,*
1500 RESTORE1500:RETURN:DATA3,9,ARREST
1510 DATA Date of Arrest,6,1,-ARRESTED/
1520 DATA Time of Arrest,4,17,*,Arresting Officer,15,22,-BY/,Location,20,41,-AT 
1530 DATA Charge,32,81,-CHARGE/,Total Bond,6,121,-BOND/
1540 DATA On-view PC Warrant Capias,7,133,-AUTHORITY/
1550 DATA Resistence,28,161,-RESISTENCE/,Force Used,28,201,-FORCE USED/
1600 GOTO200RESTORE1600:RETURN:DATA 3,13,MISSING
1700 RESTORE1700:RETURN:DATA 3,15,ENTRY
1710 DATA (FORCIBLE/NO FORCE/ATTEMPT),8,1,+ ENTRY,What was Entered,10,16,*
1720 DATA Occupied (Y/N),1,27,@OCCUPIED/,DAY NIGHT UNKN,5,41,+ TIME
1730 DATA Intent,19,54,-INTENT/,Point of Entry,15,81,-ENT/
1740 DATAPoint of Exit,15,101,-EXT/,Entry Tool,15,121,*,Used to,20,137,-TO 
1750 DATA Date Last Secured,6,161,-SECURE/,Time Secured,4,175,*
1760 DATA Secured by (Name),15,180,-BY ,Date Discovered,6,201,-DISCOV/
1770 DATA Time Discovered,4,215,*,Discovered by (NAME),15,220,-BY 
1800 RESTORE1800:RETURN:DATA3,8,ASSAULT
1810 DATA Type of Assault,20,1,*,Weapon,15,22,-W/,How Assaulted,20,41,-VIC/
1820 DATA Injury (Y/N),1,70,@INJURY/,Words,33,81,-WORDS/
1830 DATA Words,39,121,*/,Exact Location,20,161,-OCCURRED/
1840 DATA Location Found,20,201,-FOUND/
1900 RESTORE1900:RETURN:DATA3,12,INJURY
1910 DATA Nature of Injury,20,1,*,Location on Body,15,22,-ON/
1920 DATA Result of,29,41,-RESULT OF ,First Aid By,15,81,-AID/
1930 DATA Hospital,19,101,*,Physician,20,121,-DR/,Chart Number,10,146,-CHT/
1940 DATA Admitted (Y/N),1,161,@ADMIT/,Condition,8,171,*
1950 DATA Medical Examiner,15,181,-ME/,Disposition of Body,15,201,*
1960 DATA Next of Kin Notified (Y/N),1,222,@KIN NOTIFIED/
2000 RESTORE2000:RETURN:DATA2,10,THEFT
2010 DATA Stolen Property Type,15,1,-THEFT OF ,Value,5,26,-VAL/
2020 DATA Date Last Seen,6,36,-LAST SEEN ,Time Last Seen,4,53,+ HRS
2030 DATA By (Name),15,62,-BY ,Exact Location,20,81,*
2040 DATA Recovery (Y/N),1,102,@REC/,Disposition,10,110,*
2050 DATA Condition on Recovery,12,121,*,Owner (Name),14,134,-PROPERTY OF 
2100 RESTORE2100:RETURN:DATA 1,3,DAMAGE
2110 DATA Property,29,1,-DAMAGE TO ,Damage,30,41,*,$ Amount,4,72,-AMT/
2200 RESTORE2200:RETURN:DATA3,14,DOCUMENT
2210 DATA Bank drawn on,18,1,*,Bank Address,20,20,*,City,15,41,*
2220 DATA State,2,57,*,Date Accepted,6,60,-ACCEPTED/,Time Accepted,4,75,--
2230 DATA Date of Document,6,81,-DOC DATE/,Document Number,5,97,-DOC#
2240 DATA Amount of Document,8,107,-AMT $,Document accepted for,12,121,-FOR/
2250 DATA Reason not honored,12,139,-REASON/,Made Payable to,20,161,-PAY TO/
2260 DATA Maker of Document,20,201,-MAKER/,Identification Used,8,229,-ID/
2300 RESTORE2300:RETURN:DATA1,2,MODUS OPERANDI
2310 DATA 1 M.O.,39,1,*,2 M.O.,39,41,*
2400 RESTORE2400:RETURN:DATA2,7,SCENE
2410 DATA Type of Premises,15,1,-SCENE/,Lighting,10,23,-LIGHT/
2420 DATA Weather,10,41,-WEATHER/,Number of Offenders,2,61,+ OFFENDERS
2430 DATA Suspect Approach,20,81,-APPROACH/,Path of Flight,20,121,-FLIGHT/
2440 DATA Neighborhood Canvas (Y/N),1,150,@CANVAS/
2500 RESTORE2500:RETURN:DATA1,4,EVIDENCE
2510 DATA Describe,15,1,-EVI/,Processed By,15,21,-BY ,Where From,20,41,-FROM 
2520 DATA Disposition,10,67,-TO 
2600 RESTORE2600:RETURN:DATA3,18,BICYCLE
2610 DATA Bicycle Status,3,1,+/BICYCLE,Size (inches),2,14,*"
2620 DATA Type (BOYS/GIRLS/MENS/LADIES),6,17,*,Brand Name,10,24,*,Model,8,41,*
2630 DATA Number of Gears,2,50,-SP/,Type Brakes,4,56,-BRAKES/,Type Handlebars
2640 DATA 7,68,-BARS/,Color Frame,6,81,-FRAME/,Color Fenders,6,94,-FENDERS/
2650 DATA Color Trim,6,109,-TRIM/,Describe Tires,10,121,-TIRES/
2660 DATA Type Seat,7,138,-SEAT/,Color Seat,6,151,*,Serial Number,20,161,-SER/
2670 DATA Licence Number,10,186,-LIC/,Year of Purchase,2,201,-19,"",34,206,*
2700 RESTORE2700:RETURN:DATA3,12,PROPERTY
2710 DATA Property Status,3,1,+/,Brand Name,10,5,*,Model,10,16,*
2720 DATA Article Type,12,27,*,Serial #,20,41,-SER/,Quantity,2,65,-QTY/
2730 DATA Value,4,72,-VAL/,From,26,81,-FROM,Recovery (Y/N),1,112,@REC/
2740 DATA "",39,121,*,"",39,161,*,"",39,201,*
2800 RESTORE2800:RETURN:DATA3,19,VEHICLE
2810 DATA Vehicle Status,3,1,+/,Vehicle Type,5,5,*,Vehicle Year,2,11,*
2820 DATA Make,10,14,*,Model,8,25,*,Body Style,6,34,*,Color/Color,18,41,*
2830 DATA License Year,2,60,*,License State,2,63,*,License Number,10,66,-LIC/
2840 DATA VIN,20,81,-VIN/,Condition,9,106,-COND/
2850 DATA Doors Locked (Y/N),1,121,@LOCKED/
2860 DATA Ignition Key (Y/N),1,132,@KEY/,Disposition,20,140,*
2870 DATA "",39,161,*,Lein Holder,15,201,-LEIN/
2880 DATA Date of Last Payment,6,222,-PAY/
2890 DATA Contacted (Y/N),1,233,@CON/
2900 RESTORE2900:RETURN:DATA 2,5,=:REM ENDING
2910 DATA Date of Report,6,55,-DATE 
2920 DATA Property/Evidence (Y/N),1,67,@Prop-Evid/
2930 DATA Special Routing,20,81,-ROUTE TO ,Cross Reference #,6,121,-REFER TO 
2940 DATA Follow-up (Y/N),1,147,@FOLLOW-UP/
3000 RESTORE3000:RETURN:DATA 3,10,_:REM SUPPLEMENT
3010 DATA Report Title,20,121,*,Report Year,2,143,-REPORT# 
3020 DATA Report Number,6,153,--,Crime code,6,161,-CODE/,Zone,4,173,-ZONE/
3030 DATA Reporting Area,3,183,-CT/,Date Reported,6,201,-REPORT/,Time,4,215,-@
3040 DATA Date Investigated,6,221,-INVEST/,Time,4,235,-@
3099 END
