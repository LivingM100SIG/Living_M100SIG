0 CLEAR6000:CLS:GOSUB37
1 PRINT"IF YOU LIKE CUESHEET","SEND $20 OR WHATEVER","TO TERRY IRVING
2 PRINT"9618 PARKWOOD DRIVE","BETHESDA MD 20814
3 FORV=1TO2000:NEXTV:CLS
4 PRINT@10,"CUE SHEET CREATOR":PRINT@290,"-CAPS LOCK ON-";
5 PRINT@90,"TITLE";:INPUTA
6 PRINT@122,"CORRESPONDENT";:INPUTB
7 PRINT@162,"CRASHTIME (Y/N)";:INPUTC
8 IFC="Y"THEN63
9 X=X+1:CLS
10 FORT=1TO5
11 IFE(X,T)<>""THENE=E(X,T)
12 IFH(X,T)<>""THENH=H(X,T)
13 PRINT@85,NN(T):PRINT@95,E:PRINT@135,H
14 D=INKEY$:IFD=""THEN14
15 IFD=CHR$(13)THEN25
16 IFD=">"THENU=1:GOTO24
17 IFUTHEN19
18 IFD=CHR$(8)THENE=LEFT$(E,LEN(E)-1):PRINT@95,E+" ":GOTO14
19 IFD=CHR$(8)THENH=LEFT$(H,LEN(H)-1):PRINT@135,H+" ":GOTO14
20 IFUTHEN23
21 E=E+D:IFLEN(E)>S(T)THENBEEP:PRINT@280,"TOO LONG--USE > FOR SECOND LINE";
22 GOTO24
23 H=H+D
24 D="":GOTO13
25 E(X,T)=E:E="":CLS:H(X,T)=H:H="":U=0
26 IFZTHEN28
27 NEXTT
28 CLS:PRINT"(1)TIME   (2)CUE ":PRINTE(X,1);:PRINT@50,E(X,2):PRINT@90,H(X,2)
29 PRINT"(3)VIDEO      (4)AUDIO     (5)EFFECT":PRINT@160,E(X,3):PRINT@200,H(X,3)
30 PRINT@175,E(X,4):PRINT@215,H(X,4):PRINT@188,E(X,5):PRINT@228,H(X,5)
31 Z=0:PRINT@280,"(6)END OR ITEM # TO CHANGE";:INPUTZ:IFZ>6THEN31
32 IFZ=6THEN40
33 IFZTHENT=Z:CLS:GOTO11
34 IFSTHENX=S:S=0
35 IFO$="E"THENX=X+1:GOTO28
36 GOTO9
37 DEFSTRA-N:DEFINTT-X:DIME(30,10):DIMNN(5):DIMS(5):DIMH(30,10)
38 NN(1)="TIME":S(1)=4:NN(2)="CUE":S(2)=20:NN(3)="VIDEO":S(3)=10
39 NN(4)="AUDIO":S(4)=10:NN(5)="EFFECT":S(5)=15:RETURN
40 IFSTHENX=S:S=0
41 CLS:INPUT"1-PRINTER,2-SCREEN,3-MEMORY";V:ONVGOSUB42,43,44:GOTO45
42 OPEN"LPT:"FOROUTPUTAS1:RETURN
43 OPEN"LCD:"FOROUTPUTAS1:RETURN
44 OPEN"RAM:CUE"FOROUTPUTAS1:RETURN
45 PRINT#1,A:PRINT#1,B:PRINT#1,DATE$:PRINT#1,"
46 PRINT#1,NN(1)+SPACE$(4)+NN(2)+SPACE$(25)+NN(3)+SPACE$(10)+NN(4)+SPACE$(10)+NN(5)
47 PRINT#1,STRING$(75,95)
48 FORT=1TO30:IFE(T,1)=""THEN50
49 NEXTT
50 X=T
51 FORT=1TOX
52 PRINT#1,E(T,1)+SPACE$(8-LEN(E(T,1)))+E(T,2)+SPACE$(28-LEN(E(T,2)));
53 PRINT#1,E(T,3)+SPACE$(15-LEN(E(T,3)));
54 PRINT#1,E(T,4)+SPACE$(15-LEN(E(T,4)))+E(T,5)
55 PRINT#1,H(T,1)+SPACE$(8-LEN(H(T,1)))+H(T,2)+SPACE$(28-LEN(H(T,2)));
56 PRINT#1,H(T,3)+SPACE$(15-LEN(H(T,3)));
57 PRINT#1,H(T,4)+SPACE$(15-LEN(H(T,4)))+H(T,5)
58 PRINT#1,"
59 NEXTT
60 CLOSE:CLS:PRINT:INPUT"END";CC:IFCC="Y"THENMENU
61 S=0:INPUT"CHANGE LINE";DD:IFDD="Y"THENINPUT"LINE #";UU
62 S=X:X=UU:GOTO28
63 CLS:P=0:X=1:PRINT"HIT ANY KEY TO START","ANY KEY TO MARK A TIME","(E) TO END
64 O$=INKEY$:IFO$=""THEN64
65 IFP=0THENTIME$="00:00:00":P=1
66 E(X,1)=RIGHT$(TIME$,5):PRINTE(X,1):X=X+1:IFO$="E"THEN67ELSEGOTO64
67 X=1:GOTO28
