1 'M0NEY CONVERSION PROGRAM BUY OR SELL IN FOREIGN CURRENCYS THIS PROGRAM IS 611 BYTES WITH REMS REMOVED
2 ' BY JERRY KOHL[72356,23]
3 REM IN ADRS FILE PUT $$ FOLLOWED BY CURRENCY TYPE FOLLOWED BY : THEN VALUE OF CURRENCY
4 REM CAN PUT MULTIPLE CURRENCY TYPES ON DIFFERANT LINES
5 REM EXAMPLES  $$MEXICAN PESO :$160
6 REM           $$HONG KONG DOLLARS :$7.90
7 REM WILL GIVE ABILITY TO CONVERT TO DOLLARS TO FOREIGN CURRENCY OR FROM CURRECNY TO $
9 'REMOVE ALL LINES PRIOR TO LINE 10
10 CLEAR1000:MAXFILES=1
2000 CLS:PRINT@11,"MONEY CONVERSION":OPEN"ADRS"FORINPUTAS1
2030 INPUT#1,A$:IFINSTR(1,A$,"$$")THEN2061
2040 IFEOF(1)THEN2080ELSEGOTO2030
2061 G=G+1:D=INSTR(A$,":"):A(G)=VAL(MID$(A$,D+1)):PRINTG".";MID$(A$,2,D-2):PRINT@G*40+20,USING"####.##";A(G):GOTO2040
2080 CLOSE#1
2081 PRINT@280,"FRM$ TO$";:PRINT@315,"Menu";:KEYON:ONKEYGOSUB2100,2200,,,,,,2800
2085 AA$=INKEY$:GOTO2085
2090 LINEINPUT" ";B$:B=VAL(B$):RETURN
2100 PRINT@240,SPACE$(39);: PRINT@240,"HOW MANY DOLLARS TO CONVERT ? ";:GOSUB2090:IFB<.001THEN2100
2110 PRINT@31,"FROM US $";:FORI=1TOG:PRINT@I*40+27,USING"$$######.##";A(I)*B;:NEXT:GOTO2081
2200 PRINT@240,SPACE$(39);:PRINT@240,"HOW MANY FOREIGN $ TO COMPUTE ? ";:GOSUB2090:IFB<.001GOTO2200
2210 PRINT@31,"  TO US $";:FORI=1TOG:PRINT@I*40+27,USING"$$######.##";B/A(I);:NEXT:GOTO2081
2800 MENU

