1 DEFINTD,G,M,Y:DEFSNGW:GOTO200
50 Q=INT(JD)+68569:X=INT(4*Q/146097):Q=Q-INT((146097*X+3)/4)'JULIAN DAY TO MM/DD
60 X=INT(4000*(Q+1)/1461001):Q=Q-INT(1461*X/4)+31:X=INT(80*Q/2447)
70 D=Q-INT(2447*X/80):Q=INT(X/11):M=X+2-12*Q
75 IFPEEK(1)=51THENZ=23273ELSEZ=28306'Check which Model
80 FORJ=0TO2:PRINTCHR$(PEEK(Z+3*(M-1)+J));:NEXT:IFM<>5THENPRINT".";ELSEPRINT" ";'Print month from ROM
90 PRINTD:RETURN
100 Q=M+1:IFM<3THENQ=Q+12'CALENDAR TO JULIAN DAY
105 YY=Y:IFM<3THENYY=YY-1
110 JD=INT(30.6*Q)+INT(365.25*YY)+D-621049:IFJD>146097THENJD=JD-1
130 IFJD<=73047THENJD=JD+1:IFJD<=36522THENJD=JD+1
140 JD=JD+2342031:RETURN
180 W=JD-2415048:W=W-7*INT(W/7):RETURN'DAY OF WEEK
200 INPUT"Year <CR for current>";Y
205 IFY=0THENY=VAL(RIGHT$(DATE$,2))+1900ELSEIFY<1752ORY>2499THENERROR6
210 GN=YMOD19:FORI=0TOGN:READD:NEXT
225 DATA14,3,23,11,31,18,8,28,16,5,25,13,2,22,10,30,17,7,27
230 M=3:IFD<20THENM=4
500 GOSUB100:GOSUB180:ED=JD+7-W:JD=ED
600 PRINT,,"Easter Day in";Y;" is ";:GOSUB50
700 PRINT:RESTORE1000:FORI=1TO7:READG,D$:JD=ED+G:PRINTD$;" ";:PRINTTAB(23);:GOSUB50:IFI=1THENSE=JD
750 IFI=6THENTS=JD
780 NEXT
800 M=1:D=6:GOSUB100:IN=SE-JD:NS=IN\7:IFINMOD7=0THENIN=IN-1
815 PRINT"Sundays after Epiphany";NS
900 M=11:D=30:GOSUB100:GOSUB180:IFW<4THENAS=JD-WELSEAS=JD+(7-W)
950 JD=AS:PRINT"Advent Sunday";Y;"is  ";:GOSUB50:PRINT"Sundays after Trinity";(JD-TS)\7-1
1000 DATA -63,Septuagesima,-46,Ash Wednesday,-2,Good Friday,39,Ascension Day,49,Pentecost,56,Trinity Sunday,60,Corpus Christi

