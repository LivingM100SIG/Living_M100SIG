0 GOTO3003
15 '<print rec status>
18 PRINT@303,E$+"pRec:";:PRINTUSING"####";RN;:PRINT" of ";:PRINTUSING"####";NR;:PRINTE$+"q";:CALL16949:RETURN
21 '<varptr>
24 'to call:set LL=legnth,pt=mem pos
27 X$="":X=VARPTR(X$):A=INT(PT/256):B=PT-A*256:POKEX,LL:POKEX+1,B:POKEX+2,A:RETURN
210 '<convert to upper case>
213 FORI=1TOLEN(RE$):C=ASC(MID$(RE$,I,1)):IFC>96ANDC<123THENMID$(RE$,I,1)=CHR$(C-32):NEXTELSENEXT
216 RETURN
537 '<use>
540 'NR=#rec,SZ=rec size,NF=#fields,F$()=field data,RC$=records,FP()=field pos in rec,FL()=field lengths
543 CLOSE1:FN$=MID$(RE$,5):IFINSTR(1,FN$,".")THENFN$=LEFT$(FN$,INSTR(1,FN$,".")-1)
546 FN$=FN$+SPACE$(6):FN$=LEFT$(FN$,6):OPENFN$FORINPUTAS1:I=1
549 LINEINPUT#1,F$(I):IFF$(I)=CHR$(255)THENF$(I)="":NF=I-1:GOTO552ELSEI=I+1:GOTO549
552 FORI=1TONF:FL(I)=VAL(MID$(F$(I),17,2)):NEXT:FP(1)=1:FORI=2TONF:FP(I)=FP(I-1)+FL(I-1):NEXT:SZ=0:FORI=1TONF:SZ=SZ+VAL(MID$(F$(I),17,2)):NEXT
555 PT=63919:LL=220:GOSUB27:FORI=1TO220STEP11:CK$=MID$(X$,I,11):IFMID$(CK$,4,6)=FN$ANDASC(LEFT$(CK$,1))=194THENRP=ASC(MID$(CK$,2,1))+256*ASC(MID$(CK$,3,1)):I=220
558 NEXT
561 CLOSE1:OPENFN$FORINPUTAS1:OF=0
564 IFEOF(1)THEN570
567 RC$=INPUT$(1,1):OF=OF+1:IFRC$<>CHR$(255)THEN564ELSEOF=OF+2
570 CLOSE1:OPENFN$FORINPUTAS1:NR=0
573 IFEOF(1)THENCLOSE1:GOTO585ELSELINEINPUT#1,RC$
576 IFRC$<>CHR$(255)THEN573
579 IFEOF(1)THENCLOSE1:GOTO585
582 LINEINPUT#1,RC$:NR=NR+1:PRINTE$"Q";:CALL16954:GOSUB18:GOTO579
585 CALL16954:PRINT@293,E$"p"FN$E$"q";:GOSUB18:IFNRTHENRN=1ELSERN=0
588 RETURN
629 '<diplay field list>
630 CLS:J=0:K=0:FORI=1TOMX:IFI=6THENJ=0:K=13ELSEIFI=11THENJ=0:K=26
633 PRINT@K+J,USING"##";I;:PRINTLEFT$(F$(I),10):J=J+40:NEXT:PRINT:LINE(0,40)-(239,40),1:RETURN
2999 '<sort>
3003 SCREEN0:CLS:CALL17034:PRINTCHR$(27)"Q"CHR$(27)"q":LINE(1,3)-(238,35),1,B:PRINT@54,"DATAbee SORT":PRINT@97,"9/8/88":PRINT@123,"Copyright 1988 by Howard H. Sprow
3006 PRINT@280,CHR$(27)"pCommand Line|         |                ";:CALL16949
3009 CLEAR1000:MAXFILES=2:MX=15:DIMF$(MX),FT$(MX),F(MX),FP(MX),FL(MX),CM$(10):R1$=CHR$(13):Q$=CHR$(34):E$=CHR$(27):ONERRORGOTO3252
3012 CALL16954:GOSUB18:PRINT@240,E$"qUSE <file>(ENT=Menu):";:LINEINPUT"";RE$:IFRE$=""THEN3027ELSEGOSUB213:IFRE$="MENU"THEN3027ELSERE$="USE "+RE$:GOSUB543:RN=1:CALL16954:GOSUB18:DIMST$(NR)
3015 PRINT:LINEINPUT"Output device:file(not RAM)?";RE$:IFRE$=""THEN3027ELSEGOSUB213:DR$=RE$
3018 IFINSTR(1,DR$,":")=0ORLEFT$(DR$,3)="RAM"THENPRINT"Invalid device.":GOTO3015
3021 OPENDR$FOROUTPUTAS2:GOTO3054
3024 '<quit>
3027 MAXFILES=1:MENU
3030 '<invalid>
3033 BEEP:PRINT"Invalid input.":FORT=1TO1000:NEXT:RUN
3051 '<select sort field>
3054 GOSUB630:PRINT@240,"";:INPUT"Sort field #";SN:IFSN<1ORSN>NFTHEN3033
3057 'set var pointers
3060 FORI=1TONR:PT=RP+OF+(SZ+2)*(I-1):LL=SZ:ST$(I)="":X=VARPTR(ST$(I)):A=INT(PT/256):B=PT-A*256:POKEX,LL:POKEX+1,B:POKEX+2,A:NEXT
3063 'field type
3066 TY$=MID$(F$(SN),12,1):PRINT"Sorting...":IFTY$="C"THEN3078
3069 IFTY$="N"THEN3108
3072 IFTY$="D"THEN3138
3075 'sort strings
3078 S1=NR
3081 S1=INT(S1/2):IFS1=0THEN3168
3084 J=1:K=NR-S1
3087 I=J
3090 L=I+S1:IFMID$(ST$(I),FP(SN),FL(SN))<=MID$(ST$(L),FP(SN),FL(SN))THEN3099
3093 H$=ST$(I):ST$(I)=ST$(L):ST$(L)=H$:I=I-S1:IFI<1THEN3099
3096 GOTO3090
3099 J=J+1:IFJ>KTHEN3081
3102 GOTO3087
3105 'sort values
3108 S1=NR
3111 S1=INT(S1/2):IFS1=0THEN3168
3114 J=1:K=NR-S1
3117 I=J
3120 L=I+S1:IFVAL(MID$(ST$(I),FP(SN),FL(SN)))<=VAL(MID$(ST$(L),FP(SN),FL(SN)))THEN3129
3123 H$=ST$(I):ST$(I)=ST$(L):ST$(L)=H$:I=I-S1:IFI<1THEN3129
3126 GOTO3120
3129 J=J+1:IFJ>KTHEN3111
3132 GOTO3117
3135 'sort dates
3138 S1=NR
3141 S1=INT(S1/2):IFS1=0THEN3168
3144 J=1:K=NR-S1
3147 I=J
3150 L=I+S1:D1$=MID$(ST$(I),FP(SN),FL(SN)):GOSUB3192:CI$=D2$:D1$=MID$(ST$(L),FP(SN),FL(SN)):GOSUB3192:CL$=D2$:IFCI$<=CL$THEN3159
3153 H$=ST$(I):ST$(I)=ST$(L):ST$(L)=H$:I=I-S1:IFI<1THEN3159
3156 GOTO3150
3159 J=J+1:IFJ>KTHEN3141
3162 GOTO3147
3165 '<output sorted file>
3168 OPENFN$FORINPUTAS1:PRINT"Writing sorted file...
3171 LINEINPUT#1,LL$:PRINT#2,LL$:IFLL$<>CHR$(255)THEN3171
3174 CLOSE1:FORI=1TONR:PRINT#2,ST$(I):NEXT:GOTO3027
3186 'D1$=date passed to sub,D2$=date reformatted
3189 '<format date for sort as string>
3192 D2$=RIGHT$(D1$,4)+"/"+LEFT$(D1$,5):RETURN
3249 '<errors>
3252 PRINT:PRINT"Error"ERR"Line"ERL:FORT=1TO1000:NEXT:RUN
