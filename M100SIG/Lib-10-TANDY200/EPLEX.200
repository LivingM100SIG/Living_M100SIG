1 REM                                   EASYPLEX.200        1/19/86
2 CLS:MAXFILES=4:CLEAR600:DEFSTRA-L:DEFINTM-Z:                          V$="2.0"
4 CR=CHR$(13)+CHR$(10):C2=CR+CR:GOTO 18
6 G="":V=1
8 B=INPUT$(1,1):PRINTB;:G=G+B
10 IFINSTR(G,D)THEN16
12 IFINSTR(G,D1)THENV=2:GOTO16
14 IFB=CHR$(13)THEN6ELSE8
16 D1="xyx":RETURN
18 DIMA(25):J=STRING$(4,10):K=CHR$(10)+"OK"+CR:D1="xyx"
20 H="CIS TELEPHONE #C"
22 H1="TYMNET NUMBER T":PRINT@255,H1;
24 NA$="Your Name Here":ID="CIS ID NUMBER"
26 PW$="PASS*WORD":SM=0:RM=0:KC%=0
28 PRINT@48,"CompuServe Easyplex"+V$+"200":PRINT@133,"< >-";H
30 PRINT@169,"(or dial & press <M>)";:PRINT@134,"";
32 F=INPUT$(1):CLS:IFINSTR("Mm",F)THENH=H1:GOTO38ELSEIFF="E"THENEDIT20-22
34 L=F+"="+H+"<>"
35 M=VARPTR(L):Q!=PEEK(M+1)+(PEEK(M+2)*256)
36 CLS:CALL25040:CALL25131,0,Q!
38 OPEN"MDM:7I1ENN"FORINPUTAS1:OPEN"MDM:7I1ENN"FOROUTPUTAS2
40 IFINSTR(H,"T")THENPRINT#2,"A":D="log in:":GOSUB6:PRINT#2,"CIS03":GOTO44
42 PRINT#2,CHR$(3)
44 D=":":D1="Host":GOSUB6:ONVGOTO48,46
46 PRINT#2,"CIS":GOTO44
48 PRINT#2,ID:D="word":GOSUB6
50 PRINT#2,PW$
52 D="OK":D1="!":GOSUB6:ONVGOTO56,54
54 PRINT#2,"PRO":GOTO52
56 ONERRORGOTO146
58 OPEN"MAIL.DO"FORINPUTAS3
60 Y=1:N=1:U=0:A(0)="*":IFEOF(3)THENPRINT"MAIL.DO error":GOTO150
62 LINEINPUT#3,G:IFG="Sent"THEN108ELSEQ=INSTR(G,":")
64 IFQTHENG=MID$(G,Q+1)ELSE62
66 FORX=1TOLEN(G):IFMID$(G,X,1)="/"THENY=Y+1:N=0:GOTO70
68 A(Y)=A(Y)+MID$(G,X,1)
70 NEXT:PRINT
74 AD="":FORX=1TOY:PRINTA(X)" = ";:IFINSTR(A(1),"*")THENAD=A(X):N=0
76 IFVAL(A(X))THEN82ELSEOPEN"ADRS.DO"FORINPUTAS4
78 IFEOF(4)THENPRINT" not in ADRS.DO":GOTO150
79 LINEINPUT#4,G:IFLEN(AD)ANDG="end"THENA(X)="":GOTO84
80 IFINSTR(G,A(X))THENA(X)=MID$(G,INSTR(G,"[")+1):PRINTA(X)ELSE78
81 IFLEN(AD)THENX=X+1:A(X)=AD:GOTO78
82 CLOSE4:NEXT
84 CLOSE4:Q=0:U=0:J3="":IF(X-1)>1THENJ3="  (Group EMail)"
85 LINEINPUT#3,I1:Q=INSTR(I1,":")
86 IFQANDQ<10THENJS=MID$(I1,Q+1)ELSEU=1:JS="Auto EMail"
88 PRINT#2,"R SNDMAL":D="file":GOSUB6
90 PRINT#2,"tty:":D="finished.":GOSUB6
92 GOSUB160
94 PRINT#2,C2,CHR$(26);
96 D="ID":GOSUB6:PRINT#2,A(N):A(N)=""
98 D="max)":GOSUB6:PRINT#2,JS
100 GOSUB6:PRINT#2,NA$:X=1
102 D="ID":D1="OK":GOSUB6:ONVGOTO104,106
104 PRINT#2,A(X):A(X)="":X=X+1:GOTO102
106 IFC=CHR$(130)THEN60
108 CLOSE3:IFSM=0THENOPEN"MAIL.DO"FOROUTPUTAS3:PRINT#3,"Sent"+CR:CLOSE3
110 PRINT#2,"r emadmp"
112 D="]":D1="EMDNEW":GOSUB6:ONVGOTO114,152
114 OPEN"README.DO"FORAPPENDAS3:G=MID$(G,2)
116 PRINT#3,"Rec'd: ";DATE$;" - ";TIME$;CR;G;
118 C=INPUT$(1,1)
120 E=RIGHT$(E,4)+C
122 IFRIGHT$(E,4)=JTHENPRINT#3,CR;CHR$(128):PRINTCR;CHR$(128):C="":E="":SOUND 1000,50:GOTO118
124 IFE=KTHENPRINT#3,CR;CHR$(128);CR:GOTO138
128 IFASC(RIGHT$(C,1))=9THEN131
130 IFASC(RIGHT$(C,1))<32THEN118
131 IFE=C2+CTHENC=E:GOTO136
132 IFKC%THENIFMID$(E,3,2)=CRTHENC=CR+C:GOTO136
134 IFMID$(E,3,2)=CRTHENIFVAL(C)THENC=CR+CELSEC=" "+C
136 PRINTC;:PRINT#3,C;:GOTO118
138 IFRMTHEN152
140 PRINT#2,"pro (4)email.dat":D="OK":D1="? What":GOSUB6:ONVGOTO142,140
142 PRINT#2,"del email.dat":GOSUB6
144 GOTO152
146 IFERR=52THENSM=1:RESUME110
148 PRINT"Error ";ERR;" -line ";ERL
150 ER%=1:SM=1
152 PRINT#2,"OFF":IFER%THENSTOP
154 LINEINPUT#1,G:PRINTG:IFINSTR(G,"time")THEN156ELSE154
156 CLOSE:CALL25018:IFSM=0THENKILL"MAIL.DO"
158 MENU
160 GOSUB 174
162 IFEOF(3)THENC="":RETURN
164 C=INPUT$(1,3):IFC=CHR$(13)THENZ=0ELSEZ=Z+1
166 IFC=CHR$(130)THENRETURN
168 PRINT#2,C;:PRINTINPUT$(1,1);
169 IF(Z>65ANDC=" ")THENZ=0:PRINT#2,CR;:PRINTINPUT$(2,1);
170 GOTO162
174 I="EMA"+V$+C2+"From: "+NA$+J3+C2+"Subj: "+JS+C2:GOSUB180
176 IFU=1THENI=I1:GOSUB180
178 RETURN
180 FORQ=1TOLEN(I):PRINT#2,MID$(I,Q,1);:PRINTINPUT$(1,1);
182 NEXT:RETURN
500 CALL 25483
502 'This is Ed Juge's EMAIL5 program, as modified for the Tandy 200 by Bill
504 'Walters and Michael Alonzo.  EMAIL5.DOC IS A MUST ITEM to download
506 'in order to use the program effectively.  Once downloaded, keep it
508 'handy for reference.  I make no claims that the program is my idea, or
510 'that it is original, or anything else, all I know is that it works for
512 'me.  I will try to answer any questions that I can, but I am NOT an
514 'expert, so don't expect miracles.
516 'You can make this program run in an unattended mode as well.  It looks
518 'odd but it does work.  First, change line 32 to read as shown.
520 '32 F=" ":CLS:IFINSTR("Mm",F)THENH=H1:GOTO38ELSEIFF="E"THENEDIT20-22

522 'Then change line 158 to read as shown below.
524 '158 POWER OFF
526 'Load your messages.  Then, using the "POWER" command shown on page #49
528 'of the Basic handbook (that came with the machine) instruct your 200 as
530 'to when you want the program run.  It will wake up, run the program, and
532 'then shut itself off.
534 'Best of luck!  Michael
