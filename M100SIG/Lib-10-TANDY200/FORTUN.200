10 DEFINTA-O,Q-Z
20 CLS:X=113:Y=60:X1=130:Y1=60
30 FORZ=1TO20
40 LINE(X,Y)-(X1,Y1),1,B
50 IFZ<16THENLINE(X,Y)-(X1,Y1),0,B
60 X=X-1:Y=Y-1:X1=X1+1:Y1=Y1+1
70 NEXT
80 PRINT@258,"WHEEL":PRINT@299,"of":PRINT@337,"FORTUNE":GOSUB760
90 CLS: PRINT@169,"How Many Players ";:N$=INPUT$(1):IFVAL(N$)>3ORVAL(N$)<1THEN90ELSEN=VAL(N$)
100 CLS:PRINT"Secret Phrase":LINEINPUTA$:IFLEN(A$)>39ORLEN(A$)<1THEN100
110 IFA$="END"ORA$="end"THENMENUELSEA$=" "+A$
120 L=LEN(A$):H=1:P(1)=0:P(2)=0:P(3)=0:B$=SPACE$(L):D$=SPACE$(40):B=0:L1=INT((40-L)/2)
130 FORZ=1TOL:T$=MID$(A$,Z,1):IFT$>="a"ANDT$<="z"THENMID$(A$,Z,1)=CHR$(ASC(T$)-32)
140 NEXT
150 CLS:AB$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
160 FORZ=1TOL:IFMID$(A$,Z,1)<>" "THENMID$(B$,Z,1)=CHR$(157)
170 IFMID$(A$,Z,1)="'"THENMID$(B$,Z,1)="'"
180 IFMID$(A$,Z,1)="-"THENMID$(B$,Z,1)="-"
190 NEXT
200 FORZ=1TOL:T$=MID$(A$,Z,1):IFINSTR("AEIOU '-",T$)>0THENB=B+1
210 NEXT:B=L-B:CLS
220 FORZ=1TON:PRINT@150+13*Z,"PLAYER"Z:NEXT
230 PRINT@80+L1,B$
240 PRINT@526,"Unused >"AB$:PRINT@120+L1,CHR$(148)
250 FORZ=1TON:PRINT@190+13*Z,USING"######";P(Z):NEXT
260 PRINT@149+13*H,CHR$(27);"p";" PLAYER";H;CHR$(27);"q"
270 PRINT@600,CHR$(27)"p""Spin";:PRINT@605,"Vowel";:PRINT@611,"Guess"CHR$(27)"q";
280 IFB$=A$THEN730
290 IFB<>0THEN310
300 PRINT@370,"ONLY VOWELS ARE LEFT":FORZ=1TO8:SOUND4E3,8:NEXT:GOSUB770:PRINT@371,"Buy Vowel or Guess":KEY(2)ON:KEY(3)ON:GOTO320
310 KEYON
320 ONKEYGOSUB340,520,650
330 GOTO320
340 PRINT@373,"SPINNING":T=VAL(RIGHT$(TIME$,2)):FORZ=1TOT:R=RND(1):NEXTZ
350 FORZ=1TO18:FORY=112TO113:V=(RND(1)*35+1):PRINTCHR$(27)CHR$(Y):PRINT@383,USING"####";V*50:SOUND10,1:NEXTY,Z
360 IFV=21THENGOSUB770:PRINT@373,"LOSE YOUR TURN":SOUND8E3,50:GOSUB770:GOSUB780:GOTO230
370 IFV=19THENGOSUB770:PRINT@376,"BANKRUPT":P(H)=0:SOUND8E3,50:GOSUB770:GOSUB780:GOTO230
380 IFV=29THENV=2E3ELSEV=V*50
390 GOSUB770:PRINT@365,"Value >"V:PRINT@379,"Select Letter":I$=INKEY$
400 I$=INKEY$:IFI$=""THEN400
410 IFI$>="a"ANDI$<="z"THENI$=CHR$(ASC(I$)-32)
420 IFINSTR("AEIOU",I$)>0ORASC(I$)<65ORASC(I$)>90THEN400
430 IFMID$(AB$,ASC(I$)-64,1)=" "THENGOSUB770:PRINT@373,"ALREADY CHOSEN":GOSUB760:GOSUB780:GOTO230
440 FORZ=1TOL:PRINT@119+Z+L1,CHR$(148)
450 IFMID$(A$,Z,1)=I$THENP(H)=P(H)+V:MID$(B$,Z,1)=I$:B=B-1:D=1:SOUND1E4,10
460 PRINT@80+L1,B$:PRINT@119+Z+L1," "
470 NEXTZ:MID$(AB$,ASC(I$)-64,1)=" ":GOSUB790:GOSUB770
480 IFD=1THEND=0:GOTO230
490 GOSUB770:PRINT@374,CHR$(27)"p";" ";I$;" ";CHR$(27)"q";" Not Found":D=0:GOSUB760:GOSUB780
500 IFN=1THENP(H)=P(H)-V/2:IFP(H)<0THENP(H)=0
510 GOTO230
520 IFP(H)<250THENGOSUB 770:PRINT@371,"NEED $250 MINIMUM":GOSUB760:GOTO270
530 GOSUB770:PRINT@374,"Select Vowel":I$=INKEY$
540 I$=INKEY$:IFI$=""THEN540
550 IFI$>="a"ANDI$<="z"THENI$=CHR$(ASC(I$)-32)
560 IFINSTR("AEIOU",I$)<1THEN540
570 IFMID$(AB$,ASC(I$)-64,1)=" "THENPRINT@373,"ALREADY CHOSEN":P(H)=P(H)-250:GOSUB760:GOSUB780:GOTO230
580 MID$(AB$,ASC(I$)-64,1)=" ":P(H)=P(H)-250
590 FORZ=1TOL:PRINT@119+Z+L1,CHR$(148)
600 IFMID$(A$,Z,1)=I$THENMID$(B$,Z,1)=I$:D=1:SOUND1E4,10
610 PRINT@80+L1,B$:PRINT@119+Z+L1," "
620 NEXTZ:GOSUB790:GOSUB770
630 IFD=1THEND=0:GOTO230
640 GOSUB770:PRINT@374,CHR$(27)"p";" ";I$;" ";CHR$(27)"q";" Not Found":D=0:GOSUB760:GOSUB780:GOTO220
650 GOSUB770:IFB$=A$THENC$=A$:GOTO 400ELSEPRINT@366,"Enter Guess   Type Carefully"
660 C$=B$:FORZ=1TOL
670 IFMID$(C$,Z,1)<>CHR$(157)THENPRINT@39+Z+L1,MID$(C$,Z,1):GOTO710
680 PRINT@39+Z+L1,"";:E$=INPUT$(1)
690 IFE$>="a"ANDE$<="z"THENE$=CHR$(ASC(E$)-32)
700 MID$(C$,Z,1)=E$:PRINT@39+Z+L1,E$
710 NEXTZ:IFC$<>A$THEN740
720 IFP(H)<250THENP(H)=250
730 GOSUB770:BEEP:BEEP:PRINT@370,"** YOU WIN $"P(H)"**":GOSUB760:GOTO100
740 GOSUB770:SOUND16E3,25:PRINT@375,"INCORRECT":GOSUB760:PRINT@40,D$:GOSUB780
750 GOTO230
760 FORZ=1TO1500:NEXT
770 PRINT@360,D$:RETURN
780 PRINT@149+13*H," PLAYER"H:H=1-H*(H<N):RETURN
790 FORZ=LTO1STEP-1:PRINT@119+Z+L1,CHR$(148):FORY=1TO50:NEXT:PRINT@119+Z+L1," ":NEXT:PRINT@120+L1,CHR$(148):RETURN

