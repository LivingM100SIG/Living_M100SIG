1 'Bomber.200  by James Yi/73327,1653       03/25/87
2 'This program needs SHPDRW.200 and       BOMBER.SHP- in DL10
3 'You can redefine the shapes in this program with SHPMKR.200, in DL10
10 PRINT"Bomb as many enemy bases as you can and return home before fuel runs out."
11 PRINT"You reach home when the bomber hits the top of the screen.":PRINT"Bases will appear one at a time."
12 PRINT"The bomber is initially at flight.  You must give it gas to keep it flying."
13 PRINT"The bomber ascends as it speeds up, and descends as it slows down.":PRINT"But don't fly too low!"
14 PRINT"Use left and right cursor keys to vary  thrust.  Greater thrust uses more fuel."
15 PRINT"To release bomb, press down arrow.":PRINT:PRINT"Press any key to continue.":R$=INPUT$(1)
20 LOADM"bomber":TB=PEEK(62715)+PEEK(62716)*256:LOADM"shpdrw":DR=PEEK(62715)+PEEK(62716)*256
30 POKE63277,PEEK(61983)
50 POKE-14,TB-INT(TB/256)*256:POKE-13,TB/256
60 POKE-12,0:POKE-11,0
65 CLS
70 AC=5:BM=7:TG=8:BG=0:G=10:TX=0:TC=0:VC=0:TH=0:V=100:AL=0:FL=5000:EE=1
124 LINE(0,121)-(239,127),1,BF:PRINT@58,"home":LINE(0,8)-(239,16),1,B:PRINT@0,"Fuel:":PRINT@28,"Thrust:"
130 XS=220:Y=10:X=XS
132 TL=INT(RND(1)*198):TR=TL+PEEK(TB+TG*4+3):CALLDR,TG,TL+114*256
135 GOTO 200
140 I=PEEK(-749):IFIAND16THENTH=TH+1ELSEIFIAND32THENTH=TH+(TH>0)
150 PRINT@5,FL;"  ":IFFL=0THENTH=0ELSEFL=FL-TH:IFFL<0THENTH=TH+FL:FL=0
160 PRINT@35,TH:SV=EE*TH*20:VC=V/50*(V>SV)-TH*(V<SV):V=V+VC:IF(V>SVANDVC>0)OR(V<SVANDVC<0)THENV=SV
170 Y=Y+G/5-V/100:IFY<10THENY=9ELSEIFY>115THENY=114
180 X=X-V/25:IFX<0THENX=X+XS
190 CALLDR,AC,INT(PX)+INT(PY)*256
200 CALLDR,AC,INT(X)+INT(Y)*256:PX=X:PY=Y:IFY=114THEN300ELSEIFY=9THEN340ELSEIFY>90ANDEETHENGOSUB380
210 IFIAND128THENIFNOTBGTHENBV=V:DV=0:BG=-1:Q=X+5:W=Y+7:GOTO230
215 IFNOTBGTHEN250
220 CALLDR,BM,INT(R)+INT(T)*256
230 BM=BMXOR1:CALLDR,BM,INT(Q)+INT(W)*256:IFW>120THEN280
240 DV=DV+G/30:T=W:W=W+DV:R=Q:Q=Q-BV/50:IFQ<0THENQ=237
245 BV=BV-2:IFBV<0THENBV=0
250 GOTO140
280 BG=0:IF(Q>TLANDTR>Q)OR(R>TLANDTR>R)THENCALLDR,BM,INT(Q)+INT(W)*256:GOTO330
290 GOTO 140
300 SOUND16000,5:PRINT@373,"Crash landing.":PRINT@411,"You bombed"TX"bases.":IFTX>0THENPRINT@450,"Brave but unfortunate."
310 PRINT@494,"";:INPUT"Go again";R$:IFINSTR("Yy",R$)THEN65ELSEMENU
330 CALLDR,AC,INT(X)+INT(Y)*256:PRINT@560+TL/6,"KABOOM"
335 F=0:FORL=0TO8:F=FXOR63:OUT255,0:OUT254,F:FORW=0TO20:NEXT:NEXT:PRINT@560+TL/6,"      "
337 TX=TX+1:GOTO132
340 PRINT@412,"Mission complete."
350 PRINT@451,"You bombed"TX"bases":GOTO310
380 LINE(TR,113)-(X+7,Y+4):POKE-11,1:POKE-10,255:EE=0
385 OUT186,32:FORL=0TO9:OUT189,L+80:OUT184,195:NEXT:OUT186,47
390 LINE(TR,113)-(X+7,Y+4),0:CALLDR,AC,INT(X)+INT(Y)*256:POKE-11,0:RETURN
