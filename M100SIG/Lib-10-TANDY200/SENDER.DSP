0 'SENDER.DSP  BBS Text-uploader by Andrew Zarchy [73107,3455]
1 'reinsert SOUND4000,4:SOUND2500,4:after "gosub100" in l.95
12 MAXFILES=3:CLEAR1000:DEFINTA-Z:DEFSTRA-C,F,P:C=CHR$(13)
13 FORX=0TO3:READB(X):NEXT:DATA " ",":",";",)
15 CLS:FILES:PRINT:INPUT"File to Send";F1:IFF1=""THENMENUELSEF1=F1+".do":OPENF1FORINPUTAS1
17 INPUT"Width (76) ";W:IFW=0THENW=76
18 INPUT"Char. Delay (0-10) ";T1:T1=T1*10
19 INPUT"Prompt";P:IFP=""THENINPUT"Line Delay (0-10) ";T2:T2=T2*100
20 F2="COM:":FORX=0TO6:F2=F2+CHR$(PEEK(61244+X)):NEXT:OPENF2FOROUTPUTAS2:OPENF2FORINPUTAS3
25 PRINT" SENDING FILE
50 L=L+1:PRINTMID$(STR$(L),2);:IFP=""THENPRINT">";ELSEPRINTP;
51 FORX=1TOW-LEN(A):IFEOF(1)THEN95ELSEA1=INPUT$(1,1)
52 IFA1=CTHEN55ELSEIFA1<>CHR$(9)THENA=A+A1:NEXT:GOTO60
54 T=8:A=A+SPACE$(T):X=X+T-1:NEXT:GOTO60
55 A1=INPUT$(1,1):X=LEN(A):GOTO80
60 FORX=LEN(A)TO1STEP-1:FORY=0TO3:IFMID$(A,X,1)<>B(Y)THENNEXTY:NEXTX
80 GOSUB100:A=MID$(A,X+1):GOTO50
95 GOSUB100:MAXFILES=1:PRINT:CLOSE:FORX=1TO500:NEXT:CALL25454
100 IFLEFT$(A,1)=" "ANDLEFT$(A,3)<>"   "THENA=MID$(A,2):GOTO100
105 FORY=1TOX:PRINT#2,MID$(A,Y,1);:PRINTMID$(A,Y,1);:FORZ=1TOT1:NEXT:NEXT
110 PRINT:IFP=""THENPRINT#2,C;:FORZ=1TOT2:NEXT:RETURNELSEPRINT#2,C;
120 A1=INPUT$(1,3):IFA1<>PTHEN120ELSEFORZ=1TOT2+100:NEXT:RETURN

