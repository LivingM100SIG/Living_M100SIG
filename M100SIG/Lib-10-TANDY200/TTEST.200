1 REM *TTEST.BA*
2 REM *COPYRIGHT 1987 FRED BROWN [73267,2450]
3 CLS:CLEAR100:SCREEN0,0:KEYON:DEFSNGA-Z:DIMT1(30),T2(30)
4 ONERRORGOTO46
5 BS$="FILE: [......]"+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)
6 CS$=CHR$(27)+"J":VP$=CHR$(27)+"p":VQ$=CHR$(27)+"q"
7 PRINT@10,CS$;VP$;" T-TEST (Two Means) ";VQ$
8 FORI=1TO30:READT1(I):NEXT
9 FORI=1TO30:READT2(I):NEXT
10 DATA 6.31,2.92,2.35,2.13,2.02,1.94,1.90,1.86,1.83,1.81
11 DATA 1.80,1.78,1.77,1.76,1.75,1.75,1.74,1.73,1.73,1.72
12 DATA 1.72,1.72,1.71,1.71,1.71,1.71,1.70,1.70,1.70,1.69
13 DATA 12.71,4.30,3.18,2.78,2.57,2.45,2.37,2.31,2.26,2.23
14 DATA 2.20,2.18,2.16,2.15,2.13,2.12,2.11,2.10,2.09,2.09
15 DATA 2.08,2.07,2.07,2.06,2.06,2.06,2.05,2.05,2.05,2.04
16 PRINT@55,"ALPHA=0.05":
17 PRINT:PRINT"0=DISC 1=RAM (DEFAULT=0)":INPUT"FILE TYPE";DT
18 IFDTTHENFILESELSELFILES
19 PRINT"FIRST  ";BS$;:LINEINPUTF$
20 IFDTTHENF$(1)="RAM:"+F$ELSEF$(1)="0:"+F$
21 PRINT"SECOND ";BS$;:LINEINPUTF$
22 IFDTTHENF$(2)="RAM:"+F$ELSEF$(2)="0:"+F$
23 PRINT@80,CS$
24 FORI=1TO2:N(I)=0:SX(I)=0:SS(I)=0
25 OPENF$(I)FORINPUTAS1:PRINTF$(I)
26 IFEOF(1)THEN28
27 INPUT#1,X:N(I)=N(I)+1:SX(I)=SX(I)+X:SS(I)=SS(I)+X^2:GOTO26
28 CLOSE#1
29 NEXT
30 MN(1)=SX(1)/N(1):MN(2)=SX(2)/N(2):DF=N(1)+N(2)-2
31 T1=SS(1)-SX(1)^2/N(1):T2=SS(2)-SX(2)^2/N(2):T3=(T1+T2)/DF:T4=T3*(1/N(1)+1/N(2))
32 T=ABS(MN(1)-MN(2))/SQR(T4)
33 PRINT"MEANS:";MN(1);MN(2)
34 PRINT"DF=";DF
35 PRINTUSING"t= ##.##";T:PRINT
36 IFDF>30THEN41
37 PRINTUSING"ONE-TAIL STAT:##.## ";T1(DF);
38 IFT>T1(DF)THENPRINT"SIGNIFICANT"ELSEPRINT"NOT SIGNIFICANT"
39 PRINTUSING"TWO-TAIL STAT:##.## ";T2(DF);
40 IFT>T2(DF)THENPRINT"SIGNIFICANT"ELSEPRINT"NOT SIGNIFICANT"
41 PRINT@600,"COPY            RUN QUIT          0:TOP";CHR$(27)"H"
42 ONKEYGOSUB43,,,3,44,,,45:GOTO42
43 PRINT@560,CS$:LCOPY:GOTO41
44 MENU
45 RUN"0:TOP"
46 IFERR=52 THENPRINT@520,CS$;VP$;".DO FILE NOT FOUND.";VQ$:RESUME16
47 IFERR=55 THENPRINT@520,CS$;VP$;"BAD FILE NAME.";VQ$:RESUME16
48 PRINT@520,CS$;VP$;"ERROR";ERR;ERL;VQ$:STOP
100 'LINES 100+ MAY BE REMOVED.
101 'TTEST.BA reads data from RAM or Disc files.
102 'Data must be in the format n [ENTER],  i.e.:
103 '23 [ENTER]
104 '8.3 [ENTER]
105 '99 [ENTER]
106 'Blank lines, characters or commas in the file will cause an error.
107 'TTEST then computes one-tail and two tail statistics
108 'at the .95 confidence level
109 'Function Key assignments are:
110 '[F1] COPY - Screen dump.
111 '[F4] RUN - Rerun program
112 '[F5] QUIT - Main Menu
113 '[F8] 0:TOP - run a disc utility program named 0:TOP
