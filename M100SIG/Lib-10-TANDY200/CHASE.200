1 REM CHASE!.BA COPYRIGHT FRED BROWN 1987
2 '------------------------------------
3 'MOVE FIGURE WITH CURSOR KEYS,
4 'CAPTURE HEARTS FOR POINTS AND POWER.
5 '<SPACE> ACTIVATES POWER, FREEZES 
6 'MASTER CLONE, DESTROYS CLONES.
7 'OBJECT IS TO SURVIVE TILL
8 'TIME IS UP, WITH A HIGH SCORE.
9 'GOOD LUCK! (AND BE CAREFULL!)
10 '------------------------------------
11 CLS:CLEAR100:PRINT" CHASE!"TAB(20);"POWER: 1 SCORE: 0 "
12 LINE(4,13)-(235,123),1,B
13 DEFINTA-Z:DIMA(39,15)
14 CR$=CHR$(148):CL$=" ":MR$=CHR$(145)
15 CS$=CHR$(27)+"p"+CR$+CHR$(27)+"q":TR$=CHR$(158)
16 POKE 63277,PEEK(61983)
17 X=20:Y=8:PO=1:CT=300
18 MX=RND(1)*38+2:MY=RND(1)*13+3
19 IF(MX=XANDMY=Y)THEN18
20 TX=RND(1)*38+2:TY=RND(1)*13+3
21 IF(TX=XANDTY=Y)OR(TX=MXANDTY=MY)THEN20
22 A(TX,TY)=2
23 POKE61191,X:POKE61190,Y:PRINTCR$
24 POKE61191,TX:POKE61190,TY:PRINTTR$
25 FORI=0TO5:READP$(I):NEXT
26 DATA OOPS,EXCELLENT,REALLY GOOD,NOT BAD,LOUSY,ROTTEN
27 PRINT@10,"CT:";CT
28 I$=INKEY$:IFI$=""THEN28
29 CT=CT-1:IFCT<0THEN54
30 XX=X:YY=Y:A(X,Y)=0
31 IFASC(I$)=28THENXX=X+1:IFXX>39THENXX=39
32 IFASC(I$)=29THENXX=X-1:IFXX<2THENXX=2
33 IFASC(I$)=30THENYY=Y-1:IFYY<3THENYY=3
34 IFASC(I$)=31THENYY=Y+1:IFYY>15THENYY=15
35 IFASC(I$)=32ANDPOTHENPO=PO-1:F1=5:GOSUB64
36 IFA(XX,YY)=1ANDF1=0THENBEEP:GOTO40
37 POKE61191,X:POKE61190,Y:PRINTCL$
38 POKE61191,XX:POKE61190,YY:IFF1THENPRINTCS$ELSEPRINTCR$
39 X=XX:Y=YY:IFA(X,Y)=2THEN47
40 IFF1THENF1=F1-1:GOTO27
41 NX=MX-SGN(MX-X):NY=MY-SGN(MY-Y)
42 IFA(NX,NY)THENNX=RND(1)*38+2:NY=RND(1)*13+3:SOUND500,10:SOUND800,5
43 MX=NX:MY=NY:A(MX,MY)=1
44 POKE61191,MX:POKE61190,MY:PRINTMR$
45 IFMX=XANDMY=YTHEN62
46 GOTO27
47 SC=SC+1:F1=2:PO=PO+1:GOSUB64
48 A(TX,TY)=0
49 TX=RND(1)*38+2:TY=RND(1)*13+3
50 IFTX=XANDTY=YTHEN49
51 A(TX,TY)=2
52 POKE61191,TX:POKE61190,TY:PRINTTR$
53 GOTO27
54 FORI=1TO20:SOUNDI*250,2:NEXT
55 GOSUB65:PRINT@122,"TIMES UP!"
56 IFSC>1THENP=5
57 IFSC>5THENP=4
58 IFSC>10THENP=3
59 IFSC>15THENP=2
60 IFSC>20THENP=1
61 PRINT@165,P$(P);"!":END
62 FORI=20TO1STEP-1:SOUNDI*250,2:NEXT
63 GOSUB65:PRINT@122,"GOTCHA!":END
64 PRINT@20,"POWER:";PO;"SCORE:";SC:RETURN
65 LINE(10,20)-(100,50),1,B:LINE(11,21)-(99,49),0,BF:RETURN

