1 REM *ANOVA.BA V1.2*
2 REM *COPYRIGHT 1987 FRED BROWN [73267,2450]*
3 CLS:CLEAR1000:KEYON
4 ONERRORGOTO41
5 BS$="FILE: [......]"+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)+CHR$(8)
6 CS$=CHR$(27)+"J":VP$=CHR$(27)+"p":VQ$=CHR$(27)+"q"
7 PRINT@5,CS$;VP$;" ANOVA - Randomized Design ";VQ$
8 DT=0:PRINT:PRINT"0=DISC 1=RAM (DEFAULT=0)":INPUT"FILE TYPE";DT
9 PRINT@40,CS$
10 IFDTTHENFILESELSELFILES
11 INPUT"Number of Files";N:DIMF$(N),S(N),P(N)
12 FORI=1TON
13 PRINT@520,I;BS$;:LINEINPUTF$
14 IFDTTHENF$(I)="RAM:"+F$ELSEF$(I)="0:"+F$
15 OPENF$(I)FORINPUTAS1:CLOSE#1
16 PRINT@520,CS$:NEXT
17 PRINT@520,CS$;"Working..."
18 FORI=1TON
19 PRINT@560,CS$;F$(I)
20 OPENF$(I)FORINPUTAS1
21 IFNOTEOF(1)THENINPUT#1,X:S(I)=S(I)+X:TS=TS+X*X:P(I)=P(I)+1:TT=TT+1:GOTO21
22 CLOSE:T=T+S(I)
23 NEXT
24 CT=T*T/TT:ST=TS-CT
25 FORI=1TON:SS=SS+S(I)^2/P(I):NEXT
26 SB=SS-CT:SW=ST-SB:DT=TT-1:DB=N-1:DW=DT-DB:MB=SB/DB:MW=SW/DW:F=MB/MW:CR=SQR(SB/ST)
27 PRINT@80,CS$
28 PRINT"SOURCE  DF     SS        MS       F":PRINTSTRING$(38,"-")
29 PRINTUSING"TREAT  ### ######.## ######.##   ##.##";DB;SB;MB;F
30 PRINTUSING"ERROR  ### ######.## ######.##";DW;SW;MW:PRINTSTRING$(20,"-")
31 PRINTUSING"TOTAL  ### ######.##";DB+DW;SB+SW
32 PRINT@600,"COPY            RUN QUIT          0:TOP";CHR$(27)"H"
33 ONKEYGOSUB36,,,3,34,,,35:GOTO33
34 MENU
35 RUN"0:TOP"
36 LPRINT"STATPAC V1.5"
37 LPRINTDATE$
38 FORI=1TON:LPRINTF$(I):NEXT
39 PRINT@560,CS$:LCOPY
40 GOTO32
41 IFERR=52THENPRINT@560,CS$;VP$;".DO FILE NOT FOUND.";VQ$:RESUME32
42 IFERR=55THENPRINT@560,CS$;VP$;"BAD FILE NAME.";VQ$:RESUME32
43 PRINT@560,CS$;VP$;"ERROR:";ERR;ERL;VQ$:RESUME32
100 'Lines 100+ may be removed.
101 'ANOVA.BA computes an F-statistic from multiple RAM or DISC Data Files.
103 'Data Files must be in the form
104 ' 10 [ENTER]
105 ' 19.2 [ENTER]
106 ' 370 [ENTER]
107 ' n [ENTER]
108 'Extra comments, commas or blank lines in the file will cause an error.
109 'Function Key assignments are:
110 '[F1] COPY - dump screen to printer.
111 '[F4] RUN - rerun program.
112 '[F5] QUIT - goto Main Menu
113 '[F8] 0:TOP - run a disc utility program named 'TOP'.

