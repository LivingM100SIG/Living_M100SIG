1 CLS:PRINT@256,"POKER"
2 PRINT@330,"By Jon Goldberger"
3 PRINT@404,"Do you want instructions? (Y/N)";
4 X$=INPUT$(1):IFX$="y"ORX$="Y"THENGOSUB368
5 DIMD(52):DIMD$(52):DIMHD(3,4):Q1$="23456789TJQKA":FORI=156TO159:Q2$=Q2$+CHR$(I):NEXT
6 DE=0:DIMPL(3,4):CL$=CHR$(27)+CHR$(75):DIMCA(3):DIMBF(3):DIMBH(3)
7 SE%=VAL(RIGHT$(TIME$,2))+VAL(MID$(TIME$,4,2))+VAL(LEFT$(TIME$,2))
8 FORI=1TOSE%:DU=RND(1):NEXT
9 K=1:D$(0)="-"
10 FORI=1TO4:FORJ=1TO13
11 D$(K)=MID$(Q1$,J,1)+MID$(Q2$,I,1):K=K+1
12 NEXTJ,I
13 FORI=0TO3:PL(I,0)=400:NEXTI
14 GOSUB56:OP=4:A=DE
15 FORI=0TO3:FORJ=0TO4:HD(I,J)=0:NEXTJ,I
16 FORI=0TO3:CA(I)=5:PL(I,4)=248:NEXT
17 PRINT@600,"Dealing";CL$;
18 GOSUB77
19 GOSUB356
20 FL=0:GOSUB218
21 B=0:RA=0:RL=ED:RN=0:FORI=0TO3:PL(I,3)=0:NEXT
22 PRINT@600,"Thinking";CL$;
23 IFPL(ED,1)=1THEN31
24 IFF1=1THENFORI=1TO1000:NEXT:GOTO28
25 GOSUB104
26 GOSUB113
27 GOSUB127
28 GOSUB176
29 PRINT@600,CL$;
30 IFFL=1THENFL=0:GOTO50
31 GOSUB202:IFF1=0THENGOSUB203
32 IFED<>RLTHEN22
33 PRINT@600,"Dealing";CL$;
34 GOSUB77
35 GOSUB356
36 FL=0:IFOP=4THENGOSUB218:ELSEED=OP:IFOP=0THENDE=3:ELSEDE=OP-1
37 B=0:RA=0:RL=ED:RN=0:FORI=0TO3:PL(I,3)=0:NEXT
38 PRINT@600,"Thinking";CL$;
39 IFPL(ED,1)=1THEN47
40 IFF1=1THENFORI=1TO1000:NEXT:GOTO44
41 GOSUB104
42 IFBF(ED)=7ORBF(ED)=8THEN44
43 GOSUB113
44 GOSUB176
45 PRINT@600,CL$;
46 IFFL=1THENFL=0:GOTO50
47 GOSUB202:IFF1=0THENGOSUB203
48 IFED<>RLGOTO38
49 GOSUB234
50 GOSUB283
51 GOSUB204
52 DE=A
53 IFDE=3THENDE=0:ELSEDE=DE+1
54 IFPL(DE,1)=1THEN53
55 GOTO14
56 CLS:PRINT@294,"SHUFFLING";:CO=1:FORI=1TO52:D(I)=I:NEXT
57 FORI=1TO52:R%=I+(53-I)*RND(1):T=D(I):D(I)=D(R%):D(R%)=T:NEXT
58 CLS:FORI=0TO2:J=I*72
59 PRINT@I*12,"PL";I+1;" $";
60 LINE(J+26,11)-(J+62,35),1,B
62 LINE(J+24,9)-(J+64,37),1,B
64 NEXT
65 FORI=44TO140STEP24
66 LINE(I,91)-(I+18,115),1,B
67 LINE(I-1,90)-(I+19,116),1,B
70 NEXT
71 PO=1:PL(0,0)=PL(0,0)-1
72 FORI=0TO2:ED=I+1:IFPL(ED,1)=0THENPO=PO+1:PL(ED,0)=PL(ED,0)-1:ELSEM$="OUT":GOSUB210
73 PRINT@I*12+7,PL(ED,0);:NEXTI
74 PRINT@299,"POT";:PRINT@338,"$";PO;:PRINT@376,"Dealer=";:PRINT@480,"HUMAN";:PRINT@520,"$";PL(0,0);
75 IFDE=0THENPRINT@384,"H";:ELSEPRINT@383,DE
76 RETURN
77 F1=0:GOSUB218
78 IFF1=1THENF1=0:RETURN
79 IFPL(ED,1)=1THEN85
80 IFED=0ANDCA(0)<>5THENGOSUB86
81 K=8:FORI=0TO4
82 IFPL(ED,4)ANDKTHENHD(ED,I)=D(CO):CO=CO+1:IFED=0THENPRINT@I*4+488,D$(HD(0,I));
83 K=K*2:NEXTI
84 IFCA(ED)<5ANDED<>0THENM$="DRAW":GOSUB210:M=CA(ED):GOSUB220
85 GOSUB202:GOSUB203:GOTO78
86 PRINT@600,"Draw:";CL$;:PRINT@630,"<E>nd";CL$;
87 FORI=1TO5:PRINT@I*4+603,I;:NEXT
88 PL(0,4)=0:CA(0)=0:J=0
89 PRINT@606,"";:X$=INPUT$(1)
90 IFX$="e"ORX$="E"THENPRINT@600,CL$;:RETURN
91 IFX$<"1"ORX$>"5"THEN89
92 X=VAL(X$)
93 IFCA(0)=4ANDX=JTHEN89
94 PL(0,4)=PL(0,4)XOR2^(X+2)
95 IFPL(0,4)AND2^(X+2)THENCA(0)=CA(0)+1:PRINT@X*4+605,CHR$(239);:GOTO97
96 CA(0)=CA(0)-1:PRINT@X*4+605," ";
97 IFCA(0)=4THEN99
98 GOTO89
99 I=0
100 IF(PL(0,4)AND2^(I+3))THEN103
101 IFLEFT$(D$(HD(0,I)),1)<>"A"THENPRINT@600,"You need an ACE to draw four. Redo.";CL$;:FORK=1TO500:NEXT:GOTO86
102 J=I+1:GOTO89
103 I=I+1:GOTO100
104 GOSUB322:IFFH=5ANDST=5THENBH(ED)=1:RETURN
105 IFPA=4THENBH(ED)=2:RETURN
106 GOSUB338:IFP2=1THENBH(ED)=3:RETURN
107 IFFH=5THENBH(ED)=4:RETURN
108 IFST=5THENBH(ED)=5:RETURN
109 IFPA=3THENBH(ED)=6:RETURN
110 GOSUB345:IFP3=1THENBH(ED)=7:RETURN
111 IFPA=2THENBH(ED)=8:RETURN
112 BH(ED)=9:RETURN
113 IFED=0THENCA(0)=0:RETURN
114 ONBH(ED)GOSUB116,115,116,116,116,117,118,120,123:RETURN
115 CA(ED)=INT(RND(1)+.5):BF(ED)=1:RETURN
116 CA(ED)=0:BF(ED)=1:RETURN
117 CA(ED)=2:BF(ED)=2:RETURN
118 CA(ED)=1:IFH3>9THENBF(ED)=2:RETURN
119 BF(ED)=3:RETURN
120 CA(ED)=3:IFH2>11THENBF(ED)=3:RETURN
121 IFH2>9THENBF(ED)=4:RETURN
122 IFINT(RND(1)+.3)=1THEN125:ELSEBF(ED)=5:RETURN
123 IFCA(ED)=5AND(FH=4ORST=4)THENBF(ED)=5:CA(ED)=1:RETURN
124 IFINT(RND(1)+.3)=1THEN126:ELSEBF(ED)=6:TU=HD(ED,0):GOSUB223:IFTU=13THENCA(ED)=4:RETURN:ELSECA(ED)=3:RETURN
125 CA(ED)=INT(RND(1)*3+.5):BF(ED)=7:RETURN
126 CA(ED)=INT(RND(1)*3+.5):BF(ED)=8:RETURN
127 IFCA(ED)=0THENPL(ED,4)=0:RETURN
128 IFBH(ED)<>2THEN133
129 TU=HD(ED,0):GOSUB223:Y=TU
130 TU=HD(ED,1):GOSUB223
131 IFTU=YTHENPL(ED,4)=128:ELSEPL(ED,4)=8
132 RETURN
133 IFBH(ED)<>6THEN141
134 TU=HD(ED,0):GOSUB223:Y=TU
135 TU=HD(ED,1):GOSUB223
136 IFTU=YTHENPL(ED,4)=192:RETURN
137 TU=HD(ED,1):GOSUB223:Y=TU
138 TU=HD(ED,2):GOSUB223
139 IFTU<>YTHENPL(ED,4)=24:ELSEPL(ED,4)=136
140 RETURN
141 IFBH(ED)<>7THEN149
142 TU=HD(ED,0):GOSUB223:Y=TU
143 TU=HD(ED,1):GOSUB223
144 IFTU<>YTHENPL(ED,4)=8:RETURN
145 TU=HD(ED,2):GOSUB223:Y=TU
146 TU=HD(ED,3):GOSUB223
147 IFTU=YTHENPL(ED,4)=128:ELSEPL(ED,4)=32
148 RETURN
149 IFBH(ED)<>8THEN158
150 I=0
151 TU=HD(ED,I):GOSUB223:Y=TU
152 TU=HD(ED,I+1):GOSUB223
153 IFTU=YANDCA(ED)=1THENPL(ED,4)=128:RETURN:ELSEIFTU=YANDCA(ED)=2THENIFI=2THENPL(ED,4)=144:RETURN:ELSEPL(ED,4)=192:RETURN
154 IFTU=YTHENPL(ED,4)=248-(2^(I+3)+2^(I+4)):RETURN
155 I=I+1:IFI<3THEN151
156 IFCA(ED)=1THENPL(ED,4)=32:RETURN:ELSEIFCA(ED)=2THENPL(ED,4)=48:RETURN
157 PL(ED,4)=56:RETURN
158 IFFH=4THEN163:ELSEIFST=4THEN171
159 IFCA(ED)=1THENPL(ED,4)=128:RETURN
160 IFCA(ED)=2THENPL(ED,4)=192:RETURN
161 IFCA(ED)=3THENPL(ED,4)=224:ELSEPL(ED,4)=240
162 RETURN
163 I=0
164 TU=HD(ED,I):GOSUB223:W=SU
165 TU=HD(ED,I+1):GOSUB223:X=SU
166 TU=HD(ED,I+2):GOSUB223
167 IFW=XANDW<>SUTHENPL(ED,4)=2^(I+5):RETURN
168 IFW=SUANDW<>XTHENPL(ED,4)=2^(I+4):RETURN
169 IFX=SUANDX<>WTHENPL(ED,4)=2^(I+3):RETURN
170 I=I+1:GOTO164
171 TU=HD(ED,0):GOSUB223:IFTU=13THENTU=HD(ED,2):GOSUB223:IFTU=4THENPL(ED,4)=16:RETURN
172 TU=HD(ED,0):GOSUB223:Y=TU
173 TU=HD(ED,1):GOSUB223
174 IFTU=Y-1THENPL(ED,4)=128:ELSEPL(ED,4)=8
175 RETURN
176 IFED=0THENGOSUB297:PRINT@600,CL$;:GOTO178
177 ONBF(ED)GOSUB182,184,187,190,193,196,198,200
178 IFT=1THENOP=ED:RL=ED:GOTO181
179 IFT=3THENRL=ED:RN=RN+1:GOTO181
180 IFT=4THENPL(ED,1)=1:M$="OUT":GOSUB210:GOSUB214
181 RETURN
182 IFB=0THENT=1:M$="I BET":M=INT(RND(1)*3+7.5):GOSUB210:GOSUB220:RA=M:GOSUB229:RETURN
183 IFRN>2THEN186:ELSET=3:M$="RAISE":M=INT(RND(1)*5+5.5):GOSUB210:GOSUB220:RA=M:M=M+B-PL(ED,3):GOSUB229:RETURN
184 IFB=0THENT=1:M$="I BET":M=INT(RND(1)*5+5.5):GOSUB210:GOSUB220:RA=M:GOSUB229:RETURN
185 IFINT(RND(1)+.5)=1ANDRN<3THENT=3:M$="RAISE":M=INT(RND(1)*5+5.5):GOSUB210:GOSUB220:RA=M:M=M+B-PL(ED,3):GOSUB229:RETURN
186 T=2:M$="CALL":RA=0:M=B-PL(ED,3):GOSUB210:GOSUB220:GOSUB229:RETURN
187 IFB=0THENT=1:M$="I BET":M=INT(RND(1)*5+5.5):GOSUB210:GOSUB220:RA=M:GOSUB229:RETURN
188 IFINT(RND(1)+.5)=1ANDRN<3THENT=3:M$="RAISE":M=INT(RND(1)*3+5.5):GOSUB210:GOSUB220:RA=M:M=M+B-PL(ED,3):GOSUB229:RETURN
189 T=2:M$="CALL":RA=0:M=B-PL(ED,3):GOSUB210:GOSUB220:GOSUB229:RETURN
190 IFB=0ANDINT(RND(1)+.5)=1THENT=1:M$="I BET":M=INT(RND(1)*5+5.5):GOSUB210:GOSUB220:RA=M:GOSUB229:RETURN
191 IFB=0THENT=2:M$="CHECK":GOSUB210:RETURN
192 T=2:M$="CALL":RA=0:M=B-PL(ED,3):GOSUB210:GOSUB220:GOSUB229:RETURN
193 IFB=0THENT=2:M$="CHECK":GOSUB210:RETURN
194 IFINT(RND(1)+.5)=1THENT=2:M$="CALL":RA=0:M=B-PL(ED,3):GOSUB210:GOSUB220:GOSUB229:RETURN
195 T=4:M$="FOLD":GOSUB210:RETURN
196 IFB=0THENT=2:M$="CHECK":GOSUB210:RETURN
197 T=4:M$="FOLD":GOSUB210:RETURN
198 IFB=0THENT=1:M$="I BET":M=INT(RND(1)*3+7.5):GOSUB210:GOSUB220:RA=M:GOSUB229:RETURN
199 IFRN>2THEN186:ELSET=3:M$="RAISE":M=INT(RND(1)*3+7.5):GOSUB210:GOSUB220:RA=M:M=M+B-PL(ED,3):GOSUB229:RETURN
200 IFB=0THENT=1:M$="I BET":M=INT(RND(1)*3+5.5):GOSUB210:GOSUB220:RA=M:GOSUB229:RETURN
201 IFRN>2THEN186:ELSET=3:M$="RAISE":M=INT(RND(1)*5+5.5):GOSUB210:GOSUB220:RA=M:M=M+B-PL(ED,3):GOSUB229:RETURN
202 IFED=3THENED=0:RETURN:ELSEED=ED+1:RETURN
203 IFED=DE+1OR(ED=0ANDDE=3)THENF1=1:RETURN:ELSERETURN
204 OU=0:FORI=1TO3:IFPL(I,0)<1THENPL(I,1)=1:PL(I,0)=0:OU=OU+1:NEXT:ELSEPL(I,1)=0:NEXT
205 IFPL(0,0)<1THENCLS:PRINT@244,"You're out of money! Game over.";:FORI=1TO2000:NEXT:MENU:ELSEPL(0,1)=0
206 IFOU=3THENCLS:PRINT@243,"You are the only one with money!";:PRINT@286,"You have won:";PL(0,0)-400;"dollars!":END:ELSERETURN
207 IFED=0THEN209
208 PRINT@ED*12+73,"     ";:PRINT@ED*12+113,"     ";
209 RETURN
210 GOSUB207
211 IFED=0THENPRINT@561,M$;:GOTO213
212 PRINT@ED*12+73,M$;
213 RETURN
214 OU=0
215 FORI=0TO3:IFPL(I,1)=1THENOU=OU+1:NEXT:ELSEWI=I:NEXT
216 IFOU=3THENFL=1
217 RETURN
218 IFDE=3THENED=0:ELSEED=DE+1
219 RETURN
220 IFED=0THEN222
221 PRINT@ED*12+113,M;
222 RETURN
223 SU=1
224 IFTU=0THENSU=0
225 IFTU>39THENTU=TU-39:SU=4
226 IFTU>26THENTU=TU-26:SU=3
227 IFTU>13THENTU=TU-13:SU=2
228 RETURN
229 B=B+RA:PL(ED,0)=PL(ED,0)-M:PL(ED,3)=PL(ED,3)+M:PO=PO+M
230 IFED=0THENPRINT@521,PL(0,0):GOTO232
231 PRINT@(ED-1)*12+7,PL(ED,0)
232 PRINT@339,PO:RETURN
233 FORI=0TO4:PRINT@ED*12+40*I+69,D$(HD(ED,I)):NEXT:RETURN
234 F1=0:GOSUB218
235 IFPL(ED,1)=1THENGOSUB202:GOTO235
236 WI=ED:IFED<>0THENGOSUB233
237 GOSUB202
238 IFPL(ED,1)=1THEN244
239 IFED<>0THENGOSUB233
240 IFBH(WI)>BH(ED)THENWI=ED:GOTO244
241 IFBH(WI)<BH(ED)THEN244
242 W1=WI:W2=ED
243 ONBH(WI)GOSUB245,249,249,253,245,249,258,268,253
244 GOSUB202:GOSUB203:IFF1=1THENRETURN:ELSEGOTO238
245 ED=W1:GOSUB322:T=H1
246 ED=W2:GOSUB322
247 IFH1>TTHENWI=W2:ELSEWI=W1
248 RETURN
249 ED=W1:GOSUB322:T=H2
250 ED=W2:GOSUB322
251 IFH2>TTHENWI=W2:ELSEWI=W1
252 RETURN
253 FORI=0TO4:TU=HD(W1,I):GOSUB223:Y=TU
254 TU=HD(W2,I):GOSUB223
255 IFTU=YTHENNEXT:RETURN
256 IFTU>YTHENWI=W2:ELSEWI=W1
257 I=4:NEXT:RETURN
258 ED=W1:GOSUB345:W=H3:X=H4:Z=H5
259 ED=W2:GOSUB345
260 IFH3=WTHEN263
261 IFH3>WTHENWI=W2:ELSEWI=W1
262 RETURN
263 IFH4=XTHEN266
264 IFH4>XTHENWI=W2:ELSEWI=W1
265 RETURN
266 IFH5>ZTHENWI=W2:ELSEWI=W1
267 RETURN
268 ED=W1:GOSUB322:T=H2
269 ED=W2:GOSUB322
270 IFH2=TTHEN273
271 IFH2>TTHENWI=W2:ELSEWI=W1
272 RETURN
273 I=0:J=0
274 IFI<5THENTU=HD(W1,I):GOSUB223:Y=TU:ELSERETURN
275 IFI<4THENTU=HD(W1,I+1):GOSUB223:ELSEGOTO277
276 IFTU=YTHENI=I+2:GOTO274
277 IFJ<5THENTU=HD(W2,J):GOSUB223:Z=TU:ELSERETURN
278 IFJ<4THENTU=HD(W2,J+1):GOSUB223:ELSEGOTO280
279 IFTU=ZTHENJ=J+2:GOTO277
280 IFZ=YTHENI=I+1:J=J+1:GOTO274
281 IFZ>YTHENWI=W2:ELSEWI=W1
282 RETURN
283 IFWI=0THENPRINT@600,"You won the hand!";CL$;:GOTO286
284 PRINT@600,"Player";WI;"won the hand.";CL$;
285 ED=WI:M$="I WON":GOSUB210
286 FORI=0TO3:IFPL(I,0)<0THENPO=PO-PL(I,0):NEXTI
287 PL(WI,0)=PL(WI,0)+PO
288 IFWI=0THENPRINT@521,PL(0,0):GOTO290
289 PRINT@(WI-1)*12+7,PL(WI,0)
290 FORI=1TO1000:NEXTI:PRINT@600,"<C>ontinue or <Q>uit :";CL$;
291 X$=INPUT$(1):IFX$<>"q"ANDX$<>"Q"ANDX$<>"c"ANDX$<>"C"THEN291
292 IFX$="c"ORX$="C"THENRETURN
293 CLS:IFPL(0,0)>400THENPRINT@290,"You won";PL(0,0)-400;"dollars!":GOTO296
294 IFPL(0,0)=400THENPRINT@292,"You broke even.":GOTO296
295 PRINT@289,"You lost";400-PL(0,0);"dollars."
296 FORI=1TO2000:NEXT:MENU
297 IFB<>0ANDRN>2THEN308
298 IFB<>0THEN313
299 PRINT@600,"<C>heck  <B>et :";CL$;
300 X$=INPUT$(1):IFX$<>"c"ANDX$<>"C"ANDX$<>"b"ANDX$<>"B"THEN300
301 IFX$="b"ORX$="B"THEN303
302 T=2:RETURN
303 PRINT@600,"Enter bet (5-10) :";CL$;
304 X$=INPUT$(1):PRINTX$;:IFX$<"5"ANDX$<>"1"ORX$>"9"THEN303
305 IFX$="1"THENX$=X$+INPUT$(1):IFX$<>"10"THEN303
306 X=VAL(X$)
307 T=1:M=X:RA=X:GOSUB229:RETURN
308 PRINT@600,"<C>all";B-PL(ED,3);"  <F>old :";CL$;
309 X$=INPUT$(1):IFX$<>"c"ANDX$<>"C"ANDX$<>"f"ANDX$<>"F"THEN309
310 IFX$="c"ORX$="C"THEN312
311 T=4:RETURN
312 T=2:RA=0:M=B-PL(ED,3):GOSUB229:RETURN
313 PRINT@600,"<C>all";B-PL(ED,3);"  <R>aise  <F>old :";CL$;
314 X$=INPUT$(1):IFX$<>"c"ANDX$<>"C"ANDX$<>"r"ANDX$<>"R"ANDX$<>"f"ANDX$<>"F"THEN314
315 IFX$="c"ORX$="C"THEN312
316 IFX$="f"ORX$="F"THEN311
317 PRINT@600,"Enter raise (5-10) :";CL$;
318 X$=INPUT$(1):PRINTX$;:IFX$<"5"ANDX$<>"1"ORX$>"9"THEN317
319 IFX$="1"THENX$=X$+INPUT$(1):IFX$<>"10"THEN317
320 X=VAL(X$)
321 T=3:RA=X:M=RA+B-PL(ED,3):GOSUB229:RETURN
322 FH=1:ST=1:PA=1
323 FORI=0TO3:TU=HD(ED,I):GOSUB223:Y=TU:Z=SU:V=1:W=1:X=1:L=1:U=0
324 FORJ=I+1TO4:TU=HD(ED,J):GOSUB223
325 IFSU=ZTHENV=V+1
326 IFTU=YTHENX=X+1:GOTO330
327 IFU=1THEN330
328 IFTU=Y-LTHENW=W+1:L=L+1
329 IFY-L=0THENTU=HD(ED,0):GOSUB223:IFTU=13THENW=W+1:U=1
330 NEXTJ
331 IFV>FHTHENFH=V
332 IFW>STTHENST=W:IFST>4THENH1=Y
333 IFX>PATHENPA=X:IFPA>1THENH2=Y
334 NEXTI
335 IFFH>5THENFH=5
336 IFST>5THENST=5
337 RETURN
338 IFPA<>3THENP2=0:RETURN
339 TU=HD(ED,0):GOSUB223:Y=TU
340 TU=HD(ED,1):GOSUB223:IFTU<>YTHEN344
341 TU=HD(ED,2):GOSUB223:IFTU<>YTHENP2=1:RETURN
342 TU=HD(ED,3):GOSUB223:Y=TU
343 TU=HD(ED,4):GOSUB223:IFTU=YTHENP2=1:RETURN
344 P2=0:RETURN
345 I=0
346 TU=HD(ED,I):GOSUB223:Y=TU
347 TU=HD(ED,I+1):GOSUB223:IFTU=YTHENI=I+2:H3=Y:GOTO351
348 IFI=1THENP3=0:RETURN
349 H5=Y
350 I=I+1:GOTO346
351 TU=HD(ED,I):GOSUB223:Y=TU
352 TU=HD(ED,I+1):GOSUB223:IFTU=YTHENP3=1:H4=Y:IFI=2THENTU=HD(ED,4):GOSUB223:H5=TU:RETURN:ELSERETURN
353 IFI=3THENP3=0:RETURN
354 H5=Y
355 I=I+1:GOTO351
356 Y=0:Z=0
357 FORI=0TO3:IFPL(I,1)=1THEN366:REM if
358 FORJ=0TO3
359 FORK=J+1TO4
360 TU=HD(I,J):GOSUB223:Y=TU
361 TU=HD(I,K):GOSUB223
362 IFTU>YTHENZ=HD(I,J):HD(I,J)=HD(I,K):HD(I,K)=Z
363 NEXTK
364 NEXTJ
365 IFI=0THENFORL=0TO4:PRINT@L*4+488,D$(HD(0,L));:NEXTL
366 NEXTI
367 RETURN
368 CLS:PRINT"   This is basic draw poker with no"
369 PRINT"wild cards. Betting can be opened with"
370 PRINT"any hand. You play against 3 computer"
371 PRINT"controlled players.
372 PRINT"   The bottom line is a menu/message"
373 PRINT"line. Type the letter in brackets to"
374 PRINT"make a selection. For ENTER BET/RAISE"
375 PRINT"prompt,simply enter the amount you wish"
376 PRINT"to bet/raise. When you are prompted to"
377 PRINT"DRAW cards, simply press the numbers"
378 PRINT"corresponding to the cards you wish to"
379 PRINT"discard. A black box appears indicat-"
380 PRINT"ing your choice. Pressing the number"
381 PRINT"again deselects the card, and the box"
382 PRINT"dissappears. Press E when done."
383 PRINT@608,"PRESS ANY KEY TO BEGIN";
384 X$=INPUT$(1):RETURN

