

0 REM TXTDEL.BA COPYRIGHT 1987 FRED BROWN [73267,2450]
1 REM FOR SINGLE-USER M100SIG USE
2 CLEAR1000:DEFINTJ-Z:DIMF$(39),T(39):ONERRORGOTO35
3 CS$=CHR$(27)+"J":VP$=CHR$(27)+"p":VQ$=CHR$(27)+"q":SCREEN0,0:L=1
4 PRINT@0,CS$;VP$;"  FILES: [T]AG  [K]ILL  [N]AME  [M]ENU  ";VQ$
5 FORI=62122TO62629STEP11
6 IF(PEEK(I)<>192ANDPEEK(I)<>200)THEN11
7 N=N+1:PRINT@560,I;N
8 F$(N)="":T(N)=0:FORJ=1TO8:F$(N)=F$(N)+CHR$(PEEK(I+J+2))
9 IFJ=6THENF$(N)=F$(N)+"."
10 NEXTJ
11 NEXTI
12 PRINT@40,CS$:FORI=1TON:PRINT@I*10+111,F$(I):NEXTI
13 PRINT@L*10+111,VP$;F$(L);VQ$
14 IN$=INKEY$:IFIN$=""THEN14ELSEIN=ASC(IN$)
15 IFIN=77THENMENU
16 IFIN=75THEN25
17 IFIN=78THEN29
18 IFIN=84THEN31
19 PRINT@L*10+111,F$(L)
20 IFIN=28THENL=L+1:IFL>NTHENL=1
21 IFIN=29THENL=L-1:IFL<1THENL=N
22 IFIN=30THENL=L-4:IFL<1THENL=1
23 IFIN=31THENL=L+4:IFL>NTHENL=N
24 GOTO13
25 IFTTHEN26ELSEKILLF$(L):GOTO2
26 PRINT@560,"Working...":FORI=1TON
27 IFT(I)THENKILLF$(I)
28 NEXT:GOTO2
29 PRINT@560,CS$;"NEW FILE NAME: [______]"+STRING$(7,8);
30 LINEINPUTN$:N$=N$+".DO":NAMEF$(L)ASN$:F$(L)=N$:GOTO12
31 IFT(L)THENPRINT@L*10+110," ":T(L)=0:T=T-1:GOTO33
32 PRINT@L*10+110,CHR$(62):T(L)=1:T=T+1
33 PRINT@L*10+111,F$(L):L=L+1:IFL>NTHENL=1
34 GOTO13
35 REM ERROR
36 IFERL=30THENBEEP:RESUME29
37 PRINT"ERROR ";ERR;ERL:STOP
