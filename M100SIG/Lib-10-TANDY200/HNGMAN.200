1 'HNGMAN.DO (C) FRED BROWN 11/87 [73267,2450]
2 REM *********************************
3 '<SPACE> FOR GUESS, OR SELECT A
4 'LETTER. 7 TRIES TO GET IT RIGHT!
5 '************************************
6 CLEAR1000:DEFINTA-Z:DIML$(20)
7 POKE63277,PEEK(61983)
8 C$=CHR$(27)+"J":VP$=CHR$(27)+"p":VQ$=CHR$(27)+"q"
9 CLS:PRINTVP$" HANGMAN ";DATE$;SPACE$(23);VQ$
10 REM
11 PRINT@40,C$
12 T=0:CT=465:W$="":TL=0
13 LINE(154,85)-(210,108),1,B
14 LINE(155,20)-(160,85),1,B
15 LINE(160,20)-(205,25),1,B
16 PRINT@560,"<SPACE> FOR GUESS."
17 S=RND(1)*99+1
18 FORI=1TOS:READE$:NEXT:RESTORE
19 PRINT@80,"Word #";S
20 C=VAL(MID$(E$,1,1)):L=LEN(E$)-1
21 N$=RIGHT$(E$,L)
22 FORI=1TOL
23 LT=ASC(MID$(N$,I,1))+C:L$(I)=CHR$(LT):W$=W$+L$(I)
24 NEXT
25 PRINT@120,"This word has";L;"letters."
26 FORI=1TOL:PRINT@240+I*2,CHR$(255):NEXT
27 CT=CT+1:T=T+1:FL=1
28 PRINT@400,SPACE$(20)
29 PRINT@400,VP$;" SELECT A LETTER ";VQ$;:L$=INPUT$(1)
30 IFASC(L$)=32THEN42
31 IFCT=475THENCT=506
32 PRINT@CT,L$
33 FORJ=1TOL
34 IFL$(J)=L$THENPRINT@240+J*2,L$:FL=0:TL=TL+1
35 NEXTJ
36 IFFLTHENGOSUB46ELSET=T-1
37 IFTL=LTHEN45
38 GOTO27
39 FORI=1TOL:PRINT@280+I*2,L$(I);:NEXT
40 PRINT@480," PLAY AGAIN (Y/N)?";:I$=INPUT$(1)
41 IFI$="Y"THEN10ELSEMENU
42 PRINT@560,"YOUR GUESS";C$;:INPUTG$
43 IFG$=W$THEN45ELSEELSEPRINT" WRONG!";C$
44 FORT=TTO7:GOSUB46:NEXT:GOTO39
45 PRINT@585," GOOD SHOW!":GOTO39
46 ONTGOSUB48,49,50,51,52,53,54
47 RETURN
48 LINE(180,30)-(195,45),1,B:RETURN
49 LINE(185,45)-(190,50),1,B:RETURN
50 LINE(180,50)-(195,75),1,B:RETURN
51 LINE(180,50)-(175,60),1,B:RETURN
52 LINE(195,50)-(200,60),1,B:RETURN
53 LINE(180,75)-(185,83),1,B:RETURN
54 LINE(190,75)-(195,83),1,B
55 PRINT@586,"SORRY...":GOTO39
56 DATA 4=>QJ@=JP,1@M@KXRHR,89L@D=L=,2?RRMPLCW,3?RODI>O,4>QEOJAOO,3@>IBKABO
57 DATA 7<>F>M>KR,1BGNHQ,8;GDMEF,5>JHHDNNDJI,5>JPIO@MA@DO,4?UHEJ@AN,6>?M=?H>;HN
58 DATA 1CHRD@RD,8=D=;LJA;A9F,1DWDQBHRD,3BUMBKPB,1DWODQHLDMS,7?>;N:KR,5AD@IPRINT
59 DATA 7?HK>B@G,7?KB>G=,6A;OA?,7@HO>KGF>GM,5BM<HH<M,2ES?P?LRCC,1FTHC@MBD
60 DATA 2FWEGCLC,5DI>M@?D=G@,1HMCHBSLDMS,4EOH=J@,6DO>A?G?HN,1JMNVKDCFD,5GD=M<MT
61 DATA 8DA;=FK=,1KHSDQ@STQD,8E9?9RAF=,4I=NNE=CA,4IA@E?EJA,8EAFA9LMJ=,3JLO>IB
62 DATA 1LTRBKD,2LCGEF@MP,5ID>F@G,5ID@>@,2MAA?QGML,7HFBLLBHG,7HKB@BG,4L=N=HHAH
63 DATA 3M>O>IVPFP,6JH?OGIHC;,2NMJGRGAG?L,7IHLL>LLBHG,4LNAFQ@E?A,8HJAEALAN=
64 DATA 6JLCPCF?A?,1OQNEDRRNQ,1ORXBGNKNFX,2NQWAFG?RPW,8HMJKMAL,3NR>KQFQV,7K><>BIM
65 DATA 7K>EB@BHG,1QDRS@TQ@MS,7KARMAF,5N<A@OT,1RBDMDQX,4O?DA@QHA,2QCAPCAW
66 DATA 8K=;J=L9JQ,7L>BSNK>,4OANCA=JP,5NC@KC@M?,6MBIOF>?L,3PFBDB,7LH<B>MR
67 DATA 7LHIAHFHK>,7LI><BF>G,5NOM@IBOC,3PROMOFPB,6N?GJ?L;NOL?,3QBKABK@V,1SQ@FDCX
68 DATA 5OM<IND@IO,3S>@>K@V,8N9;MME,2TCLEC?LAC,3SFII>FK,8O=AJ<,8Q9;@L
69 DATA 1YDAQ@,1YDOGXQ,8R=KL>MD,7SBII>K,6TCL=IH,7SHF;B>,4PNE>=H,2RCJCNFMLC,6;=NL?MM

