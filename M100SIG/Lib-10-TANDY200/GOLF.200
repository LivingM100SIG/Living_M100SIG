10 CLS:F$="COMPUTERGOLFJ.C. WASSERMAN"
20 FORY=1TO8:A=Y+15
30 FORZ=ATOA+200STEP40
40 GOSUB780
50 NEXTZ
60 PRINT@Z,MID$(F$,Y,1)
70 NEXTY
80 FORY=9TO12:A=Y+290
90 FORZ=319TOASTEP-1
100 GOSUB780
110 NEXTZ
120 PRINT@Z,MID$(F$,Y,1)
130 NEXTY
140 FORZ=320TO339
150 PRINT@Z,"by"
160 FORX=1TO20:NEXT
170 PRINT@Z," "
180 NEXT:PRINT@339,"by"
190 FORY=13TO26:A=Y+600:B=200
200 IFY=26THENB=240
210 FORZ=ATOA-BSTEP-40
220 GOSUB780
230 NEXTZ
240 PRINT@Z,MID$(F$,Y,1)
250 NEXTY
260 FORX=1TO250:NEXT
270 PRINT@346," ":PRINT@386,"N":BEEP
280 LINE(104,50)-(104,11):PRINT@98,"1":LINE-(130,20):LINE-(104,27)
290 FORX=1TO500:NEXT
300 CLS:DEFSNGA-C:DEFINTY,P,N,S,T,X,Y,Z:DIMY(18),P(18)
310 INPUT"# of Players";N:IFN>4THEN310
320 FORZ=1TON:PRINT"Name-Player";Z;:INPUTG$(Z):IFG$(Z)=""THENG$(Z)="  "+STR$(Z)
330 IFLEN(G$(Z))>6THENG$(Z)=LEFT$(G$(Z),6)
340 NEXT
350 FORZ=1TON
360 PRINT"Driver for ";G$(Z);:INPUTD(Z):IFD(Z)<6ORD(Z)>9ORD(Z)=INT(D(Z))THEN360
370 NEXT
380 TP=0:FORZ=1TO4:TS(Z)=0:NEXT:SF=0:PRINT"Laying Out the Course"
390 V=VAL(RIGHT$(TIME$,2)):FORZ=1TOV:G=RND(1):NEXT
400 FORZ=1TO18:Y(Z)=INT(RND(1)*412+187)
410 IFY(Z)>491THENP(Z)=5ELSEIFY(Z)>215THENP(Z)=4ELSEP(Z)=3
420 TP=TP+P(Z)
430 NEXT
440 CLS:FORZ=9TO-39+48*(N+1)STEP48:LINE(Z,110)-(Z+28,120),1,B:NEXT
450 FORZ=1TON:PRINT@473+8*Z,G$(Z):NEXT:PRINT@441+8*N,"Thru":PRINT@481+8*N,"For Par"
460 LINE(136,38)-(150,48),1,B
470 PRINT@140,"Course is a Par";TP:LINE(118,22)-(228,32),1,B
480 FORZ=1TO18
490 PRINT@42,"Hole No.";Z:PRINT@56,"Yardage";Y(Z):PRINT@71,"Par";P(Z):SF=SF+P(Z)
500 PRINT@446+8*N,USING"##";Z:PRINT@564+8*N,USING"##";SF
510 FORY=1TON
520 PRINT@132,SPACE$(6):PRINT@121,"On the Tee ";G$(Y)
530 SH(Y)=1:TS(Y)=TS(Y)+1:A=INT((Y(Z)/D(Y)+.00005)*1E4)/1E4
540 PRINT@372,SPACE$(22)
550 PRINT@201,USING"###.####";A
560 B=INT((A-INT(A))*100)/100:IFB=0THENB=0
570 IFA>=9THENPRINT@281,USING"Subtract a square or times .##";B:PRINT@361,"Your Choice";:INPUTC:GOTO600
580 C=B:PRINT@281,USING"Must X .## (Press ANY KEY to continue)";C:PRINT@361,SPACE$(12)
590 I$=INKEY$:IFI$=""THEN590
600 IFC<1ANDC=BTHENA=INT(A*C*1E4)/1E4:GOTO630
610 IFC>=9ANDC<=AANDSQR(C)=INT(SQR(C))THENA=A-C:GOTO630
620 PRINT@372,"   NOT A VALID NUMBER":FORX=1TO500:NEXTX:GOTO540
630 SH(Y)=SH(Y)+1:TS(Y)=TS(Y)+1:PRINT@223,USING"##";SH(Y)
640 IFA>1THEN540ELSESOUND1000,5
650 IFSH(Y)>P(Z)THENH$=" BOGIE"
660 IFSH(Y)=P(Z)THENH$=" PAR"
670 IFP(Z)-SH(Y)=1THENH$=" BIRDIE"
680 IFP(Z)-SH(Y)=2THENH$="n EAGLE"
690 IFP(Z)-SH(Y)=3THENH$=" DBL EAGLE"
700 PRINT@361,"Your Score is a";H$;SH(Y):FORX=1TO600:NEXTX:PRINT@223," 1":PRINT@281,CHR$(27)"K"
710 :PRINT@555+8*Y,USING"###";TS(Y)
720 IFTS(Y)<=SFTHENPRINT@554+8*Y,"*"ELSEPRINT@554+8*Y," "
730 NEXTY
740 NEXTZ
750 PRINT@361,"THE FINAL SCORES ARE.......":FORX=1TO1500:NEXT:PRINT@361,SPACE$(27)
760 PRINT@361,"Play Again (Y/N) ";:I$=INPUT$(1)
770 IFI$="Y"ORI$="y"THENCLS:GOTO350ELSEMENU
780 PRINT@Z,MID$(F$,Y,1);
790 FORX=1TO20:NEXT
800 PRINT@Z," ";
810 RETURN
820 'Driver MUST have a decimal. Ex. 6.8154;7.093; 8.3365477
830 'Ball is "holed" when you get your number below 1. Computer makes the drive off tee by dividing hole yardage by your driver.
840 'You make succeeding shots by subtracting a perfect square(9,16,25,etc) or multiplying by the first two decimals.
850 'If number showing is 54.7823 you could subtract 9,16,25,36, or 49 OR multiply entire number by .78
860 'The lowest square you can use is 9.
870 'Hint: It's not always best to choose the highest square. Combinations of squares and/or mulitipliers may work out better
880 'NO FAIR using a calculator...

