10 CLS:DIMB(25),B$(25)
20 GOSUB910
30 A$="BIG BOGGLE BY JOE WASSERMAN":B$="73117,3600 JULY 2, 1986  "
40 POKE63277,PEEK(61983)
50 FORY=1TO52
60 FORZ=1TO3
70 A=30+INT(RND(1)*6+1)*40
80 PRINT@A+RND(1)*2,CHR$(RND(1)*26+65)
90 NEXTZ
100 IFY>27THEN130
110 PRINT@230,MID$(A$,Y,1):PRINT@229-Y,LEFT$(A$,Y)
120 IFY<=27THEN140
130 PRINT@270,MID$(B$,Y-27,1): PRINT@296-Y,LEFT$(B$,Y-27)
140 NEXTY
150 FORZ=1TO1000:NEXT
160 CLS:PRINT@285,"Do you want instructions Y/N ";:I$=INPUT$(1)
170 IFI$="Y"ORI$="y"THENGOSUB760
180 A=1:FORZ=128TO140STEP3
190 FORY=0TO320STEP80
200 B(A)=Y+Z:A=A+1
210 NEXTY,Z
220 CLS:LINE(41,19)-(131,99),1,B
230 LINE(41,35)-(131,35):LINE(41,51)-(131,51):LINE(41,67)-(131,67):LINE(41,83)-(131,83)
240 LINE(59,19)-(59,99):LINE(77,19)-(77,99):LINE(95,19)-(95,99):LINE(113,19)-(113,99)
250 GOSUB910
260 LINE(176,11)-(196,53),1,BF
270 W=54:X=176:Y=196:FORZ=1TO8
280 LINE(X,W)-(Y,W):X=X+1:W=W+1:Y=Y-1
290 NEXT
300 POKE63277,PEEK(61983)
310 RESTORE
320 PRINT@560,"Press ANY Key to Start Game"
330 IFINKEY$=""THEN330
340 PRINT@560,SPACE$(27)
350 FORZ=1TO25
360 READA$:B$=MID$(A$,RND(1)*6+1,1)
370 A=INT(RND(1)*25+1):IFB$(A)<>""THEN370
380 IFB$="Q"THENPRINT@B(A)-1,"Qu"ELSEPRINT@B(A),B$
390 SOUND6000,3
400 B$(A)=B$:NEXT
410 FORZ=1TO500:NEXT:BEEP
420 A=186:B=120:C=1:D=176:E=11:F=197:G=176:H=55:W=1:X=186:Y=120:Z=1
430 FORL=1TOZ
440 PSET(X,Y)
450 PRESET(D,E):D=D+1:IFD=FTHENE=E+1:D=G:IFE>=54THENF=F-1:G=G+1:D=G
460 IFE=64THEN600
470 I=RND(1)*3+185:J=RND(1)*H+62
480 PSET(I,J)
490 FORK=1TO71:NEXT
500 PRESET(I,J)
510 X=X+1
520 Y=Y-W
530 IFX=186THENW=-1
540 NEXT
550 H=H-1:Z=Z+2:W=1:A=A-1:IFA<176THENA=176:Z=Z-2
560 X=A
570 IFA=176THENB=B-1:IFC=1THENB=B+1:C=0
580 Y=B
590 GOTO430
600 BEEP:BEEP
610 PRINT@520,"4= 1pt    5= 2pts    6= 3pts
620 PRINT@560,"7= 4pts   8= 5pts   >8=10pts
630 PRINT@607,"Play Again Y/N ";:P$=INPUT$(1)
640 IFP$<>"Y"ANDP$<>"y"THENMENU
650 PRINT@520,SPACE$(28):PRINT@560,SPACE$(28):PRINT@607,SPACE$(14);
660 FORZ=1TO25:PRINT@B(Z)-1,"  ":B$(Z)="":NEXT
670 X=186:W=X:Y=68
680 FORZ=1TO11
690 LINE(X,Y)-(W,Y),0
700 X=X-1:W=W+1:Y=Y+1
710 NEXT
720 LINE(176,79)-(196,120),0,BF
730 GOTO260
740 DATA FSPRYI,AGEMUE,AEEMEE,DOTHDN,NOUTWO,SIFARA,TIECIL,RGWROV,OUTTOO,SRAFYI,DENANN,PECTIS,AEEAEE
750 DATA ARAFAS,HONDRL,RRIRYP,GEMNAN,MOTTET,TCCSEN,SESSUN,HOHDRL,DRLHON,BZXJQK,TIETII,PECTIL
760 CLS:PRINT"A separate paper and pencil are requiredto play BIG BOGGLE"
770 PRINT:PRINT"The object of the game is to make as    many ";
780 PRINT"words of four or more letters as   you can within the 5 min. time limit.
790 PRINT:PRINT"Start at any square and move up, down,  ";
800 PRINT"sideways or diagonally. You cannot use  the same letter twice in one word.
810 PRINT@447,"Press any key to continue ";
820 IFINKEY$=""THEN820
830 CLS:PRINT"You ARE allowed to add <s>, <ed>, etc.  to a word to form a new word but no capitalized words are permitted.
840 PRINT:PRINT"If more than one person plays, the wordscommon to each list must be crossed off before scores are totalled.
850 PRINT:PRINT"Scoring is as follows":
860 PRINT"Four  letter words 1 pt.":PRINT"Five  letter words 2 pts.
870 PRINT"Six   letter words 3 pts.":PRINT"Seven letter words 4 pts.
880 PRINT"Eight letter words 5 pts.":PRINT"Nine or more ltrs 10 pts.":PRINT@426,"Press any key":PRINT@469,"to start ";
890 IFINKEY$=""THEN890
900 RETURN
910 LINE(175,7)-(197,7)
920 LINE-(197,53):LINE-(188,62):LINE-(188,67)
930 LINE-(197,76):LINE-(197,121):LINE-(175,121)
940 LINE-(175,76):LINE-(184,67):LINE-(184,62)
950 LINE-(175,53):LINE-(175,7)
960 LINE(165,0)-(207,7),1,B:LINE(165,121)-(207,127),1,B:RETURN
970 REM For 3 minute timer change line 490 to: FORK=1TO29:NEXT

