0 REM PBAR.BA COPYRIGHT FRED BROWN (73267,2450) 1986
5 CLS:CLEAR:SCREEN0,0
10 PRINT"PBAR.BA ";DATE$
15 INPUT"Title";T$
20 INPUT"Number of Bars";N:DIMB(N),B$(N):INPUT"Max value";MX
25 FORI=1TON:PRINT@240,"BAR"I;CHR$(27)"J":INPUT"TITLE";B$(I)
30 INPUT"VALUE";B(I)
35 IFB(I)>MXTHENBEEP:GOTO30
40 NEXTI
50 CLS:LINE(125,10)-(225,110),1,B:Y1=10
55 FORI=1TON:X=125+B(I)/MX*100:Y2=Y1+100/N:LINE(125,Y1)-(X,Y2),1,B:Y1=Y2:NEXTI
60 PRINT@21,"0%":PRINT@35,"100%"
65 PRINT@581,"0":PRINT@(598-LEN(STR$(MX))),MX
100 I$="":PRINT@200,"<E> TO END":PRINT"<R> TO RUN":PRINT"<C> TO CHANGE":INPUT"<ENTER> TO LPRINT";I$
105 IFI$="E"ORI$="e"THENMENU
110 IFI$="R"ORI$="r"THEN0
115 IFI$="C"ORI$="c"THEN300
200 REM LPRINT
205 FORI=1TON:IFLEN(B$(I))>X1THENX1=LEN(B$(I))+1
210 NEXTI
215 X2=50-X1:LPRINT
220  FORI=1TON
225   X=B(I)/MX*X2
230   LPRINTB$(I);:IFB(I)=0THENLPRINT:GOTO240
235   LPRINTTAB(X1);CHR$(28);CHR$(X);CHR$(239)
240  NEXTI
245 LPRINTTAB(X1);CHR$(28);CHR$(X2);CHR$(243)
250 LPRINTTAB(X1)"0";TAB((X1+X2)/2);TAB(1+X1+X2-LEN(STR$(MX)));MX
255 LPRINTTAB(X1)T$:LPRINTTAB(X1);DATE$
260 PRINT@360,;:INPUT"<D> TO LIST DATA";I$
265 IFI$="D"ORI$="d"THEN270ELSE100
270 FORI=1TON:LPRINTTAB(X1)I;B$(I);B(I):NEXTI
299 GOTO100
300 REM change
301 PRINT@200,"               "
302 PRINT"                    "
303 PRINT"                    "
304 PRINT"                    "
305 PRINT"                    "
310 PRINT@200,"Change Bar";:INPUTC
315 PRINT"Title:";B$(C):PRINT"Value:";B(C)
320 INPUT"New Title";B$(C)
325 INPUT"New Value";B(C)
330 IFB(C)>MXTHENBEEP:GOTO325
399 GOTO50
400 REM PRINTER CODES AS FOLLOWS:
405 ' CHR$(28);CHR$(X);CHR$(243)=REPEAT X TIMES ASCII 239

