0 REM BACKUP.BA - Cassette Backup
2 REM James yi  73327,1653  01/16/88
4 REM Saves all .BA, .DO, and .CO type files in the current ram bank to cassette.
6 REM If for some reason you stop the program, running it again will continue to save the next file,
8 REM but POKE Q,0 will cause it to start over.
10 MAXFILES=2:CLEAR600:IFPEEK(1)=171THENSL=62122:KB=64798ELSESL=63919:KB=65450
12 Q=SL+3:F=PEEK(Q):IFF>48THENF=0
14 F=F+1:RD=F*11+SL:POKEQ,F:S=PEEK(RD):IFS=255THENPOKEQ,0:BEEP:PRINT"Done.":BEEP:END
16 IFS=0THEN14
18 F$="":FORM=RD+3TORD+10:F$=F$+CHR$(PEEK(M)):NEXT:F$=LEFT$(F$,6)+"."+RIGHT$(F$,2)
20 IFS<>192ANDS<>128ANDS<>160THENBEEP:PRINT"File type not recognized --";S;F$:GOTO14
22 PRINT"Saving:";CHR$(27)+"p "+F$+CHR$(27)+"q"
24 MOTORON:SOUND0,150:MOTOROFF:REM Write 3 sec. pause.
26 IFS=128THEN44:REM BASIC file
28 IFS=160THEN38:REM BINARY file
30 REM TEXT file
32 OPENF$FORINPUTAS1:F$="Cas:"+F$:OPENF$FOROUTPUTAS2
34 IFNOTEOF(1)THENLINEINPUT#1,L$:PRINT#2,L$:GOTO34
36 CLOSE:GOTO 14
38 M=PEEK(RD+1)+PEEK(RD+2)*256:T=PEEK(M)+PEEK(M+1)*256:E=T+PEEK(M+2)+PEEK(M+3)*256-1:X=PEEK(M+4)+PEEK(M+5)*256
40 IFT<HIMEMTHENBEEP:PRINT"Cannot load "F$:PRINT"HIMEM must be set to";T:PRINTF$" not saved.":GOTO 14
42 LOADMF$:CSAVEMF$,T,E,X:GOTO14
44 K$=CHR$(127)+"LoadF$"+CHR$(13)+"CSave"+CHR$(34)+LEFT$(F$,6)+CHR$(13)+"Run"+CHR$(34)+"BACKUP"+CHR$(13)
46 REM Activate Keyboard Buffer
48 FORL=1TOLEN(K$):POKEKB+L*2-1,ASC(MID$(K$,L,1)):NEXT
50 POKEKB,L-1
