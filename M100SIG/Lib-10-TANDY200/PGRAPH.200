0 REM PGRAPH COPYRIGHT FRED BROWN (73267,2450) 1986
5 CLS:CLEAR
10 PRINT"PGRAPH-PRINTER MUST BE ON LINE":LPRINTCHR$(30);CHR$(27);CHR$(22)
15 INPUT"NUMBER OF POINTS";N
20 INPUT"X:MIN,MAX";X1,X2
25 INPUT"Y:MIN,MAX";Y1,Y2
30 INPUT"X-AXIS SCALE";XT
35 INPUT"Y-AXIS SCALE";YT
40 DIM X(N),Y(N)
45 XS=100/(X2-X1):YS=100/(Y2-Y1)
50 FORI=1TON
55 PRINT@280,"POINT"I;CHR$(27);CHR$(74):INPUT"X,Y";X(I),Y(I)
60 IFX(I)>X2ORX(I)<X1THENBEEP:GOTO55
65 IFY(I)>Y2ORY(I)<Y1THENBEEP:GOTO55
70 NEXTI
75 CLS:SCREEN0,0:LINE(100,10)-(200,110),1,B
80 FORI=Y1TOY2STEPYT:PSET(201,INT((I-Y1)*YS+10)):NEXT
85 FORI=X1TOX2STEPXT:PSET(INT(100+(I-X1)*XS),111):NEXT
90 PRINT@74,Y2:PRINT@554,Y1:PRINT@576,X1:PRINT@591,X2
95 FORI=1TON:X=INT(100+(X(I)-X1)*XS):Y=INT(110-(Y(I)-Y1)*YS):PSET(X,Y):PSET(X+1,Y):PSET(X-1,Y):PSET(X,Y+1):PSET(X,Y-1):NEXT
100 I$="":PRINT@41,"<R> TO REDO":PRINT@81,"<L> TO LPRINT":PRINT@121,"<ENTER> TO END":INPUTI$
105 IFI$="R"THEN 5
110 IFI$<>"L"THEN END
200 INPUT"TITLE";T$:INPUT"X-AXIS";X$:INPUT"Y-AXIS";Y$:Y$=" "+Y$:CT=1
205 LPRINTTAB(15);T$
210 LPRINTTAB(15);DATE$:LPRINT
215 LPRINTTAB(10-LEN(STR$(Y2)));Y2;
220 LPRINTCHR$(27);CHR$(21) 'CR=CR
225 LPRINTCHR$(18) 'GRAPHICS MODE
230 FOR I1= 100 TO 0 STEP-7
235 LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(70);CHR$(255);
240 FORI2=1TON:Y=INT((Y(I2)-Y1)*YS):X=70+INT((X(I2)-X1)*XS)
245 IFY=I1+1THENP1=0:P2=129:GOSUB1000
250 IFY=I1THENP1=129:P2=131:GOSUB1000
255 IFY=I1-1THENP1=130:P2=135:GOSUB1000
260 IFY=I1-2THENP1=132:P2=142:GOSUB1000
265 IFY=I1-3THENP1=136:P2=156:GOSUB1000
270 IFY=I1-4THENP1=144:P2=184:GOSUB1000
275 IFY=I1-5THENP1=160:P2=240:GOSUB1000
280 IFY=I1-6THENP1=192:P2=224:GOSUB1000
285 IFY=I1-7THENP1=0:P2=192:GOSUB1000
290 NEXTI2
300 FORIY=Y1TOY2STEPYT 'Y-TICKS
305 IFINT((IY-Y1)*YS)=I1THENPY=129:GOSUB2000
310 IFINT((IY-Y1)*YS)=I1-1THENPY=130:GOSUB2000
315 IFINT((IY-Y1)*YS)=I1-2THENPY=132:GOSUB2000
320 IFINT((IY-Y1)*YS)=I1-3THENPY=136:GOSUB2000
325 IFINT((IY-Y1)*YS)=I1-4THEN PY=144:GOSUB2000
330 IFINT((IY-Y1)*YS)=I1-5THENPY=160:GOSUB2000
335 IFINT((IY-Y1)*YS)=I1-6THENPY=192:GOSUB2000
340 NEXTIY
345 IFI1<>2THEN360
350 LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(65);CHR$(28);CHR$(106);CHR$(132) 'X-AXIS
355 FORI=X1TOX2STEP XT:LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(INT(70+(I-X1)*XS));CHR$(136):NEXTI 'X-TICKS
360 LPRINTCHR$(10) 'CR
365 IFCT<=LEN(Y$)THENLPRINTCHR$(30):LPRINTTAB(8);MID$(Y$,CT,1):LPRINTCHR$(18):CT=CT+1 'Y-LABEL
370 NEXTI1
375 LPRINTCHR$(30) 'CR MODE
380 LPRINTTAB(10-LEN(STR$(Y1)));Y1
385 LPRINTCHR$(27);CHR$(22) 'CR+LF
390 LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(65);X1;CHR$(27);CHR$(16);CHR$(0);CHR$(160);X2
395 LPRINTTAB(14);X$
400 I$="":PRINT"<D> TO LIST DATA":PRINT"<R> TO RUN":INPUT"<ENTER> TO END";I$
405 IFI$="D"THENLPRINT:FORI=1TON:LPRINTTAB(10);I":";X(I);Y(I):NEXTI   
410 IFI$="R"THEN5
999 END
1000 LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(X-1);CHR$(P1)
1005 LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(X);CHR$(P2)
1010 LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(X+1);CHR$(P1)
1015 RETURN
2000 LPRINTCHR$(27);CHR$(16);CHR$(0);CHR$(69);CHR$(PY):RETURN

