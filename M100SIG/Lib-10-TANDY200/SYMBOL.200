5 CLS:PRINT@293,"CONCENTRATION":PRINT@326,"By Joe Wasserman 73117,3600"
10 GOSUB225:PRINT@411,"Instructions Y/N ";:I$=INPUT$(1)
15 IFI$="Y"ORI$="y"THEN235
20 CLS:PRINT@289,"No. of Players(1-4) ";:N$=INPUT$(1):N=VAL(N$):IFN<1ORN>4THEN20
25 CLS:LINE(15,12)-(158,123),1,B
30 FORZ=32TO140STEP18:LINE(Z,12)-(Z,123):NEXT
35 FORZ=28TO108STEP16:LINE(15,Z)-(158,Z):NEXT
40 FORZ=3TO24STEP3
45 FORY=80TO560STEP80
50 PRINT@Y+Z,CHR$(Y/80+48)CHR$(Z/3+48)
55 NEXTY,Z
60 FORZ=1TON:PRINT@68+Z*40,"Player"Z:NEXT
65 PRINT@308,CHR$(27)"p"" MIXING "CHR$(27)"q":POKE63277,PEEK(61983)
70 FORZ=1TOVAL(RIGHT$(TIME$,2)):R=RND(1):NEXT
75 RESTORE:FORZ=1TO28
80 READC
85 FORY=1TO2
90 A=INT(RND(1)*7)+1:B=INT(RND(1)*8)+1
95 IFP(A,B)=0THENP(A,B)=CELSE90
100 NEXTY,Z
105 P=1:BEEP
110 IFT=28THEN195
115 PRINT@67+P*40,CHR$(27)"p"" Player"PCHR$(27)"q"
120 PRINT@308,"Select ";
125 A$=INPUT$(2):IFINT(VAL(A$)/10)=0ORINT(VAL(A$)/10)>7ORVAL(RIGHT$(A$,1))<1ORVAL(RIGHT$(A$,1))>8THEN120
130 PRINT@315,A$:A=VAL(LEFT$(A$,1)):B=VAL(RIGHT$(A$,1))
135 IFP(A,B)=0THENPRINT@315,"USED":GOSUB225:PRINT@315,"    ":GOTO120
140 L=A*80+B*3:PRINT@L,STRING$(2,P(A,B))
145 PRINT@355,"";:B$=INPUT$(2)
150 IFINT(VAL(B$)/10)=0ORINT(VAL(B$)/10)>7ORVAL(RIGHT$(B$,1))=0ORVAL(RIGHT$(B$,1))>8ORB$=A$THENPRINT@355,"";:GOTO145
155 PRINT@355,B$:C=VAL(LEFT$(B$,1)):D=VAL(RIGHT$(B$,1))
160 IFP(C,D)=0THENPRINT@355,"USED":GOSUB225:PRINT@355,"    ":GOTO145
165 M=C*80+D*3:PRINT@M,STRING$(2,P(C,D))
170 G=G+1:IFP(A,B)<>P(C,D)THEN185
175 PRINT@431,"MATCH":BEEP:BEEP:GOSUB225:PRINT@431,"     ":PRINT@L,"  ":PRINT@M,"  "
180 S(P)=S(P)+1:T=T+1:P(A,B)=0:P(C,D)=0:PRINT@78+P*40,USING"##";S(P):PRINT@315,"  ":PRINT@355,"  ":GOTO110
185 PRINT@430,"NO MATCH":SOUND8866,5:GOSUB225:PRINT@430,"        ":PRINT@315,"  ":PRINT@355,"  "
190 PRINT@L,A$:PRINT@M,B$:PRINT@67+P*40," Player"P:P=1-P*(P<N):GOTO110
195 PRINT@429,"GAME OVER"
200 IFN=1THENPRINT@469,"You took":PRINT@508,G;"tries"
205 PRINT@548,"Play Again":PRINT@592,"Y/N"
210 I$=INKEY$:IFI$=""THEN210
215 IFI$<>"Y"THENMENU
220 S(1)=0:S(2)=0:S(3)=0:S(4)=0:G=0:T=0:GOTO25
225 FORZ=1TO750:NEXT:RETURN
230 DATA60,61,62,63,64,128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143,144,145,146,147,148,149,150
235 CLS:PRINT@10,"USE UPPER CASE ONLY":PRINT:PRINT"There are 28 pairs of symbols hidden on the board. ";
240 PRINT"Match pairs by revealing one pair at a time. Enter the number shown  for each square."
245 PRINT:PRINT"The player keeps his turn as long as he continues to make matches.":PRINT
250 PRINT"The game is more interesting if two or  more people play. If only one";
255 PRINT" plays the computer will tell you how many tries ittook to find all 28 pairs."
260 PRINT@609,"Press any key to Start";
265 IFINKEY$=""THEN265
270 GOTO20

