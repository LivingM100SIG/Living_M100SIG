0 ' SCROFF.200  Scroll toggle for LCD in T200 TERM mode.
1 ' (c)1987  Wilson Van Alst  [76576,2735]
10 CLS:GOSUB1000:IFT=40104THENN$="is NOT"ELSEN$="IS"
12 PRINT@322,"TELCOM <F6> function "N$" in use."
14 PRINT@90,"<1> Install":PRINT@130,"<2> Remove":PRINT@170,"<3> Menu":GOSUB5000
20 IFI<1ORI>3THENBEEP:GOTO10ELSECLS:ONIGOTO100,200,300
100 GOSUB1000:IFT<>40104THEN9999
110 CLS:PRINT"Installation options:":PRINT@85,"<1> Hide behind MENU key":PRINT@125,"<2> Hide behind RUN key":PRINT@165,"<3> Top of RAM":PRINT@205,"<4> You name it":PRINT@245,"<5> Cancel installation":GOSUB5000
120 IFI<1ORI>5THENBEEP:GOTO110ELSEONIGOTO130,140,150,160,10
130 A=61802:GOSUB170:GOTO10
140 A=61738:GOSUB170:GOTO10
150 A=HIMEM-10:GOSUB170:CLEAR256,A:GOTO10
160 CLS:PRINT:PRINT:INPUT"Install 10 bytes starting at ";A:GOSUB800:GOSUB170:GOTO10
170 GOSUB800:POKE62783,L:POKE62784,H:FORN=0TO9:READD:POKEA+N,D:NEXT:RESTORE:RETURN
200 CLS:PRINT@90,"Are you sure?":GOSUB5000:IFINSTR("Yy",I$)=0THEN10
210 GOSUB1000:POKE62783,168:POKE62784,156:FORN=0TO9:POKET+N,0:NEXT:GOTO10
300 MENU
800 H=INT(A/256):L=A-256*H:RETURN
1000 T=PEEK(62783)+256*PEEK(62784):RETURN
5000 I$=INKEY$:IFI$=""THEN5000ELSEI=VAL(I$):RETURN
8000 DATA 245,58,11,239,47,50,11,239,241,201
9999 PRINT@162,"Potential conflict!!":PRINT:PRINT"If you wish to proceed, run":PRINT"this program again to remove":PRINT"existing code; then run it to":PRINT"install the no-scroll feature.":PRINT:PRINT"Press any key to continue...":GOSUB5000:GOTO10
