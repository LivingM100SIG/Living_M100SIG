1 'By Peter Stanwyck 73117,2006
2 '   1 Kaiser Plaza, Suite 2050
3 '   Oakland, California 94612
4 'Licensed only for private use of CIS
5 'M100 Sig Members--Program and Doc-
6 'umentation not available for red-
7 'istribution in whole or in part.
8 '  (c)1986  ALL RIGHTS RESERVED
10 CLS:MAXFILES=2:DATE$=LEFT$(DATE$,6)+"86":GOTO34
11 FORA=1TO3000:NEXT:RETURN
12 IFMR=2THENRETURNELSECLOSE:OPENVFORINPUTAS1:RETURN
13 PRINT@280,Z2;Z;Z3;Z1;:RETURN
14 ONERRORGOTO32:PRINT@240,Z1:PRINTZ2;Z;Z3;" Which .DO File "Z1;:V="":PRINT@240,Y3;:INPUTV:IFV=""THEN34ELSEGOSUB12:CLS:RETURN
15 OPEN"TTEM"FOROUTPUTAS2:RETURN
16 PRINT@0,X+SPACE$(255-LEN(X));:RETURN
17 IFINSTR(V,".")=0THENV=V+".do"
18 KILLV:NAME"TTEM.DO"ASV:GOTO34
19 IFEOF(1)THENONMMGOTO17,113,24,105ELSEGOTO22
20 X=STRING$(A,10)
21 MM=3:GOSUB19:B=B+1:C=LEN(X)+C:PRINT@162,Z4;BTAB(20)Y4;C:GOTO21
22 LINEINPUT#1,X:A=INSTR(X,U):G=VAL(LEFT$(X,A)):RETURN
23 GOSUB22:GOTO16
24 B=B-1:IFE2>0THEN42ELSE81
25 C=INSTR(X,"'"):IFC>0THENRETURNELSEC=INSTR(X,U1):IFC=0THENC=256:RETURNELSERETURN
26 Z="Printer Ready"
27 CLS:Q=0:PRINT@240,Z" Y/N ";Z1:X=INPUT$(1):CLS:IFX="y"ORX="Y"THENQ=1ELSEIFQQ<2THEN34ELSERETURN
28 IFQQ=1THENRETURNELSECLS:PRINT@160,"Printing.":RETURN
29 A=INSTR(B,X,U):IFA=0ORA>CTHENRETURNELSED=INSTR(B,X,Z5):IFD=0THEND=256
30 IFA<DTHENX=LEFT$(X,A-1)+MID$(X,A+1):GOTO31ELSED=INSTR(D+1,X,Z5):IFD=0ORD=LEN(X)THENRETURNELSEB=D+1
31 IFM=2THEN29ELSEGOSUB16:GOTO29
32 IFERR=52ORERR=55THENA=0ELSEIFERR=7THENA=1ELSEIFERR=14THENA=2ELSEA=4
33 Z=MID$("File Error Need MemoryNeed StringBad Config Error ??",A*11+1,11):BEEP:CLS:PRINT@200,Z2"** "Z" **"Z3:GOSUB11:GOTO34
34 CLEAR750:GOSUB110
35 CLS:PRINT@162,"M100 Programming Tool Kit":PRINT@247,"by Peter Stanwyck":PRINT@300,"All Rights Reserved";:GOSUB11:CLS:FILES
36 KEYON:PRINT@280,"ReLn Pack List Fyte RKey Data Csum Menu";:ONKEYGOSUB38,61,66,80,106,99,102,109
37 GOTO37
38 M=1:Z="ReLine":GOSUB14:Y="Don't Renumber"+Z4:Z="<EN>=No Limit":GOSUB13:PRINT@0,X;V:PRINTY"<= ";:INPUTE3:IFE3=0THENE3=-1
39 PRINTY;">= ";:INPUTH1:PRINTZ1:Z="Deflt=x5 at#100":GOSUB13:PRINT@120,""
40 E1=100:E2=5:INPUT"Increments: ";E2:INPUT"Start Line# ";E1:IFH1=0THENH1=65000
41 IFE1<=E3THENA=3:GOTO33ELSEZ="Ready":QQ=1:GOSUB27:GOSUB21
42 IFFRE(0)-500<CTHENA=2:GOTO33
43 II=E1:DIMG(B),H(B):GOSUB12:FORC=0TOB:GOSUB22
44 IFG<=E3ORG>=H1THENH=GELSEH=II:II=II+E2
45 PRINT@162,"Old"Z4;GTAB(20)"New"Z4;H:G(C)=(G):H(C)=H:NEXT:CLOSE
46 GOSUB12:GOSUB15:FORC=0TOB:GOSUB23:X=MID$(STR$(H(C)),2)+MID$(X,A):GOSUB16
47 RESTORE:FORD=1TO7:A=1:READZ:GOSUB13:E=LEN(Z):GOSUB48:NEXT:ONMGOTO59,56
48 L=0:F=INSTR(A,X,Z):IFF=0THENRETURNELSEF=F+E:GOSUB54:IFL=1THENONMGOTO49,87ELSEONMGOTO53,90
49 FORK=0TOB:IFI=G(K)THEN50ELSENEXT:X1="??"+STR$(I):BEEP:GOTO51
50 X1=MID$(STR$(H(K)),2)
51 J=F+LEN(STR$(I))-2:IFJ=LEN(X)THENX2=""ELSEX2=MID$(X,J+1)
52 X=LEFT$(X,F-1)+X1+X2:GOSUB16:J=LEN(X1):GOSUB57:IFLL=1THEN49ELSE53
53 A=F+J:GOTO48
54 I=VAL(MID$(X,F)):IFI>0ORMID$(X,F,1)="0"THENL=1ELSEIFMID$(X,F,2)=" 0"THENL=1:F=F+1
55 IFL=1ANDMID$(X,F,1)=UTHENF=F+1
56 RETURN
57 LL=0:IFMID$(X,F+J,1)<>","THENRETURNELSEF=F+J
58 LL=1:F=F+1:IFMID$(X,F,1)=","THEN58ELSEI=VAL(MID$(X,F)):RETURN
59 PRINT#2,X:NEXT:CLOSE:GOTO17
60 DATAGOTO,GOSUB,THEN,ELSE,RESUME,RUN,RESTORE
61 Z=U2:GOSUB14:GOSUB15:Z="Delete "+U1:QQ=2:GOSUB27:M=Q
62 MM=1:GOSUB19:Y="":L=0:B=A+1:GOSUB16:GOSUB25:GOSUB29:GOTO63
63 IFC>0ANDM=1THEN65
64 PRINT#2,X:GOTO62
65 IFC<7THEN62ELSEX=LEFT$(X,C-1):GOTO64
66 Z="List":GOSUB14:GOSUB26:GOSUB79
67 C=2:D=1:E=0:F=2:MM=2:GOSUB19
68 J=INSTR(C,X,":"):K=INSTR(C,X,Z5):IFJ=0THEN72ELSEE=1
69 IFK=0ORJ<KTHENY=MID$(X,J):X=LEFT$(X,J-1):GOTO72
70 M=INSTR(K+1,X,Z5):IFM=0THENM=K
71 C=M+1:GOTO68
72 IFD=0THEN73ELSEN=70:B=2:X=LEFT$(X,A)+SPACE$(6-A)+MID$(X,A+1):GOTO74
73 N=64:B=7:IFE=1THENN=59:B=11
74 O=0:IFLEN(X)>NTHENO=1:Z=MID$(X,N+1):X=LEFT$(X,N)
75 LPRINTTAB(B)X:L=L+1:IFL=57THENLPRINTSTRING$(8,13):GOSUB79
76 IFO=0THEN78ELSEX=Z:Z="":IFB=2THENB=8ELSEIFE=1THENX=U+X
77 GOTO74
78 C=2:D=0:X=Y:Y="":IFJ=0THEN67ELSE68
79 P=P+1:LPRINTU;V" As of "DATE$UTIME$U"Page"P:LPRINT:L=2:B=2:RETURN
80 CLEAR1500:GOSUB110:M=2:Z="Fyte":GOSUB14:GOSUB21
81 Y2=": ":BB=B:CC=C:N=B+1:DIMX(N),A(N),G(N):GOSUB12
82 CLS:PRINTY3;V:PRINTY4;CC:PRINTY7;B:PRINTY6;INT(C/B):PRINTU2;Y2:PRINTU1;Y2
83 FORE=0TOBB:GOSUB22:G(E)=G:A(E)=LEN(X):PRINT@175,Z4;G;U;A(E)U:
84 GOSUB25:IFC=256THEN85ELSEIFC<7THENR=R+LEN(X):X="X"ELSER=R+LEN(MID$(X,C)):X=LEFT$(X,C-1)
85 B=6:L=LEN(X):GOSUB29:P=P+L-LEN(X):PRINT@165,P;U5:PRINT@204,R;U5:NEXT:GOSUB12
86 S="     ":FORC=0TOBB:GOSUB22:GOSUB47:NEXT:GOTO91
87 FORK=0TOBB:IFI=G(K)THEN88ELSENEXT:X(N)=X(N)+STR$(I):GOTO89
88 X(K)=X(K)+STR$(G(C))
89 J=LEN(STR$(I))-1:PRINT@175,"From:"G(C);S;S:PRINT@215,"To:";G(K);S
90 GOSUB57:IFLL=1THEN87ELSEA=F+J:GOTO48
91 CLS:GOSUB26:LPRINTTAB(2)Y3;V;U;TIME$;U;DATE$:LPRINT
92 LPRINTTAB(2)Y4;CC;U;Y7;BB;Y6INT(CC/BB)U;U2;P;U;U1;R:LPRINTTAB(2)"Line"TAB(9)"Bytes"TAB(20)"Branch"
93 LPRINTTAB(2)STRING$(70,"-"):LPRINT:L=6
94 FORE=0TOBB:IFL>57THENLPRINTSTRING$(66-L,13):L=1
95 LPRINTTAB(2)G(E);TAB(9)A(E);:X=X(E)
96 M=0:IFLEN(X)<56THEN98ELSEM=1:F=55
97 IFMID$(X,F,1)<>UTHENF=F+1:GOTO97ELSEY=MID$(X,F):X=LEFT$(X,F-1)
98 LPRINTTAB(16)X:L=L+1:IFM=1THENX=Y:GOTO96ELSENEXT:IFX(N)<>""THENLPRINT"  UL Errors: "X(N):GOTO113ELSEGOTO113
99 MR=2:Z="Data to":GOSUB14:OPENVFORAPPENDAS1
100 CLS:Z="Enter In "+V:GOSUB13:PRINT@0,"Sgle Entry-<EN> To Rtrn":PRINT
101 X="":LINEINPUT"Data ";X:IFX=""THEN34ELSEPRINT#1,X:GOTO100
102 Z="CHECKSUM":GOSUB14:CLS
103 MM=4:GOSUB19:E=E+1:IFLEN(X)=0THEN104ELSEFORB=1TOLEN(X):I=I+ASC(MID$(X,B)):NEXT
104 PRINT@162,Z4;E;TAB(15);Z;I:GOTO103
105 Z="Any Key":GOSUB13:X=INPUT$(1):GOTO34
106 CLS:RESTORE108:FORA=1TO8:IFA=6ORA=7THENZ=""ELSEREADZ:IFA=2ORA=3THENZ=Z+CHR$(34)ELSEZ=Z+CHR$(13)
107 KEYA,Z:NEXT:SCREEN0,1:GOSUB11:SCREEN0,0:GOTO34
108 DATAFiles,Load,Save,Run,List,Menu
109 MAXFILES=1:CLEAR256:MENU
110 DEFINTA-F,J-R:DEFSTRS-Z:Z4=" Line#:":Z1=CHR$(27):Z2=Z1+"p":Z3=Z1+"q"
111 Y3="File:":Y4="Size:":Y6="Avr:":Y7="Lines:"
112 Z1=Z1+CHR$(74):U1="REM":U=" ":U4="File":U2="Pack":Z5=CHR$(34):RETURN
113 LPRINTSTRING$(66-L,13):GOTO34
