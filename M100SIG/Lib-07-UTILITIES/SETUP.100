10 REM SETUP.100 by larry gensch, ESQ
20 REM Revised 'power on' set-up pgm
100 MAXFILES=0:IPL
110 CLS:SCREEN,1
120 Q$=CHR$(34):CR$=CHR$(13)
130 KEY1,"Cls:Files"+CR$
140 KEY2,"Clear256"+CR$
150 KEY3,"Edit"+CR$
160 KEY4,"Run"+CR$
170 KEY5,"List"+CR$
180 KEY6,"?Fre(0)Fre("+Q$+A$+")"+CR$
190 KEY7,"Run"+Q$+"Rfu"+Q$+CR$
200 KEY8,"Menu"+CR$
210 ONERRORGOTO500
220 PRINT"Date = "DATE$
230 LINEINPUT"Date:  ";A$
240 IFLEN(A$)>0THENDATE$=A$
250 PRINT"Time = "TIME$
260 LINEINPUT"Time:  ";A$
270 IFLEN(A$)>0THENTIME$=A$
280 PRINT"Day  = "DAY$
290 LINEINPUT"Day:   ";A$
300 IFLEN(A$)>0THENDAY$=A$
310 LINEINPUT"IPL: ";A$
320 IFLEN(A$)=0THEN400
330 IPLA$
400 P=0:INPUT"Power Off (in 1/10 sec): ";P
410 IFP=0THENPOWERCONTELSEPOWERP
499 MENU
500 IF(ERL=240)OR(ERL=270)OR(ERL=300)OR(ERL=410)OR(ERL=400)THEN550
510 ONERRORGOTO
520 PRINT"Line ";ERL": ";ERROR ERR
530 END
550 PRINT"Format error - Please Reenter"
560 IFERL=240THENRESUME230
570 IFERL=270THENRESUME260
580 IFERL=300THENRESUME290
590 IFERL=400THENRESUME400
600 IFERL=410THENRESUME400
