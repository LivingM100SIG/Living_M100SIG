0 REM - RECVER.100 by Russ Villanueva.    [70526,1434]. All rights reserved.
1 REM - File and Basic program; recovery is acomplished with this program.
2 REM - from a CAS: file that was made either before or after a cold start
3 REM - by the using the instructions in RECOVR.INS
4 REM - Known defects... Usually a '0' or other garbarage appears at the end
5 REM - of the recovered program and any line that starts with a REM will
6 REM - be ":REM" after recovery.  I don't know why.
10 CLS:MAXFILES=2:CLEAR300:DEFSNGP:DEFINTA-O,Q-Z:DIMT$(127):F=1:D$="LCD:
30 FORA=0TO127:READT$(A):NEXT
40 PRINT@120,"Press any key to begin File Recovery    from CAS:":CALL4811
50 INPUT"Output Device:Filename <LCD:>";D$
52 OPEND$FOROUTPUTAS2
102 OPEN"CAS:"FORINPUTAS1
104 Z$=INPUT$(20,1)
110 A=ASC(Z$):GOSUB400:GOTO122
120 Z$=RIGHT$(Z$,LEN(Z$)-1):IFLEN(Z$)<20THENZ$=Z$+INPUT$(20,1)
121 A=ASC(Z$):IFA=0 AND ASC(MID$(Z$,2,1))=0 THEN GOSUB 400
122 IF A=0 THEN Z$=RIGHT$(Z$,LEN(Z$)-3):PRINT#2,"":F1=1:F=1
124 IF F1 THEN GOSUB400
300 A=ASC(Z$):X$=LEFT$(Z$,1)
320 IFA=34ANDF=0THENF=1:GOTO330
321 IFA=34ANDF=1THENF=0
322 IFA=39THENF=0
330 IF A>127 AND F=1 THEN X$=T$(A-128)
332 IFA=142 THENF=0:IFMID$(Z$,2,1)=CHR$(255)THENZ$=RIGHT$(Z$,LEN(Z$)-1)
340 PRINT#2,X$;
344 GOTO120
400 PRINT#2,ASC(Z$)+ASC(MID$(Z$,2,1))*256;" ";:Z$=RIGHT$(Z$,LEN(Z$)-2)
402 F1=0:A=ASC(Z$):RETURN
1000 DATA END,FOR,NEXT,DATA,INPUT
1002 DATA DIM,READ,LET,GOTO,RUN
1004 DATA IF,RESTORE,GOSUB,RETURN,REM
1006 DATA STOP,WIDTH,ELSE,LINE,EDIT
1008 DATA ERROR,RESUME,OUT,ON,DSKO$
1010 DATA OPEN,CLOSE,LOAD,MERGE,FILES
1012 DATA SAVE,LFILES,LPRINT,DEF,POKE
1014 DATA PRINT,CONT,LIST,LLIST,CLEAR
1016 DATA CLOAD,CSAVE,TIME$,DATE$,DAY$
1018 DATA COM,MDM,KEY,CLS,BEEP,SOUND
1020 DATA LCOPY,PSET,PRESET,MOTOR,MAX
1022 DATA POWER,CALL,MENU,IPL,NAME
1024 DATA KILL,SCREEN,NEW,TAB(,TO
1026 DATA USING,VARPTR,ERL,ERR,STRING$
1028 DATA INSTR,DSKI$,INKEY$,CSRLIN,OFF
1030 DATA HIMEM,THEN,NOT,STEP,+,-,*,/,^
1032 DATA AND,OR,XOR,EQV,IMP,MOD,\,>,=
1034 DATA <,SGN,INT,ABS,FRE,INP,LPOS
1036 DATA POS,SQR,RND,LOG,EXP,COS,SIN
1038 DATA TAN,ATN,PEEK,EOF,LOC,LOF,CINT
1040 DATA CSNG,CDBL,FIX,LEN,STR$,VAL
1042 DATA ASC,CHR$,SPACE$,LEFT$,RIGHT$
1044 DATA MID$,'

