10 REM DIRSRT.100 (C) 1990 by P.Globman
20 CLEAR1000:DA=63930:ST=DA:CLS
30 PRINT@90,"Reading Directory
40 DIM F$(19)
50 FOR I=1 TO 19
60 IF PEEK(DA)<128THEN90 ELSE N=N+1
70 FOR J=0TO10
75 F$(N)=F$(N)+CHR$(PEEK(DA+J)):NEXTJ
80 F$(N)=MID$(F$(N)+F$(N),4,11)
90 DA=DA+11:NEXTI
100 PRINT@130,"Sorting Directory
110 M=N
120 M=INT(M/2):IFM=0THEN200
130 J=1:K=N-M
140 I=J
150 L=I+M:IF F$(I)<=F$(L)THEN180
160 T$=F$(I):F$(I)=F$(L):F$(L)=T$:I=I-M
170 IFI>0THEN150
180 J=J+1:IFJ>KTHEN120
190 GOTO140
200 PRINT@170,"Writing Directory
210 FOR I=1 TO N
220 F$(I)=MID$(F$(I)+F$(I),9,11)
230 FOR J=0TO10
240 POKE ST+J,ASC(MID$(F$(I),J+1,1))
250 NEXT J
260 ST=ST+11:NEXT I:IF I=20 THEN MENU
270 FOR J=ITO19:POKE ST,0:ST=ST+11:NEXT
280 MENU
