0 'Written for CPCG by Camz.
1 '
2 CLS:BEEP:PRINT:PRINT"CPCG - Calgary Portable Computer Group":PRINT:PRINT"  BBS (403) 291-9188":PRINT"  300/1200baud 24hrs":FORN=0TO1500:NEXT:BEEP:CLS
3 '
10 MAXFILES=3:CLEAR1400:PT=0
12 CLS:PRINT"Patcher v1.00 by Camz."
14 PRINT:PRINT"ORIGINAL File:";
16 LINEINPUTF$(0):IFF$(0)=""THENMENU
18 PRINT"UPDATED  File:";
20 LINEINPUTF$(1):IFF$(1)=""THENMENU
22 ONERRORGOTO300'Trap BAD Filenames
24 OPENF$(0)FORINPUTAS1
26 OPENF$(1)FORINPUTAS2
28 OPEN"PATCH.DO"FOROUTPUTAS3
30 ONERRORGOTO0'Turn ERROR Trap OFF
31 '
32 'Main Program
33 '
34 FORL=0TO1:GOSUB220:NEXTL
36 IFN$(0)=N$(1)THENGOTO48
38 IFN(0)<N(1)THENGOTO44
40 PRINT#3,N$(1);L$(1):PT=PT+1
42 L=1:GOSUB220:GOTO36
44 PRINT#3,N$(0):PT=PT+1
46 L=0:GOSUB220:GOTO36
48 IFL$(0)=L$(1)THENGOTO34
50 PRINT#3,N$(1);L$(1):PT=PT+1:GOTO34
199 '
200 'Break into Line Number & Remainder
201 '
210 N$(L)=MID$(STR$(VAL(L$(L))),2)
212 N(L)=VAL(N$(L))
214 L$(L)=MID$(L$(L),LEN(N$(L))+1)
216 RETURN
219 '
220 'Read a Line From A File
221 '
222 IFEOF(L+1)THENONL+1GOTO226,232
224 LINEINPUT#L+1,L$(L):GOTO200
226 IFEOF(2)THENGOTO400
228 LINEINPUT#2,L$(1):PRINT#3,L$(1)
230 PT=PT+1:GOTO226
232 IFEOF(1)THENGOTO400
234 L=0:LINEINPUT#1,L$(0):GOSUB200
236 PRINT#3,N$(0):PT=PT+1:GOTO232
299 '
300 'Bad File Name Error Routine
301 '
310 CLS:PRINT"Bad Filename..."
312 PRINT:PRINT"Press A Key ";
314 PRINTINPUT$(1):MENU
399 '
400 'Finished...
401 '
410 CLOSE1,2,3:CLS
412 IFPTTHENPRINTPT" Changes Made...":GOTO312
414 PRINT"No Changes..."
416 KILL"PATCH.DO":GOTO312
