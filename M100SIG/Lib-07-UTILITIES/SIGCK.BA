1 'SIGCK.BA (.DO file checksum and max. upload line len. check)
2 'Andrew Zarchy [73107,3455]
3 'adapted from DOWNCK.BA
10 MAXFILES=1:CLEAR 1000:DEFINTI:DEFDBLC:DEFSTRS
15 ON ERROR GOTO 90
20 CLS:FILES:PRINT:SF="":INPUT".DO File to check";SF:IFSF=""THENMENU
22 SF=SF+".DO":OPENSFFORINPUTAS1
25 PRINT"(c)hecksum   (l)ine length  ? ";:S=INPUT$(1):PRINTS:IFS="l"ORS="L"THEN100ELSEIFS<>"c"ANDS<>"C"THENBEEP:GOTO25
50 IFEOF(1)=0THENLINEINPUT#1,S:PRINT".";:IFLEN(S)=0THEN50ELSEFORI=1TOLEN(S):C=C+ASC(MID$(S,I)):NEXT:GOTO50
60 CLS:PRINTSF:PRINT:PRINTUSING" Checksum = #####,###";C:GOTO220
90 IFERL=22THENRUNELSEIFERL=50THENRESUME60ELSEONERRORGOTO0
100 DIMI(100):ONERRORGOTO200
110 LINEINPUT#1,S:PRINT".";:IFLEN(S)>131THENI(X)=VAL(S):X=X+1
120 GOTO110
200 IFERR<>54THENONERRORGOTO0
210 CLS:PRINTSF:PRINT
215 IFX=0THENPRINT" All lines 132 chars. or less"ELSEBEEP:PRINT"Lines over 132 chars.:":FORY=0TOX-1:PRINTI(Y);:NEXT
220 CLOSE:PRINT:PRINT"Press any key":S=INPUT$(1):RUN

