0 REM Examine/change memory by Joel Hassell [73126,1104]
5 DEFINTR-V:HX$="0123456789ABCDEF":CC=0:CT=0:CLS:E$=CHR$(27)
10 PRINT"Address ";:J$=""
12 I$=INPUT$(1):IFI$>CHR$(96)THENI$=CHR$(ASC(I$)-32)
13 IFLEN(J$)<4ANDINSTR(1,HX$,I$)THENJ$=J$+I$:PRINTI$;:GOTO12
14 IFI$=CHR$(8)ANDLEN(J$)THENJ$=LEFT$(J$,LEN(J$)-1):PRINTCHR$(8)+" "+CHR$(8);
15 IFI$<>CHR$(13)THEN12ELSEPRINT:GOSUB1000:I=J
20 IFI<0THENI=I+65536ELSEIFI>65535THENI=I-65536
25 K=I:IFK>32767THENK=K-65536
26 FORR=1TO4:PRINTMID$(HX$,(INT(K/16^(4-R))AND15)+1,1);:NEXT:AX$=""
30 FORK=ITOI+7:R=PEEK(K):PRINT" "MID$(HX$,R/16+1,1);MID$(HX$,(RAND15)+1,1);
60 IFR<32THENR=46
65 AX$=AX$+CHR$(R):NEXT:PRINT" "AX$;
68 IFCT<>0THENCT=CT-1:GOTO75
70 PRINT@CSRLIN*40+6+CC*3,"";:I$=INPUT$(1):IFI$>CHR$(96)THENI$=CHR$(ASC(I$)-32)
71 IFINSTR(1,HX$,I$)THENPOKEI+CC,(PEEK(I+CC)AND15)*16+INSTR(1,HX$,I$)-1:PRINT@CSRLIN*40,"";:GOTO20
72 T=ASC(I$):IFT=20ORT=23THENCT=7:I=I+192*(T=23):T=30
73 IFT=2ORT=26THENCT=7:I=I-192*(T=26):T=31
75 IFT=30THENI=I-8:IFCSRLIN=0THENPRINTE$"L";:GOTO20ELSEPRINT@(CSRLIN-1)*40,"";:GOTO20
76 IFT=31THENI=I+8:PRINT:GOTO20
77 IFT=28THENCC=CC+1:IFCC>7THENCC=0:I=I+8:PRINT:GOTO20
78 IFT=29THENCC=CC-1:IFCC<0THENCC=7:T=30:GOTO75
79 IFI$=CHR$(27)THEN0
90 GOTO70
1000 J=0
1010 IFJ$=""THENRETURNELSEJ=J*16+INSTR(1,HX$,LEFT$(J$,1))-1:J$=RIGHT$(J$,LEN(J$)-1):GOTO1010

