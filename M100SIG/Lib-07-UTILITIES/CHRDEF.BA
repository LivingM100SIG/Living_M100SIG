64000 REM CUSTOM CHARACTER PRINT VER 1.1.1 3/24/85 PRINTS AT CURRENT CURSOR POS
64002 REM RESTORE A 6 BYTE DATA STATEMENT DEFINING CHAR
64004 REM USES RZ%,CZ%,DZ%,IZ%,NZ%
64006 CZ%=POS(0):RZ%=CSRLIN 'ROW & COLUMN
64008 PRINT @(POS(0)+CSRLIN*40)," "; 'INCREMENT POSITION POINTER
64010 DZ%=2^(6*CZ%\50):IF RZ%>3 THEN DZ%=DZ%*32 'SELECT DRIVER
64012 CZ%=(CZ%*6)MOD 50 'COLUMN WITHIN DRIVER
64014 RZ%=RZ% MOD 4 'SELECT BANK
64016 CALL 30300 'TURN OFF BACKGROUND
64018 GOSUB 64034 'SET UP PORTS
64020 FOR IZ%=0 TO 5
64022 IF IZ%+CZ%>49 THEN GOSUB 64030
64024 READ NZ%:OUT 255,255 AND NZ%
64026 NEXT IZ%:CALL 29756 'ENABLE BACKGROUND
64028 RETURN 'END OF ROUTINE
64030 REM SELECT NEXT LCD DRIVER
64032 DZ%=DZ%*2:CZ%=0:GOSUB 64034:RETURN
64034 REM SET UP OUT-PORTS
64036 OUT 185,DZ% AND 255
64038 P1=INP(186) AND 252
64040 OUT 186,P1 OR (3 AND DZ%\256)
64042 OUT 254,64*(RZ% AND 3)+(63 AND CZ%):RETURN

