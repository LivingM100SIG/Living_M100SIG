0 'CHGHEX.BA - based on CHANGE.BA by Rick Perry
1 MAXFILES=1:ONERRORGOTO100
3 CLS:PRINT@80,"Hex file to translate";:INPUTI$:IFNOTINSTR(I$,".")THENI$=I$+".DO"
5 H$="0123456789ABCDEF":OPENI$FORINPUTAS1:INPUT#1,T,E,S:IFE>=MAXRAMTHENERRORELSEIFT>=HIMEMTHEN9
7 CLOSE:I$=I$+CHR$(0):FORJ=1TOLEN(I$):POKEJ-185,ASC(MID$(I$,J)):NEXT:CLEAR256,T:I$="":FORJ=1TO9:A=PEEK(J-185):IFA=0THENJ=9:NEXTELSEI$=I$+CHR$(A):NEXT:GOTO5
9 LINEINPUT".CO output file ";O$:N=0:ZC=T+E+S:CLS:PRINT@80,"Working...";
10 FORZ=TTOE
12 A$=INPUT$(1,1):I=INSTR(H$,A$)-1:IFI<0THEN12
14 N=NOTN:IFNTHENA=I:GOTO12ELSEA=16*A+I:ZC=ZC+A:POKEZ,A:PRINT".";:NEXT
16 LINEINPUT#1,A$:INPUT#1,Z:CLOSE
18 IFZ<>ZCTHENSOUND9394,8:PRINT"Checksum error!":END
20 BEEP:PRINT"Kill hex input file? ";:A$=INPUT$(1):PRINTA$:IFA$="Y"ORA$="y"THENKILLI$
22 IFO$=""THEN24ELSESAVEMO$,T,E,S
24 MAXFILES=0:MENU
100 CLS:PRINT"Error #";ERR;"in Line";ERL:END
