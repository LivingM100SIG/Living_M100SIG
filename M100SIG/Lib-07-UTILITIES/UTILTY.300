10 CLEAR2000:MAXFILES=1:CLS:E$=CHR$(27):RV$=E$+"p":NV$=E$+"q":T$=SPACE$(40):KEYON:GOSUB12000
11 KEY1,"":KEY2,"Schd":KEY3,"Date":KEY4,"Alrm":KEY5,"":KEY6,"Mem":KEY7,"File":SCREEN,1
210 ONERRORGOTO10000:ONKEYGOSUB,2000,3000,4000,,6000,7000,8000
250 C$=INKEY$:GOSUB15000:PRINT@110,DATE$:GOTO250
1000 KEY8,"Menu"+CHR$(13)+CHR$(254)+CHR$(13)+CHR$(245)+CHR$(62)+CHR$(10)+CHR$(204)+CHR$(63)+CHR$(109)+CHR$(241)+CHR$(201)
1001 A=64228:I$=CHR$(27)+"p":N$=CHR$(27)+"q"
1002 F$=" Off ":P=PEEK(A)+256*PEEK(A+1):IFP=63615THENF$=" On "
1003 CLS:PRINT:PRINT"Extra Line Feed "I$F$N$:PRINT:PRINT,"(E)nable",,"(D)isable",,"(M)enu
1004 ONINSTR("eEdDmM",INPUT$(1))GOTO1005,1005,1006,1006,10,10:GOTO1004
1005 POKEA,127:POKEA+1,248:GOTO1002
1006 POKEA,243:POKEA+1,127:GOTO1002
2000 OPEN"NOTE"FORINPUTAS1
2030 INPUT#1,A$:IFINSTR(1,A$,MID$(DATE$,1,5))THEN2060
2040 IFEOF(1)THEN2080ELSEGOTO2030
2060 IFG<1THENCLS
2061 G=G+1:PRINTA$:GOTO2040
2080 CLOSE#1:IFG<1THEN210ELSEFORI=1TO1000:NEXT:GOTO10
3000 PRINT@202,"Date= "DATE$:PRINT@162,"Day = "DAY$:PRINT@242,"Time= "TIME$;
3010 PRINT@184,"";:LINEINPUT"Day= ";A$:IFLEN(A$)>0THENDAY$=A$
3020 PRINT@224,"";:LINEINPUT"Date= ";A$:IFLEN(A$)>0THENDATE$=A$
3030 PRINT@264,"";:LINEINPUT"Time= ";A$:IFLEN(A$)>0THENTIME$=A$
3035 GOTO10
4000 DEFSTRU-V:V=TIME$:PRINT@162,"";:LINEINPUT"SOUND ALARM (HH:MM) ";U:IFLEN(U)<1GOTO10ELSEU=U+":00":TIME$=U:TIME$=V
4049 GOSUB15000:V=LEFT$(TIME$,5):IFINSTR(U,V)=0THEN4049ELSECLS
4050 PRINT@250,"Any key to silence"
4051 FORI=375TO1950STEP12:X$=INKEY$:IFLEN(X$)>0GOTO10ELSESOUNDI,3:NEXT:D=D+1:IFD=15THEN10ELSEGOTO4051
6000 Y=FRE(0):Y=Y+2240:PRINT@211,RV$;Y;"BYTES FREE";NV$:FORI=1TO750:NEXT:C=211:C1=15:GOSUB13000:RETURN
7000 CLEAR1000:GOSUB7900:CLS:DEFINTG-Z:DEFSTRC-F:DIMF(23),A(23)
7001 FORX=-1408TO-1639STEP-11:IFPEEK(X)THENFORZ=0TO7:F(Y)=F(Y)+CHR$(PEEK(X+3+Z)):NEXT:GOSUB7170
7002 NEXT:F(Y-1)="NonameBF":F(Y-2)="Paste BF":X=-1103:GOSUB7170:Y=Y-1:X=Y\2
7003 FORZ=0TOY-X:W=Z
7004 IFA(W)>A(W+X)THENB=A(W):A(W)=A(W+X):A(W+X)=B:E=F(W):F(W)=F(W+X):F(W+X)=E:W=W-X:IFW>-1THEN7004
7005 NEXT:X=X\2:IFXTHEN7003ELSEF(Y)="Reserved":F(Y+1)="File  BF":A(Y+1)=A(Y)+MAXRAM-HIMEM:Y=Y+2:A(Y)=A(Y-1)+MAXFILES*267:Z=0
7155 IFW=99THENW=0:Z=0
7156 CLS:FORW=ZTO9+Z:W$=STR$(W+1)+".":W$=RIGHT$(W$,3):PRINT@(W-Z)*20,W$;LEFT$(F(W),6)"."RIGHT$(F(W),2);
7157 PRINTUSING" #####";A(W+1)-A(W);:IFW+1=YTHENW=98
7160 NEXT:GOSUB7950
7170 A(Y)=PEEK(X+2)*256+PEEK(X+1):Y=Y+1:RETURN
7200 GOSUB7960:PRINT@201,"KILL "MID$(F(Q),1,6);:GOSUB7975:KILLF(Q):GOTO7000
7300 GOSUB7960:PRINT@201,"RENAME "MID$(F(Q),1,6)" AS ";
7301 LINEINPUTQ$:IFLEN(Q$)>6THEN7301ELSEIFLEN(Q$)=0THEN7000
7302 NAMEF(Q)ASQ$+C:GOTO7000
7400 GOSUB7985:POKEX,PEEK(X)OR8:GOTO7000
7500 GOSUB7985:POKEX,PEEK(X)AND247:GOTO7000
7900 KEY1,"More":KEY2,"Kill":KEY3,"Name":KEY4,"Invi":KEY5,"Visi":KEY6,"":KEY7,"":RETURN
7950 Z=W:KEYON:ONKEYGOSUB7155,7200,7300,7400,7500,,,10
7955 E=INKEY$:GOTO7955
7960 PRINT@205,"# WANTED ";:LINEINPUTQ$:Q=VAL(Q$):IFQ<1ORQ>YTHEN7960ELSEQ=Q-1
7961 C="."+RIGHT$(F(Q),2):IFINSTR(".CO.DO.BA",C)=0THEN7155ELSEC1=MID$(F(Q),1,6):RETURN
7975 PRINT" Y OR N ? ";
7976 ONINSTR("yYnN",INPUT$(1))GOTO7980,7980,7155,7155:GOTO7976
7980 RETURN
7985 GOSUB7960:A$="":FORX=-1614TO-1398:A$=A$+CHR$(PEEK(X)):NEXT:C$=C1+C
7987 X=INSTR(C$,"."):D$=LEFT$(LEFT$(C$,X-1)+"       ",6)
7988 D$=D$+RIGHT$(C$,2):X=INSTR(A$,D$):X=X-1618:RETURN
8000 CALL23164,0,23366:CALL27795:MAXFILES=0:CLEAR256:MENU
10000 BEEP:IF(ERL=3010)OR(ERL=3020)OR(ERL=3030)OR(ERL=4001)THEN11000
10010 CLS:PRINT@170,"Line ";ERL":ERROR";ERR:GOTO10
11000 PRINT@162,"Format error ":FORI=1TO800:NEXT:GOSUB13000
11005 IFERL=3010THENRESUME3010
11010 IFERL=3020THENRESUME3020
11015 IFERL=3030THENRESUME3030
11016 IFERL=4001THENRESUME4000
12000 CLS:LINE(0,0)-(237,24),1,B:PRINT@52,RV$;SPACE$(16);:PRINT@92," SYSTEM UTILITY ";NV$"
12001 PRINT@43,RV$;"TIME:";:PRINT@71,"DATE:";NV$:RETURN
13000 PRINT@C,MID$(T$,1,C1):PRINT@224,MID$(T$,1,15):PRINT@264,MID$(T$,1,15);:RETURN
15000 PRINT@82,TIME$:RETURN

