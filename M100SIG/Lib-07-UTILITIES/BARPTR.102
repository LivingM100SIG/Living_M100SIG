1 ' BARPTR.101 Copyright 1984 by BarUser
2 ' (CIS I.D.72126,1735) Use granted ONLY to 100 Sig members!!!!
10 CLS:CLEAR2000:DIMA$(255):MAXFILES=2
15 PRINT@46,"Materials Handling Software":LINE(32,5)-(199,18),1,B
20 DEFINTI,Z:FORZ=20TO219:X1=(RND(1)+.5):LINE(Z,35)-(Z,55),X1:NEXT:PRINT@126," Barcode Printer 1984 ":BEEP
25 FORI=280TO315:PRINT@I,"I";:FORZ=1TO60:NEXTZ:PRINT@I," ";:NEXT:PRINT@315,"I";
30 CLS:PRINT@88,"Loading system ......"
35 CLEAR2000:DIMA$(255)
40 FORTY=1TO44:READB$:T$=T$+B$
45 FORY=1TO5:READB(Y):NEXT:FORY=1TO5:READS(Y):NEXT
50 T=ASC(B$)
55 FORY=1TO5:A$(T)=A$(T)+STRING$(B(Y),255):A$(T)=A$(T)+STRING$(S(Y),128):NEXT:NEXT
60 CLS:H$="":PRINT@1,"PRINTER":PRINT@81,SPACE$(7):PRINT@162,"SETUP":PRINT@241,SPACE$(7)
65 LINE(2,13)-(51,25),1,B:LINE(2,45)-(51,57),1,B
70 P$(1)="DMP-420":P$(2)="DMP2100":P$(3)="DMP2150":T=2
75 PRINT@81,P$(T)
80 AA$=INKEY$:IFAA$=""THEN80
85 IFAA$=CHR$(32)THEN95
90 IFAA$=CHR$(13)THEN105
95 T=T+1:IFT>3THENT=1
100 GOTO75
105 S$(1)="FILE   ":S$(2)="CUSTOM ":S$(3)="SEQUENT":T=1
110 PRINT@241,S$(T)
115 AA$=INKEY$:IFAA$=""THEN115
120 IFAA$=CHR$(32)THEN130
125 IFAA$=CHR$(13)THEN140
130 T=T+1:IFT>3THENT=1
135 GOTO110
140 ONTGOTO145,225,275
145 PRINT@93,"FILE NAME":LINE(80,29)-(125,41),1,B
150 PRINT@174,SPACE$(6)
155 ONERRORGOTO175
160 T=174:GOSUB345
165 NM$=H$:H$="":NM$=NM$+".DO"
170 OPENNM$FORINPUTAS1:GOTO180
175 BEEP:PRINT@174,SPACE$(6):NM$="":RESUME150
180 PRINT@103,"ALPHA?":LINE(140,29)-(152,41),1,B:T=184:GOSUB345:PA$=H$:IFPA$=""THENPA$="Y"
185 IFPA$="Y"ORPA$="N"THENPRINT@T,PA$ELSEBEEP:H$="":PRINT@T," ":GOTO180:H$=""
190 GOSUB410
195 IFPA$="Y"THEN200ELSE215
200 IFEOF(1)THENCLOSE:GOTO60
205 LINEINPUT#1,A$:IFEOF(1)THENCLOSE:GOTO60
210 LINEINPUT#1,B$:GOSUB475:GOTO200
215 IFEOF(1)THENCLOSE:GOTO60
220 LINEINPUT#1,A$:B$=A$:GOSUB475:GOTO215
225 PRINT@16,"Custom Label Creation":PRINT@93,"  Text":PRINT@175,"Code"
230 HD$="":T=100:XT=14:XB=24:XR=235:SW=1:GOSUB340:GOSUB345:HD$=H$:H$=""
235 CD$="":T=180:XT=30:XB=40:XR=185:SW=1:GOSUB340:GOSUB345:CD$=H$:H$=""
240 PRINT@191,"Copies":LINE(224,30)-(236,40),1,B:T=198
245 GOSUB345:CP$=H$:IFCP$=""THENCP$="1"
250 IFCP$<"1"ORCP$>"9"THENBEEP:H$="":CP$="":PRINT@T," ":GOTO240
255 PRINT@198,CP$:H$="":GOSUB410
260 FORB=1TOVAL(CP$):A$=CD$
265 IFHD$=""THENB$=A$ELSEB$=HD$
270 GOSUB475:NEXT:GOTO60
275 PRINT@56,"Width":LINE(128,5)-(140,17),1,B:PRINT@66,"Skip"
280 T=62:JZ$="A":GOSUB345:WD$=H$:H$=""
285 LINE(182,5)-(196,17),1,B:T=71:GOSUB345:SK$=H$:IFSK$=""THENSK$="1"
290 PRINT@71,SK$:H$=""
295 FW=VAL(WD$):FL=(FW+1)*6
300 PRINT@130,"Start":PRINT@146,"End"
305 LINE(92,21)-(92+FL,33),1,B:LINE(176,21)-(176+FL,33),1,B
310 T=136:GOSUB345:GOSUB330:ST$=H$:H$=""
315 T=150:GOSUB345:GOSUB330:ED$=H$:H$="":IFED$<ST$THENBEEP:PRINT@150,SPACE$(LEN(ED$)):GOTO315
320 PRINT@219,"Copies":LINE(152,37)-(172,48),1,B:T=226:H$="":GOSUB345:CP$=H$:IFCP$=""THENCP$="1":PRINT@T,CP$:H$="":JZ$=""
325 GOSUB410:GOTO425
330 IFLEN(H$)<FWTHENH$="0"+H$:PRINT@T,H$:GOTO330
335 RETURN
340 LINE(115,XT)-(XR,XB),SW,B:RETURN
345 A$=INKEY$:IFA$=""THEN345
350 IFA$=CHR$(27)THEN400
355 IFA$=CHR$(13)THENRETURN
360 IFA$=CHR$(8)THEN390
365 IFA$=CHR$(30)THENCLOSE:CLEAR:MENU
370 IFA$=CHR$(32)THEN385
375 IFA$<CHR$(48)ORA$>CHR$(91)THENA$="":BEEP:GOTO345
380 IFA$<"0"ANDJZ$="A"ORA$>"9"ANDJZ$="A"THENA$="":BEEP:GOTO345
385 H$=H$+A$:L=LEN(H$):GOTO405
390 L=LEN(H$):IFL>0THENHH$=LEFT$(H$,L-1)
395 H$=HH$:HH$="":GOTO405
400 H$=""
405 PRINT@(T),SPACE$(L):PRINT@(T),H$:GOTO345
410 PRINT@298,"ESC=>redo ENTER=>cont";
415 A$=INKEY$:IFA$=""THEN415
420 IFA$=CHR$(27)THENCLEAR:GOTO60ELSEGOTO770
425 ST=VAL(ST$):ED=VAL(ED$):CP=VAL(CP$):SK=VAL(SK$)
430 FORB=STTOEDSTEPSK
435 FORC=1TOCP
440 A$=STR$(B)
445 A$=MID$(A$,2)
450 IFLEN(A$)<VAL(WD$)THENA$="0"+A$:GOTO450
455 IFPA$=""ORPA$="Y"THENB$=A$ELSEB$=""
460 GOSUB475
465 NEXT:NEXT
470 GOTO60
475 A$="*"+A$+"*":B$="*"+B$+"*"
480 LPRINTCHR$(18);
485 FORYU=1TO4
490 FORT=1TOLEN(A$)
495 LPRINTA$(ASC(MID$(A$,T,1)));
500 NEXT
505 LPRINT
510 NEXT
515 LPRINT
520 LPRINTCHR$(30);
525 LPRINT
530 LPRINTB$:LPRINT
535 LPRINTCHR$(27)+CHR$(28)
540 LPRINT
545 RETURN
550 DATA"1",2,1,1,1,2,1,2,1,1,1
555 DATA"2",1,2,1,1,2,1,2,1,1,1
560 DATA"3",2,2,1,1,1,1,2,1,1,1
565 DATA"4",1,1,2,1,2,1,2,1,1,1
570 DATA"5",2,1,2,1,1,1,2,1,1,1
575 DATA"6",1,2,2,1,1,1,2,1,1,1
580 DATA"7",1,1,1,2,2,1,2,1,1,1
585 DATA"8",2,1,1,2,1,1,2,1,1,1
590 DATA"9",1,2,1,2,1,1,2,1,1,1
595 DATA"0",1,1,2,2,1,1,2,1,1,1
600 DATA"A",2,1,1,1,2,1,1,2,1,1
605 DATA"B",1,2,1,1,2,1,1,2,1,1
610 DATA"C",2,2,1,1,1,1,1,2,1,1
615 DATA"D",1,1,2,1,2,1,1,2,1,1
620 DATA"E",2,1,2,1,1,1,1,2,1,1
625 DATA"F",1,2,2,1,1,1,1,2,1,1
630 DATA"G",1,1,1,2,2,1,1,2,1,1
635 DATA"H",2,1,1,2,1,1,1,2,1,1
640 DATA"I",1,2,1,2,1,1,1,2,1,1
645 DATA"J",1,1,2,2,1,1,1,2,1,1
650 DATA"K",2,1,1,1,2,1,1,1,2,1
655 DATA"L",1,2,1,1,2,1,1,1,2,1
660 DATA"M",2,2,1,1,1,1,1,1,2,1
665 DATA"N",1,1,2,1,2,1,1,1,2,1
670 DATA"O",2,1,2,1,1,1,1,1,2,1
675 DATA"P",1,2,2,1,1,1,1,1,2,1
680 DATA"Q",1,1,1,2,2,1,1,1,2,1
685 DATA"R",2,1,1,2,1,1,1,1,2,1
690 DATA"S",1,2,1,2,1,1,1,1,2,1
695 DATA"T",1,1,2,2,1,1,1,1,2,1
700 DATA"U",2,1,1,1,2,2,1,1,1,1
705 DATA"V",1,2,1,1,2,2,1,1,1,1
710 DATA"W",2,2,1,1,1,2,1,1,1,1
715 DATA"X",1,1,2,1,2,2,1,1,1,1
720 DATA"Y",2,1,2,1,1,2,1,1,1,1
725 DATA"Z",1,2,2,1,1,2,1,1,1,1
730 DATA"-",1,1,1,2,2,2,1,1,1,1
735 DATA".",2,1,1,2,1,2,1,1,1,1
740 DATA" ",1,2,1,2,1,2,1,1,1,1
745 DATA"*",1,1,2,2,1,2,1,1,1,1
750 DATA"$",2,2,2,2,2,3,3,3,1,1
755 DATA"/",2,2,2,2,2,3,3,1,3,1
760 DATA"+",2,2,2,2,2,3,1,3,3,1
765 DATA"%",2,2,2,2,2,1,3,3,3,1
770 BEEP:LPRINTCHR$(30);
775 LPRINTCHR$(27)+CHR$(20);
780 LPRINT;
785 LPRINTCHR$(18);
790 LPRINTCHR$(30);:LPRINTCHR$(27)+CHR$(28);
795 CLS:BEEP:PRINT"   Let's Double-check the Printer.....":PRINT:PRINT"  Please make sure that the print head"
800 PRINT"  is lined up to the TOP of the label":PRINT:PRINT"   Press any key when you're ready "
805 ZP$=INKEY$:IFZP$=""THEN805
810 CLS:PRINT@130,"Printing....."
815 RETURN

