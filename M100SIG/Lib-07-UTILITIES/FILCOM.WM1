0 REM FILCOM.WM1 12/26/83 Woods Martin [70235,232]
1 MAXFILES=2:CLEAR1000
2 E$=CHR$(27)
3 REM SU$=E$+"@"+E$+"B"+CHR$(2)+E$+"M"+CHR$(10)+E$+"N"+CHR$(10):rem Gemini/Epson
4 GOSUB 210
9 ONERRORGOTO50:GOTO100
10 IFL1>=0THEN LINEINPUT#1,L1$:S%=INSTR(L1$," "):L1=VAL(LEFT$(L1$,S%))
15 RETURN
20 IFL2>=0THENLINEINPUT#2,L2$:S%=INSTR(L2$," "):L2=VAL(LEFT$(L2$,S%))
25 RETURN
50 IF ERL=10THENL1=-1:RESUME15ELSEIFERL=20THENL2=-1:RESUME20ELSEONERRORGOTO0
100 GOSUB10:GOSUB20
110 IFL1<0ANDL2<0THEN300ELSEIFL1$=L2$THEN100ELSELPRINTF1$
112 IFL1>=0ANDL1<L2THENLPRINTL1$:GOSUB10:GOTO112ELSEIFL1$=L2$THEN120ELSELPRINTL1$:LPRINTF2$
114 IFL2>=0ANDL1>L2THENLPRINTL2$:GOSUB20:GOTO114ELSEIFL1$<>L2$THENLPRINTL2$
120 LPRINT" ":GOTO100
130 GOTO 110
210 CLS:PRINT"FILCOM":FILES
220 PRINT@0,"RAM: .DO FILE NAME";:INPUTF1$:IFINSTR(F1$,":")ORINSTR(F1$,".")THENBEEP:GOTO4
230 F1$="RAM:"+F1$+".DO":OPENF1$FORINPUTAS1
240 PRINT@0,"DEV:FILNAM  for comparision";:INPUTF2$:OPENF2$FORINPUTAS2
260 CLS:PRINT "FILCOM - ";F1$;" vs ";F2$:LPRINTSU$;"FILCOM - ";F1$;" vs ";F2$;"    ";DATE$:LPRINT" "
270 RETURN
300 LPRINT"COMPLETE":PRINT"COMPLETE":CLOSE:END
