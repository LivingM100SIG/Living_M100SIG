5 CLS:CLEAR0:CLEARFRE(0)-1000:DEFINTA-Z
10 FILES:A$="File to sort:":GOSUB210
15 INPUTF$
20 IFNOT(INSTR(F$,"."))THENF$=F$+".DO"
25 OPENF$FORINPUTAS1:LINEINPUT#1,Z$
30 P=INSTR(Z$,CHR$(9)):IFPTHENMID$(Z$,P)=CHR$(239):GOTO30
35 CLS
40 PRINT@40,"----.----1----.----2----.----3----.----4"Z$
45 A$="First Sort Field (start,end)":GOSUB210
50 INPUTA,B
55 IFA=0ORB<ATHEN45
60 A$="Second Sort Field (start,end)":GOSUB210' <ENTER> if none
65 INPUTC,D:IFC=0ANDD=0THENC=A:D=B
70 A$="HIGH first or LOW first (H,L)":GOSUB210
75 INPUTB$:IFINSTR("HhLl",B$)=0THEN70
80 A$="Scanning":GOSUB210:N=1
85 N=N+1:LINEINPUT#1,Z$:IFNOTEOF(1)THEN85
90 CLOSE
95 IFB$="H"ORB$="h"THENF=N:G=1:H=-1:ELSEF=1:G=N:H=1
100 DIMD$(N):PRINT@160,N"Items to sort"
105 A$="Loading":GOSUB210
110 OPENF$FORINPUTAS1
115 FORI=1TON:LINEINPUT#1,D$(I):NEXT:CLOSE
120 A$="Sorting":GOSUB210:K=B-A+1:L=D-C+1
125 V=N
130 V=V/2:PRINT@180,USING"###<finished at 0";V
135 IFV=0THENA$="Finished. Saving to File":GOSUB210:GOTO195
140 W=1:X=N-V
145 Y=W
150 Z=Y+V
155 IFMID$(D$(Y),A,K)+MID$(D$(Y),C,L)<MID$(D$(Z),A,K)+MID$(D$(Z),C,L)THEN180
160 X$=D$(Y):D$(Y)=D$(Z):D$(Z)=X$
165 Y=Y-V
170 IFY<1THEN180
175 GOTO150
180 W=W+1
185 IFW>XTHEN130
190 GOTO145
195 KILLF$:OPENF$FOROUTPUTAS1
200 FORI=FTOGSTEPH:PRINT#1,D$(I):NEXT:CLOSE
205 MENU
210 PRINT@240,CHR$(27)"K"A$;:RETURN

