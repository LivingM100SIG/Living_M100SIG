1 'A "universal" Line Feed Utility which implements Hugo Ferreyra's FIXLF.HF1 and Don Corbitt's TELCOM linefeed patch
2 'in a single, menu-driven utility. It is a module of my current on-board Utility program, hence the structure.
3 'Note that FIXLF.HF1 is copyrighted by Hugo, and released "for non-commercial use only".
4 '                Phil Wheeler:4/14/84
5 '
7 GOTO20
8 MAXFILES=1:CLEAR256,MAXRAM:DATE$=LEFT$(DATE$,6)+"84":MENU
20 CLS:RV$=CHR$(27)+"p":NO$=CHR$(27)+"q":A=64228:B=63066
22 KEY8,"Menu"+CHR$(13)+CHR$(254)+CHR$(13)+CHR$(245)+CHR$(62)+CHR$(10)+CHR$(204)+CHR$(63)+CHR$(109)+CHR$(241)+CHR$(201)
24 PRINT:PRINT"Select Option:":PRINT:PRINT,"P)rnt LF",,"C)omm LF",,"T)op Menu"
26 ONINSTR("pPcCtT",INPUT$(1))GOTO30,30,40,40,8,8:GOTO26
30 CLS:F$=" Off ":P=PEEK(A)+256*PEEK(A+1):IFP=63615THENF$=" On "
32 PRINT:PRINT"Printer LF"RV$F$NO$:PRINT:PRINT,"E)nable",,"D)isable",,"T)op Menu"
34 ONINSTR("eEdDtT",INPUT$(1))GOTO36,36,38,38,8,8:GOTO34
36 POKEA,127:POKEA+1,248:GOTO30
38 POKEA,243:POKEA+1,127:GOTO30
40 CLS:F$=" Off ":IFPEEK(B)<>0THENF$=" On "
42 PRINT:PRINT"Upload LF"RV$F$NO$:PRINT:PRINT,"E)nable",,"D)isable",,"T)op Menu"
44 ONINSTR("eEdDtT",INPUT$(1))GOTO46,46,48,48,8,8:GOTO44
46 POKEB,1:GOTO40
48 POKEB,0:GOTO40

