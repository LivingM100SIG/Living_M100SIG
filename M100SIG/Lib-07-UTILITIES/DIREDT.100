10 'Model 100 Directory Editor
20 'Address any comments to Phillip
30 'Wherry [70007,567] on the Model
40 '100 SIG (NOT via EMAIL)
50 CLS:DEFINTA-Z:BA=-1694:RV$=CHR$(27)+"p":NR$=CHR$(27)+"q":A=0
60 BB=BA+A*11
70 NF$="":FORB=3TO10:NF$=NF$+CHR$(PEEK(BB+B)):NEXTB
75 PRINTCHR$(27);"H[F] - Filename:     ";RV$;:PRINTUSING" \     \.\\ ";LEFT$(NF$,6);MID$(NF$,7);:PRINTNR$
80 PRINT"[S] - Status:       ";RV$;" ";:IF PEEK(BB)AND128THENPRINT"Valid   "ELSEPRINT"Invalid "
90 PRINTNR$;"[A] - ASCII:        ";RV$;" ";:IFPEEK(BB)AND64THENPRINT"Yes ";NR$ELSEPRINT"No  ";NR$
100 PRINT"[M] - Machine code: ";RV$;" ";:IFPEEK(BB)AND32THENPRINT"Yes ";NR$ELSEPRINT"No  ";NR$
110 PRINT"[R] - ROM file:     ";RV$;" ";:IFPEEK(BB)AND16THENPRINT"Yes ";NR$;ELSEPRINT"No  ";NR$;
115 PRINT"    [E]-exit"
120 PRINT"[I] - Invisible:    ";RV$;" ";:IFPEEK(BB)AND8THENPRINT"Yes ";NR$;ELSEPRINT"No  ";NR$;
125 PRINT"    Cmd: _";:CP=CSRLIN*40+POS(0)-1:PRINT
130 PRINT
140 IF A<26 THEN PRINT"[+] - next file";ELSEPRINT"                ";
150 IF A>0 THEN PRINTTAB(20);"[-] - previous file";ELSEPRINTCHR$(27);"K";
160 A$=INKEY$:IFA$=""THEN160
165 IFA$>" "THENPRINT@CP,A$;
170 ONINSTR("FfSsMmAaRrIi+=-QqEe",A$) GOTO 190,190,280,280,280,280,280,280,280,280,280,280,300,300,310,320,320,320,320
180 BEEP:GOTO 160
190 PRINT@240,"New filename: ";:LINEINPUTL$
200 PRINT@240,CHR$(27);"K";:IFLEN(L$)>9THENBEEP:GOTO190
205 IF L$="" THEN 160
210 X=INSTR(L$,"."):IFX=0ANDLEN(L$)>6THENBEEP:GOTO190
215 IFX=0THENL$=L$+".  ":GOTO210
220 L1$=LEFT$(L$,X-1):L2$=MID$(L$,X+1)
230 IFL1$=""ORL2$=""ORINSTR(L1$,".")ORINSTR(L2$,".")THENBEEP:GOTO190
240 IFLEN(L1$)>6ORLEN(L2$)>2THENBEEP:GOTO190
250 L1$=L1$+SPACE$(6-LEN(L1$))
260 IFLEN(L2$)=1THENL2$=L2$+" "
270 L$=L1$+L2$:FORZ=3TO10:POKEBB+Z,ASC(MID$(L$,Z-2,1)):NEXTZ:GOTO70
280 IFA$>="a"THENA$=CHR$(ASC(A$)-32)
285 X=2^(INSTR("IRMAS",A$)+2)
290 POKE BB,PEEK(BB)XORX:GOTO70
300 A=A-(A<26):GOTO60
310 A=A+(A>0):GOTO60
320 MENU
