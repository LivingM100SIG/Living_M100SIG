1 'LPTCOM - Redirect LPT I/O to COM
2 ' Copyright (C) 1987,1988  Stanley Wong
3 ' Version 1.0 : 29 September 1988
4 '
10 KEY7,CHR$(13)+CHR$(245)+CHR$(205)+CHR$(50)+CHR$(110)+CHR$(241)+CHR$(254)+CHR$(13)+CHR$(192)+CHR$(62)+CHR$(10)+CHR$(205)+CHR$(50)+CHR$(110)+CHR$(201)
12 R$=CHR$(27)+"p" : N$=CHR$(27)+"q"
14 VA!=64228 : CL!=63066 : FK!=63600
27 '
28 'Main Menu
29 '
30 CLS : PRINT@53,R$" LPT --> COM "N$:PRINT
32 PRINT,"I)nitialize",,"L)PT-->COM",,"C)om LF",,"M)enu"
34 PRINT@280,"Stan Wong"; : PRINT@307,"[70346,1267]";
36 ONINSTR("iIlLcCmM",INPUT$(1)) GOTO40,40,50,50,70,70,82,82 : GOTO30
37 '
38 'Initialize
39 '
40 MAXFILES=1
42 OPEN "COM:88N1E" FOR OUTPUT AS 1
44 CLS : PRINT@170,R$" Done... "N$
46 BEEP : CALL21264 : CLOSE1 : GOTO10
47 '
48 'LPT-to-COM
49 '
50 CLS : AD!=PEEK(VA!)+256*PEEK(VA!+1)
52 IF AD!=63595 THEN F$=" On " ELSE F$=" Off "
54 PRINT@41,R$" LPT-to-COM "N$;:PRINT@70,R$F$N$:PRINT
56 PRINT,"E)nable",,"D)isable",,"P)revious Menu",,"T)op Menu"
58 ONINSTR("eEdDpPtT",INPUT$(1))GOTO 60,60,62,62,30,30,90,90 : GOTO58
60 POKE VA!,107 : POKE VA!+1,248 : GOTO 50
62 POKE VA!,243 : POKE VA!+1,127 : GOTO 50
67 '
68 'COM LF
69 '
70 CLS : F$=" Off " : POKE FK!,201
72 IF PEEK(CL!)<>0 THEN F$=" On " : POKE FK!,254
74 PRINT@41,R$"COM LF"N$;:PRINT@70,R$+F$+N$:PRINT
76 PRINT,"E)nable",,"D)isable",,"P)revious Menu",,"T)op Menu"
78 ONINSTR("eEdDpPtT",INPUT$(1))GOTO 80,80,82,82,30,30,90,90 : GOTO 78
80 POKE CL!,1 : POKE FK!,254 : GOTO 70
82 POKE CL!,0 : POKE FK!,201 : GOTO 70
87 '
88 'Exit
89 '
90 MAXFILES=1 : CLEAR256 : MENU
