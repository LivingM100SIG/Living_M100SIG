0 'RELOCA.BA  James Yi 73327,1653  10/18/86
1 ' mods by: Bill Templeton 75655,1056
2 '>>See RELOC.DOC for detailed instructions<<
10 DIMN$(255):FORL=0TO255:READN$(L):NEXT:PRINT:PRINT"Make sure file is LOADM'ed in memory.":PRINT:INPUT"File name";F$
20 INPUT"Top";SA:INPUT"End";EA:INPUT"Exe";EX:PRINT:PRINT"Is there a data table in the file? ";:R$=INPUT$(1):PRINTR$
30 TD=0:IFINSTR("Yy",R$)THENINPUT"Top of data";TD:INPUT"End of data";ED
40 PRINT:INPUT"Relocate to";RA:D=RA-SA:PRINT"Auto or Manual? ";:R$=INPUT$(1):PRINTR$
45 A=0:IFINSTR("Mm",R$)THENA=1:PRINT"Press space bar at each pause to adjust or any other key to ignore.":SOUND0,99
50 PRINT:PRINT"Relocating...":PRINT
60 IFD>0THENFORL=EATOSASTEP-1:POKERA+L-SA,PEEK(L):NEXTELSEFORL=SATOEA:POKERA+L-SA,PEEK(L):NEXT
70 FORM=SATOEA:PRINTSTR$(M)"("STR$(M+D)")-"TAB(18);:B=PEEK(M+D):IFTDTHENIFM=>TDANDM=<EDTHENPRINTB:GOTO80
75 PRINTN$(B);:IFRIGHT$(N$(B),2)="  "THENGOSUB100ELSEIFRIGHT$(N$(B),1)<>" "THENPRINTELSEPRINTPEEK(M+D+1):M=M+1
80 NEXT:PRINT:PRINT"Done.":PRINT"Save '"F$"' at the new location? ";
90 R$=INPUT$(1):PRINTR$:IFINSTR("Yy",R$)THENSAVEMF$,SA+D,EA+D,EX+D
95 END
100 PRINTCHR$(8);:V=PEEK(M+D+1)+PEEK(M+D+2)*256:PRINTSTR$(V);:IFATHEN130ELSEIFV<SAORV>EATHEN120
110 V=V+D:POKEM+D+1,V-INT(V/256)*256:POKEM+D+2,INT(V/256):PRINT"("STR$(V)")";
120 PRINT:M=M+2:RETURN
130 R$=INPUT$(1):IFR$=" "THEN110ELSE120
700 DATANOP,LXI B  ,STAX B,INX B,INR B,DCR B,MVI B ,RLC,---,DAD B,LDAX B,DCX B,INR C,DCR C,MVI C ,RRC
705 DATA---,LXI D  ,STAX D,INX D,INR D,DCR D,MVI D ,RAL,---,DAD D,LDAX D,DCX D,INR E,DCR E,MVI E ,RAR
710 DATARIM,LXI H  ,SHLD  ,INX H,INR H,DCR H,MVI H ,DAA,---,DAD H,LHLD  ,DCX H,INR L,DCR L,MVI L ,CMA
715 DATASIM,LXI SP  ,STA  ,INX SP,INR M,DCR M,MVI M ,STC,---,DAD SP,LDA  ,DCX SP,INR A,DCR A,MVI A ,CMC
720 DATAMOV B B,MOV B C,MOV B D,MOV B E,MOV B H,MOV B L,MOV B M,MOV B A
722 DATAMOV C B,MOV C C,MOV C D,MOV C E,MOV C H,MOV C L,MOV C M,MOV C A
725 DATAMOV D B,MOV D C,MOV D D,MOV D E,MOV D H,MOV D L,MOV D M,MOV D A
727 DATAMOV E B,MOV E C,MOV E D,MOV E E,MOV E H,MOV E L,MOV E M,MOV E A
730 DATAMOV H B,MOV H C,MOV H D,MOV H E,MOV H H,MOV H L,MOV H M,MOV H A
732 DATAMOV L B,MOV L C,MOV L D,MOV L E,MOV L H,MOV L L,MOV L M,MOV L A
735 DATAMOV M B,MOV M C,MOV M D,MOV M E,MOV M H,MOV M L,HLT,MOV M A
737 DATAMOV A B,MOV A C,MOV A D,MOV A E,MOV A H,MOV A L,MOV A M,MOV A A
740 DATAADD B,ADD C,ADD D,ADD E,ADD H,ADD L,ADD M,ADD A,ADC B,ADC C,ADC D,ADC E,ADC H,ADC L,ADC M,ADC A
745 DATA SUB B,SUB C,SUB D,SUB E,SUB H,SUB L,SUB M,SUB A,SBB B,SBB C,SBB D,SBB E,SBB H,SBB L,SBB M,SBB A
750 DATAANA B,ANA C,ANA D,ANA E,ANA H,ANA L,ANA M,ANA A,XRA B,XRA C,XRA D,XRA E,XRA H,XRA L,XRA M,XRA A
755 DATAORA B,ORA C,ORA D,ORA E,ORA H,ORA L,ORA M,ORA A,CMP B,CMP C,CMP D,CMP E,CMP H,CMP L,CMP M,CMP A
760 DATA RNZ,POP B,JNZ  ,JMP  ,CNZ  ,PUSH B,ADI ,RST 0,RZ,RET,JZ  ,---,CZ  ,CALL  ,ACI ,RST 1
765 DATARNC,POP D,JNC  ,OUT ,CNC  ,PUSH D,SUI ,RST 2,RC,---,JC  ,IN ,CC  ,---,SBI ,RST 3
770 DATARPO,POP H,JPO  ,XTHL,CPO  ,PUSH H,ANI ,RST 4,RPE,PCHL,JPE  ,XCHG,CPE  ,---,XRI ,RST 5
775 DATARP,POP PSW,JP  ,DI,CP  ,PUSH PSW,ORI ,RST 6,RM,SPHL,JM  ,EI,CM  ,---,CPI ,RST 7

