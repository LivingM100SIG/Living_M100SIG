10 'DISK-READ.BA (C)M-R PROD.1986
15 CLEAR500:ONERRORGOTO80:B$="To On/Off scrolling press <SPACE BAR>   To exit file press <Q>"
20 RV$=CHR$(27)+"p":NV$=CHR$(27)+"q":C$="        DISK-READ  (C) M-R PROD.        ":
25 CLS:PRINTRV$C$NV$:PRINT:PRINT" Do you want to examine":PRINT" the disk directory ?":PRINT:PRINT" Enter: Y/N/Q..."
30 I$=INKEY$:IFI$=""THEN30ELSEIFI$="Y"ORI$="y"THENLFILESELSEIFI$="Q"ORI$="q"THENMENUELSEIFI$="N"ORI$="n"THENGOTO35ELSEGOTO30
35 I$="":CLS:PRINTRV$C$NV$:PRINTB$:PRINT:PRINT"Enter the name of file":PRINT"you wish to read....."
36 PRINT"(':' prefix for disk file)...";:INPUT"";F$:IFF$=""ORF$="Q"ORF$="q"THENRUN
40 PRINT:PRINT@289,RV$" **** START FILE **** "NV$:PRINT:OPENF$FORINPUTAS1
45 IF EOF(1)THENGOTO65
50 I$=INKEY$:IFI$="Q"ORI$="q"THENGOTO70ELSEIFI$=CHR$(32)THENGOSUB75
55 LINEINPUT#1,A$
60 PRINTA$:GOTO45
65 CLOSE:BEEP:PRINT:PRINT:PRINT@249,RV$" **** END FILE **** "NV$:GOSUB95:RUN
70 CLOSE:BEEP:PRINT:PRINT:PRINT@249,RV$" **** QUIT READ **** "NV$:GOSUB95:RUN
75 O$=INKEY$:IFO$=""THEN75ELSERETURN
80 CLOSE:CLS:BEEP:BEEP:BEEP:PRINT@165,"Sorry, but you goofed up !":GOSUB95
85 CLS:PRINT@162,"Make sure the disk & drive are O.K.":GOSUB95
90 CLS:PRINT@162,"Sure you enter the right file name ?":GOSUB95:RUN
95 FORE=1TO1000:NEXTE:RETURN
100 'LAST LINE
