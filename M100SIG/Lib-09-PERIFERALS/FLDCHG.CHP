0 'FLDCHG.CHP
1 'Copyright 1987 Eiji Miura
2 'To use this program from X-TEL, replace END statement in line 13
3 'with CALLn statement, where n is the TOP address of X-TEL.
4 'Pressing ENTER at the prompt will display the help screen.
5 CLEAR2000:DIMB(24),D$(24),C(24)
6 CLS:ONERRORGOTO26:LOAD"0:!@!":F=PEEK(58226):E=PEEK(58227)
7 D=1:B=E\3:J=(EMOD3)*6+1:FORG=0TO2STEP2:B$=DSKI$(0,B,J+G,0)+LEFT$(DSKI$(0,B,J+G,1),127):IFGTHEN9
8 C(0)=ASC(MID$(B$,12,1))
9 FORH=19TO217STEP18:B(D)=ASC(MID$(B$,H,1)):IFB(D)=0ORB(D)>246THEN12
10 IFB(D)=240THEND$(D)=MID$(B$,H+1,9)ELSED$(D)=MID$(B$,H+1,6)+"."+MID$(B$,H+7,2)
11 C(D)=ASC(MID$(B$,H+11,1)):D=D+1
12 NEXTH,G:D=D+1:CLS:FORG=1TOD:PRINTD$(G)" ";:NEXT:PRINT@280,">";:C$="
13 A$=INPUT$(1):PRINTA$;:A=ASC(A$):IFA=29THEN15ELSEIFA=23THEN17ELSEIFA=20THEN18ELSEIFA=13THEN19ELSEIFA$=CHR$(27)THENEND
14 C$=C$+A$:GOTO13
15 PRINT"Wait...";:I=0:FORG=0TO239:H=PEEK(57967+G):IFH=0THENI=I+1
16 NEXT:PRINT@281,I*1.5"K free on disk";:GOTO23
17 IFE>0THENE=0:F=0:GOTO22ELSE7
18 IFE>0THENE=C(0):GOTO22ELSE7
19 IFC$=""THEN25ELSEIFLEN(C$)<9THENC$=C$+CHR$(32):GOTO19
20 FORG=1TOD:IFC$=D$(G)THEN21ELSENEXT:BEEP:PRINT@281,"No match.";:GOTO23
21 IFB(G)=240ANDD>0THENF=E:E=C(G)
22 POKE58226,F:POKE58227,E:GOTO5
23 PRINT@306,"Press [ENTER]";
24 IFINKEY$=CHR$(13)THEN7ELSE24
25 CLS:PRINT"Enter folder name to change the folder.":PRINT"[CTRL]["CHR$(152)"]  = Main Folder":PRINT"[SHIFT]["CHR$(152)"] = Previous Folder":PRINT"["CHR$(155)"]        = Free Disk Space":PRINT"[ESC]      = Exit":GOTO23
26 IFERR=52ANDERL=6THENRESUMENEXT
27 IFERR=68THENBEEP:PRINT"No Disk.":STOP
28 PRINT"Error #"ERR"in line"ERL:STOP
