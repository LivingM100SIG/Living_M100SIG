0 GOTO6:'Direct SORT
1 CLEAR256,58060:LOADM"sort":DEFINTJ,Z:DEFSTRF-I:E$=CHR$(27)
2 CLS:A$="Direct":FORW=1TO8:READX%:POKEW+63108,X%:NEXT
3 PRINT@120,"Sorting...":J=0:CALLHIMEM+5,0,VARPTR(J):IFJTHENSOUND9394,10ELSE243
4 IFJ=1THENPRINT@120,E$"p File not found "E$"q"ELSEPRINT"...":PRINT:PRINTE$"p Line too long "E$"q"
5 IFINKEY$=""THEN5ELSEMENU
6 'DIR - 8/12/84 - Disk directory manager; Copyright ORR ASSOCIATES, INC., 21 Chambers Rd., Danbury, CT, 06811. All rights reserved.

7 'Permission is hereby given for use of this program by M100 SIG members. Please report bugs to me at 72126,1613.
10 CLEAR(FRE(0))*2/3:MAXFILES=2
20 DEFSTRA-M:DEFINTN-Z:R=0:DIMA(55),B(55)
25 CLS
30 F1=DATE$:F2=TIME$
40 OPEN"0:DIRECT"FORINPUTAS1:INPUT#1,E1,E2,E3
50 FORN=1TO15:FORP=0TO1:FORQ=0TO15
70 A(R)=MID$(DSKI$(0,20,N,P),1+16*Q,10):H=MID$(A(R),1,1)
75 IFH=CHR$(255)THEN90
76 IFH=" "ORH=CHR$(0)ORLEN(A(R))=0THEN80ELSER=R+1:PRINT@130,R-1;A(R-1);SPACE$(20):IFR>55THENPRINT"DIRECT FULL":GOTO90
80 NEXT:NEXT:NEXT
90 T=0:C=" "
100 IFEOF(1)THEN110ELSELINEINPUT#1,B(T):PRINT@80,"Old: ";T:PRINTB(T);SPACE$(20):T=T+1:GOTO100
110 CLS:PRINT@133,"Thinking...":T1=T:FORU=0TOR-1:V=0
120 IFMID$(A(U),1,9)=LEFT$(B(V),9)THENB(V)=B(V)+"*":GOTO130ELSEV=V+1:IFV>TTHENB(T1)=A(U)+F1+F2:T1=T1+1ELSEGOTO120
130 NEXT
140 CLS:IFT=T1THEN150ELSEFORW=TTOT1-1:PRINT"NEW ENTRY:":PRINTB(W):INPUT"Comment";G:B(W)=B(W)+G:G=" ":NEXT
150 CLS:PRINT"Disk Comment:":PRINTE3:INPUT"New";E3
190 CLOSE:OPEN"DIRECT.DO"FOROUTPUTAS1:PRINT#1,F1;","F2;",";E3
200 FORW=0TOT-1:D=RIGHT$(B(W),1):IFD<>"*"THEN210ELSEPRINT#1,MID$(B(W),1,LEN(B(W))-1)
210 NEXT
220 IFT<>T1THENFORW=TTOT1-1:PRINT#1,B(W):NEXT
225 CLOSE:GOTO1
230 CLOSE:CLS:H=" ":INPUT"<P>rint (CR=no)";H:ONINSTR("Pp",H)+1GOTO234,235,235
234 MENU
235 OPEN"DIRECT.DO"FORINPUTAS1:INPUT#1,G:LPRINT"Disk Directory as of ";G;" at ";:INPUT#1,G:LPRINTG:INPUT#1,G:LPRINTG:LPRINT
236 LPRINT"File      P/D Created   At       Bytes    Comments":LPRINTSTRING$(80,95)
237 IFEOF(1)THENLPRINT:LPRINT"TOTAL BYTES: ";256*T0;CHR$(12):GOTO230
238 LINEINPUT#1,G:F=MID$(G,10,1):I=CHR$(128)+CHR$(01):Z=INSTR(I,F):I=MID$("DPM",Z+1,1):IFZ<>0THENZ=1
241 CLOSE2:OPEN"0:"+LEFT$(G,9)FORINPUTAS2:J8=LOF(2):T0=T0+J8
242 LPRINTMID$(G,1,9);"  ";I;"  ";MID$(G,10+Z,8);" ";MID$(G,18+Z,8);" ";256*J8,MID$(G,26+Z):GOTO237
243 OPEN"0:DIRECT"FOROUTPUTAS1:OPEN"DIRECT.DO"FORINPUTAS2
245 IFEOF(2)THEN230
250 LINEINPUT#2,G:PRINT#1,G:GOTO245
900 DATA68,73,82,69,67,84,0,65

