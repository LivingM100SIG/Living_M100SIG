1 CLS:PRINT"RAMPAC file renamer
2 PRINT" by T. Allen 76670,326
3 DEFINT A-Z
4 A$="" 'file types
5 S$="" 'file start sectors
6 NF=0 'number of files
10 PRINT:PRINT"scanning FAT.."
12 OUT129,0:A=INP(131):B=INP(131)
14 IFA<>64THENOUT129,0:OUT131,64:OUT131,4:OUT129,0:A=INP(131):B=INP(131):IFA<>64THENPRINT"bad RAMPAC":END
16 FORS=1TO127
18 A=INP(131):B=INP(131)
20 PRINT@160,USING"####";S;A;B
22 IF(A=128)OR(A=192)OR(A=160)THENS$=S$+CHR$(S):A$=A$+CHR$(A):NF=NF+1
24 NEXTS
26 CLS:PRINTNF"files found":IFNF=0THEN66
28 PRINT"enter a new name for each file,":PRINT"or ENTER alone to leave it as it is."
30 PRINT"file#  sectr  ftype  fsize  fname."
32 FORI=1TONF
34 A=ASC(MID$(A$,I,1)):S=ASC(MID$(S$,I,1))
36 OUT129,S:N$=""
38 FORK=1TO8:N$=N$+CHR$(INP(131)):NEXTK
40 N1=INP(131):N2=INP(131)
42 PRINT@160,USING"#####  ";I;S;A;N1+256*N2;:PRINTN$
44 PRINT">>"LEFT$(N$,6)"."RIGHT$(N$,2)CHR$(27)"K":PRINTCHR$(27)"K";:X$="":INPUTX$
46 IFX$=""THEN64
48 P=INSTR(X$,"."):IFP=1THENBEEP:I=I-1:GOTO64ELSEIFPTHENX$=LEFT$(X$,P-1)
50 X$=X$+SPACE$(5):X$=LEFT$(X$,6)
52 FORK=1TO6:C=ASC(MID$(X$,K,1)):IF(C>96)AND(C<123)THENC=C-32:C$=CHR$(C):MID$(X$,K,1)=C$
54 NEXTK
56 C$="DO":IFA=128THENC$="BA"ELSEIFA=160THENC$="CO"
58 X$=X$+C$
60 OUT129,S:FORK=1TO8:C=ASC(MID$(X$,K,1)):OUT131,C:NEXTK
62 I=I-1
64 NEXTI:SOUND1000,5:PRINT"done:press any key";
66 IFINKEY$=""THEN66ELSEMENU
