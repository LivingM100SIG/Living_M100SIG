10 REM - DISK TO 100 AND BACK PROGRAM BY GEORGE SALADINO 70210,445
20 CLS
30 POWER CONT
40 CLEAR 50,HIMEM
50 SCREEN 0,0
60 MAXFILES=1
70 IF MAXRAM=62960 THEN 130
80 ON ERROR GOTO 370
90 OPEN "DDT" FOR INPUT AS 1
100 INPUT #1,S$
110 IF VAL(MID$(S$,21,1))=1 THEN 190
120 CLOSE: GOTO 170
130 CLEAR 30,HIMEM:T=1
140 PRINT @ 132,"GOING TO DISK "
150 OPEN"DDT"FOR OUTPUT AS 1:PRINT#1,DAY$;TIME$;DATE$;T:CLOSE
160 CALL27862
170 PRINT @123,"GOING BACK TO 100 MEMORY!": CALL 32454:CLEAR 0,62960:MENU
180 END
190 PRINT @ 120,"GET READY TO BUMP TIME!"
200 DAY$=LEFT$(S$,3)
210 DATE$=MID$(S$,12,8)
220 TIME$=MID$(S$,4,8)
230 CLOSE
240 CLEAR 40
250 OPEN "DDT" FOR OUTPUT AS 1
260 SP$=SPACE$(19)+"0"
270 PRINT #1,SP$
280 CLOSE
290 CLEAR 40
300 CLS:PRINT @ 40,"PUSH <ENTER> TO BUMP TIME!"
310 I$=INKEY$:IFI$=""THENPRINT@135,TIME$:GOTO310
320 TS=0:PRINT@40,"";:INPUT"ENTER # SECONDS (0-TO MENU):";TS
330 IFTS=0THENMENU
340 A1$=TIME$:GOSUB360:A1=A1+TS:GOSUB350:TIME$=A1$:GOTO300
350 A1$=RIGHT$("0"+MID$(STR$(INT(A1/3600)),2),2)+":"+RIGHT$("0"+MID$(STR$(INT((A1-INT(A1/3600)*3600)/60)),2),2)+":"+RIGHT$("0"+MID$(STR$(INT(A1-INT(A1/60)*60)),2),2):RETURN
360 A1=VAL(RIGHT$(A1$,2))+VAL(RIGHT$(A1$,5))*60+VAL(RIGHT$(A1$,8))*3600:RETURN
370 RESUME 380
380 CLOSE:GOTO 170
