10 CLEAR1000:H$="0123456789ABCDEF"
20 INPUT"drive";DR:IFDR<0ORDR>1THEN20
30 LPRINT"	   Atribute Starting  <directory entry location>"
40 LPRINT"Name  Ext    Type    cluster  sector  sub  entry"
100 FORSE=1TO15
110 FORSS=0TO1
120 A$=DSKI$(DR,20,SE,SS)
130 FORX=0TO7
140 F$=MID$(A$,1+X*16,16)
145 IFF$=STRING$(16,255)THEN230:REM unused entry
150 N$=LEFT$(F$,9):IFASC(N$)=0ORASC(N$)=255THEN230:REM killed entry
160 TY=ASC(MID$(F$,10))
170 IFTY>1ANDTY<>128THENDE=TY:GOSUB1000:TY$="*"+HX$+"*":REM unknown file attribute
180 IFTY=0THENTY$="ASCII"
190 IFTY=1THENTY$="COM"
200 IFTY=128THENTY$="BASIC"
210 CL=ASC(MID$(F$,11))
220 LPRINTUSING"\       \   \    \     ##       ##     #     #";N$,TY$,CL,SE,SS,X
225 'a$=INKEY$:IFZZ$=""THEN225
230 NEXTX,SS,SE
900 LPRINT "           FAT Table"
920 A$=DSKI$(DR,20,16,0)
930 FORA2=0TO70STEP10:FORA1=1TO10:A=A1+A2
940 DE=ASC(MID$(A$,A)):IFDE>192ANDDE<254THENDE=-(DE-192)
960 LPRINTUSING"### ";DE;
970 NEXTA1:LPRINT:LPRINT:NEXTA2
999 END
1000 HX$=MID$(H$,DE\16+1,1)+MID$(H$,DEMOD16+1,1)
1010 RETURN
