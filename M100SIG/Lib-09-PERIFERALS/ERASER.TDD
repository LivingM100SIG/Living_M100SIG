1 'ERASER.TDD (c)1986 AfterHours Software - All Rights Reserved
2 '
3 'This utility will allow the deletion of any "illegally"-named
4 'files accidentally SAVEd to the TDD. You must make the filename
5 'six characters with trailing spaces PLUS a period and two-letter
6 'extension.
7 '
10 MAXFILES=2:CLEAR500:ONERRORGOTO200
20 OPEN"COM:98N1D"FORINPUTAS1:OPEN"COM:98N1D"FOROUTPUTAS2
30 PRINT#2,"ZZ"CHR$(7)CHR$(0)CHR$(248)CHR$(13);
40 GOSUB100
42 LINEINPUT"Enter File Name ";N$:IFLEN(N$)<9THENBEEP:PRINT"INCLUDE TRAILING SPACES":PRINT" AND/OR EXTENSION!":GOTO42
44 CS$=N$+SPACE$(24-LEN(N$))+"F"+CHR$(0)
45 CS$=CHR$(LEN(CS$))+CS$
46 FORI=1TOLEN(CS$):SM=SM+ASC(MID$(CS$,I,1)):NEXT
47 CK=(SMMOD256)XOR255
50 PRINT#2,"ZZ"CHR$(0)CS$CHR$(CK);
60 R$=INPUT$(31,1):R1$=RIGHT$(R$,4):H=ASC(R1$):R1$=RIGHT$(R$,3):L=ASC(R1$)
70 IFH+L=0THENERROR52
80 PRINT#2,"ZZ"CHR$(5)CHR$(0)CHR$(250);
90 R$=INPUT$(4,1):GOTO201
100 R$=INPUT$(4,1):IFASC(R$)<>18THENERROR17ELSERETURN
200 IFERR=54THENCLOSE1:OPEN"COM:98N1D"FORINPUTAS1:RESUMEELSEPRINT"ERROR #"ERR"IN LINE"ERL
201 CLOSE:MAXFILES=0:END
