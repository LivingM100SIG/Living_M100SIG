100 ' MGRLDR PROGRAM FOR DSKMGR AND UTIL MODULES
110 ' DSKUTL, DSKSVL AND DSKFMT SHOULD ALL BE RESIDENT
120 DEFSNG L: DEFSTR C: DEFINT O,W
130 LA=60700 'DSKMGR LOAD ADDR
140 CLS:PRINT@43,"WHICH UTILITY MODULE IS NEEDED?"
150   PRINT@125,"[D] DSKUTL - just need DSKMGR";
160   PRINT@165,"[S] DSKSVL - save all files"
170   PRINT@205,"[F] DSKFMT - format a disk"
180   CH=INKEY$: IF CH="" THEN 180
190 WH=INSTR("SDFsdf",CH) MOD 3 '1-save all 0-format 2-DSKUTL only
200 IF HIMEM=MAXRAM THEN 230
210   CLS:PRINT@45,"Something is stored in memory where the program will be located."
220   PRINT@165,;:INPUT"OK to overwrite it";CH: IF INSTR("Nn",LEFT$(CH,1)) THEN MENU
230 IF WH=2 THEN OF=0: GOTO 250
240 IF WH THEN OF=100 ELSE OF=500 'LOAD ADR OFFSET
250   IF FRE(0)>MAXRAM-(LA-OF)+512 THEN 280
260     CLS:PRINT@90,"NOT ENOUGH FREE RAM!"
270     CH=INKEY$: IF CH="" THEN 270 ELSE MENU
280 POKE LA,WH 'SAVE WHICH FUNCTION
290 CLEAR 0,LA-OF
300 WH=PEEK(60700)
310 IF WH=2 THEN 320 ELSE IF WH THEN LOADM "DSKSVL" ELSE LOADM "DSKFMT"
320 RUNM "DSKUTL"
