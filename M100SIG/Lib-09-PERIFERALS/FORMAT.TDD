1 'FORMAT.TDD v1.1 (c)1986 AfterHours Software - All Rights Reserved
2 '
3 'Utility to format disks on the Tandy Portable Disk Drive
4 'Works without DOS on both Model 100 & 200
5 '
10 MAXFILES=2:CLEAR1000:ONERRORGOTO200
20 OPEN"COM:98N1DNN"FORINPUTAS1:OPEN"COM:98N1DNN"FOROUTPUTAS2
30 GOSUB300:GOSUB100
40 PRINT#2,"ZZ"CHR$(6)CHR$(0)CHR$(249)CHR$(13);
50 GOSUB100
70 BEEP:CLS:PRINT"Format successful"
71 LINEINPUT"Format Another Disk ? ";Z$
73 IF(ASC(Z$)AND223)=89THEN30
76 GOTO205
100 PRINT#2,"ZZ"CHR$(7)CHR$(0)CHR$(248)CHR$(13);
101 R$=INPUT$(4,1):IFASC(R$)=18THENRETURN
102 PRINT"Drive Error!! Re-boot!":GOTO205
200 IFERR=54THENCLOSE1:OPEN"COM:98N1DNN"FORINPUTAS1:RESUME
201 PRINT"ERROR #"ERR"IN LINE"ERL
205 CLOSE:MAXFILES=0:END
300 CLS:PRINT@12,"TDD "CHR$(135)" FORMATTER"
305 PRINT@127,"o Power Up Drive"
306 PRINT@167,"o Insert BLANK Disk"
307 PRINT@207,"o Press Any Key to Begin"
310 R$=INPUT$(1):RETURN
