0 REM KEEP IT SIMPLE START-UP...KISS1 (DISK)
10 SCREEN1:WIDTH80
15 CLS:RV$=CHR$(27)+"p":NV$=CHR$(27)+"q"
20 PRINTRV$" MODEL 100 AUTO-LOAD PROGRAM **KISS1** ":PRINT"  by RICHARD K. HELMAN   ONALASKA, WI. "NV$:PRINT
30 REM LOADING BASIC PROGRAMS USING FUNCTION KEYS
40 KEY1,"LOAD"+CHR$(34)+"0:AAAAAA"+CHR$(13)
42 KEY2,"SAVE"+CHR$(34)+"AAAAAA"+CHR$(13)
44 KEY3,"LOAD"+CHR$(34)+"0:BBBBBB"+CHR$(13)
46 KEY4,"SAVE"+CHR$(34)+"BBBBBB"+CHR$(13)
48 KEY5,"LOAD"+CHR$(34)+"0:CCCCCC"+CHR$(13)
50 KEY6,"SAVE"+CHR$(34)+"CCCCCC"+CHR$(13)
52 KEY7,"RUN"+CHR$(34)+"0:KISS2"+CHR$(13)
55 PRINT">>  CONSECUTIVELY PRESS FUNCTION KEYS 1 THROUGH 7  <<":BEEP:END
600 REM ********  SEPARATE HERE AND DELETE LINE 600  ********
0 REM KEEP IT SIMPLE START-UP...KISS2 (DISK)
10 CLEAR1000:MAXFILES=2:SCREEN1:WIDTH80:CLS:ONERRORGOTO600
15 RV$=CHR$(27)+"p":NV$=CHR$(27)+"q"
20 PRINTRV$" MODEL 100 AUTO-LOAD PROGRAM **KISS2** ":PRINT"  by RICHARD K. HELMAN   ONALASKA, WI. "NV$:PRINT
50 PRINT"NOW LOADING '.DO' FILES!"
100 REM LINES 101-189 USED TO LOAD ANY ASCII FILE FROM DISK--USE YOUR OWN FILE NAME
110 DO$="0:AAAAAA.DO":DD$="AAAAAA.DO":GOSUB500
120 DO$="0:BBBBBB.DO":DD$="BBBBBB.DO":GOSUB500
180 DO$="0:CCCCCC.DO":DD$="CCCCCC.DO":GOSUB500
190 PRINT"**LOADING FINISHED.  NOW DEFINING KEYS**"
200 KEY1,"LFILES"+CHR$(13):KEY2,"LOAD"+CHR$(34):KEY3,"SAVE"+CHR$(34):KEY4,"SCREEN0"+CHR$(13)
210 KEY5,"SCREEN1"+CHR$(13):KEY6,"WIDTH40"+CHR$(13):KEY7,"WIDTH80"+CHR$(13):KEY8,"MENU"+CHR$(13)
220 REM TO RESTORE KEYS TO COLDSTART DEFINITIONS, DELETE LINES 200-210 AND REMOVE REM IN LINE 230
230 REM CALL23164,0,23366:CALL27795
300 BEEP:PRINTDATE$" ";DAY$" ";TIME$" ";"CORRECT? ";:TE$=INPUT$(1)
310 IFTE$="N"ORTE$="n"THENPRINT:GOTO320ELSE400
320 ONERRORGOTO390:PRINT"ENTER DATE ";:INPUTTE$:DATE$=TE$
330 ONERRORGOTO390:PRINT"ENTER DAY ";:INPUTTE$:DAY$=TE$
340 ONERRORGOTO390:PRINT"ENTER TIME ";:INPUTTE$:TIME$=TE$
380 GOTO400
390 BEEP:RESUME300
400 MAXFILES=1:PRINT:PRINT"KISS IS ALL DONE":SOUNDON:PRINT:PRINT"PRESS >>> F8 <<<":NEW
500 PRINT"LOADING: "DO$:OPENDO$FORINPUTAS1:OPENDD$FOROUTPUTAS2
510 IFEOF(1)THENCLOSE:RETURN
520 LINEINPUT#1,A$:PRINT#2,A$
530 GOTO510
600 CLOSE:PRINT:IFERR=52THENPRINT"FILE >>>  "DO$"  <<< NOT FOUND!":BEEP:STOP ELSE PRINTERR" ERROR IN LINE "ERL:BEEP:STOP

