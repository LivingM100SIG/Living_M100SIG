5 'SRCHUP.DVI; Copyr. l985 Madeline P. Durholz
10 'For DVI & (probably) Chipmunk use.
20 'Performs Search, Replace, Delete & Update to disk (& RAM) of single words & phrases.
30 'Keep SRCHUP.DVI in MENU as a reminder/tool to update your current session to disk. It's FAST used this way!
40 CLS:MAXFILES=2:CLEAR1006:DEFSTRD,I,L,W:FILES:PRINT:LINEINPUT"NAME of .DO file to update or search? ";D:PRINT:D1=D+".DO"
50 LINEINPUT"LOOK FOR? (Word(s) to be deleted must be preceded by SPACE; hit ENTER for update only) ";L:PRINT
55 IFL=""THENCLS:PRINT"Stand by...":GOTO160
60 LINEINPUT"SUBSTITUTE? (Hit ENTER to DELETE word(s) from text) ";I:CLS:PRINT"Stand by..."
70 OPEND1FOR INPUTAS1:OPEN"1:"+DFOR OUTPUTAS2
80 W=INPUT$(1,1):W1=W1+W:X=INSTR(W1,L)
90 IFX>0THENPRINT#2,LEFT$(W1,X-1);:PRINT#2,I;:W1="":GOTO80
100 IFEOF(1)ORLEN(W1)>254THENPRINT#2,W1;:W1=""
110 IFEOF(1)THEN120ELSE80
120 CLOSE:OPEN"1:"+DFOR INPUTAS1:OPEND1FOR OUTPUTAS2
130 IFEOF(1)THENCLOSE:MAXFILES=1:CLS:BEEP:PRINT"DONE!":MENUELSELINEINPUT#1,W
140 IFLEN(W)=255THENPRINT#2,W;ELSEPRINT#2,W
150 GOTO130
160 OPEND1FOR INPUTAS1:OPEN"1:"+DFOR OUTPUTAS2:GOTO130

