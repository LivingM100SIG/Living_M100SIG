10 ' Insert COCO Disk in Model 100 DVI       Power up Printer in 10cpi Mode and      Run this Program.                       Enjoy!!    R.L Banks [72737,2551]      
20 ' Program COJACK.DVI Reads COCO Disk      Directory and Makes a Disk Sleeve.
30 CLEAR3000,MAXRAM:CLS:DIMN(65),E$(65),S$(65)
40 ONERRORGOTO310
50 CLS:INPUT" Title of Disk ";C$
60 LPRINT" ";:FORX=1TO67:LPRINT"-";:NEXTX:LPRINT"-"
70 LPRINTCHR$(27);CHR$(14);TAB(10)C$
80 LPRINTCHR$(27);CHR$(15):LPRINT":";TAB(48);DATE$;TAB(69)":"
90 LPRINT":";TAB(69)":"
100 X=1:N=1
110 GOSUB170
120 L=3:FORQ=1TO60:IFLEFT$(S$(Q),1)=CHR$(255)THENS$(Q)=" ":E$(Q)=" "
130 NEXTQ
140 IFL=20THEN260ELSE150
150 LPRINT":";TAB(10)S$(N)"  ";TAB(20)E$(N)"  ";TAB(26)S$(N+1);TAB(36)E$(N+1);TAB(42)S$(N+2);TAB(52)E$(N+2);TAB(69)":"
160 N=N+3:L=L+1:GOTO140
170 FORI=3TO11:A$=DSKI$(0,17,I,0):B$=DSKI$(0,17,I,1):A$=A$+LEFT$(B$,120):FORJ=0TO7:S$=MID$(A$,J*32+1,8):E$=MID$(A$,J*32+9,3)
180 PRINTS$;E$
190 S$(N)=S$:E$(N)=E$
200 N=N+1
210 A=ASC(S$):IFA=255THENJ=7:I=11ELSE220
220 NEXTJ,I
230 PRINT
240 N=1
250 RETURN
260 LPRINT":";TAB(10)"Formatted by the CoCo";TAB(38)"Produced by the M-100";TAB(69)":"
270 FORX=1TO17:LPRINT"-   ";:NEXTX:LPRINT"-   "
280 LPRINTTAB(7);".";TAB(62);"."
290 L=L+1:IFL=48THEN300ELSE280
300 LPRINTTAB(7);:FORX=1TO55:LPRINT"-";:NEXTX:LPRINT"-"
310 CLS:PRINT" Do Another DISK  ?"
320 Q$=INKEY$:IFQ$=""THEN320
330 IF(Q$="Y")OR(Q$="y")THENCLS:LPRINTCHR$(12):RUN
340 IF(Q$="N")OR(Q$="n")THENCLS:LPRINTCHR$(12);CHR$(12):GOTO360
350 BEEP:GOTO310
360 PRINT:PRINT"  Thank You ... Have a Nice ";:IFTIME$>"05:00:00"THENIFTIME$<"11:59:00"THENPRINT"Morning!"
370 IFTIME$>"11:59:00"THENIFTIME$<"19:00:00"THENPRINT"Afternoon!"ELSEPRINT"Night!"
380 FORX=1TO1000:NEXT
390 BEEP:MENU
