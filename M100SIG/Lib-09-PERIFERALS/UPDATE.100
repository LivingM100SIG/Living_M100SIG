0 X$="                            ":S$=SPACE$(7):P$=CHR$(27)+"p":Q$=CHR$(27)+"q":IFLEFT$(X$,1)=CHR$(144)THEN9
1 CLS:PRINT@16,"UPDATE.BA":PRINT@46,"by Don Zeikel [CIS 75775,1430]":PRINT@133,P$" W A R N I N G "Q$:PRINT" SAVE A COPY OF THIS PROGRAM TO DISK":PRINT@205,"BEFORE FIRST USING IT!":BEEP:PRINT@287,"HAVE YOU SAVED IT (Y OR N)?";:GOSUB17
2 IFINSTR(1,"Yy",A$)THEN3ELSEMENU
3 V=VARPTR(X$):P=PEEK(V+1)+PEEK(V+2)*256
4 CLS:PRINT:PRINT"When you enter the file name of your":PRINT"choice, UPDATE.BA will be customized to":PRINT"update your disk backups with Powr-DOS.
5 BEEP:PRINT:PRINT"INCLUDE EXTENSION! (.DO, .BA, .CO, ETC.)";:LINEINPUT"     FILE NAME TO UPDATE? ";Y$:FORI=1TOLEN(Y$):C=ASC(MID$(Y$,I,1)):IFC>90THENMID$(Y$,I,1)=CHR$(C-32)
6 NEXT:CLS:PRINT@43,"IS"P$" "Y$" "Q$"CORRECT?  (Y or N)?
7 GOSUB17:IFINSTR(1,"Yy",A$)THEN8ELSE4
8 POKEP,144:FORI=1TOLEN(Y$):POKEP+I,ASC(MID$(Y$,I,1)):NEXT
9 N$=MID$(X$,2,9):ONERRORGOTO14:CLS:LFILESV:PRINTP$"UPDATE.BA CUSTOMIZED TO UPDATE "N$Q$:IFDSKI$(":"+N$)=0THENBEEP:PRINTS$N$" IS NOT ON DISK!":F=1:GOTO10ELSEGOTO10
10 IFDSKI$(N$)=0THENBEEP:PRINTS$N$" IS NOT IN RAM!":F=1:GOTO12ELSEIFF=1THEN12
11 PRINTS$"Killing :"N$" on disk":KILL":"+N$:PRINTS$"Saving new "N$" to disk":SAVEM":"+N$:PRINTS$"UPDATING COMPLETED":GOTO13
12 BEEP:PRINTS$N$P$" NOT "Q$"UPDATED!":GOTO13
13 PRINT:PRINTS$P$" PRESS ANY KEY TO EXIT "Q$;:GOSUB17:MENU
14 IFERR=5THENGOSUB18:PRINT"Powr-DOS IS NOT LOADED!":RESUME12
15 IFERR>58THENGOSUB18:PRINT"DISK OR DRIVE TROUBLE
16 PRINTS$"Error"ERR"in line"ERL:F=1:RESUME12
17 A$=INKEY$:IFA$=""THEN17ELSERETURN
18 CLS:BEEP:PRINTS$;:RETURN
