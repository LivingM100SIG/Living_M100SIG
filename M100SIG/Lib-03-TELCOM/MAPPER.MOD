1 CLS:PRINT:PRINT"CompuServe Weather Mapper"
2 'For TRS-80 Model 100 by A. Trevor
3 'Copyright 1985, CompuServe Inc.
4 'v.9 MODIFIED BY Dr. L. D. Preble for automatic sensing of M100 or Tandy 200
5 'Format of required ADRS.DO entry:
6 'cis:5552105<=^C?U70000,0^M?Ppassword^M>
7 MAXFILES=3:CLEAR 600:DEFINT C,I,S:P=PEEK(1):IFP<>51THENM$="NN":'M100 OR M200?
8 'Hours of map auto-download
9 DATA 00,04,08,12,16,20,END
10 CH$="1": C$="1"
11 PRINT@240,"Press any key for manual start"
12 PRINT@80,TIME$;:A$=INKEY$:IFA$<>"" THEN GOSUB47ELSEIF MID$(TIME$,4,2)<>"00"THEN12ELSE GOSUB57
13 IF TI$="END"THEN12
14 OPEN "RADTIM" FOR OUTPUT AS 1
15 IF C$="3"THEN PRINT#1,"SURFACE Map"ELSE PRINT#1,"RADAR Map"
16 PRINT#1,DATE$:PRINT#1,TIME$:PRINT#1,C$:PRINT#1,CH$
17 CLOSE 1
18 E$=CHR$(27):B$=CHR$(7):SOUND ON:ON ERROR GOTO38
19 OPEN"ADRS"FOR INPUT AS3:GOSUB44
20 IF EOF(3)THEN PRINT"cis:"+CHR$(128)+" not in ADRS":STOP
21 LINEINPUT#3,LG$:IF LEFT$(LG$,4)<>"cis:"THEN20
22 LG$=MID$(LG$,5,INSTR(1,LG$,">")-5)+"?!go awx4^m?I#M100,GH,GM^m?:"+C$+"^m?:"+"=="+CH$+"?"+CH$+">"
23 M=VARPTR(LG$):AD=PEEK(M+1)+(PEEK(M+2)*256):CLOSE 3
24 FOR I=14000TO1000 STEP-999:SOUND I,3:NEXT:CLS
25 IFP=51THENCALL 21200:CALL 21293,0,ADELSE CALL25040:CALL25131,0,AD
26 SOUND OFF:ON ERROR GOTO42:OPEN"MDM:8N1E"+M$FOR INPUT AS1:OPEN"MDM:8N1E"+M$FOR OUTPUT AS2
27 SOUND ON:ON ERROR GOTO42
28 OPEN"USMAP"FOR OUTPUT AS3
29 PRINT#2,"":IF CH$>"1"THEN S=32 ELSE S=125
30 CLS:PRINT E$;"pDownloading Graphics";E$;"q"
31 PRINT"       25%     50%     75%     100%"
32 C$=INPUT$(1,1):C=C+1:IF ASC(C$)=127 THEN C$=CHR$(30)
33 PRINT@C/S+80,CHR$(255);C$;:PRINT#3,C$;:IF C$<>B$THEN32
34 PRINT#2,"":W$=":":GOSUB40:PRINT#2,"":GOSUB40
35 PRINT#2,"/OFF":W$="=":GOSUB40:CLOSE:IFP=51THENCALL 21179ELSECALL25018
36 GOSUB44:PRINT:PRINT E$;"pWeather data ready in USMAP.DO";E$;"q":PRINT
37 FOR I=999 TO 14000 STEP 500:SOUND I,3:NEXT I:RUN"GRAPHR"
38 PRINT"? No ADRS file":STOP
39 'Wait on W$
40 PR$=CHR$(ASC(INPUT$(1,1))AND 127):PRINT PR$;:IF PR$=W$ THEN RETURN ELSE40
41 'Errors
42 PRINT"Error ";ERR
43 BEEP:CLOSE:STOP
44 'Set Default Modem status
45 IFP=51THENFORI=0TO4:POKE63067+I,PEEK(965+I):NEXTELSEFORI=0TO6:POKE61244+I,PEEK(998+I):NEXT
46 RETURN
47 CLS:PRINT"Map type"
48 PRINT"1 Radar map":PRINT"2 Surface map":PRINT"3 Coverage map"
49 C=3:GOSUB54:C$=W$
50 CLS:PRINT"1 Entire U.S.":PRINT"2 North East":PRINT"3 South East"
51 PRINT"4 North West":PRINT"5 South West":PRINT"6 Central"
52 C=6:GOSUB54:CH$=W$
53 RETURN
54 PRINT"Your choice (1 -";C;")";:INPUT I
55 IF((I>C) OR (I<1))THEN54ELSE W$=MID$(STR$(I),2)
56 RETURN
57 RESTORE9
58 READ TI$:IF TI$="END" THEN RETURN ELSE IF TI$<>LEFT$(TIME$,2) THEN58
59 RETURN
