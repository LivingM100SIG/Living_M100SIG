1 REM CISCHK V1.2.1 5/30/85
4 CLEAR 500:MAXFILES=2:DEFINT A-Z:ON ERROR GOTO 90
5 CLS:PRINT "CIS-CHECK, CHECKS PROGRAM FILES TO":PRINT"MAX 128 CHAR/LINE, FLAGS ANY GRAPHICS   CHARACTERS"
10 FILES:INPUT "NAME OF .DO FILE TO CHECK";F$:OPEN F$ FOR INPUT AS 1
13 FO$="LCD:":INPUT "OUTPUT FILE [LCD:]";FO$:OPEN FO$ FOR OUTPUT AS2:FL=-1
14 IF FO$<>"LCD:" THEN PRINT #2,"FILE CHECKED BY CISCHK:";F$
15 PRINT "WORKING..."
20 LINE INPUT #1,L$:IF LEN(L$)>128 THEN PRINT #2,"LINE OVERLENGTH--"; LEFT$(L$,5);"...":FL=0
25 FOR N=1 TO LEN(L$):CH=ASC(MID$(L$,N,1))
30 IF CH<128 AND CH>31 THEN 35
32 IF CH=13 OR CH=10 THEN 35
34 PRINT #2,"UNUSABLE CHARACTER--(";CH;")--";LEFT$(L$,5);"...":FL=0
35 NEXT N
40 IF NOT EOF(1) THEN 20
42 IF FL=-1 THEN PRINT #2,"NO ERRORS FOUND"
45 CLOSE:BEEP:PRINT "FILE COMPLETE":MAXFILES=1:END
90 IF ERR=52 AND ERL=10 THEN BEEP:PRINT"FILE NAME ERROR, TRY AGAIN":RESUME 10
95 PRINT "Err=";ERR;" in line ";ERL:END

