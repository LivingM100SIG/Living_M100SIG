0 'RLE Driver for RS CGP-115 Plotter
1 ' Bob Scott 73125,1437  10/86
5  KX=1.87:KY=2:X=1:Y=1
10 INPUT "File to Print ";FI$
20 OPEN FI$ FOR INPUT AS #1
30 I$=INPUT$(1,1)
40 IF EOF(1) THEN PRINT "wrong type":STOP
50 IF I$<>CHR$(27) THEN 30
60 I$=INPUT$(1,1):IF I$<>"G" THEN 30
70 I$=INPUT$(1,1):IF I$<>"H" THEN 30
73 LPRINT:LPRINT CHR$(18):LPRINT "M1,0"
75 IF EOF(1) THEN 900
80 BL$=INPUT$(1,1)
90 BP=ASC(BL$)-32
100 IF BP=0 THEN 190
110 T=X+BP
120 IF T>256 THEN 150
130 X=T:LPRINT "D";X*KX;",";-Y
140 GOTO 190
150 LPRINT "D";256*KX;",";-Y
160 Y=Y+2:LPRINT "M1,";-Y
170 X=T-256
180 LPRINT "D";X*KX;",";-Y
190 IF EOF(1) THEN 900
200 WT$=INPUT$(1,1):WP=ASC(WT$)-32
205 IF WP=0 THEN 75
210 T=X+WP
220 IF T>256 THEN 250
230 X=T:LPRINT "M";X*KX;",";-Y
240 GOTO 75
250 LPRINT "M";256*KX;",";-Y
260 Y=Y+2
270 LPRINT "M1,";-Y
275 X=T-256:LPRINT "M";X*KX;",";-Y
280 GOTO 75
900 LPRINT "A"
910 CLOSE:MENU
