0  REM  DIRACC.BA  by Stephane Burgoyne
1  REM
2  REM  Program for loading DIRACC.CO
3  REM  (Copyright Hugo Ferreyra)
5  REM
6  REM  Install/Remove Driver added by
7  REM  Phil Wheeler (v. 3, 4/24/84)
8  REM  Use this vers with DACMAC.LDR
9  REM
10 GOTO70
11 CLS:PRINT@81,"HIMEM is currently at ";HIMEM
12 PRINT"Do you want it just below HIMEM (Y/N) ";:A$=INPUT$(1)
13 IFA$="Y"ORA$="y"THEN EN=HIMEM:GOTO15
14 PRINT:INPUT"End address";EN:IFEN<32767OREN>62808THEN14
15 CLEAR256,EN-152:HI=HIMEM
16 RE=HI:F6=HI+10:F7=HI+25:FN=HI+47:ID=HI+84:CP=HI+107
17 V1=HI+11:V2=HI+26:V3=HI+32:V4=HI+64:V5=HI+79:V6=HI+88
18 REM
20 D$="0123456789ABCDEF"
21 A=HIMEM: FOR J=1 TO 11
22 READ LN$: FOR I=1 TO LEN(LN$) STEP 2
23 C1 =(INSTR(1,D$,MID$(LN$,I,1))-1)*16
24 C2 =INSTR(1,D$,MID$(LN$,I+1,1))-1
25 POKE A,C1+C2 :A=A+1 :NEXTI,J
26 KEY6,"Term"+CHR$(24)+"CALLHIMEM"+CHR$(13)
27 REM
30 CH=ID:GOSUB45:POKEV1,LO:POKEV1+1,HI
31 POKEV2,LO:POKEV2+1,HI
32 CH=FN:GOSUB45:POKEV3,LO:POKEV3+1,HI
33 CH=RE:GOSUB45:POKEV4,LO:POKEV4+1,HI
34 POKEV5,LO:POKEV5+1,HI
35 CH=CP:GOSUB45:POKEV6,LO:POKEV6+1,HI
36 CH=F6:GOSUB45: POKE64268,LO:POKE64269,HI
37 CH=F7:GOSUB45: POKE64270,LO:POKE64271,HI
38 REM
41 GOTO70' Done, show it!
45 HI=INT(CH/256):LO=CH-HI*256:RETURN
46 REM
50 DATA "E1CD622CCD3142C36854CD0000CD7F
51 DATA "6C2100002252F6C34F6CCD0000CD22
52 DATA "422100002252F621225ECD7C5AAF32
53 DATA "50F6C42942CD535D21155ECDB127CD
54 DATA "3E46D7CA0000CD0622E521000022E7
55 DATA "F63E01210000C3715FCD3142210000
56 DATA "CDB1272E000EC0CD7059CD2242CDAC
57 DATA "7EC91B704449524143432031303020
58 DATA "20436F707972696768742031393835
59 DATA "204875676F204665727265
60 DATA "7972611B7100
61 REM *** END OF DATA*** 
70 CLS:RV$=CHR$(27)+"p":NO$=CHR$(27)+"q":B=64268
72 PRINT"        DIRACC Install/Remove"
74 F$=" Removed ":IFPEEK(HIMEM+1)+256*PEEK(HIMEM+2)=65442ORPEEK(HIMEM+2)+256*PEEK(HIMEM+3)=11362THENF$=" Installed "
76 PRINT:PRINT"DIRACC"RV$F$NO$:PRINT:PRINT,"I)nstall",,"R)emove",,"M)100 Menu"
78 ONINSTR("iIrRmM",INPUT$(1))GOTO11,11,80,80,82,82:GOTO78
80 POKE64268,243:POKE64269,127:POKE64270,243:POKE64271,127:KEY6,"":KEY7,"":CLEAR256,MAXRAM:GOTO70
82 IFF$=" Installed "THENMENUELSEIFPEEK(-4)=1THENLFILESOFF:CALL0ELSEMENU
