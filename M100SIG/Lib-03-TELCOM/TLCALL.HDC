1 'PROGRAM FOR ACCESSING A HONEYWELL       6600 OR EQUIVALENT MAINFRAME WITHOUT 
   GETTING ITS RUBOUTS.
2 'MAY 13,83. KENNETH RATZLAFF; MODIFIED   BY HANNE CHRISTIANSEN.
3 'PRESS F2 TO START DOWNLOAD, APPENDED    TO DESIGNATED FILE.  PRESS F2 AGAIN  
   TO STOP DOWNLOAD AND CLOSE FILE.        LABEL IS "DOWN" WHEN ACTIVE, "DOWN"  
   WHEN NOT.
4 'F3 STARTS UPLOADING OF A FILE, LINE     BY LINE, ECHOED ON SCREEN, AND ENDED 
   BY DISPLAYED "EOF".
5 'GET PRINTER ECHO BY PRINTING WHOLE      SCREENS.  YOU MAY ASK YOUR MAINFRAME 
   FOR 8-LINE PAGES.
7 'KEY F7 WILL START A BREAK, BUT CANNOT   RETURN (LINE 330)
8 'AUTOMATIC PHONE DISCONNECT WHEN THE     HONEYWELL LEAVES. KEY F8 WILL ALSO   
   DISCONNECT.
9 'SET MAXFILES=4 BEFORE RUNNING IF UP-    AND DOWNLOAD DESIRED.
10 INPUT"LOCAL CALL (Y/N)";L$
15 'PUT IN DESIRED PHONE NUMBERS BELOW.
20 IF L$="Y"OR L$="Y"THEN PH$="45300<>" ELSE PH$= "8417010<>"
30 M=VARPTR(PH$)
40 AD=PEEK(M+1)+(PEEK(M+2)*256)
50 CALL 21200
60 CALL 21293,0,AD
70 PRINT " DIAL COMPLETE."
80 OPEN "MDM:7E1D" FOR INPUT AS 1
82 OPEN "MDM:7E1D" FOR OUTPUT AS 2
90 ON KEY GOSUB ,500,600,,,,300,400
91 KEY 2,"DOWN":KEY 3,"UP":KEY 7,"BREAK"   :KEY 8,"BYE"
93 ON MDM GOSUB 200
95 MDM ON: KEY ON: D%=0
97 ON ERROR GOTO 700
100 B$=INKEY$:IF B$="" THEN 100
120 PRINT #2,B$;:PRINT B$;
130 GOTO 100
200 A$=INPUT$(1,1)
205 IF ASC(A$)=127 THEN RETURN
206 IF ASC(A$)=255 THEN GOTO 400
220 PRINT A$;:IF D%=1 THEN PRINT#3,A$;
240 RETURN
300 B$="!":'BREAK
305 CLOSE 2:MDM OFF
310 OPEN "MDM:8N1D" FOR OUTPUT AS 2:PRINT B$;
320 FOR N=1 TO 4:PRINT"B";:PRINT #2,CHR$(0);:NEXT
325 CLOSE 2
330 OPEN"MDM:7E1D" FOR OUTPUT AS 2
335 MDM ON:PRINT"BREAK SENT":RETURN
400 CLOSE:CALL 21179
410 PRINT" ":PRINT"PHONE DISCONNECTED"
412 CALL 23164,0,23366:CALL 27795
418 KEY OFF
420 END:'CLOSE FILES, DISCONNECT PHONE.
500 D%=1-D%
510 IF D%=0 THEN KEY 2,"DOWN":CLOSE 3:RETURN ELSE INPUT"DOWNLOAD TO";F$
530 OPEN F$ FOR APPEND AS 3
540 KEY2,"DOWN":RETURN
600 INPUT"FILE TO UPLOAD";G$
610 OPEN G$ FOR INPUT AS 4
620 LINE INPUT #4,Z$
630 IF EOF(4) THEN PRINT"EOF": CLOSE 4: RETURN
640 PRINT #2,Z$: PRINT Z$: GOTO 620
700 IF ERR=7 THEN D%=1-D%:KEY2"DOWN"
710 RESUME NEXT

