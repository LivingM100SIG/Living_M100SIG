1 'AXCES   (Phone Number)   2/10/84
3 ' By Ted Thompson [71656,1301]
5 MAXFILES=2:CLEAR800:DEFINTI,J:DEFSTRL-V
7 T=CHR$(27)+"p":U=CHR$(27)+"q"
9 CLS:PRINT@55,STRING$(7,CHR$(232)):PRINT@95,T;" AXCES ";U:PRINT@135,STRING$(7,CHR$(231))
11 PRINT@168,STRING$(23,CHR$(232)):PRINT@208,T;" Phone Access Modifier ";U:PRINT@248,STRING$(23,CHR$(231))
13 OPEN"ADRS.DO"FORINPUTAS1
15 IFEOF(1)THEN77
17 LINEINPUT#1,N:IFINSTR(N,"cisp")THENCLOSEELSE15
19 O=N:GOSUB61:I=INSTR(M,"<"):IFI>1THENP=LEFT$(M,I-1)ELSEP="Unknown"
21 S=L:V=MID$(M,I)+":"+R:I=INSTR(N,"PRO")
23 CLS:PRINT@42,"The 'cisp' phone number is ";P
25 IFITHENPRINT@122,"The access code WILL branch to your":PRINT@162,"  programming area":GOTO29
27 PRINT@122,"The access code will NOT branch to":PRINT@162,"  your programming area"
29 PRINT@242,"Use this number (y/n) ? ":GOSUB69:IFJTHEN83
31 OPEN"ADRS.DO"FORINPUTAS1
33 IFEOF(1)THEN57
35 LINEINPUT#1,P:IFINSTR(P,"CSIS")=0THEN33
37 O=P:GOSUB61:CLS:PRINT@91,L:PRINT@171,"Use ";M;" (y/n) ? ":GOSUB69:IFJ=0THEN33
39 CLOSE:I=INSTR(N,"<"):P=MID$(N,I):N=S+":"+M+V
41 ONERRORGOTO79
43 CLS:PRINT@128,"Revising ADRS.DO File "
45 OPEN"TEMP$.DO"FOROUTPUTAS1:OPEN"ADRS.DO"FORINPUTAS2
47 IFEOF(2)THEN55
49 LINEINPUT#2,V:IFINSTR(V,"cisp")THEN53
51 PRINT#1,V:GOTO47
53 PRINT#1,N:GOTO47
55 CLOSE:KILL"ADRS.DO":NAME"TEMP$.DO"AS"ADRS.DO":GOTO83
57 CLS:CLOSE:PRINT@84,"'CSIS' phone entries exhausted":PRINT@169,"Repeat search (y/n) ?":GOSUB69:IFJTHEN31
59 CLS:PRINT@86,"Phone number not determined":PRINT@171,T;" Job terminated!! ";U:GOTO81
61 L="":R="":M=""
63 I=INSTR(O,":"):IFI=0THENRETURN
65 L=LEFT$(O,I-1):O=MID$(O,I+1):I=INSTR(O,":"):IFI=0THENRETURN
67 M=LEFT$(O,I-1):R=MID$(O,I+1):RETURN
69 Q=INKEY$:IFQ=""THEN69
71 IFINSTR("Yy",Q)THENJ=1ELSEJ=0
73 RETURN
75 FORJ=1TO2000:NEXT:RETURN
77 CLS:PRINT@83,"'cisp' entry not found in ADRS.DO":PRINT@130,T;" AXCES job Terminated ";U:GOTO81
79 CLS:PRINT@82,"AXCES failed to rewrite ADRS.DO file":PRINT@162,"  'cisp' entry unchanged"
81 CLOSE:GOSUB75:MENU
83 CLS:PRINT@41,"Enter name of BA file to be run next;":PRINT@83,"if none, you will be returned to":INPUT"  the MENU.";N
85 IFN=""THENMENUELSERUNN

