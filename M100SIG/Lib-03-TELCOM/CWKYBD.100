10 'Robert Curtis <70160,170>  CW.100 VER 2.0 20-NOV-83
20 'Sends CW at up to 65 WPM--ZZ$ is accessed by"=" -- LINE 5000 IS ACCESSED BY"@"
22 '-- LINE 6000 IS ACCESSED BY"^" -- LINE 8000 IS ACCESSED BY"!"
25 'Put your own information in these lines!
30 CLEAR 5000
40 ON ERROR GOTO 70
50 DEFINT A-Z:DEFSTR C:DIM C(255): CLS:LINE INPUT"ZZ$ ";ZZ$
60 INPUT"WPM ";W:IFW<1THENW=13:T=INT((800/W)-4.5):DT=3*T:GOSUB 3000 ELSE T=INT((800/W)-4.5):DT=3*T:GOSUB 3000
63 LINEINPUT"TEXT: ";A$:GOTO 75
70 A$=INKEY$:IF A$="" THEN 70
71 IF A$=CHR$(13) THEN 63
75 I=0
76 IFA$="@"THENGOSUB5000ELSEIFA$="^"THENGOSUB6000ELSE IFA$="="THEN GOSUB7000
77 IFA$="!"THENGOSUB8000ELSEIFA$="*"THENGOSUB8500EL SEIFA$=""THEN70
80 FOR J=1 TO LEN(A$):IF ASC(MID$(A$,J,1))>91 THEN MID$(A$,J,1)=CHR$(ASC(MID$(A$,J,1))-32):NEXT J ELSE NEXT J
90 IF A$=""ORI=LEN(A$)THEN70
92 I=I+1:I$=MID$(A$,I,1):PRINT@280,I$; :B$=C(ASC(I$))
94 IFASC(I$)=32THENFORK=1TO5*T:NEXTK:GOTO90ELSEIF ASC(I$)=64 THEN GOSUB 5000:GOTO90
100 FOR J=1 TO LEN(B$)
110 IF MID$(B$,J,1)="1" THEN GOSUB 1000 ELSE GOSUB 2000
120 NEXT J:GOTO 2500
1000 MOTOR ON:FOR K=1 TO T:NEXT K:MOTOR OFF
1100 FOR K=1 TO T:NEXT K:RETURN
2000 MOTOR ON:FOR K=1 TO DT:NEXT K:MOTOR OFF
2100 FOR K=1 TO T:NEXT K:RETURN
2500 FOR K=1 TO 2*T:NEXT K:IF I=>LEN(A$) THEN 70 ELSE 90
3000 C(65)="12":'A
3020 C(66)="2111":'B
3040 C(67)="2121":'C
3060 C(68)="211":'D
3080 C(69)="1":'E
3100 C(70)="1121":'F
3120 C(71)="221":'G
3140 C(72)="1111":'H
3160 C(73)="11":'I
3180 C(74)="1222":'J
3200 C(75)="212":'K
3220 C(76)="1211":'L
3240 C(77)="22":'M
3260 C(78)="21":'N
3280 C(79)="222":'O
3300 C(80)="1221":'P
3320 C(81)="2212":'Q
3340 C(82)="121":'R
3360 C(83)="111":'S
3380 C(84)="2":'T
3400 C(85)="112":'U
3420 C(86)="1112":'V
3440 C(87)="122":'W
3460 C(88)="2112":'X
3480 C(89)="2122":'Y
3500 C(90)="2211":'Z
3520 C(49)="12222":C(50)="11222":C(51)="11122":C(52)="11112":C(53) ="11111":C(54)="21111":C(55)="22111":C(56)="22211"
3540 C(57)="22221":C(48)="22222"
3560 C(47)="21121":C(46)="121212"
3580 C(45)="21112":C(44)="221122"
3600 C(37)="11111111":C(63)="112211"
3620 C(58)="222111": C(59)="212121"
3640 C(35)="12121":' # gives AR end of message
3660 C(38)="111212":' & gives SK end of work
4000 RETURN
5000 A$="CQ CQ CQ DE K2RLW K2RLW K2RLW CQ CQ CQ DE K2RLW K2RLW K2RLW K"+RIGHT$(A$,LEN(A$)-I-1):I=0
5100 GOTO 9000
6000 A$="NAME IS BOB NAME IS BOB BOB BOB -- QTH QTH QTH IS HACKENSACK "
6002 A$=A$+"ACKENSACK HACKENSACK, NEW JERSEY NEW JERSEY. --"+RIGHT$(A$,LEN(A$)-I-1):I=0
6100 GOTO 9000
7000 A$=ZZ$+RIGHT$(A$,LEN(A$)-I-1):I=0
7100 GOTO 9000
8000 A$="--RIG HR IS ICOM 740 RUNNING 80 WATTS INTO A LOOP WHICH GOES OVER "
8002 A$=A$+"TO THE GARAGE AND WRAPS AROUND IT AT 10 FOOT--"+RIGHT$(A$,LEN(A$)-I-1):I=0
8100 GOTO 9000
8500 A$="TIME IS "+LEFT$(TIME$,5)+" EST "+DAY$+" "+DATE$+RIGHT$(A$,LEN(A$)-I-1):I=0
9000 PRINTA$
9100 RETURN
