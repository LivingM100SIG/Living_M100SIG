1 CLS:PRINT"Terminal package for CMS":PRINT 
10 MAXFILES=4 
20 INPUT"In-Plant Call(Y/N)";L$ 
30 IF L$="Y"OR L$="y"THEN PH$="6222<>" ELSE PH$= "9286222<>" 
40 M=VARPTR(PH$) 
50 AD=PEEK(M+1)+(PEEK(M+2)*256) 
60 CALL 21200 
70 CALL 21293,0,AD 
80 PRINT " DIAL COMPLETE." 
90 OPEN "MDM:7E1D" FOR INPUT AS 1 
100 OPEN "MDM:7E1D" FOR OUTPUT AS 2 
110 ON KEY GOSUB 400,290,330,,,,,240 
120 KEY2,"down":KEY3,"up":KEY8,"bye" 
130 ON MDM GOSUB 190 
140 MDM ON: KEY ON: D%=0 
150 ON ERROR GOTO 380 
160 B$=INKEY$:IF B$="" THEN 160 
170 PRINT #2,B$;:PRINT B$; 
180 GOTO 160 
190 A$=INPUT$(1,1) 
200 IF ASC(A$)=127 THEN RETURN 
210 IF ASC(A$)=255 THEN RETURN 
220 PRINT A$;:IF(D%=1)AND(A$<>CHR$(19))AND(A$<>CHR$(17))THEN PRINT#3,A$; 
230 RETURN 
240 CLOSE:CALL 21179 
250 PRINT" ":PRINT"PHONE DISCONNECTED" 
260 CALL 23164,0,23366:CALL 27795 
270 KEY OFF 
280 MENU 
290 D%=1-D% 
300 IF D%=0 THEN KEY 2,"down":CLOSE 3:RETURN ELSE INPUT"DOWNLOAD TO";F$ 
310 OPEN F$ FOR APPEND AS 3 
320 KEY2,"DOWN":RETURN 
330 INPUT"FILE TO UPLOAD";G$ 
340 OPEN G$ FOR INPUT AS 4 
350 IFEOF(4)THENPRINT"EOF":CLOSE4:RETURN 
361 FORDL=0TO12*LEN(Z$):NEXTDL 
365 IFA$<>CHR$(17)THEN365 
366 LINEINPUT#4,Z$ 
370 PRINT#2,Z$:PRINTZ$:GOTO350 
380 IFERR=7THEND%=1-D%:KEY2"DOWN" 
390 RESUME NEXT 
400 FILES:PRINT:PRINTFRE(0):RETURN 

