100 'SEND transmits data from a
105 '.DO file in the Mod 100 to
110 'another computer via the
115 'RS-232 port. Change the par-
120 'ameters for COM: and the
125 'EOF character depending on
130 'your system.  -bob b.
135 '
140 'SEND FILE TO COMPUTER
145 MAXFILES=2:F$=CHR$(26):'END-OF-FILE CHARACTER
150 CLS:FILES:INPUT"Filename:";N$
155 ON ERROR GOTO 225
160 IF N$="" THEN 150
165 OPEN "RAM:"+N$+".DO" FOR INPUT AS 1
170 OPEN "COM:58N1E" FOR OUTPUT AS 2
175 INPUT"HIT 'ENTER' TO START";X$
180 '
185 IF EOF(1) THEN 205
190 C$="":LINE INPUT #1,C$
195 PRINT #2,C$:PRINT C$:GOTO 185
200 '
205 PRINT #2,F$
210 CLOSE 1,2:PRINT"FILES CLOSED"
215 PRINT"DONE":BEEP:END
220 '
225 IF ERR=52 THEN PRINT"FILE "+N$+".DO NOT FOUND"ELSE 235
230 FORI=1 TO 2000:NEXT:RESUME 150
235 PRINT"ERROR";ERR;"IN LINE";ERL:STOP
