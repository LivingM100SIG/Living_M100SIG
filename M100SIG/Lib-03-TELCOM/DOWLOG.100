10 REM DOWLOG.100, Version 1.0
20 REM Created April 1983
30 NM=0
40 MAXFILES=6
50 CLS:PRINTCHR$(27)"p":PRINT@48,"DOW JONES NEWS RETRIEVAL":LINE(46,6)-(193,17),1,B:PRINTCHR$(27)"q"
60 PRINTTAB(10)"(";CHR$(27);"p";"1";CHR$(27);"q";") Tymnet":PRINTTAB(10)"(";CHR$(27);"p";"2";CHR$(27);"q";") Telenet"
70 PRINTTAB(10)"(";CHR$(27);"p";"3";CHR$(27);"q";")";" Auto Logon"
80 PRINT:PRINTTAB(5)"Enter your choice ";:INPUT P
90 IF P <1 OR P >3 THEN 50
100 IF P=3 THEN 1170
110 IF P=1 THEN I$="<?pA?pDOW1;;?WDJNS^M?P" ELSE IF P=2 THEN I$="<=^M=^M?TD1^M?@C 60942^M?WDJNS^M?D"
120 CLS:PRINTCHR$(27)"p":PRINT@48,"DOW JONES NEWS RETRIEVAL":LINE(46,6)-(193,17),1,B:PRINTCHR$(27)"q"
130 PRINTTAB(5)"Dow Jones Phone No. :";:LINEINPUT A$:PRINTTAB(5)"Dow Jones Password  :";:LINEINPUT B$
140 PRINTTAB(5)"File to save to     : ":PRINT
150 PRINT"<";CHR$(27);"p";"ENTER";CHR$(27);"q";"> defaults to the ";CHR$(27);"p";" QUOTE ";CHR$(27);"q";" file.";
160 PRINT@226,"";:LINEINPUT SA$
170 IF SA$="" THEN SA$="QUOTE"
180 CLS:PRINT"Type your requests. Press <";CHR$(27);"p";"ENTER"CHR$(27);"q";"> to end"
190 NM=NM+20
200 X=X+1:PRINT@61+NM,X;:PRINT@64+NM,". ";:LINEINPUT D$(X)
210 IF X=9 THEN PRINT@282,CHR$(27);"p";"Only 10 requests please";CHR$(27);"q";:FOR PL= 1 TO 1000:NEXT PL
220 PRINT@280,STRING$(25,32);
230 IF X=10 THEN 260
240 IF LEN(D$(X))  => 1 THEN 190ELSE IF LEN(D$(X)) < 1 THEN 260
250 ON ERROR GOTO 850
260 PH$=A$+I$+B$+"^M>"
270 M=VARPTR(PH$)
280 AD=PEEK(M+1)+(PEEK(M+2)*256)
290 ST$=CHR$(19)
300 PRINT@282,CHR$(27);"p";" DIALING ";CHR$(27);"q";" ";
310 CALL 21200
320 CALL 21293,0,AD
330 CLS
340 OPEN "MDM:7I1D" FOR INPUT AS 1
350 OPEN "MDM:7I1D" FOR OUTPUT AS 2
360 OPEN  SA$       FOR  APPEND AS 3
370 ON ERROR GOTO 850
380 Z$=INPUT$(1,1)
390 ' ON ERROR GOTO 730
400 IFZ$<>ST$THEN 380
410 PRINT #3,DATE$;" ";TIME$
420 PRINT "STARTING QUOTES REQUEST":PRINT
430 X=0
440 X=X+1:PRINT #2,D$(X)
450 IF LEN(D$(X)) =0 THEN 480
460 Z$=INPUT$(1,1)
470 IF Z$ <> ST$ THEN 650
480 PRINT "SIGNING OFF"
490 ST$=CHR$(7)
500 PRINT #2,"DISC"
510 Z$=INPUT$(1,1)
520 CLOSE 1,2,3
530 CALL 21179
540 PRINT"Look at request again (Y/N) : ";:LINEINPUT L$
550 IF L$="Y" OR L$="y" THEN 700ELSE IF L$="N" OR L$="n" THEN 560
560 PRINT"Delete  ";CHR$(27);"p";" ";SA$;".DO ";CHR$(27);"q";"         (Y/N) : ";:LINEINPUT DE$
570 IF DE$="Y" OR DE$="y" THEN KILL SA$+".DO" ELSE IF DE$="N" OR R$="n" THEN 580
580 PRINTSTRING$(40,241);
590 PRINT"Repeat this request   (Y/N) : ";:LINEINPUT R$
600 '
610 IF R$="Y" OR R$="y" THEN 270ELSE IF L$="N" OR L$="n" THEN 620
620 PRINT"Another request       (Y/N) : ";:LINEINPUT AG$
630 IF AG$="Y" OR AG$="y" THEN 30ELSE IF AG$="N" OR AG$="n" THEN 640
640 CLOSE:MENU
650 Z$=INPUT$(1,1)
660 IF Z$=ST$ THEN 440
670 PRINT #3,Z$;
680 PRINT Z$;
690 GOTO 650
700 F$=CHR$(26):'END-OF-FILE CHARACTER
710  OPEN "RAM:"+SA$+".DO"  FOR INPUT AS 1
720 CLS
730 '
740 PRINT:PRINT"Press the ";CHR$(27);"p";" BREAK ";CHR$(27);"q";" key to pause."
750 PRINT"Press the ";CHR$(27);"p";" BREAK ";CHR$(27);"q";" key again to continue. "
760 PRINT:PRINT"Press the ";CHR$(27);"p";" ENTER ";CHR$(27);"q";" key to start. ";:LINEINPUT X$
770 FOR PA=1 TO 100:NEXT PA:CLS
780 IF EOF(1) THEN 820
790 C$="":LINE INPUT #1,C$
800 PRINT C$:GOTO 780
810 '
820 ' PRINT #2,F$
830 CLOSE 1:PRINTCHR$(27);"p";"FILE CLOSED";CHR$(27);"q":FOR Z=1 TO 500:NEXT Z
840 GOTO 540
850 IF ERR = 18 THEN PRINT"An error has occurred":STOP
860 PRINT #2,"DISC":CALL 21179:CLOSE:END
870 REM Procedure for creating default file named (AUTO.DO)
880 PH$="":X=0
890 CLS
900 PRINT"You have not defined an";CHR$(27);"p";" auto-logon ";CHR$(27);"q";"."
910 OPEN "AUTO.DO" FOR OUTPUT AS 5
920 PRINT:PRINTTAB(5)"Utilize which phone link :":PRINT
930 PRINTTAB(10)"(";CHR$(27);"p";"1";CHR$(27);"q";") Tymnet":PRINTTAB(10)"(";CHR$(27);"p";"2";CHR$(27);"q";") Telenet"
940 PRINT:INPUT"     Please choose one";P
950 IF P <1 OR P >2 THEN 910
960 CLS:PRINT:PRINTTAB(3)"Dow Jones Phone No.: -->";:LINEINPUT A$
970 PRINT:PRINTTAB(3)"Dow Jones Password : -->";:LINEINPUTB$
980 IF P=1 THEN I$="<?pA?pDOW1;;?WDJNS^M?P" ELSE IF P=2 THEN I$="<=^M=^M?TD1^M?@C 60942^M?WDJNS^M?D"
990 PH$=A$+I$+B$+"^M>"
1000 PRINT #5,PH$
1010 CLS:PRINT"Type your requests. Press <";CHR$(27);"p";"ENTER"CHR$(27);"q";"> to end"
1020 NM=NM+20
1030 X=X+1:PRINT@61+NM,X;:PRINT@64+NM,". ";:LINEINPUT D$(X)
1040 PRINT #5,D$(X)
1050 IF X=9 THEN PRINT@282,CHR$(27);"p";"Only 10 requests please";CHR$(27);"q";:FOR PL= 1 TO 1000:NEXT PL
1060 IF X=10 THEN 1080
1070 IF LEN(D$(X))  => 1 THEN 1020ELSE IF LEN(D$(X)) < 1 THEN 1080
1080 CLOSE 5:CLS:PRINT:PRINT"Your default auto-logon":PRINT"file has been created.":PRINTSTRING$(40,241);
1090 PRINT"What file name do you wish to use ":PRINT"to store your Dow Jones news?"
1100 PRINT:PRINT"<";CHR$(27);"p";"ENTER";CHR$(27);"q";"> defaults to the ";:PRINTCHR$(27);"p";
1110 PRINT" QUOTE ";:PRINTCHR$(27);"q";" file.";:PRINT@230,"";:LINEINPUT SA$
1120 '
1130 IF SA$="" THEN SA$="QUOTE"
1140 CLS:PRINT@42,"Do you wish to logon now  (Y/N) ";:LINEINPUTLG$
1150 IF LG$="Y" OR LG$="y" THEN 270ELSE IF LG$="N" OR LG$="n" THEN 1160
1160 CLOSE:MENU
1170 '
1180 ON ERROR GOTO 1350
1190 OPEN "AUTO.DO" FOR INPUT AS 6
1200 LINEINPUT #6,PH$
1210 X=0
1220 X=X+1
1230 LINEINPUT #6,D$(X)
1240 IF EOF(6) THEN GOTO 1260
1250 GOTO 1220
1260 CLOSE 6
1270 CLS:PRINT:PRINT"Your default auto-logon":PRINT"file will be used.":PRINTSTRING$(40,241);
1280 PRINT"What file name do you wish to use ":PRINT"to store your Dow Jones news?"
1290 PRINT:PRINT"<";CHR$(27);"p";"ENTER";CHR$(27);"q";"> defaults to the ";:PRINTCHR$(27);"p";
1300 PRINT" QUOTE ";:PRINTCHR$(27);"q";" file.";
1310 PRINT@230,"";:LINEINPUT SA$
1320 PRINT@280,STRING$(38,32);
1330 IF SA$="" THEN SA$="QUOTE":GOTO 270
1340 GOTO 270
1350 IF ERR = 52 THEN RESUME 870
1360 IF ERR = 19 THEN RESUME 1380
1370 '
1380 CLOSE 6:CLS:PRINTTAB(2)"Your ";CHR$(27);"p";" AUTO.DO ";CHR$(27);"q";" file is incorrect."
1390 FOR R=1 TO 700:NEXT R:RESUME 910
