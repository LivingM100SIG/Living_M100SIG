1 'RLE GRAPHICS PRINT PROGRAM MODEL 100 WITH RADIO SHACK PRINTER
2 'VER 2.0 BY J R HANNA  29 SEP 86  (74126,2703)
10 DEFINTA-Z:DIMA(256),N(256):CLS:PRINT"RLE Graphics Printer Program":PRINT
20 PRINT"  <R>everse Image (Maps)":PRINT"  <N>ormal Image (Pictures)":PRINT"  <M>enu"
30 Z$=INKEY$:IFZ$=""THEN 30
40 IFINSTR("NnRrMm",Z$)=0THEN30
50 IFZ$="M"ORZ$="m"THEN390
60 IFZ$="n"ORZ$="N"THENC=0ELSEC=-1
70 N$="":A$="":W=0
80 CLS:FILES:INPUT"RLE File to print ";N$:IFN$=""THENRUN
90 CLS:IF(INP(187)AND6)-2THENBEEP:PRINT@132,"Printer not Ready":GOTO380
100 OPENN$FORINPUTAS1
110 IFEOF(1)THEN370
120 A$=INPUT$(1,1):IFA$<>CHR$(27)THEN110
130 A$=INPUT$(2,1)
140 IFA$="GH"THENW=256ELSEIFA$="GM"THENW=128ELSE370
150 Z=0:B=3:Y=0:PRINT@122,"Printing in progress... Please wait"
160 LPRINTCHR$(27)CHR$(20)CHR$(18)
170 IFEOF(1)THEN340
180 A$=INPUT$(1,1):Z=ASC(A$)-32:IFZ<0THENGOSUB280:GOTO340
190 C=(C=0)
200 IFZ=0THEN170
210 Y=Y+1:Z=Z-1:IFNOTCTHEN240
220 IFB=192THENA(Y)=A(Y)OR64:N(Y)=N(Y)OR1:GOTO240
230 A(Y)=A(Y)ORB
240 IFY=WTHENY=0:B=B*4
250 IFB=768THENB=6:Y=0:GOSUB280:FORX=1TO256:A(X)=N(X):N(X)=0:NEXT
260 IFB=384THENB=3:Y=0:GOSUB280
270 IFZ=0THEN170ELSE210
280 FORX=1TO256:A(X)=A(X)OR128
290 FORP=1TO3:LPRINTCHR$(A(X));:NEXTP
300 A(X)=0
310 NEXTX
320 LPRINT
330 RETURN
340 LPRINTCHR$(30)CHR$(27)CHR$(19)
350 CLOSE:FORS=1TO20:BEEP:FORT=1TO200:NEXT:NEXT
360 RUN
370 CLOSE:BEEP:PRINT@124,N$" not an RLE graphics file !"
380 FORS=1TO3000:NEXT:RUN
390 MAXFILES=1:MENU
