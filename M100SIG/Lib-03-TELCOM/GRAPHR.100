1 'CompuServe Vidtex Graphics Plotter
2 'Use with MAPPER program, or
3 'Download to USMAP with Telcom
4 '  v.5
5 'By A.B. Trevor, Sept 85
6 DEFINT A-Z
7 ON ERROR GOTO36
8 FL$="USMAP"
9 OPEN FL$ FOR INPUT AS 1
10 ON ERROR GOTO0
11 IF EOF(1) THEN PRINT FL$" not a graphics file":STOP
12 IF ASC(INPUT$(1,1))<>27 THEN11
13 CLS:GOSUB32
14 IF A$<>"3"THEN LM=-12 ELSE LM=0
15 T=0:MX=240-LM:A$=INPUT$(2,1):IF A$="GH"THEN SF=256 ELSE IF A$="GM"THEN SF=128:MX=SF:LM=56 ELSE11
16 'Now decode the RLE
17 GOSUB23:X1=X:Y1=Y:GOSUB23:IF B=0THEN17
19 IF X1>=MXTHEN21
20 IFX1>=LM*-1THENIF X<MX AND X>X1 THEN LINE(X1+LM,Y1)-(X+LM-1,Y1) ELSE LINE(X1+LM,Y1)-(MX+LM-1,Y1)
21 IFLM>=0THENIF Y>Y1 THEN LINE(LM,Y)-(X+LM-1,Y)
22 GOTO17
23 B=ASC(INPUT$(1,1))-32:X=X+B:IF X>=SF THEN X=X-SF:Y=Y+1:IF Y=64THEN GOSUB26
24 IF EOF(1)THEN GOSUB26
25 RETURN
26 FORS=5000TO999STEP-400:SOUNDS,1:NEXT
27 IF INKEY$=""THEN27
28 T=T+128:IF T>SF THEN CLS:RUN"MAPPER"
29 Y=0:CLS:RETURN
32 ON ERROR GOTO35:FL$="radtim.do":OPEN FL$ FOR INPUT AS 2:PRINT
33 FORL=1TO3:LINEINPUT#2,A$:PRINTA$:NEXTL:INPUT#2,A$:CLOSE 2
34 ON ERROR GOTO0: RETURN
35 FL$="":RESUME34
36 INPUT"Graphics file to plot";FL$
37 RESUME9
