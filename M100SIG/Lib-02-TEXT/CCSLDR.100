1 'CCSLDR  Custom Character Set Loader
2 'by Neil Wick 71056,613
3 'Add filename to end of .DO file for auto save to .CO after loading
10 DEFINTB-Z:DIMC$(3):A=PEEK(64226)+256*PEEK(64227)
15 IFA=32755ORA=58715THENPRINTELSE30
20 PRINT"You must activate alternate characters  using CHRDEF.BA or CUSTOM.BA before"
25 PRINT"running this program":INPUT"Press <ENTER> for Menu";A$:MENU
30 C2=A-65537:C1=C2-767
40 T$="Custom Character Set Loader/Saver":L=0:GOSUB600:PRINT
50 PRINT" 1 S Save characters to .DO file":PRINT" 2 L Load characters from .DO file"
60 PRINT" 3 C Copy characters to .CO file":PRINT" 4 D Display current characters"
70 PRINT" 5 M Menu":PRINT" Choose one: ";
90 ONINSTR("  1Ss2Ll3Cc4Dd5Mm",INPUT$(1))/3GOTO100,200,300,400,500:GOTO90
100 T$="Save to .DO":GOSUB700:IFFI$=""THEN40ELSEOPENFI$FOROUTPUTAS1
110 FORX=C1TOC2
120 P=PEEK(X):PRINT#1,CHR$(48+P\16)CHR$(48+(PMOD16));:L=L+2
130 IFL>127THENL=0:PRINT#1,""
140 NEXT:CLOSE:GOTO40
200 T$="Load from .DO":GOSUB700:IFFI$=""THEN40ELSEOPENFI$FORINPUTAS1
210 FORX=C1TOC2
220 A$=INPUT$(2,1):IFASC(A$)<48THENX=X-1:GOTO230ELSEPOKEX,16*(ASC(A$)-48)+ASC(RIGHT$(A$,1))-48
230 NEXT
240 A$=INPUT$(2,1):IFNOTEOF(1)THENINPUT#1,FI$:CLOSE:GOTO310
250 CLOSE:GOTO40
300 T$="Copy to .CO":GOSUB700:IFFI$=""THEN40
310 FORL=-85TO-79STEP2:READX:POKEL,X:POKEL+1,0:NEXT:POKE-86,4:DATA82,85,78,13
320 SAVEMFI$,C1-1,C2
400 IFC$(0)=""THENT$="One Moment Please":L=4:GOSUB600:FORX=0TO3:FORL=0TO31:C$(X)=C$(X)+CHR$(128+X*32+L):NEXT:NEXT
410 CLS:PRINT:PRINTC$(0):PRINT:PRINTC$(1):PRINT:PRINTC$(2):PRINT:PRINTC$(3);
420 PRINT@153,CHR$(27)"p Press ":PRINT@193,"key for":PRINT@233," menu."CHR$(27)"q";:A$=INPUT$(1):GOTO40
500 MENU
600 CLS:PRINT@40*L+(40-LEN(T$))/2,CHR$(27)"p "T$" "CHR$(27)"q":RETURN
700 L=1:GOSUB600:PRINT:FI$="":INPUT"Filename or <ENTER> for menu";FI$:RETURN

