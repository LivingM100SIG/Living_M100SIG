1 ' THIS PROGRAM SETS THE PRINT 2 CHARACTERISTICS OF THE EPSON FX80 PRINTER.  IT ALSO LOADS THE 100'S SPECIAL CHARACTERS TO THE PRINTER
10 ' *********************************
200 DEFINTA-Z:CLEAR300:SCREEN0,0
240 E$=CHR$(27):IF2<>(INP(187)AND6)THENPRINT" <<PRINTER OFFLINE>>";:GOTO240ELSECLS
241 IF (PEEK(65433)AND32)<>32THENPRINT"            Push <CAPS LOCK>":GOTO241ELSECLS
242 PRINTE$"q";:RESTORE:READA$:FORI=1TO28:READA$:PRINT@10*I-5,A$;:NEXTI:PRINTE$"V"E$"p";
248 GOSUB290:LINEINPUT"SELECT:";S$:N%=1:RESTORE
249 FORI=1TO30:READA$:IF(INSTR(A$,S$)=1)THENN%=I:I=30
250 NEXTI
251 ONN%GOSUB261,262,263,264,261,266,267,268,261,270,271,272,261,274,275,276,261,278,279,280,261,281,282,283,261,285,286,287,261,287
252 GOSUB295:GOTO242
261 BEEP:BEEP:RETURN:DATA"ERROR, TRY AGAIN"
262 LPRINTE$"P"E$"p0";:RETURN:DATA "PICA"
263 LPRINTE$"M";:RETURN:DATA"ELITE"
264 LPRINTE$"p1";:RETURN:DATA"PROPORTIONAL",
266 LPRINTE$"F"E$"P"E$"p0"CHR$(15);:RETURN:DATA"COMPRESS"
267 LPRINTE$"S0";:RETURN:DATA"SUBSCRIPT"
268 LPRINTE$"S1";:RETURN:DATA"SUPERSCRIPT",
270 LPRINTE$"W1";:RETURN:DATA"EXPAND"
271 GOSUB286:LPRINTE$"4";:RETURN:DATA"ITALIC"
272 LPRINTE$"-1";:RETURN:DATA"UNDERLINE",
274 LPRINTE$"E";:RETURN:DATA"EMPHASIZE"
275 LPRINTE$"G";:RETURN:DATA"DOUBLESTR"
276 LPRINTE$"O"E$"@"E$"i0"E$"s0";:RETURN:DATA"MASTER RESET",
278 LPRINTE$"N"CHR$(6);:RETURN:DATA"SKIP=6"
279 GOSUB290:INPUT"LEFT MARGIN (N or <cr> for 8)";L$:L=VAL(L$):GOSUB294:RETURN:DATA"LMARGIN"
280 GOSUB290:GOSUB291:RETURN:DATA"LINES/INCH",
281 LPRINTE$"%"CHR$(1)CHR$(0);:RETURN:DATA"RAM(FX80)"
282 LPRINTE$"%"CHR$(0)CHR$(0);:RETURN:DATA"ROM(FX80)"
283 GOSUB400:RETURN:DATA"MOD100CHR$",
285 GOSUB288:CLS:KEY8,"Menu"+CHR$(13):RETURN:DATA"TYPEWRITE"
286 PRINTE$"q":CLS:SCREEN0,0:CALL24046:DATA"TEXT"
287 MENU:DATA"MENU",,"Menu"
288 CLS:KEY8,CHR$(24):PRINT@280,E$"T   TYPEWRITER MODE--TO QUIT TYPE ^X  "E$"q"E$"H"E$"W"E$"P";
289 Q$=INKEY$:IFQ$=CHR$(24)THENPRINTE$"U"E$"V"E$"Q":RETURNELSELPRINTQ$;:IFQ$=CHR$(13)THENPRINTCHR$(143):GOTO289ELSEIFQ$=CHR$(27)THENPRINT"^[";:GOTO289ELSEPRINTQ$;:GOTO289
290 PRINT@280,SPACE$(20);:PRINT@280,;:RETURN
291 LINEINPUT"LINES/INCH  (1,2,3,8,9,12, <cr>for6)";L$:L=VAL(L$):L$="="+L$
292 IFL=0THENLPRINTE$"2";:L$="=6"ELSEIFL=8THENLPRINTE$"0";ELSELPRINTE$"A"CHR$(72\L);293 RETURN
294 IFL=0THENL$="8":L=8:GOTO294ELSELPRINTE$"l"CHR$(L);:L$="="+L$:RETURN
295 RESTORE:FORI=1TON%:READX$:NEXTI:PRINT@300,E$"p"X$L$" OK"SPACE$(20);:L$="":RETURN
400 CLS:PRINT @42,E$"qLoading Model 100 Character Set        to FX80 RAM..."
410 E$=CHR$(27)
420 LPRINTE$":"CHR$(0)CHR$(0)CHR$(0);E$"%"CHR$(1)CHR$(0);
430 LPRINTE$"6";E$"&"CHR$(0)CHR$(128)CHR$(255);
440 FORM=128TO255:PRINT@M-8,E$"P"CHR$(M);:PRINT@M-8,;:S=0:F=0:TR=0
441 FOR I=0TO6:B(I)=0:NEXTI
442 PRINTE$"Q";
443 FOR I=1TO6
444 FOR J=0TO7:B(I)=B(I)+((PEEK(-21+I)AND2^J)*2^(7-2*J)):NEXTJ
445 IFB(I)<>0THENF=2*I-1
446 IFS=0ANDB(I)<>0THENS=2*I-1
447 TR=1
448 NEXTI
449 A=128*TR+16*S+F
450 FORI=1TO6:CALL27967,A:CALL27967,B(I):A=0:NEXTI
452 NEXTM
480 BEEP:PRINT:INPUT"Reduce line spacing";Q$
485 Q$=LEFT$(Q$,1):IFQ$="Y"ORQ$="y"THENLPRINTCHR$(27)"A"CHR$(8);
487 INPUT"Set Uni-directional mode";Q$
489 Q$=LEFT$(Q$,1):IFQ$="Y"ORQ$="y"THENLPRINTCHR$(27)"U1";
490 CLS:RETURN
