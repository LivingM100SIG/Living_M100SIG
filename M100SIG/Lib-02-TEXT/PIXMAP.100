10 '           "PIXMAP.BA"
20 '      A PROGRAM THAT PRODUCES
30 '     A PIXEL MAP OF TEXT FILES
40 '               BY
50 '     JAMES JACKSON 71316,1451
60 '          APRIL 7, 1986
70 ' ----------------------------------
80 ' ANSWER QUESTIONS PRESENTED BY PRO-
90 ' GRAM ON HOW TO FORMAT YOUR TEXT.
100' WHEN PROGRAM FINISHES PLOTTING,
110' PRESS ANY KEY TO SEE THE NEXT
120' PAGE.  WHEN THE LAST PAGE IS VIEW-
130' ED, PRESS ANY KEY TO RETURN TO THE
140' MODEL 100 MENU.  PROGRAM USES LESS
150' THAN 0.8K WHEN REMARKS ARE DELETED
160' ----------------------------------
170 CLS:CLEAR1620:P=1:L=0:R=79:TM=0:BM=63:LP=63:LS=1:ST=2:FILES
180 PRINTSTRING$(40,241);:INPUT".DO File to Display";N$
190 OPENN$FORINPUTAS1
200 CLS:INPUT"Top Margin (0 - 63)";TM:IFTM<0ORTM>63THEN200
210 PRINT"Bottom Margin (";TM+1;"- 63)";:INPUTBM:IFBM<TM+1ORBM>63THEN210
220 LP=BM-TM:INPUT"Left Margin (0 - 79)";L:IFL<0ORL>79THEN220
230 PRINT"Right Margin (";L+1;"- 79)";:INPUTR:IFR<L+1ORR>79THEN230
240 INPUT"Line Spacing (1 - 2)";LS:IFLS<1ORLS>2THEN240
250 CLS:PRINT@0,"File:";N$:PRINT@80,"Page:   ";P:PRINT@160,"Line"
260 PRINT@200,"Spacing:";LS;:PRINT@28,"Margins:"
270 PRINT@108,"Left:  ";L:PRINT@148,"Right: ";R
280 PRINT@188,"Top:   ";TM:PRINT@228,"Bottom:";BM
290 LINE(78,0)-(161,63),1,B:ST=TM+2:SP=LP
300 FORY=STTOSPSTEPLS:FORX=80+LTO80+R
310 IFEOF(1)THENCLOSE:GOTO380
320 A$=INPUT$(1,1):IFA$=CHR$(10)THEN350
330 IFA$>CHR$(32)ANDA$<CHR$(127)THENPSET(X,Y)
340 NEXTX
350 NEXTY
360 A$=INKEY$:IFA$=""THEN360
370 P=P+1:GOTO250
380 A$=INKEY$:IFA$=""THEN380ELSEMENU

