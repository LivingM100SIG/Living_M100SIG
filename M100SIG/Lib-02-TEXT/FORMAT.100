1' FORMAT.100 Rev.2 copyright 1983 by Alan Stein. No commercial use without permission.
2' Permission given for use and non-profit duplication by SIG-100 members.
100 CLEAR1024:DEFINT L,P,I:GOSUB2000
110 IFF=1THENCLS
150 BS$=chr$(136):RT$=CHR$(13):UL$=CHR$(95):ES$=CHR$(27):LI=0:FL=0:F1=0
200 IFNM$<>""THENGOSUB20000
300 PA=1:IFTI$=""THEN500
400 TY$="     "+TI$:GOSUB5000:GOSUB5000
500 US=LS:F2=F1:F1=0 
600 CH$=INPUT$(1,1):IFEOF(1)THEN1600
700 IF(CH$=" "ANDLEN(TY$)>=US)THEN 1000
750 IF CH$=RT$THEN F1=1:GOTO 1000
800 IFCH$=UL$THENGOSUB4000:GOTO600
900 TY$=TY$+CH$:GOTO600
1000 GOSUB5000
1200 IFLI<PSGOTO500
1250 PRINT@220,"PLEASE CHANGE PAPER":PRINT@263,"press 'c' to":PRINT@302,"resume printing";
1300 BEEP:FORI=1TO4000:NEXT
1400 IFINKEY$<>CHR$(99)THEN1300
1500 PA=PA+1:LI=0:CLS:TY$=HE$+"    page "+STR$(PA):GOSUB5000:GOSUB5000
1550 GOTO500
1600 GOSUB5000:GOSUB5000
1700 IFTI$<>""THENTY$=" The End.":GOSUB5000
1800 IFFTHEN1800ELSEEND
2000 CLS:FILES
2100 INPUT"   Which file do you wish";TE$
2200 OPENTE$FORINPUTAS1
2250 PRINT"Do you want to preview ";TE$;:INPUTQ$:IFQ$="y"THENF=1ELSEF=0
2300 INPUT"   How many characters to a line";X:LS=X-4
2400 INPUT"   How many lines on a page";PS
2500 INPUT"   What name do you want to use";NM$
2600 INPUT"   What should the title read";TI$
2700 INPUT"   What should the other page headings read";HE$
2800 CLS:RETURN
4000 IFFL=1THENFL=0:TY$=TY$+ES$+"R":GOTO4200
4100 IFFL=0THENFL=1:TY$=TY$+ES$+"E"
4200 US=US+2:RETURN
5000 LI=LI+1:IFF=0THENLPRINTTY$:GOTO6900
5100 IFLI<>1THEN6000
5200 FORI=0TOLS+6:IFIMOD5=2THENPSET(I,0):PSET(I,PS+1)
5300 NEXT
5400 FORI=0TOPS+1:IFIMOD5=1THENPSET(1,I):PSET(LS+6,I)
5500 NEXT
6000 IF F2THENTY$=MID$(TY$,2)
6100 FORI=0TO4:I2=19+40*I:PRINT@I2,SPACE$(21)
6200 PRINT @I2,"";
6300 FORI3=1TO21:CH$=MID$(TY$,21*I+I3,1):IF CH$<>ES$ THEN PRINT CH$;:
6400 NEXT:NEXT
6450 I=3
6500 CH$=LEFT$(TY$,1):TY$=MID$(TY$,2)
6600 IFCH$<>" "THENPSET(I,LI)
6700 IFCH$=BS$ OR CH$=ES$THENI=I-2
6800 IFLEN(TY$)<>0THENI=I+1:GOTO 6500
6900 TY$="":RETURN
20000 TY$=NM$+"  37 Skyline Dr. Daly City  CA  94015":PH$="   (415)756-5557"
20050 IFX=>62THEN21000
20100 TY$="   "+TY$:GOSUB5000
20200 TY$=SPACE$(14)+PH$:GOSUB5000
20300 GOTO21100
21000 TY$=TY$+PH$:GOSUB5000
21100 GOSUB5000:GOSUB5000
21200 RETURN

