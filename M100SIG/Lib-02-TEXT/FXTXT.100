200 'FXTXT.BA 3.0, by Rick Phillips [71666,1721], 1986
201 CLEAR:MAXFILES=2:ONERRORGOTO250
202 CLS:PRINT@52,"Welcome to FXTXT":PRINT@174,"(R)AM input":PRINT@214,"(C)AS input":PRINT@254,"(M)enu":PRINT@83,"Check free RAM! - Select option: ";
203 ONINSTR("rRcCpPM",INPUT$(1))GOTO210,210,260,260,280,280,270:GOTO203
210 CLS:FILES:GOSUB300:CLS
211 OPENF1$FORINPUTAS1:GOSUB400
212 F2$="TXT":OPENF2$FOROUTPUTAS2:B$=""
213 IFEOF(1)THEN240ELSEGOSUB350
214 IFA$=""THEN220ELSEIFLEFT$(A$,1)="."THEN230
215 B$="X":IFRIGHT$(A$,1)<>" "THENPRINT#2,A$;" ";:GOSUB450:GOTO213ELSEPRINT#2,A$;:GOSUB450:GOTO213
220 PRINT#2,"":IFB$="X"THENPRINT#2,""
221 GOSUB500:B$="":GOTO213
230 IFB$="X"THENPRINT#2,"":B$=""
231 PRINT#2,A$:GOSUB500:IFA$<>".X1"THEN213
232 IFEOF(1)THEN240ELSEGOSUB350
233 PRINT#2,A$:GOSUB500:IFA$=".X2"THEN213ELSE232
240 IFLEFT$(F1$,4)="CAS:"THENF1$=MID$(F1$,5)+".DO":GOTO241:ELSEF1$=F1$+".DO":KILLF1$
241 F2$=F2$+".DO":NAMEF2$ASF1$:BEEP:GOTO202
250 IF ERR=52ORERR=55THENBEEP:RESUME210ELSE251
251 BEEP:PRINT:PRINT" Error code";ERR;"in line";ERL:PRINT" See M-100 manual, page 217":END
260 CLS:GOSUB300:PRINT@121,"Searching...":F1$="CAS:"+F1$:GOTO211
270 MENU
280 GOTO202
300 PRINT:PRINT" Enter TEXT file name: ";:LINEINPUTF1$:IFF1$="Menu"THENMENU
301 IFRIGHT$(F1$,3)=".DO"ORRIGHT$(F1$,3)=".do"THENF1$=LEFT$(F1$,LEN(F1$)-3)
302 IFLEN(F1$)>6THENF1$=RIGHT$(F1$,6)
303 RETURN
350 A$="":LINEINPUT#1,A$:RETURN
400 PRINT:PRINT" TEXT file found, PROCEEDING...":PRINT:PRINT" ";:RETURN
450 PRINT"* ";:RETURN
500 PRINT"- ";:RETURN
