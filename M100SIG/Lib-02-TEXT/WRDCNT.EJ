1 'WRDCNT.BA:	07/07/84:	EJ
2 GOTO16
4 IFWLTHENWD=WD+1ELSERETURN
6 IFWL>10THENWL=10
8 PRINT@134,USING"#,###";WD:LPRINTW$,TAB(20),WL
10 LN(WL)=LN(WL)+1:PRINT@(236+(WL*4)),USING"###";LN(WL)
12 IFSTHENPRINT@154,USING"##.#";WD/S
14 W$="":WL=0:RETURN::
16 CLS:MAXFILES=1:CLEAR2000:DEFINTA-Z:FILES
18 PRINT@285,".DO file to check";:INPUTN$:IFINSTR(N$,".")THEN20ELSEN$=N$+".DO"
20 O$=CHR$(27)+"p":F$=CHR$(27)+"q"
22 CLS:PRINT@13,"File = ";N$:PRINT@82,"Characters= ":PRINT@102,"Sentences = "
24 PRINT@122,"Words     = ":PRINT@142,"Wrds/Sent.= "
26 PRINT@160,"- Occurrences of various length words -";
28 FORX=1TO10:PRINT@(196+X*4),O$;USING"###";X;:PRINTF$;:NEXT:PRINTO$;"+";F$
30 OPENN$FORINPUTAS1::
32 LINEINPUT#1,A$:IFA$=""THEN32
34 IFEOF(1)THENFL=1
36 CH=CH+LEN(A$):PRINT@93,USING"##,###";CH
38 FORN=1TOLEN(A$)
40 C$=MID$(A$,N,1)
42 IFC$>="A"ANDC$=<"z"THEN64
44 IFC$>="0"ANDC$=<"9"THEN64
46 IFC$=" "ANDL$<>" "THENGOSUB4:GOTO66
48 IFN=LEN(A$)THEN52
50 IFC$="."ANDINSTR(" 1234567890",L$)ANDINSTR("1234567890",MID$(A$,N+1,1))THEN64
52 IFC$="-"ORC$="/"THENGOSUB4:GOTO66
54 IFINSTR("'$#%",C$)THEN64
56 C=INSTR(".!?",C$):IFC=0THEN66
58 IFN=LEN(A$)THEN66
60 IFC$=MID$(A$,N+1,1)ORC$=L$THEN66
62 S=S+1:PRINT@113,USING"###";S:GOTO66
64 WL=WL+1:W$=W$+C$
66 L$=C$:NEXT
68 IFW$>""ANDN<255THENGOSUB4
70 IFFL=0THEN32
72 IFWLTHENS=S+1:GOSUB4:PRINT@113,USING"###";S
74 PRINT@293,"Another File? ";:A$=INPUT$(1)
76 IFINSTR("Yy",A$)THENRUNELSEMENU
