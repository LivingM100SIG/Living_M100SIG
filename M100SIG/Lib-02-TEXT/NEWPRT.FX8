10 CLS:CLEAR2000:S=0:FILES
20 L=11:R=75:HD$="Y":LS=1:NLQ$="n"
30 C$=STRING$(60,32):LN=0:PG=1
40 GOSUB 100
42 INPUT"File to Print";N$
45 ONERRORGOTO400
46 OPENN$FORINPUTAS1
47 CLOSE1
50 GOSUB 100
52 INPUT"Margins (L[=11],R[=75])";L,R
54 IFL=0THENL=1
60 L$=STRING$(L-1,32):GOSUB 100
62 INPUT"Line Spacing (1 or 2)";LS
70 GOSUB100
72 LINEINPUT"Page Title:";H$
80 GOSUB100
82 INPUT"Title on Page 1 (y/n)";HD$:GOSUB100:INPUT"NLQ (y,n)";NLQ$
84 IFNLQ$="Y"ORNLQ$="y"THEN LPRINT CHR$(27);"x";CHR$(1)
85 OPENN$FORINPUTAS1:GOTO110
100 PRINT@205,C$:PRINT@205,"";
105 RETURN
110 CLS:IF H$=""THENPH$=L$+DATE$:GOTO 140
120 PH$=STRING$(R-8,32)+DATE$
130 IF H$<>""THENMID$(PH$,L,LEN(H$))=H$
140 IF HD$="Y"ORHD$="y"THENPR$=L$:GOSUB360
160 PR$=L$
170 IFLN>=50THENGOTO330
180 FORJ=LEN(PR$)TOR
190 PR$=PR$+INPUT$(1,1)
200 IFEOF(1)THENCLOSE:EN=1:GOTO280
210 IFRIGHT$(PR$,1)=CHR$(10)THENPF=1:GOTO270
220 IFRIGHT$(PR$,1)=CHR$(128)THENPR$=LEFT$(PR$,J-1):LN=LN-1:GOTO330
230 NEXT J
240 IF MID$(PR$,J,1)=" " THEN 260
250 J=J-1:GOTO 240
260 NX$=MID$(PR$,J+1,R)
262 PR$=MID$(PR$,1,J):GOTO 280
270 PR$=LEFT$(PR$,LEN(PR$)-2)
280 IFMID$(PR$,L,3)="  "THEN300
290 IFMID$(PR$,L,1)=" "THENPR$=MID$(PR$,2,LEN(PR$)):GOTO290
300 LPRINTPR$;CHR$(10):LN=LN+1:IFEN=1THEN330
310 IFLS=2THENLPRINT;CHR$(10):LN=LN+1
320 PR$=L$+NX$:NX$="":GOTO170
330 FORJ1=LNTO66:LPRINT;CHR$(10):NEXTJ1
332 PG=PG+1
340 IFEN=1THENMENU
350 IFX$<>""THEN360ELSEPRINT@160,"";:LINEINPUT"<Enter>=Next Page, <N>=Nonstop";X$
360 CLS:LPRINTPH$;CHR$(10):LPRINTL$;"Page ";PG;CHR$(10)
370 LPRINT:LPRINT:LN=4:GOTO180
400 CLS:BEEP:PRINT@133,"No such file...":FORW=1TO200:NEXTW
410 GOTO10
