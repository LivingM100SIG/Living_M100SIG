10 DEFINTA-Z:MAXFILES=2:PARM$="COM:58N1E":FT=-1
20 CLS:SCREEN0,0:PRINT:FLAG=0:FILE$=""
30 TITLE$="   Model 100  Port Driver   "
40 GOSUB490
50 IFNOTFTGOTO80
60 PRINT@134,"Version  1.2":PRINT@211,"Bill Steinberg NYC"
70 FORX=1TO1000:NEXT:FT=0
80 PRINT@134,SPACE$(120):PRINT@120,"";
90 PRINT"1) Select a file for printing"
100 PRINT"2) Change output port ";
110 PRINT"["+PARM$+"]"
120 PRINT"3) Exit to system menu"
130 PROMPT$="Choice ?"
140 GOSUB450
150 IFCHOICE$="1"GOTO160ELSEIFCHOICE$="2"GOTO280ELSEIFCHOICE$="3"GOTO440ELSEBEEP:GOTO140
160 ONERRORGOTO540
170 PRINT@120,SPACE$(199);
180 LINE(234,56)-(236,57),0,B
190 PRINT@240,"File to print ";
200 INPUTFILE$
210 OPENFI$FORINPUTAS1
220 ONERRORGOTO570
230 OPENPARM$FOROUTPUTAS2
240 DA$=INPUT$(1,1)
250 PRINT#2,DA$;
260 IFNOTEOF(1)GOTO240
270 CLOSE:GOTO20
280 PRINT@120,SPACE$(199);
290 LINE(234,56)-(236,57),0,B
300 PRINT@120,"Current output parms =	";
310 PRINTCHR$(27)+"p "+PA$+CHR$(27)+"q"
320 LINEINPUT"Enter new ouput parms:	";NP$
330 IFNP$="lcd"ORNP$="LCD"ORNP$="lpt"ORNP$="LPT"ORNP$="cas"ORNP$="CAS"THENNP$=NP$+":"
340 PRINT@160,SPACE$(40)
350 PRINT@160,"Change output parms to	";
360 PRINTCHR$(27)+"p "+NP$+CHR$(27)+"q"
370 PROMPT$="Y/N/A  ?"
380 PRINT:PRINT"Is this OK (Yes/No/Abort)?"
390 GOSUB450
400 IFCHOICE$="Y"ORCHOICE$="y"THENPA$=NP$:GOTO20
410 IFCHOICE$="N"ORCHOICE$="n"THENGOTO280
420 IFCHOICE$="A"ORCHOICE$="a"THENGOTO20
430 BEEP:GOTO390
440 MENU
450 PRINT@271,PR$+CHR$(8);
460 LINE(183,46)-(236,57),1,B
470 CH$=INPUT$(1)
480 RETURN
490 PRINTSPACE$(6)+CHR$(27)+"p";
500 PRINTTI$+CHR$(27)+"q"
510 LINE(203,8)-(203,15),0
520 LINE(36,7)-(202,7)
530 RETURN
540 IFFLAGTHENBEEP:RESUME20
550 FLAG=-1
560 CLS:FILES:RESUME190
570 CLOSE:BEEP:CLS
580 PRINTCHR$(27)+"p   You have specified an illegal output ";
590 PRINT" parameter.  Please check your manual, &";
600 PRINT" re-enter the correct output parameters "+CHR$(27)+"q";
610 RESUME280
