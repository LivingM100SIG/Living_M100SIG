10 DEFINT A:DIM A(239):BC=65516
20 CLS:FOR X=0 TO 170 STEP 10:LINE(X,0)-(X+63,63):LINE(X,63)-(X+63,0):NEXT
30 LINE(0,0)-(239,63),1,B:PRINT@90,"Okidata Screen Dump":PRINT@133,"By Don Corbitt":GOSUB 900:END
900 'Written and (C) Copyright Don Corbitt 73216,1022
901 'Jan 16,1985.  For non-commercial use only
902 'Works on Okidata u92 printer and compatibles
905 PRINT CHR$(27)"P"; ' TURN ON LCD CURSOR
906 LPRINT CHR$(156); ' SET PRINTER TO 12 CPI FOR SQUARE DOTS
907 FOR X=0 TO 7:READ D(X):NEXT ' DOUBLE SIZE GRAPHICS CHARS
908 FOR L=0 TO 2:READ S(L):NEXT ' READ LINE SPACING
910 FOR Y=0 TO 7:FOR X=0 TO 39:PRINT@X+Y*40,""; ' COPY SCREEN
915 FOR X1=0 TO 5:A(X*6+X1)=255-PEEK(BC+X1):NEXT X1,X ' COPY A CHAR TO BUFFER
920 FOR L=0 TO 2:L1=8^L:LPRINTCHR$(27)"%9"CHR$(S(L))CHR$(131); ' SET LINE SPACING, ENTER GRAPHICS MODE
921 FOR X=0 TO 239 ' PRINT EACH LINE IN 3 PARTS
925 A=(A(X)/L1)AND7:LPRINTSTRING$(2,D(A));:IFD(A)=131THENLPRINTSTRING$(2,D(A));
930 NEXT X:LPRINTCHR$(131)CHR$(130)CHR$(138)CHR$(141); ' EXIT GRAPHICS, DO LINE FEED
931 NEXT L,Y:LPRINTCHR$(24):RETURN ' RESET PRINTER
940 DATA 128,131,140,143,176,179,188,191,140,140,136
