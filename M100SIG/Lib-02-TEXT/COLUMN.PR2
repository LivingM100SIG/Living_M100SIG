0 'COLUMN.BA  by Ron Bean 2/86, revised 6/86
1 'Prints long lists of short items in several columns on each page. Does not format (for text: format to one column-width first)
2 'NOTE: Set MAXFILES to max # of columns. T,B,PL are Top,Bottom margins & Page Length.
10 MAXFILES=5:T=5:B=5:PL=66:L=PL-T-B:P=1:CLS:FILES:INPUT"filename";F$
20 INPUT"# of Cols";C:INPUT"Col width";W:INPUT"# spaces bet. cols";S:INPUT"Left margin";M:PRINT"Total line length is ";C*(W+S)+M-S:INPUT"Is this OK";K$:IFK$<>"y"ANDK$<>"Y"THEN20
30 INPUT"Starting page #";P:INPUT"Pause bet. pages";P$:INPUT"Enter Title";T$
40 ONERRORGOTO110:S$=SPACE$(S):M$=SPACE$(M):W$=SPACE$(W):OPENF$FORINPUTAS1:FORI=0TOC-2:C$(I)="RAM:C"+STR$(I)+".DO":NEXT
50 PRINT"Columnizing page";P:FORI=0TOC-2:OPENC$(I)FOROUTPUTAS2:FORJ=1TOL:GOSUB100:PRINT#2,Y$:NEXT:CLOSE2:NEXT
65 IFP$="Y"ORP$="y"THENBEEP:PRINT"READY FOR PAGE";P;"(PRESS ANY KEY)":K$=INPUT$(1)
70 PRINT"Printing page";P:LPRINTSTR$(P)+"  "+T$+"  "+DATE$:FORI=1TOT-1:LPRINT:NEXT
80 FORI=2TOC:OPENC$(I-2)FORINPUTASI:NEXT:FORI=1TOL:LPRINTM$;:FORJ=2TOC:LINEINPUT#J,X$:LPRINTX$+S$;:NEXT:GOSUB100:LPRINTY$:NEXT:FORJ=2TOC:CLOSEJ:NEXT:FORI=1TOB:LPRINT:NEXT
90 IFEOF(1)THENFORJ=0TOC-2:KILLC$(J):NEXT:CLOSE1:BEEP:MENUELSE:P=P+1:GOTO50
100 Y$=W$:IFEOF(1)THENRETURNELSELINEINPUT#1,X$:MID$(Y$,1)=X$:RETURN
110 BEEP:IFERL=40THENPRINT"No file "+F$:INPUT"filename";F$:RESUME40ELSEIFERR=7THENPRINT"OUT OF MEMORY":ENDELSEPRINT"ERROR #";ERR;"IN LINE";ERL:END
