1 ' Printing Utility
2 ' 4/13/83
3 ' Bill Walters
4 '
5 CR$=CHR$(13):SP$=" "
10 CLS
20 LC=1
30 FILES
40 PRINT@170,CHR$(27);"p";" Text files only (.DO) ";CHR$(27);"q";
50 PRINT@240,"File to print :";:INPUT F$
60 GOSUB 300:PRINT@240,"Left Margin :";:INPUT M
70 GOSUB 300:PRINT@240,"Right Margin :";:INPUT W
80 LPRINT TAB(M);
90 OPEN F$ FOR INPUT AS 1
100 IF EOF(1) THEN 270
110 A$=INPUT$(1,1)
115 IF LPOS(0)=M AND A$=SP$ THEN 110
120 IF LPOS(0) < W-10 THEN 180
130 IF A$ <>SP$ THEN GOTO 180
140 LPRINT A$
150 LC=LC+1
160 LPRINT TAB(M);
170 IF LC < 57 THEN 100 ELSE GOTO 210
180 LPRINT A$;
190 IF A$<>CR$THEN200
192 LC=LC+1
194 A$=INPUT$(1,1)
196 IF A$=SP$ THEN 194
198 LPRINT TAB(M);
200 IF LC < 57 THEN GOTO 100
210 LC=1
220 FOR I=1 TO 10
230	LPRINT
240 NEXT I
250 LPRINT TAB(M);
260 GOTO 100
270 CLOSE
280 END
300 PRINT@240,SPACE$(40);:RETURN
