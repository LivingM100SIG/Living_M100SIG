5 REM 20-Mar-84 22:52 Fr [72236,3516]
10 REM --------------------------------
20 REM Print.100 for Model 100 SIG
30 REM General Purpose Print Formatter
40 REM Modified by larry gensch 3/19/84
50 REM --------------------------------
100 CLS:CLEAR2000:S=0:RV$=CHR$(27)+"p":NO$=CHR$(27)+"q":L=10:R=70:HD$="N":LS=1:C$=STRING$(60,32):LN=0:PG=1
110 GOSUB540
120 PRINT"You have these files in memory.....":PRINTRV$;:FILES
130 PRINTNO$;:PRINT@280,"Print       .DO";:PRINT@286,"";:LINEINPUTN$
140 CLS:GOSUB540:PRINT@40,"Page Margins (L,R)      ";:PRINT@80,"Page Spacing (1 or 2)   ";:PRINT@120,"Page Title             ";
150 PRINT@160,"Page 1 Title (Y/N)?     ";:PRINT@200,"Non Stop or Pause (N/P)";:PRINT@240,"Parallel or Serial (P/S)";
160 PRINT@65,"";:INPUTL,R:IFL=0THENL=1
170 L$=STRING$(L-1,32):PRINT@105,"";:INPUTLS
180 PRINT@145,"";:INPUTH$
190 PRINT@185,"";:INPUTHD$
200 PRINT@225,"";:INPUTX$
210 PRINT@265,"";:INPUTSP$
220 IFSP$="s"ORSP$="S"THENOPEN "COM:37E1E"FOROUTPUT AS 2:GOTO240
230 OPEN"LPT:"FOROUTPUTAS2
240 OPENN$FORINPUTAS1
250 CLS
260 IFH$=""THENPH$=L$+DATE$:GOTO290
270 PH$=STRING$(R-8,32)+DATE$
280 IFH$<>""THENMID$(PH$,L,(LEN(H$)))=H$
290 IFHD$="Y"ORHD$="y"THENPR$=LS$:GOSUB500
300 PR$=L$
310 IFLN>=50THENGOTO470
320 FORJ=LEN(PR$)TOR
330 PR$=PR$+INPUT$(1,1)
340 IFEOF(1)THENCLOSE1:EN=1:GOTO420
350 IFRIGHT$(PR$,1)=CHR$(10)THENPF=1:GOTO410
360 IFRIGHT$(PR$,1)=CHR$(128)THENPR$=LEFT$(PR$,J-1):GOTO470
370 NEXTJ
380 IFMID$(PR$,J,1)=" "THEN400
390 J=J-1:GOTO380
400 NX$=MID$(PR$,J+1,R):PR$=MID$(PR$,1,J):GOTO420
410 PR$=LEFT$(PR$,LEN(PR$)-2)
420 IFMID$(PR$,L,3)="   "THEN440
430 IFMID$(PR$,L,1)=" "THEN PR$=MID$(PR$,2,LEN(PR$)):GOTO430
440 PRINTCHR$(255);:PRINT#2,PR$;CHR$(13)+CHR$(10);:LN=LN+1:IFEN=1THEN470
450 IFLS=2THENPRINT#2,CHR$(13)+CHR$(10);:LN=LN+1
460 PR$=L$+NX$:NX$="":GOTO 310
470 GOSUB520:PG=PG+1
480 IFEN=1THENCLOSE2:GOTO 100
490 IFX$<>"n"ANDX$<>"N"THENGOSUB530
500 PRINT#2,PH$:PRINT#2,L$;"Page ";PG
510 PRINT#2,"":PRINT#2,"":LN=4:GOTO320
520 FORJ=1TO66-LN:PRINT#2,"":NEXTJ:RETURN
530 INPUTX;:RETURN
540 PRINTRV$+"       Radio Shack Printer Program      "+NO$;:RETURN

