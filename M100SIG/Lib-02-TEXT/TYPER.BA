1 CLEAR 1000
2 ON ERROR GOTO 7000
3 ROW=40:X=1:CLS:PRINT@ROW*1+2,"*** Select font ***"
4 PRINT@ROW*3+2,"<19> regular <20> conpressed (tiny)"
5 PRINT@ROW*4+2,"<18> corrspnd <23> elite <99> DEFAU"
9 PRINT@ROW*6+2,;:LINEINPUT "Enter 18/19/20/23 > ";SX$
10 IFLEN(SX$)<>2THENBEEP:GOTO9
11 IF INSTR("18/19/20/23",SX$)=0 THEN15
12 SX=VAL(SX$)
13 LPRINTCHR$(27);CHR$(SX)
14 REM --- text formatter by Sue Widemark v2 8/23/85 ----
15 CLS
20 FILES
30 PRINT:LINEINPUT"Filename: 0 - quit: ";F$
32 IF F$="0"THENMENU
35 INPUT"Number of lines-page";Y
36 INPUT"Width";W:IFW<30THENW=40
37 CLS:PRINT@ROW*2+4,;:INPUT"Left Margin";LM
38 IF(W+LM)>80THENLM=LM-1:GOTO38
39 PRINT@ROW*3+4,;:INPUT"1 - single space/2-Double sp.";DS
40 PRINT@ROW*4+4,"Header wanted ([CR] for none)":PRINT@ROW*5+4,;:LINEINPUT"==> ";A$:IFLEN(A$)>W-5THENBEEP:PRINTTAB(4)"--HEADER TOO LONG <press CR>  ";:INPUTZ$:CLS:GOTO40
41 IFA$=""THENL=0ELSEL=-1
42 IFLTHENPRINT@ROW*6+4,;:INPUT"Print page numbers <Y-N> ";PPX$:IFPPX$="Y"ORPPX$="y"THENPPX=-1ELSEPPX=0
45 CLS:IFLTHENPRINT@ROW*2+4,;:INPUT"Header on first page <Y-N>";HFP$:IFHFP$="Y"ORHFP$="y"THENHFP=-1ELSEHFP=0
46 IFNOTLTHENHFP=0
47 PRINT@ROW*3+4,"Is printer ready <Y-N> 0=quit?";
50 I$=INPUT$(1):IFI$="Y"THEN100ELSEIFI$="N"THEN40ELSEIFI$="0"THENMENU
100 CLS:PRINT@ROW*3+5,"Printing file":P=1
110 OPEN F$ FOR INPUT AS 1
118 IF NOT L THEN LPRINTTAB(LM)DATE$" - "DAY$:LPRINT:X=2:GOTO160
120 IFPPXTHENA$=A$+STR$(P)
130 IFHFPTHENLPRINTTAB(LM)A$:LPRINT
150 LPRINT:X=2
160 IF EOF(1)THEN500
165 LINEINPUT#1,B$:B=W:C=LEN(B$)
170 IFLEN(B$)<WTHENPR$=B$:IFLEN(PR$)<>0THENGOSUB1000:GOTO160ELSEGOTO160
180 GOSUB700
190 PR$=LEFT$(B$,B):GOSUB1000:IFLEN(MID$(B$,B+1))<>0THENB$=MID$(B$,B+1):B=WELSEGOTO160
200 IF LEN(B$)>WTHEN180
210 IFC<255THENPR$=B$:IFLEN(PR$)<>0THENGOSUB1000:GOTO160ELSEGOTO160
280 IFEOF(1)THENCLOSE 1:PR$=B$:GOSUB1000:GOTO500
290 LINEINPUT#1,C$:C=LEN(C$):IFLEN(B$)+C<BTHENPR$=B$+C$:GOSUB1000:GOTO160
300 A1=LEN(B$):B$=B$+LEFT$(C$,B-A1)
320 GOSUB700
330 PR$=LEFT$(B$,B):GOSUB1000:IFA1>BAND LEN(MID$(B$,B+1))+LEN(MID$(C$,W-A1+1))>255THENB$=MID$(B$,B+1)+MID$(C$,W-A1+1,W):C$=MID$(C$,(W-A1+1)+W):B=W:GOTO350
335 IFA1>BTHENB$=MID$(B$,B+1)+MID$(C$,W-A1+1):B=W:GOTO180ELSEB$=MID$(C$,B-A1+1):B=W:GOTO170
350 GOSUB700
360 PR$=LEFT$(B$,B):GOSUB1000:B$=MID$(B$,B+1)+C$:B=W:GOTO170
500 REM
600 CLOSE1:PRINT@ROW*7+2,"[1]ANOTHER/[2]MENU/[3]CHANGE FONTS: ";:BEEP
610 Z$=INPUT$(1):PRINTZ$
620 IFZ$="1"THENX=0:P=0:L=0:GOTO15
625 IFZ$="3"THENRUNELSEMENU
650 END
700 IFMID$(B$,B,1)<>" "ANDMID$(B$,B,1)<>"."ANDMID$(B$,B,1)<>"!"ANDMID$(B$,B,1)<>"?"THENIFB>1THENB=B-1:GOTO700
725 RETURN
1000 IF DS=1THENLPRINTTAB(LM)PR$:X=X+1:IFX>YTHENP=P+1:GOSUB5000:RETURNELSERETURN
1005 LPRINTTAB(LM)PR$:LPRINT:X=X+2:IFX>YTHENP=P+1:GOSUB5000
1010 RETURN
5000 CLS:PRINT@ROW*2+2,"Change paper.<RET> when ready";
5005 IFPPXTHENA$=MID$(A$,1,LEN(A$)-2)+STR$(P)
5010 INPUTZ$:IF LTHENLPRINTTAB(LM)A$ELSE LPRINTTAB(LM)" Page "P
5015 X=2
5020 LPRINT:CLS:PRINT@ROW*5+2,"Continuing printing...":RETURN
7000 CLOSE1:BEEP:IFERR>50THENCLS:PRINT:PRINT" --- File "F$" not found!":PRINT:INPUT"PRESS RETURN to continue";Z$:RUN
7010 CLS:PRINT:PRINT"Error "ERR" in line "ERL:END
