1 'NEWPRT.100    Ed Juge    04/19/83
2 '<GRPH>p causes forced end of page
3 'Defaults for margins (L,R), print
4 '   title on page 1 (HD$) and line
5 '   spacing (LS) exist in line 30.
6 '
7 '
10 CLS:CLEAR2000:S=0:FILES
20 L=20:R=60:HD$="Y":LS=1
30 C$=STRING$(60,32):LN=0:PG=1
40 GOSUB100:INPUT".DO file to print  ";N$
50 GOSUB100:INPUT"Margins (L,R)   ";L,R:IFL=0THENL=1
60 L$=STRING$(L-1,32):GOSUB100:INPUT"Line Spacing (1/2)  ";LS
70 GOSUB100:INPUT"Enter page title";H$
80 GOSUB100:INPUT"Title on Pg.1  (Y/N) ";HD$
85 OPENN$FORINPUTAS1
90 GOTO110
100 PRINT@205,C$:PRINT@205,"";:RETURN
110 CLS:IFH$=""THEN PH$=L$+DATE$:GOTO140
120 PH$=STRING$(R-8,32)+DATE$
130 IFH$<>""THENMID$(PH$,L,(LEN(H$)))=H$
140 IFHD$="Y"ORHD$="y"THENPR$=L$:GOSUB360
160 PR$=L$
170 IFLN>=50THENGOTO330
180 FORJ=LEN(PR$)TOR
190   PR$=PR$+INPUT$(1,1)
200   IFEOF(1)THENCLOSE:EN=1:GOTO280
210   IFRIGHT$(PR$,1)=CHR$(10)THENPF=1:GOTO270
220 IFRIGHT$(PR$,1)=CHR$(128)THENPR$=LEFT$(PR$,J-1):GOTO330
230   NEXTJ
240 IFMID$(PR$,J,1)=" "THEN260
250 J=J-1:GOTO240
260 NX$=MID$(PR$,J+1,R):PR$=MID$(PR$,1,J):GOTO280
270 PR$=LEFT$(PR$,LEN(PR$)-2)
280 IFMID$(PR$,L,3)="   "THEN300
290 IFMID$(PR$,L,1)=" "THEN PR$=MID$(PR$,2,LEN(PR$)):GOTO290
300 LPRINTPR$:LN=LN+1:IFEN=1THEN330
310 IFLS=2THENLPRINT:LN=LN+1
320 PR$=L$+NX$:NX$="":GOTO 170
330 FOR J1=LNTO65:LPRINT:NEXTJ1:PG=PG+1
340 IFEN=1THENMENU
350 IFX$<>""THEN360 ELSEPRINT@160,"";:INPUT"  <ENTER> = next page, <N> = nonstop";X$
360 CLS:LPRINTPH$:LPRINTL$;"Page ";PG
370 LPRINT:LPRINT:LN=4:GOTO180
