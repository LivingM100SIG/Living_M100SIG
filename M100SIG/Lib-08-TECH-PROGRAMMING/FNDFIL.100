0  'FILFND.BA -- for Model 100/102
10 CLS:E$=CHR$(27):F$="":PRINTE$"pFile to Find"E$"q <CR to End>";
20 INPUTF$:IFF$=""THENMENU
30 B=INSTR(F$,".")
40 IFB=0ORLEN(MID$(F$,B+1,2))<>2THENBEEP:GOTO10
50 FORK=1TOLEN(F$):X=ASC(MID$(F$,K,1))
60 IFX>96ANDX<123THENMID$(F$,K,1)=CHR$(X-32):NEXTELSENEXT
70 H$="":FORK=1TOB-1:H$=H$+MID$(F$,K,1):NEXT
80 H$=H$+SPACE$(7-B)+RIGHT$(F$,2)
90 FORA=63842TO64129STEP11
100 G$="":FORK=3TO10:G$=G$+CHR$(PEEK(A+K)):NEXT
110 IFG$=H$ANDPEEK(A)<>0THENI=PEEK(A+1)+256*PEEK(A+2):GOTO130ELSENEXT
120 BEEP:PRINTE$"pFile Not Found!"E$"q";:FORK=1TO500:NEXT:MENU
130 PRINTF$" Starts at Address"I
140 B1=256*(I/256-INT(I/256)):B2=INT(I/256)
150 PRINT"Byte 1 ="B1"; Byte 2 ="B2
160 PRINTE$"pHit any key to Continue"E$"q"
170 A$=INKEY$:IFA$=""THEN170ELSE10
