10 GOTO900:'Dec-Byte-Sin-Oct-Hex-Bin Converter S A French 72275,1622 12-28-84
100 DATA"0000","0001","0010","0011","0100","0101","0110","0111","1000","1001","1010","1011","1100","1101","1110","1111"
500 C$(5)=NC$:NC=0:FORL=1TOLEN(NC$):NC=NC+(VAL(MID$(NC$,L,1))*(2^(LEN(NC$)-L))):NEXT
502 C$(0)=STR$(NC):D$="":FORL=2TO0 STEP-1:D=FIX(NC/(256^L)):NC=NC-(D*(256^L)):D$=D$+"/"
504 IFD<100THEND$=D$+"0"
506 IFD<10THEND$=D$+"0"
510 D$=D$+STR$(D):NEXT:FORL=1TOLEN(D$):IFMID$(D$,L,1)<>" "THENC$(1)=C$(1)+MID$(D$,L,1)
512 NEXT:FORL=1TOLEN(C$(1)):IFMID$(C$(1),L,1)="/"THENMID$(C$(1),L,1)=" "
514 NEXT:NC=VAL(C$(0))
515 IFNC>65535THENNC=NC-65535:GOTO515 ELSEIFNC>32767THENNC=32767-NC
518 C$(2)=STR$(NC)
520 IFLEN(C$(5))<24THENC$(5)="0"+C$(5):GOTO520 ELSEFORL=1TO24 STEP4:D=1:RESTORE100
522 READD$:IFD$<>MID$(C$(5),L,4)THEND=D+1:GOTO522
524 C$(3)=C$(3)+MID$(N$,D,1):NEXT
530 FORL=1TO24 STEP3:D=1:RESTORE100
532 READD$:IFRIGHT$(D$,3)<>MID$(C$(5),L,3)THEND=D+1:GOTO532
534 C$(4)=C$(4)+MID$(N$,D,1):NEXT
540 GOTO700
600 NC=VAL(NC$):IFNC>16777215THEN880 ELSENC$="":L=24:D=0
602 IFNC-(2^L)>=0THENNC=NC-(2^L):NC$=NC$+"1":D=1 ELSEIFD=1THENNC$=NC$+"0"
603 IFL>0THENL=L-1:GOTO602 ELSE RETURN
610 D1=0:D$=" "+NC$:NC=0:FORL=LEN(D$)-2TO1 STEP-3:D=VAL(MID$(D$,L,3)):IFD>255THEN880 ELSE L=L-1:NC=NC+(D*(256^D1)):D1=D1+1
612 NEXT:NC$=STR$(NC):GOTO600
620 NC=VAL(NC$):IFNC>32767ORNC<-32768THEN880 ELSEIFNC<0THENNC$=STR$(32767+ABS(NC))
622 GOTO600
630 D$=NC$:NC$="":FORL=1TOLEN(D$):RESTORE100:FORL1=1TOINSTR(1,N$,MID$(D$,L,1)):READD1$:NEXT
632 IFC=4THENNC$=NC$+RIGHT$(D1$,3):NEXT ELSENC$=NC$+D1$:NEXT
650 RETURN
700 CLS:FORL=0TO5:IFL=CTHENCALL17001
702 PRINT@40+L*40," ";A$(L);" ";:CALL17006
705 IFLEN(C$(L))<24THENC$(L)=" "+C$(L):GOTO705
706 PRINT@55+L*40,C$(L):NEXT:PRINT@294,CHR$(226);
707 IFC=4THENFORL=1TO8:PRINT"  ";CHR$(226);:NEXT ELSEFORL=1TO6:PRINT"   ";CHR$(226);:NEXT
708 PRINT@280," Hit any key ";:CALL17006:GOSUB890:C$=INKEY$:IFC$<>""THENRUN ELSEIFVAL(T$)MOD2THENCALL17001:GOTO708 ELSE708
800 CLS:PRINT@42,"BASE CONVERTER":FORL=0TO6:PRINT@58+(L*40),L+1;"= ";A$(L);:NEXT
802 PRINT@245," Choose ":CALL17006:GOSUB890:C$=INKEY$:IFC$=""THENIFVAL(T$)MOD2THENCALL17001:GOTO802 ELSE802
803 C=VAL(C$)-1:IFC<0ORC>5THENMENU
804 CLS:D$=A$(C)+" number to convert":PRINT@100-(LEN(D$)/2),D$:D=180-(A(0,C)/2):PRINT@D,STRING$(A(0,C),"_"):PRINT@D-2,"";
806 INPUTNC$:CLS:PRINT@133,"I'm thinking...":IFLEN(NC$)>A(0,C)THEN880
812 FORL=1TOLEN(NC$):IFC=1ANDMID$(NC$,L,1)=" "THEN814 ELSEIFL=1ANDC=2ANDMID$(NC$,L,1)="-"THEN814
813 IFINSTR(1,LEFT$(N$,A(1,C)),MID$(NC$,L,1))=0THEN880
814 NEXT:NC=0:ONC+1GOSUB600,610,620,630,630,650:GOTO500
880 CLS:PRINT@129,"INVALID NUMBER FORMAT !":SOUND10000,100:RUN
890 TV$=RIGHT$(TIME$,2):IFT$<>TV$THENT$=TV$:RETURN ELSE890
900 DEFINTA-L:DEFDBLM-Z:DIMA$(6),A(1,6),M$(5),M(5):N$="0123456789ABCDEF":RESTORE920
902 FORL=0TO6:READA$(L),A(0,L),A(1,L):NEXT:GOTO800
920 DATA"Decimal",8,10,"Byte Decimal",11,10,"Signed 16 bit",6,10,"Hexadecimal",6,16,"Octal",8,8,"Binary",24,2,"Quit",0,0
