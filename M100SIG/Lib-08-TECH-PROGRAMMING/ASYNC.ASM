	ORG	58000	;676 BYTES FREE
	;Written and (C) Copyright Don Corbitt 73216,1022
	;For personal use only
	;ASYNCHRONOUS SOUND COMMAND
	;10 LOADM"ASYNC.CO
	;20 CALL 58000,D,FR
	;30 'SAME AS SOUND FR,D
	;DURATION*20ms (5 BEATS)
RST75	EQU	0F5FFH
RST751	EQU	0F600H
START	PUSH	PSW
S1	LDA	RST75
	CPI	0C9H
	JNZ	S1	;WAIT FOR READY
	POP	PSW
	STA	LENGTH	;SOUND LENGTH
	MOV	A,L
	OUT	0BCH
	MOV	A,H
	ORI	40H
	OUT	0BDH
	MVI	A,0C3H
	OUT	0B8H
	IN	0BAH
	ANI	0F8H
	ORI	20H
	OUT	0BAH
	LXI	H,INTVEC
	SHLD	RST751
	MVI	A,0C3H	;JMP
	STA	RST75
	RET

INTVEC	PUSH	PSW
	PUSH	H
	LXI	H,PULSE
	DCR	M
	JNZ	ENDINT
	MVI	M,5
	INX	H	;COUNT
	DCR	M
	JNZ	ENDINT
	MVI	A,0C9H	;RET
	STA	RST75
	IN	0BAH
	ORI	4
	OUT	0BAH
	CALL	6E83H	;RESET SOUND
ENDINT	POP	H
	POP	PSW
	RET

PULSE	DB	5	;COUNTER
LENGTH	DB	0	;LENGTH OF SOUND
	END	72C4H	;RETURN
