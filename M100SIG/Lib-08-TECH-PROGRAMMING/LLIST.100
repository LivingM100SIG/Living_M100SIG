0 REM - ?_BA.100 by Russ Villanueva.      [70526,1434]. All rights reserved.
1 REM This program simply lists a		program	directly from RAM to 		LCD:,LPT:, or to ram file BDUMP
2 REM for use with a print formating		program <since everybody has a		different version or
3 rem   preference>.				
4 REM Only part or all of the programs'		name is used w/o the ".BA"		extension.  Comments welcome.
10 MAXFILES=2:CLEAR300:DEFSNGP,X,Y:DEFINTA-O,Q-W,Z
11 DIMT$(127):F=1
20 FORA=0TO127:READT$(A):NEXT
30 PR$="x":CLS:E$=";-":GOSUB40:PR$="":PRINT:INPUT"File to list";E$
32 FORA=1TOLEN(E$):IFMID$(E$,A,1)>"`"THENMID$(E$,A)=CHR$(ASC(MID$(E$,A,1))-32)
34 NEXT
40 PRINT@280,"";:FORM=-1408TO-1639STEP-11:F$="":IFPEEK(M)THENFORN=0TO7:F$=F$+CHR$(PEEK(M+3+N)):NEXT
42 IFMID$(F$,7,2)="BA"ANDPR$<>""THENPRINTLEFT$(F$,6)+"    ";
44 IFINSTR(1,F$,E$)THENGOSUB48:GOTO50
46 NEXT:RETURN
48 P=PEEK(M+2)*256+PEEK(M+1)-2:RETURN
50 F$=LEFT$(F$,6)+"."+RIGHT$(F$,2):CLS:PRINT"Found  <"F$">":D$="LCD:"
51 PRINT"Output Device:",,"	1) <LCD:>",,"	2) LPT:",,"	3) RAM:Bdump"
52 A$=INPUT$(1):PRINT:IFA$="2"THEND$="LPT:"ELSEIFA$="3"THEND$="Bdump"
54 '
56 OPEND$FOROUTPUTAS2
58 PRINT#2,"Listing for program "+F$:PRINT#2,""
120 P=P+1:A=PEEK(P)
122 IFA=0THENPRINT#2,"":P=P+3:IFPEEK(P-2)=0THENMENUELSEF1=1:F=1
124 IFF1=1THENGOSUB400
300 A=PEEK(P)
320 IFA=34ANDF=0THENF=1:GOTO330
321 IFA=34ANDF=1THENF=0
322 IFA=39THENF=0
324 IFA=58ANDF=1THEN PRINT#2,"":PRINT#2,SPACE$(5);
326 IFA=9ANDF=1THENGOTO120
330 IFA>127ANDF=1THENPRINT#2,T$(A-128);
332 IFA=142THENF=0:IFPEEK(P+1)=255THENP=P+1
334 IFA>127THENGOTO120
340 PRINT#2,CHR$(A);
344 GOTO120
400 X=PEEK(P)+PEEK(P+1)*256
401 PRINT#2,USING"##### ";X;
402 P=P+2:IFPEEK(P)=58THENP=P+1
403 F1=0:A=PEEK(P):RETURN
1000 DATA END,FOR,NEXT,DATA,INPUT,DIM,READ,LET,GOTO,RUN
1004 DATA IF,RESTORE,GOSUB,RETURN,REM ,STOP,WIDTH,ELSE,LINE,EDIT
1008 DATA ERROR,RESUME,OUT,ON,DSKO$,OPEN,CLOSE,LOAD,MERGE,FILES
1012 DATA SAVE,LFILES,LPRINT,DEF,POKE,PRINT,CONT,LIST,LLIST,CLEAR
1016 DATA CLOAD,CSAVE,TIME$,DATE$,DAY$,COM,MDM,KEY,CLS,BEEP,SOUND
1020 DATA LCOPY,PSET,PRESET,MOTOR,MAX,POWER,CALL,MENU,IPL,NAME
1024 DATA KILL,SCREEN,NEW,TAB(,TO,USING,VARPTR,ERL,ERR,STRING$
1028 DATA INSTR,DSKI$,INKEY$,CSRLIN,OFF,HIMEM,THEN,NOT,STEP,+,-,*,/,^
1032 DATA AND,OR,XOR,EQV,IMP,MOD,\,>,=,<,SGN,INT,ABS,FRE,INP,LPOS
1036 DATA POS,SQR,RND,LOG,EXP,COS,SIN,TAN,ATN,PEEK,EOF,LOC,LOF,CINT
1040 DATA CSNG,CDBL,FIX,LEN,STR$,VAL,ASC,CHR$,SPACE$,LEFT$,RIGHT$,MID$,'

5 rem if for any reason this program stops in the middle of listing a program
6 rem add a 'space' in the front part of the program you are listing.
