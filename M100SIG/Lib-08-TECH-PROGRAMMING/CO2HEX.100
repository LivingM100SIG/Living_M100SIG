1 ' CO2HEX -- convert .CO file to HEX      copyright 1983 Michael M Rubenstein
2 ' modified 1986 Jay Maynard [71036,1603]
3 GOTO8
4 B1%=B%\16:GOSUB5:B1%=B%AND15:GOSUB5:CK%=(CK%+B%)AND255:RETURN
5 PRINT#1,MID$("0123456789ABCDEF",B1%+1,1);:RETURN
6 B%=INT(W/256):GOSUB4:B%=W-256*B%:GOSUB4:RETURN
7 P=PEEK(FP)+256*PEEK(FP+1):RETURN
8 MAXFILES=1:DIMBY%(15)
9 CLS
10 F$="":LINEINPUT".CO file to convert? ";F$:IFF$=""THENMENU
11 I=INSTR(F$,"."):IFI=1THEN10
12 IFI<>0THENF$=LEFT$(F$,I-1)
13 IFLEN(F$)>6THEN10
14 FORI=1TOLEN(F$)
15 C$=MID$(F$,I,1):IFC$>="a"ANDC$<="z"THENMID$(F$,I,1)=CHR$(ASC(C$)-32)
16 NEXT
17 F1$=LEFT$(F$+"      ",6)+"CO"
18 FORI=63919TO64128STEP11
19 IFPEEK(I)=0THEN24
20 FORJ=1TO8
21 IFCHR$(PEEK(I+J+2))<>MID$(F1$,J,1)THEN24
22 NEXTJ
23 GOTO26
24 NEXTI
25 GOTO10
26 OPENF$FOROUTPUTAS1:FP=I+1:GOSUB7:ST=PEEK(P)+256*PEEK(P+1):L=PEEK(P+2)+256*PEEK(P+3)-1:EP=PEEK(P+4)+256*PEEK(P+5)
27 FORI=1TOLSTEP16
28 CK%=0:J=I+15:IFJ>LTHENJ=L+1
29 PRINT#1,":";:B%=J-I+1:GOSUB4
30 W=ST+I-1:GOSUB6:PRINT#1,"00";
31 GOSUB7:P=P+5+I
32 FORK=0TOJ-I:BY%(K)=PEEK(P+K):NEXTK
33 FORK=0TOJ-I:B%=BY%(K):GOSUB4:NEXTK
34 B%=(255-CK%+1)AND255:GOSUB4:PRINT#1,""
35 NEXTI
36 CK%=0:PRINT#1,":00";:W=EP:GOSUB6:B%=0:GOSUB4:B%=(255-CK%+1)AND255:GOSUB4:PRINT#1,""
37 CLOSE:MENU

