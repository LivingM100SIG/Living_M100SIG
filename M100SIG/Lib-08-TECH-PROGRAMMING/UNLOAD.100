0 ' unload -- convert .CO file to HEX      copyright 1983 Michael M Rubenstein
1 GOTO6
2 B1%=B%\16:GOSUB3:B1%=B%AND15:GOSUB3:RETURN
3 PRINT#1,MID$("0123456789ABCDEF",B1%+1,1);:RETURN
4 B%=INT(W/256):GOSUB2:B%=W-256*B%:GOSUB2:RETURN
5 P=PEEK(FP)+256*PEEK(FP+1):RETURN
6 MAXFILES=1:DIMBY%(15)
7 CLS
8 F$="":LINEINPUT".CO file to convert? ";F$:IFF$=""THENMENU
9 I=INSTR(F$,"."):IFI=1THEN8
10 IFI<>0THENF$=LEFT$(F$,I-1)
11 IFLEN(F$)>6THEN8
12 FORI=1TOLEN(F$)
13 C$=MID$(F$,I,1):IFC$>="a"ANDC$<="z"THENMID$(F$,I,1)=CHR$(ASC(C$)-32)
14 NEXT
15 F1$=LEFT$(F$+"      ",6)+"CO"
16 FORI=63919TO64128STEP11
17 IFPEEK(I)=0THEN22
18 FORJ=1TO8
19 IFCHR$(PEEK(I+J+2))<>MID$(F1$,J,1)THEN22
20 NEXTJ
21 GOTO24
22 NEXTI
23 GOTO8
24 OPENF$FOROUTPUTAS1:FP=I+1:GOSUB5:ST=PEEK(P)+256*PEEK(P+1):L=PEEK(P+2)+256*PEEK(P+3)-1:EP=PEEK(P+4)+256*PEEK(P+5)
25 FORI=1TOLSTEP16
26 J=I+15:IFJ>LTHENJ=L
27 PRINT#1,":";:B%=J-I+1:GOSUB2
28 W=ST+I-1:GOSUB4:PRINT#1,"00";
29 GOSUB5:P=P+5+I
30 FORK=0TOJ-I:BY%(K)=PEEK(P+K):NEXTK
31 FORK=0TOJ-I:B%=BY%(K):GOSUB2:NEXTK
32 PRINT#1,"00"
33 NEXTI
34 PRINT#1,":00";:W=EP:GOSUB4:PRINT#1,"00"
35 CLOSE:MENU

