0 REM CO2BA 4/23/85 R. Perry
10 CLEAR256,HIMEM:CLS:FILES:LINEINPUT"Input .CO file name: ";A$
15 ONERRORGOTO110:LOADMA$
20 ONERRORGOTO:GOSUB50
25 X=64206:P=PEEK(X)+256*PEEK(X+1):IFP<>HIMEMTHENCLEAR256,P:GOSUB50:GOTO15
30 Q=P-1+PEEK(X+2)+256*PEEK(X+3):R=PEEK(X+4)+256*PEEK(X+5)
35 OPENA$+".do"FOROUTPUTAS1:PRINT"Working...":C=0:GOSUB60
40 FORJ=PTOQ:Z=PEEK(J):GOSUB95:NEXT
45 Z=C:GOSUB95:PRINT#1,:CLOSE:BEEP:MENU
50 A$="":FORJ=63109TO63129:A=PEEK(J):IFA=0ORA=46THENJ=63129ELSEA$=A$+CHR$(A)
55 NEXT:RETURN
60 Q$=CHR$(34):PRINT#1,"1 READP:CLEAR256,P:READP,Q,R,A$:C=P+Q+R"
65 PRINT#1,"2 PRINT"Q$"Working..."Q$
70 PRINT#1,"3 FORJ=PTOQ:READX:C=C+X:POKEJ,X:NEXT:READZ"
75 PRINT#1,"4 IFC<>ZTHENSOUND9394,9:PRINT"Q$"Checksum is wrong"Q$":END"
80 PRINT#1,"5 BEEP:PRINT"Q$"Done."Q$":SAVEMA$,P,Q,R"
85 K=9:N=16:Z=P:GOSUB95:Z=Q:GOSUB95:Z=R:GOSUB95
90 PRINT#1,","Q$;A$;Q$:N=16:RETURN
95 N=N+1:IFN>16THEN105ELSEPRINT#1,",";
100 PRINT#1,MID$(STR$(Z),2);:C=C+Z:RETURN
105 N=1:K=K+1:PRINT#1,:PRINT#1,MID$(STR$(K),2)" DATA";:GOTO100
110 IFERR=7THENRESUMENEXTELSEONERRORGOTO:RESUME
