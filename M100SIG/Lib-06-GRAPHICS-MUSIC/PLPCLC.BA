0 PRINT"Decimal to PLP100 Encoder"
1 'Input file should include mode commands without "~", coordinates separated by SPACES. Must include * to initialize. See PLP100.DOC for further info.
2 'Example: * % B 1 1 239 63 converts to ~*~%~B! !U"_
5 DEFINTA-Z:T$="LW#%IN":FILES:INPUT"Input File";F$:OPENF$FORINPUTAS1:INPUT"Output File";F$:OPENF$FOROUTPUTAS2:GOSUB12:GOTO18
9 GOSUB25:IFX$=""THEN9
10 ONINSTR(C$,M$)GOTO20,18,20,40,40,50,50,74,60
11 BEEP:PRINT"Error":END
12 IFEOF(1)THENPRINT#2,:CLOSE:MENUELSEX$="":GOSUB13:IFX$=""THEN12ELSERETURN
13 K$=INPUT$(1,1):IFEOF(1)ORK$=" "THENRETURNELSEX$=X$+K$:GOTO13
14 GOSUB15:GOTO25'temp modes
15 PRINT#2,"~";X$;:RETURN
18 C$="*"'EXIT PLP mode
20 A=1:IFINSTR(C$,X$)THEN:C$="*!AD.BRCM":GOSUB26:GOTO10ELSEIFINSTR(T$,X$)THENGOSUB28:GOTO20ELSEPRINT#2,X$;:GOSUB12:GOTO20'ASCII mode
25 GOSUB12:A=0
26 IFINSTR(C$,X$)THENM$=X$:PRINT#2,"~";X$;:GOTO25
28 ONINSTR(T$,X$)GOTO35,30,14,14,14,14
29 RETURN
30 GOSUB15:GOSUB12:GOSUB90:GOTO25'Wait
35 GOSUB15:GOSUB12:IFA=1THENGOSUB90:GOTO25ELSEGOSUB80:GOTO25'Locate cursor
40 GOSUB80:GOTO9'lines & points
50 GOSUB80:GOSUB12:GOSUB80:GOTO9'boxes
60 GOSUB95:GOSUB12:GOSUB90:GOTO9'sound
74 GOSUB80:GOSUB85:GOSUB12:GOSUB90:GOSUB12:GOSUB90:GOTO9'circles
80 GOSUB90'2+1 chr values:
85 GOSUB12:PRINT#2,CHR$(32+VAL(X$));:RETURN'1 chr value
90 C1=VAL(X$):C3=INT(C1/93):PRINT#2,CHR$(C1-C3*93+32);CHR$(C3+32);:RETURN'2 chr value
95 C1=VAL(X$):C4=INT(C1/8650):C5=C4*8650:C3=INT((C1-C5)/93):PRINT#2,CHR$(C1-C5-C3*93+32);CHR$(C3+32);CHR$(C4+32);:RETURN'3 chr value
