10 W=4 '           STEP
20 D=10 '          NUMBER OF BARS
30 K=2 '           ADD TO RND(W)
31 L=238:H=62
35 FORX=1TO4:Z=RND(W):NEXT
40 XI=RND(1)*W:XJ=RND(1)*W
50 YI=RND(1)*W:YJ=RND(1)*W
60 X=RND(1)*L
70 X1=RND(1)*L
80 Y=RND(1)*H
90 Y1=RND(1)*H
100 DIM A(D,4)
110 I=1:E=2
120 CLS
130 LINE(0,0)-(L,H),1,B
140 PRINT CHR$(2)
150 LINE(A(E,1),A(E,2))-(A(E,3),A(E,4)),0:LINE(X,Y)-(X1,Y1)
155 LINE(L-A(E,1),A(E,2))-(L-A(E,3),A(E,4)),0:LINE(L-X,Y)-(L-X1,Y1)
160 A(I,1)=X:A(I,2)=Y:A(I,3)=X1:A(I,4)=Y1
170 I=I+1:IF I>D THEN I=1
180 E=E+1:IF E>D THEN E=1
190 IF X+XI<1 OR X+XI>L THEN XI=-1*SGN(XI)*((RND(1)*W)+K)
200 IF X1+XJ<1 OR X1+XJ>L THEN XJ=-1*SGN(XJ)*((RND(1)*W)+K)
210 IF Y+YI<1 OR Y+YI>H THEN YI=-1*SGN(YI)*((RND(1)*W)+K)
220 IF Y1+YJ<1 OR Y1+YJ>H THEN YJ=-1*SGN(YJ)*((RND(1)*W)+K)
230 X=X+XI:Y=Y+YI:X1=X1+XJ:Y1=Y1+YJ
240 GOTO150
