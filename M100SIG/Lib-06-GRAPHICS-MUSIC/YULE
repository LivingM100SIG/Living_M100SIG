10 REM Yuletide Notes
20 REM By Varon Fugman
30 REM CSIS I.D. # 70476,1036
40 CLS:DEFINTA-Z:DIMA(12,4):FORY=1TO4:FORX=1TO12:READA(X,Y):NEXTX,Y:O=2:D=2
50 READA$:IFA$="END"THENRESTORE310:GOTO50
60 READZ:FORX=1TOLEN(A$)
70 B$=MID$(A$,X,1):N=0:IFB$>"0"ANDB$<"5"THENO=VAL(B$):GOTO120
80 IFB$="a"THENN=1ELSEIFB$="A"THENN=2ELSEIFB$="b"THENN=3ELSEIFB$="c"THENN=4ELSEIFB$="C"THENN=5ELSEIFB$="d"THENN=6
90 IFB$="D"THENN=7ELSEIFB$="e"THENN=8ELSEIFB$="f"THENN=9ELSEIFB$="F"THENN=10ELSEIFB$="g"THENN=11ELSEIFB$="G"THENN=12
100 IFN>0THENSOUNDA(N,O),D*Z:D=2:GOTO120
110 D=2:IFB$=","THEND=4ELSEIFB$=";"THEND=6ELSEIFB$="."THEND=3ELSEIFB$="/"THEND=1ELSEIFB$="'"THEND=8
120 NEXT:FORX=1TO5000:NEXT:GOTO50
200 REM Note Data
210 DATA11172,10544,9952,9394,8866,8368,7900,7456,7032,6642
220 DATA6269,5918,5586,5272,4976,4697,4433,4184,3950,3728,3516,3321
230 DATA3134,2959,2793,2636,2484,2348,2216,2092,1975,1864,1758,1660
240 DATA1567,1479,1396,1318,1244,1174,1108,1046,987,932,879,830,783,739
300 REM Song Data
310 REM Joy to the World
320 DATA "3,d.C/b;a2g,F,e;d3a;bb;CC;dddCba.a2/gF3ddCba.a2/gFFFFF/F/g3;a2/g/Feee/e/F;g/F/ed3,db.a2/gFg,F,e'd"
330 DATA6
340 REM Hark the Herald Angel
350 DATA"2cf.f/ef3aa2g3cc.c/Aa2g3,a2cf.f/ef3aa2g3c2g.g/eed,c3ccc2f3Aaa2g3ccc2f3Aaa2g3.d/ddcAa,A2g3/a/A.c2/ffg3,a.d/ddcAa,A2g3/a/A.c2
/ffg,f"
360 DATA9
370 REM O Come All Ye Faithful
380 DATA"2g,gdg3,a2dd3babc,ba2g,gFeFg3ab2,F.e/d;dd3,dcb,cbbab2g3a2.F/edggFg3a2,gd3bbabc,babcba2g,Fg3c,b.a2/g;g"
390 DATA9
400 REM We Three Kings
410 DATA"2.e/d.c/a/b/c/b,a.e/d.c/a/b/c/b,a.c/c.d/d.e/e/g/f/e/d/e/d.c/b;a;b;dc/cc1/g2c/a.cc/cc1/g2c/a.c.c/c.d/e.f/e.d/ec/cc1/g2c/a;c"

420 DATA15
430 REM O Little Town of Bethlehem
440 DATA"3bbbAbdc2e3a2g/F/g3a2d3;bbbbeddc2e3a2g/F/g3ba2;g3bbba2g,FFFeFg3a;bbbbAbdc2e3ed2g3.b/a2;g"
450 DATA12
460 REM Silent Night
470 DATA"2.g3/a2g,e.g3/a2g,e3.d/d,b.c/c2,g3.a/a.c/ba2.g3/a2g,e3.a/a.c/ba2.g3/a2g,e3.dd.f/db,c,ec2ge.g/fd;c"
480 DATA12
800 REM 					You can enter your own tunes.   	This program supports 4 octives 	that
go from a to f# (F).
810 REM Type in your song using the same	format as the ones in the		program. Lowercase letters		
represent the natural note.             Capitals are for sharps.
820 REM Numbers 1 - 4 specify the octive	you want. (1=lowest, 2=defalt)		For notes other than quarter		
notes, put one of the following		symbols BEFORE the note:
830 REM						/ = 1/8th note	, = 1/2 note		. = dotted 1/4	; = dottet 1/2		' = 
whole note			
840 REM After entering the data for the		song, enter a data line for the		playing speed. Try a few until		you 
get one you like for that		tune.				
900 DATA"END"
