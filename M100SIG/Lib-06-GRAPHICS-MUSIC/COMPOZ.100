1 'COMPOZ. by RON W.JOHNSON[70116,1416],based on NEWMUS by Jim Stutsman[70130,101]
4 'requires .DO file named SONG with note-values in form NOL[E],where
5 'N=note from scale(e,E,F,g,G,a,A,b,B,C,d,D);
6 'O=octave(0-5);L=length(W=whole,H=half,..,T=thirty-second);
7 '[E]=any nmbr.of + or - L's
8 '1st # in SONG.DO must be tempo-factor(eg	1	2	.3 etc)
825 MAXFILES=2:CLEAR:DIM NT%(11,5)
830 DATA15800,14912,14064,13284,12538,11836,11172,10544,9952,9394,8866,8368
835 TN$="eEFgGaAbBCdD":TM$=".TSEQHW"
850 FORJ=0TO11:READNT%(J,0):NEXTJ
865 FORI=1TO5:FORJ=0TO11:NT%(J,I)=NT%(J,0)\2^I:NEXTJ:NEXTI
890 INPUT"Name of song";NS$
895 OPENNS$FORAPPENDAS2:OPEN"SONG.DO"FORINPUTAS1:INPUT#1,TP
910 IFEOF(1)THENCLOSE#1:CLOSE#2:GOTO999ELSEINPUT#1,X$
915 NO=INSTR(TN$,LEFT$(X$,1))-1:OC=VAL(MID$(X$,2,1)):TM=INSTR(TM$,MID$(X$,3,1)):TM=2^TM
930 I=INSTR(X$,"+"):IFI>0THENMID$(X$,I,1)="&":V=INSTR(TM$,MID$(X$,I+1,1)):TM=TM+2^V:GOTO930
955 I=INSTR(X$,"-"):IFI>0THENMID$(X$,I,1)="_":V=INSTR(TM$,MID$(X$,I+1,1)):TM=TM-2^V:GOTO955
980 IFNO<0THENP$="0"ELSEP$=STR$(NT%(NO,OC))
982 IFLEFT$(P$,1)=" "THENP$=RIGHT$(P$,LEN(P$)-1)
983 D$=STR$(CINT(TM*TP)):IFLEFT$(D$,1)=" "THEND$=RIGHT$(D$,LEN(D$)-1)
985 PRINT#2,P$;",";D$;",";
995 PRINTX$:GOTO910
1020 OPENNS$FORINPUTAS1
1030 INPUT#1,P,D:SOUNDP,D:IFNOTEOF(1)THEN1030
1040 END

