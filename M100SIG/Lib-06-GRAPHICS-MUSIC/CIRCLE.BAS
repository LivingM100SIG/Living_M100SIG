10000 REM CIRCLE V1.1 5/20/86 ADAPTED FOR M100 BY JAY HOLOVACS 74756,413
10002 REM BASED ON ALGORITHM PUBLISHED BY MIKE HIGGINS (COMPUTER LANGUAGE MAGAZINE 5/86)
10004 REM 3/31/87 MODIFIED BY NEIL WICK 71056,613
10006 REM ENTRY CONDITIONS:XC%,YC%=COORD OF CENTER; R%=RADIUS--ALSO USES XX%,R1%,YY%; ALTERS ERROR TRAP
10008 ON ERROR GOTO 10022
10010 XX%=R%:YY%=0:R1%=XX%/2
10012 PSET(XX%+XC%,YY%+YC%):PSET(XC%+YY%,YC%+XX%):PSET(XC%-YY%,YC%+XX%):PSET(XC%-XX%,YC%+YY%)
10014 PSET(XC%-XX%,YC%-YY%):PSET(XC%-YY%,YC%-XX%):PSET(XC%+YY%,YC%-XX%):PSET(XC%+XX%,YC%-YY%)
10016 IFXX%<YY%THENRETURN
10018 YY%=YY%+1:R1%=R1%-YY%:IF R1%<0 THEN XX%=XX%-1:R1%=R1%+XX%
10020 GOTO10012
10022 IF ERR=5 AND ERL=10012 OR ERL=10014 THEN RESUME NEXT ELSE PRINT "ERR=";ERR:STOP
