0 'MUSIC.MATCH v. 1.1 by Andrew Zarchy [73107,3455]
10 DEFINTA-Z:DEFSTRC,I,K,S:DIMK(16)
20 N(1)=9394:N(2)=8368:N(3)=7456:N(4)=7032:C=STRING$(3,239)
30 CLS:PRINT"        M u s i c     M a t c h"
40 FORX=1TO4:READL(X):NEXT:FORX=0TO15:READK(X):NEXT:DATA 169,175,181,187,z,x,c,v,m,",",.,/,Z,X,C,V,M,<,>,?
42 PRINT:PRINT"Instructions? (y/n) ";:I=INPUT$(1):IFI="Y"ORI="y"THENGOSUB1030:GOSUB1040
45 GOSUB1030:PRINT"Start with how many notes (1-5)? ";:I=INPUT$(1):PRINTI;:IFI=""THENI=1ELSEIFVAL(I)>5THENPRINT:BEEP:GOTO45
47 IFVAL(I)>1THENFORX=1TOVAL(I)-1:GOSUB1000:NEXT
50 GOSUB1030:PRINT@170,"+     +     +    +";
100 FORX=1TO750:NEXT:GOSUB1000
130 FORX=1TOLEN(S):FORT=1TO100:NEXTT:D=VAL(MID$(S,X,1)):GOSUB1070:NEXTX
150 FORX=1TOLEN(S):D=VAL(MID$(S,X,1))
160 I=INKEY$:IFI=""THEN160ELSEFORY=0TO15:IFI<>K(Y)THENNEXTY:GOTO160
180 FORQ=0TO12STEP4:FORZ=0TO3:IFY<>Q+ZTHENNEXTZ:NEXTQ
190 IFZ+1=DTHENGOSUB1070:NEXTX:GOTO100ELSEGOSUB1090
260 PRINT@289,"< Play again? (y/n) >";:GOSUB1065:IFI="Y"ORI="y"THENS="":GOTO45ELSEMENU
1000 FORY=1TOVAL(RIGHT$(TIME$,2)):R=RND(1):NEXT
1010 R=INT(RND(1)*10):IFR=0ORR>4THEN1010ELSES=S+MID$(STR$(R),2):RETURN
1030 PRINT@40,CHR$(27)CHR$(74):RETURN
1040 PRINT"     Use the ZXCV keys to match the":PRINT" sequence of notes.  After each correct"
1050 PRINT"    play, the sequence gets longer.":PRINT" A second player may use the M<>? keys.":PRINT
1060 PRINT"     <Press any key to start game>";
1065 I=INKEY$:IFI=""THEN1065ELSERETURN
1070 PRINT@L(D),C;:SOUNDN(D),15
1080 PRINT@L(D),"   ";:PRINT@L(D)+1,"+";:RETURN
1090 PRINT@L(Z+1),C;:FORQ=1TO7:SOUND14400,2:SOUND14007,2:NEXTQ
1100 FORX=1TO4:PRINT@L(D)+1," ";:FORT=1TO250:NEXT:PRINT@L(D)+1,"+";:FORT=1TO150:NEXT:NEXT:RETURN

